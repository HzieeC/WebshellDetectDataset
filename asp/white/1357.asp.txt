<!--#include file="../inc/mdb.asp"-->
<!--#include file="../inc/config.asp"-->
<!--#include file="../inc/cmsfunction.asp"-->
<!--#include file="../inc/md5.asp"-->
<%
'****************************************************
' 零柒。蜀工作室网站管理系统 Power by 07ing.com
' Web: http://www.07ing.net, http://www.07ing.com
' Copyright (C) 2006 零柒。蜀工作室 All Rights Reserved.
'****************************************************
if request.form("source")="ok" then
dim sendmsg:sendmsg=""
dim founderr:founderr=false
dim UserName,Password,PwdConfirm,Question,Answer,Sex,Email,userlock,ucode
dim Errtext
UserName=decode(request.form("userid"))
Sex=CheckStr(decode(Request.form("sex")))
Password=decode(request.form("pwdid"))
PwdConfirm=decode(request.form("spwdid"))
Question=CheckStr(decode(request.form("Question")))
Answer=CheckStr(decode(request.form("Answers")))
Email=CheckStr(decode(request.form("sEmail")))
ucode=decode(request.form("ucode"))
if upass="1" then
	userlock=2
else
	userlock=0
end if
if mailkg="2" then
randomize 
Password= Int((89999999 * Rnd) + 10000000)  
end if
if badReg<>"" then
badwords=split(badReg,"|")
for z=0 to ubound(badwords)
if instr(UserName,badwords(z))>0 then
founderr=true
    Errtext="Err_106"
exit for
end if
next
end if
if int(ucode)<>int(Session("RndNum")) then
    founderr=true
    Errtext="Err_100"
end if
if IsValidEmail(Email)=false or strLength(Email)>100 then
    founderr=true
    Errtext="Err_105"
end if
if Password<>PwdConfirm then
    founderr=true
    Errtext="Err_104"
end if
if not checkname(Password) then
    founderr=true
    Errtext="Err_103"
end if
if not checkname(UserName) or strLength(UserName)>14 or strLength(UserName)<4 then
    founderr=true
    Errtext="Err_102"
end if
if UserName="" or Sex="" or Password="" or PwdConfirm="" or Question="" or Answer="" or Email="" or ucode="" then
    founderr=true
    Errtext="Err_101"
end if
if founderr=false then
   dim sqlReg,rsReg
   sqlReg="select * from [User] where UserName='" & Username & "' or Email='" & Email & "'"
   set rsReg=server.createobject("adodb.recordset")
   rsReg.open sqlReg,conn,1,3
   if not(rsReg.bof and rsReg.eof) then
      founderr=true
      Errtext="Err_100"
   else
        rsReg.addnew
        rsReg("UserName")=UserName
        rsReg("Password")=md5(Password)
        rsReg("Question")=Question
        rsReg("Answer")=md5(Answer)
	rsReg("Sex")=Sex
	rsReg("Email")=Email
	rsReg("RegDate")=Now()
	rsReg("userlock")=userlock
        rsReg.update
	rsReg.close
	set rsReg=nothing
        if fsMessage="yes" then
           title="『"&webname&"』欢迎您的到来"
           conn.execute("insert into Message(sender,incept,title,content,sendtime) values('『"&webname&"』','"&UserName&"','"&title&"','"&bodyMessage&"','"&now()&"')")
        end if
        if mailkg<>"0" then
        dim emailto,emailtopic,emailbody
        emailto=Email
        emailtopic="您在"&webname&"的注册资料"
        emailbody=""&vbCrlf&""&UserName&", 您好!"
        emailbody=emailbody & ""&vbCrlf&""&vbCrlf&"欢迎您注册"&webname&"会员，我们将提供给您最好的服务！"
        emailbody=emailbody & ""&vbCrlf&"下面是您在"&webname&"的注册信息："
        emailbody=emailbody & ""&vbCrlf&"注册名："&UserName&""
        emailbody=emailbody & ""&vbCrlf&"密  码："&Password&""
        emailbody=emailbody & ""&vbCrlf&"本站地址："&weburl
        emailbody=emailbody & ""&vbCrlf&""&vbCrlf&"============================================"
        emailbody=emailbody & ""&vbCrlf&"再次感谢您注册"&webname&"！"&vbCrlf&""&weburl&""&vbCrlf&""&vbCrlf&""
%>
       <!-- #include file="../inc/email_inc.asp" -->
<%
        if SendMail="OK" then
           if mailkg="2" then
	      sendmsg="本站启用邮箱验证功能，随机密码已发往您的邮箱，请查收后登陆。"
           else
	      sendmsg="您的注册信息已经同时发往您的邮箱，请注意查收。"
           end if
        else
	    sendmsg="由于系统设置错误，给您邮箱发送的注册资料未成功。"
        end if
        end if
        if upass=1 then sendmsg=sendmsg&"本站系统开启了会员注册审核功能，请等待管理员验证通过……"
        conn.Execute ("Update [allcount] Set usercount=usercount+1")
        response.Write(escape("注册成功！欢迎您的到来。"&sendmsg))
        Session("RndNum")=""
    end if
end if		
if founderr=true then response.Write(escape(Errtext))
else
dim sResult,fieldID,inputValue,incok,incno
incok="<img align='absMiddle' alt='' src='../images/icn_ok.gif' />"
incno="<img align=""absMiddle"" alt="""" src=""../images/icn_no.gif"" />"
'inputValue=decodeURIComponent(Request("inputValue"))
inputValue=decode(Request("inputValue"))
fieldID=Lcase(Request("fieldID"))
sResult=""
If inputValue&"" = "" Then
  Select Case fieldID
    Case "userid"
      sResult=incno&"Sorry,用户名不能为空!"
    Case "pwdid"
      sResult=incno&"Sorry,密码不能为空!"
    Case "spwdid"
      sResult=incno&"Sorry,确认密码不能为空!"
    Case "question"
      sResult=incno&"Sorry,密码问题不能为空!"
    Case "answers"
      sResult=incno&"Sorry,问题答案不能为空!"
    Case "semail"
      sResult=incno&"Sorry,邮箱不能为空!"
    Case "ucode"
      sResult=incno&"Sorry,验证码不能为空!"
  End Select
Else
  Select Case fieldID
    Case "userid"
        call susername()
    Case "pwdid"
  	if not checkname(inputValue) then
		sResult=incno&"密码含有非法字符"
        elseif strLength(inputValue)<6 then
		sResult=incno&"密码不能小于6字符"
        else
                sResult = incok
        end if
    Case "spwdid"
  	if not checkname(inputValue) then
		sResult=incno&"确认密码含有非法字符"
        elseif strLength(inputValue)<6 then
		sResult=incno&"确认密码不能小于6字符"
        else
                sResult = incok
        end if
    Case "question"
                sResult = incok
    Case "answers"
                sResult = incok
    Case "semail"
        if IsValidEmail(inputValue)=false then
                sResult=incno&"非法的电子邮件格式"
        else
                call Emails()
        end if
    Case "ucode"
        if int(inputValue)<>int(Session("RndNum")) then
                sResult=incno&"验证码不正确"
        else
                sResult = incok
        end if        
  End Select
end if
response.Write(escape(sResult))	
sub susername()
if inputValue="" or strLength(inputValue)>14 or strLength(inputValue)<4 then
	sResult=incno&"用户名不能大于14或小于4字符(2-7个汉字)"
        founderr=true
else
  	if not checkname(inputValue) then
		sResult=incno&"用户名中含有非法字符"
                founderr=true
	end if
end if
if badReg<>"" then
badwords=split(badReg,"|")
for z=0 to ubound(badwords)
if instr(inputValue,badwords(z))>0 then
founderr=true
sResult=incno&"你的用户名含有网站禁止注册的字词"
exit for
end if
next
end if
if founderr=false then
	dim sqlCheckReg,rsCheckReg
	sqlCheckReg="select Userid from [User] where UserName='" & inputValue & "'"
	set rsCheckReg=server.createobject("adodb.recordset")
	rsCheckReg.open sqlCheckReg,conn,1,1
	if not(rsCheckReg.bof and rsCheckReg.eof) then
		sResult=incno&"Sorry,该用户名已存在"
	else
                sResult = incok
	end if
	rsCheckReg.close
	set rsCheckReg=nothing
end if	
end sub
sub Emails()
dim sqlCheckReg,rsCheckReg
	sqlCheckReg="select Userid from [User] where Email='" & inputValue & "'"
	set rsCheckReg=server.createobject("adodb.recordset")
	rsCheckReg.open sqlCheckReg,conn,1,1
	if not(rsCheckReg.bof and rsCheckReg.eof) then
		sResult=incno&"Sorry,已经有用户使用了您填写的电子邮件地址"
	else
                sResult = incok
	end if
	rsCheckReg.close
	set rsCheckReg=nothing
end sub
end if
conn.close:set conn=nothing
'****************************************************
' 零柒。蜀工作室网站管理系统 Power by 07ing.com
' Web: http://www.07ing.net, http://www.07ing.com
' Copyright (C) 2006 零柒。蜀工作室 All Rights Reserved.
'****************************************************
%>