<!--#Include File="CONN.ASP"-->
<!--#Include File="inc/Const.asp"-->
<!--#Include File="Dv_Plus_Invite_ClsMain.asp"-->
<%
If Not Dvbbs.Master Then
	Dvbbs.AddErrcode(28)
	Dvbbs.ShowErr()
End If
Dim DI_Stats,DI_Script_Name,action,DI_obj
DI_Stats = "邀请码参数设置"
DI_Script_Name = Request.ServerVariables("Script_Name")
Dvbbs.LoadTemplates("")
Dvbbs.Stats = DI_Stats
Dvbbs.Nav
Dvbbs.Head_Var 0,0,DI_Stats,DI_Script_Name
action = Dvbbs.CheckStr(Request("action"))
Set DI_obj = New Cls_DI
Select Case action
	Case "act" : saveInfo()
	Case Else
		showForm()
End Select
Dvbbs.Footer()
Dvbbs.PageEnd()

Sub saveInfo()
Dim Sql,Rs
Dim DI_UserExpend_R,DI_UserLimit_R,DI_TempMinute_R,DI_FailDate_R,DI_Readme_R,DI_UserGain_R,DI_IsOpen_R
DI_UserExpend_R = Dvbbs.CheckNumeric(Request("DI_UserExpend(0)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserExpend(1)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserExpend(2)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserExpend(3)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserExpend(4)"))
DI_UserLimit_R = Dvbbs.CheckNumeric(Request("DI_UserLimit(0)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserLimit(1)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserLimit(2)"))
DI_TempMinute_R = Dvbbs.CheckNumeric(Request("DI_TempMinute"))
DI_FailDate_R = Dvbbs.CheckNumeric(Request("DI_FailDate"))
DI_Readme_R = Request("DI_Readme")
DI_UserGain_R = Dvbbs.CheckNumeric(Request("DI_UserGain(0)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserGain(1)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserGain(2)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserGain(3)"))&"|"&_
				Dvbbs.CheckNumeric(Request("DI_UserGain(4)"))
DI_IsOpen_R = Dvbbs.CheckNumeric(Request("DI_IsOpen"))

If Not IsObject(Conn) Then ConnectionDatabase
Set Rs = Server.CreateObject("ADODB.Recordset")
Rem Set Rs = Dvbbs.iCreateObject("ADODB.Recordset") ' Dvbbs 8.0 Sp1 以上可使用这一行
Sql = "Select * From [Dv_Plus_Invite_Setup] Where DI_ID="&Dvbbs.CheckNumeric(Request("DI_ID"))
Rs.Open Sql,Conn,1,3
If Rs.Bof And Rs.Eof Then
	Response.Redirect "showerr.asp?ErrCodes=<li>没有找相关记录！&action=OtherErr"
	Response.End
Else
	Rs("DI_UserExpend") = DI_UserExpend_R
	Rs("DI_UserLimit") = DI_UserLimit_R
	Rs("DI_IsExpend") = Dvbbs.CheckNumeric(Request("DI_IsExpend"))
	Rs("DI_IsCreate") = Dvbbs.CheckNumeric(Request("DI_IsCreate"))
	Rs("DI_TempMinute") = DI_TempMinute_R
	Rs("DI_FailDate") = DI_FailDate_R
	Rs("DI_Readme") = DI_Readme_R
	Rs("DI_IsGain") = Dvbbs.CheckNumeric(Request("DI_IsGain"))
	Rs("DI_UserGain") = DI_UserGain_R
	Rs("DI_IsOpen") = DI_IsOpen_R
	Rs.Update
	Call DI_obj.DI_NoadSql("Dv_Plus_Invite_Admin.asp","提交邀请码插件设置")
	Application.Lock
	Application.contents.Remove(Dvbbs.CacheName & "_DI_IsSetup")
	Application.UnLock
End If
Rs.Close:Set Rs = Nothing
Dvbbs.Dvbbs_suc("<li>提交成功！</li>")
End Sub

Sub showForm()
	DI_obj.Load_Setup()
	%>
	<form method="post" action="<%=DI_Script_Name%>?action=act">
	<input type="hidden" name="DI_ID" value="<%=DI_obj.DI_ID%>"/>
	<table class="tableborder1" cellspacing="1">
		<tr><th><%=DI_Stats%></th></tr>
		<tr>
			<td class="tablebody1"><li>说明：以下均填写整数</li></td>
		</tr>
		<tr>
			<td class="tablebody1" style="padding:10px 10px;">
			<fieldset>
				<legend style="border:1px dashed #B4CEFF;background-color:#f5f5f5;font-weight:bold">
					邀请码插件开关
				</legend>
				<li>邀请码插件开关：<input type="radio" class="radio" name="DI_IsOpen" value="1"<%If DI_obj.DI_IsOpen=1 Then%> checked<%End If%>/>开&nbsp;<input type="radio" class="radio" name="DI_IsOpen" value="0"<%If DI_obj.DI_IsOpen=0 Then%> checked<%End If%>/>关</li>
			</fieldset>
			<br/>
			<fieldset>
				<legend style="border:1px dashed #B4CEFF;background-color:#f5f5f5;font-weight:bold">
					消耗积分开关：<input type="radio" class="radio" name="DI_IsExpend" value="1"<%If DI_obj.DI_IsExpend=1 Then%> checked<%End If%>/>开&nbsp;<input type="radio" class="radio" name="DI_IsExpend" value="0"<%If DI_obj.DI_IsExpend=0 Then%> checked<%End If%>/>关
				</legend>
				<li>申请邀请码消耗金钱数：<input type="text" name="DI_UserExpend(0)" value="<%=DI_obj.DI_UserExpend(0)%>"/></li>
				<li>申请邀请码消耗经验值：<input type="text" name="DI_UserExpend(1)" value="<%=DI_obj.DI_UserExpend(1)%>"/></li>
				<li>申请邀请码消耗魅力值：<input type="text" name="DI_UserExpend(2)" value="<%=DI_obj.DI_UserExpend(2)%>"/></li>
				<li>申请邀请码消耗金币数：<input type="text" name="DI_UserExpend(3)" value="<%=DI_obj.DI_UserExpend(3)%>"/></li>
				<li>申请邀请码消耗点券数：<input type="text" name="DI_UserExpend(4)" value="<%=DI_obj.DI_UserExpend(4)%>"/></li>
			</fieldset>
			<br/>
			<fieldset>
				<legend style="border:1px dashed #B4CEFF;background-color:#f5f5f5;font-weight:bold">
					获赠积分开关：<input type="radio" class="radio" name="DI_IsGain" value="1"<%If DI_obj.DI_IsGain=1 Then%> checked<%End If%>/>开&nbsp;<input type="radio" class="radio" name="DI_IsGain" value="0"<%If DI_obj.DI_IsGain=0 Then%> checked<%End If%>/>关
				</legend>
				<li>邀请码被使用获赠金钱数：<input type="text" name="DI_UserGain(0)" value="<%=DI_obj.DI_UserGain(0)%>"/></li>
				<li>邀请码被使用获赠经验值：<input type="text" name="DI_UserGain(1)" value="<%=DI_obj.DI_UserGain(1)%>"/></li>
				<li>邀请码被使用获赠魅力值：<input type="text" name="DI_UserGain(2)" value="<%=DI_obj.DI_UserGain(2)%>"/></li>
				<li>邀请码被使用获赠金币数：<input type="text" name="DI_UserGain(3)" value="<%=DI_obj.DI_UserGain(3)%>"/></li>
				<li>邀请码被使用获赠点券数：<input type="text" name="DI_UserGain(4)" value="<%=DI_obj.DI_UserGain(4)%>"/></li>
			</fieldset>
			<br/>
			<fieldset>
				<legend style="border:1px dashed #B4CEFF;background-color:#f5f5f5;font-weight:bold">
					用户能否申请邀请码开关：<input type="radio" class="radio" name="DI_IsCreate" value="1"<%If DI_obj.DI_IsCreate=1 Then%> checked<%End If%>/>开&nbsp;<input type="radio" class="radio" name="DI_IsCreate" value="0"<%If DI_obj.DI_IsCreate=0 Then%> checked<%End If%>/>关
				</legend>
				<li>申请邀请码要求至少文章数：<input type="text" name="DI_UserLimit(0)" value="<%=DI_obj.DI_UserLimit(0)%>"/></li>
				<li>申请邀请码要求至少登录数：<input type="text" name="DI_UserLimit(1)" value="<%=DI_obj.DI_UserLimit(1)%>"/></li>
				<li>申请邀请码要求至少注册天数：<input type="text" name="DI_UserLimit(2)" value="<%=DI_obj.DI_UserLimit(2)%>"/></li>
			</fieldset>
			<br/>
			<fieldset>
				<legend style="border:1px dashed #B4CEFF;background-color:#f5f5f5;font-weight:bold">
					其它参数：
				</legend>
				<li>用户注册时邀请码使用分钟：<input type="text" name="DI_TempMinute" value="<%=DI_obj.DI_TempMinute%>"/></li>
				<li>用户申请邀请码有效期(天)：<input type="text" name="DI_FailDate" value="<%=DI_obj.DI_FailDate%>"/></li>
				<li>验证邀请码页面显示(支持HTML代码)：<textarea name="DI_Readme" rows="5" cols="50"><%=server.HTMLEncode(DI_obj.DI_Readme)%></textarea></li>
			</fieldset>
			</td>
		</tr>
		<tr>
			<td class="tablebody1">
				<input type="submit" value="点击提交"/>&nbsp;<input type="button" value="查看邀请码" onclick="window.location='Dv_Plus_Invite.asp'"/>
			</td>
		</tr>
	</table>
	</form>
	<%
End Sub
%>