<!--#include file="../../Conn.asp" -->
<!--#include file="../../Comm/Site_Class.asp" -->
<%
PagePath = "../../"

Set Qsite = New Site_Class

Qsite.SiteConfig_Master()
%>
<!--#include file="../Check_Login.asp" -->
<%
Qsite.Check_AdminFlag(arrAdminFlag(6 , 2))

Dim FriendLinkId
Dim FriendLinkTypeId,FriendLinkSiteName,FriendLinkSiteUrl
Dim FriendLinkLogoUrl,FriendLinkIsLogo,FriendLinkSiteNote,Px,yqljdq
Dim OldPx
Dim strFriendLinkSiteName,strFriendLinkTypeId,strPage

Set Rs = server.createobject("ADODB.Recordset")

If Request.Form <> "" Then
	FriendLinkId = Qsite.chgStr(Trim(Request.Form("FriendLinkId")))
	FriendLinkTypeId = Qsite.chgStr(Trim(Request.Form("FriendLinkTypeId")))
	FriendLinkSiteName = Qsite.chgStr(Trim(Request.Form("FriendLinkSiteName")))
	FriendLinkSiteUrl = Qsite.chgStr(Trim(Request.Form("FriendLinkSiteUrl")))
	FriendLinkLogoUrl = Qsite.chgStr(Trim(Request.Form("FriendLinkLogoUrl")))
	FriendLinkIsLogo = Qsite.chgStr(Trim(Request.Form("FriendLinkIsLogo")))
	Px = Qsite.chgStr(Trim(Request.Form("Px")))
	FriendLinkSiteNote = Qsite.chgStr(Trim(Request.Form("FriendLinkSiteNote")))
	strFriendLinkSiteName = Qsite.chgStr(Trim(Request.Form("strFriendLinkSiteName")))
	strFriendLinkTypeId = Qsite.chgStr(Trim(Request.Form("strFriendLinkTypeId")))
	strPage = Qsite.chgStr(Trim(Request.Form("strPage")))
	if Qsite.gerenvipkg=1 then
	yqljdq = Qsite.Sitediqu
	else
	yqljdq = Qsite.chgStr(Trim(Request.Form("yqljdq")))
	end if
	
	If FriendLinkId = "" Or IsNull(FriendLinkId) Or Not IsNumeric(FriendLinkId) Then FriendLinkId = 0
	
	If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then
		Qsite.ShowMsg "请选择分类" , ""
	End If
	If FriendLinkSiteName = "" Or IsNull(FriendLinkSiteName) Then
		Qsite.ShowMsg "请输入网站名称" , ""
	End If
	If FriendLinkSiteUrl = "" Or IsNull(FriendLinkSiteUrl) Then
		Qsite.ShowMsg "请输入网站地址" , ""
	End If
	If InStr(FriendLinkSiteUrl , "http://") <= 0 Then
		Qsite.ShowMsg "输入的网站地址不正确，请返回重新输入" , ""
	End If
	If FriendLinkSiteNote = "" Or IsNull(FriendLinkSiteNote) Then
		Qsite.ShowMsg "请输入网站说明" , ""
	End If
	If FriendLinkIsLogo <> "1" Then FriendLinkIsLogo = "0"
	If Px = "" Or IsNull(Px) Or Not IsNumeric(Px) Then Px = 1
	
	Rs.Open "select FriendLinkSiteName from FriendLinkTbl where FriendLinkSiteName = '"& FriendLinkSiteName &"' and FriendLinkId <> "& FriendLinkId , Conn , 1 , 1
	If Not Rs.Eof Then
		Qsite.ShowMsg "此网站已经存在，请返回重新输入" , ""
	End If
	Rs.Close
	
	Rs.Open "select Px from FriendLinkTbl where FriendLinkId = "& FriendLinkId , Conn , 1 , 1
	If Rs.Eof Then
		OldPx = Px
	Else
		OldPx = Rs("Px")
	End If
	Rs.Close
	
	If Cint(Px) > Cint(OldPx) Then
		Conn.Execute("update FriendLinkTbl set Px = Px - 1 where Px > "& OldPx &" and Px <= "& Px)
	ElseIf Cint(Px) < Cint(OldPx) Then
		Conn.Execute("update FriendLinkTbl set Px = Px + 1 where Px >= "& Px &" and Px < "& OldPx)
	End If
	
	Conn.Execute("update FriendLinkTbl set FriendLinkSiteName = '"& FriendLinkSiteName &"' , FriendLinkSiteUrl = '"& FriendLinkSiteUrl &"' , FriendLinkLogoUrl = '"& FriendLinkLogoUrl &"' , FriendLinkIsLogo = '"& FriendLinkIsLogo &"' , FriendLinkSiteNote = '"& FriendLinkSiteNote &"' , FriendLinkTypeId = "& FriendLinkTypeId &" , Px = "& Px &"  , yqljdq = '"& yqljdq &"' where FriendLinkId = "& FriendLinkId)
	
	Qsite.ShowMsg "友情链接修改成功" , "FriendLink_List.asp?FriendLinkSiteName="& strFriendLinkSiteName &"&FriendLinkTypeId="& strFriendLinkTypeId & "&page="& strPage
End If

FriendLinkId = Qsite.chgStr(Trim(Request.QueryString("FriendLinkId")))
strFriendLinkSiteName = Qsite.chgStr(Trim(Request.QueryString("strFriendLinkSiteName")))
strFriendLinkTypeId = Qsite.chgStr(Trim(Request.QueryString("strFriendLinkTypeId")))
strPage = Qsite.chgStr(Trim(Request.QueryString("strPage")))

If FriendLinkId = "" Or IsNull(FriendLinkId) Or Not IsNumeric(FriendLinkId) Then FriendLinkId = 0

Rs.Open "select * from FriendLinkTbl where FriendLinkId = "& FriendLinkId , Conn , 1 , 1
If Rs.Eof Then
	Qsite.ShowMsg "非法参数" , ""
Else
	FriendLinkTypeId = Rs("FriendLinkTypeId")
	FriendLinkSiteName = Rs("FriendLinkSiteName")
	FriendLinkSiteUrl = Rs("FriendLinkSiteUrl")
	FriendLinkLogoUrl = Rs("FriendLinkLogoUrl")
	FriendLinkIsLogo = Rs("FriendLinkIsLogo")
	FriendLinkSiteNote = Rs("FriendLinkSiteNote")
	Px = Rs("Px")
	yqljdq = Rs("yqljdq")
End If
Rs.Close
%>
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
<title>友情链接管理</title>
<link href="../skins/css/main.css" rel="stylesheet" type="text/css">
<script language="javascript">
function CheckForm()
{
	if(PosForm.FriendLinkTypeId.value == "")
	{
		alert("请选择分类");
		return false;
	}
	if(PosForm.FriendLinkSiteName.value == "")
	{
		alert("请输入网站名称");
		PosForm.FriendLinkSiteName.focus();
		return false;
	}
	if(PosForm.FriendLinkSiteUrl.value == "")
	{
		alert("请输入网站地址");
		PosForm.FriendLinkSiteUrl.focus();
		return false;
	}
	if(PosForm.FriendLinkSiteNote.value == "")
	{
		alert("请输入网站说明");
		PosForm.FriendLinkSiteNote.focus();
		return false;
	}
	return true;
}

function UploadLogo()
{
	window.open('Upload_Logo.asp','Upload','width=380px,height=90px,left=300,top=300');
} 
</script>
</head>

<body>
<form name="PosForm" method="post" action="FriendLink_Edit.asp" style="margin:0" onSubmit="return CheckForm();">
	<table width="100%" border="0" cellspacing="1" cellpadding="5">
		<tr>
			<td height="25" colspan="2" class="td_title">友情链接修改
			<input name="FriendLinkId" type="hidden" id="FriendLinkId" value="<%=FriendLinkId%>">
			<input name="strFriendLinkSiteName" type="hidden" id="strFriendLinkSiteName" value="<%=strFriendLinkSiteName%>">
			<input name="strFriendLinkTypeId" type="hidden" id="strFriendLinkTypeId" value="<%=strFriendLinkTypeId%>">
			<input name="strPage" type="hidden" id="strPage" value="<%=strPage%>"></td>
		</tr>
		<tr>
			<td width="18%" height="23" class="td_content">友情链接分类：</td>
			<td width="82%" height="23" class="td_content"><select name="FriendLinkTypeId" class="input_1" id="FriendLinkTypeId">
				<option value="">请选择分类</option>
				<%
				Rs.Open "select * from FriendLinkTypeTbl order by Px asc" , Conn , 1 , 1
				Do While Not Rs.Eof
					Response.Write("<option value="""& Rs("FriendLinkTypeId") &""" ")
					If Cstr(Rs("FriendLinkTypeId")) = Cstr(FriendLinkTypeId) Then Response.Write("selected")
					Response.Write(">"& Rs("FriendLinkTypeName") &"</option>")
				Rs.MoveNext
				Loop
				Rs.Close
				%>
			</select>			</td>
		</tr>
		<tr>
			<td height="23" class="td_content">网站名称：</td>
			<td height="23" class="td_content"><input name="FriendLinkSiteName" type="text" class="input_1" id="FriendLinkSiteName" value="<%=FriendLinkSiteName%>" size="40"></td>
		</tr>
		<tr>
			<td height="23" class="td_content">网站地址：</td>
			<td height="23" class="td_content"><input name="FriendLinkSiteUrl" type="text" class="input_1" id="FriendLinkSiteUrl" value="<%=FriendLinkSiteUrl%>" size="60"></td>
		</tr>
		<tr>
			<td height="23" class="td_content">Logo地址：</td>
			<td height="23" class="td_content"><input name="FriendLinkLogoUrl" type="text" class="input_1" id="FriendLinkLogoUrl" value="<%=FriendLinkLogoUrl%>" size="60">
			<input name="Btn" type="button" class="input_1" id="Btn" value="上传Logo" onClick="UploadLogo();"></td>
		</tr>
		<tr>
			<td height="23" class="td_content">Logo显示：</td>
			<td height="23" class="td_content"><input name="FriendLinkIsLogo" type="checkbox" id="FriendLinkIsLogo" value="1" <%If FriendLinkIsLogo = "1" Then Response.Write("checked")%>></td>
		</tr>
		<tr>
			<td height="23" class="td_content">排序：</td>
			<td height="23" class="td_content"><input name="Px" type="text" class="input_1" id="Px" value="<%=Px%>" size="5"></td>
		</tr>
		<%if Qsite.fenzhankg=1 then%><%else%>
		<tr>
          <td height="23" class="td_content">地区选择：</td>
		  <td height="23" colspan="2" class="td_content">
		  <select name="yqljdq" id="yqljdq" style="width:150px; line-height:17px; border:#04477C 1px solid;">
		                <option value="<%=yqljdq%>"><%=yqljdq%></option>
						<option value="<%=Qsite.Sitediqu%>"><%=Qsite.Sitediqu%></option>
						<%
						Rs.Open "select * from myrcwwfenzhan where fenzhankg='1' order by fenzhanid asc" , Conn , 1 , 1
						Do While Not Rs.Eof%>
						<option value="<%=rs("fenzhandq")%>"><%=rs("fenzhandq")%></option>
						<%Rs.MoveNext
						Loop
						Rs.Close
						%>
		</select>
		  </td>
	  </tr>
	  <%end if%>
		<tr>
			<td height="23" class="td_content">网站说明：</td>
			<td height="23" class="td_content"><textarea name="FriendLinkSiteNote" cols="60" rows="8" class="input_1" id="FriendLinkSiteNote"><%=FriendLinkSiteNote%></textarea></td>
		</tr>
		<tr>
			<td height="23" class="td_content">&nbsp;</td>
			<td height="23" class="td_content"><input name="Submit" type="submit" class="input_1" value=" 修  改 "> &nbsp;
			<input name="Submit" type="reset" class="input_1" value=" 重  置 "> &nbsp;
			<input name="Submit" type="button" class="input_1" value=" 返  回 " onClick="javascript:history.back();"></td>
		</tr>
	</table>
</form>
</body>
</html>
