<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


'打开用户表，并检测用户是否登陆
Call OpenMemberexe("","login",0)

Call WebStyleTop("")

response.write(_
"<script language='javascript' type='text/javascript' src='js/inc/list.js'></script>"&_
"<script language='javascript' type='text/javascript' src='js/collType.js'></script>"&_
"")



%><!-- #include file="collData/conobj.asp" --><%


Select Case mudi
	Case "add","rev"
		Call IsSecMenuRight("alertBack",157,dataType)
		Call AddOrRev()

	Case "manage"
		Call IsSecMenuRight("alertBack",158,dataType)
		Call manage()

End Select


Call CloseCollObj()

Call CloseMemberexe()
Call CloseConobj()
Call WebStyleBottom()











'新增、修改信息
Private Sub AddOrRev()
	dataMode	= Trim(request.querystring("dataMode"))
	dataModeStr	= Trim(request.querystring("dataModeStr"))
	backURL		= Trim(request.querystring("backURL"))


	If mudi="rev" Then
		dataID=ToGetInt("dataID")
		Set revexe=collObj.execute("select top 1 * from OT_collType where CT_ID="& dataID)
			If revexe.EOF Then
				JS_AlertBackEnd("无该记录！")
			End If
		CT_ID			= revexe("CT_ID")
		CT_theme		= revexe("CT_theme")
		CT_imgPath		= revexe("CT_imgPath")
		CT_imgMaxSize	= revexe("CT_imgMaxSize")
		CT_saveFileType	= revexe("CT_saveFileType")
		CT_overtime		= revexe("CT_overtime")
		CT_rank			= revexe("CT_rank")
		CT_state		= revexe("CT_state")
		mudiCN="修改"
	Else
		CT_imgMaxSize	= 500
		CT_saveFileType	= "rar|zip"
		CT_overtime		= 60
		CT_rank			= ToInt(collObj.execute("select max(CT_rank) from OT_collType")(0))+10
		CT_state		= 1
		mudiCN="添加"
	End If



	response.write(_
	"<form name='addform' method='post' action='collType_deal.asp?mudi="& mudi &"&nohrefStr=close' onsubmit='return CheckForm()'>"&_
	"<input type='hidden' id='dataID' name='dataID' value='"& dataID &"' />"&_
	"<input type='hidden' id='dataTypeCN' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
	"<input type='hidden' id='dataType' name='dataType' value='"& dataType &"' />"&_
	"<input type='hidden' id='dataMode' name='dataMode' value='"& dataMode &"' />"&_
	"<input type='hidden' id='dataModeStr' name='dataModeStr' value='"& dataModeStr &"' />"&_
	"")
		If backURL<>"" Then
			response.write(_
			"<input type='hidden' id='backURL' name='backURL' value='"& backURL &"' />")
		Else
			response.write(_
			"<script language='javascript' type='text/javascript'>document.write(""<input type='hidden' id='backURL' name='backURL' value='""+ document.location.href +""' />"")</script>")
		End If

	Call ShareTableTop("share_"& mudi &".gif","",mudiCN & dataTypeCN)
		response.write(_
		"<table width='98%' align='center' cellpadding='0' cellspacing='0' summary='' class='padd3'>"&_
		"<tr>"&_
			"<td width='20%'></td>"&_
			"<td width='80%'></td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>"& IsWriteRepSign(true) &"模块名称：</td>"&_
			"<td align='left'><input type='text' id='theme' name='theme' size='50' style='width:400px;' value="""& CT_theme &""" /></td>"&_
		"</tr>"&_
		"<!-- <tr>"&_
			"<td align='right'>保存目录：</td>"&_
			"<td align='left'>"&_
				InfoImgCollAdminDir &_
				"<input type='text' id='imgPath' name='imgPath' size='50' style='width:100px;' value="""& CT_imgPath &""" />&nbsp;"&_
				"<span class='font2_2'>&nbsp;多个用竖杆“|”隔开</span>"&_
			"</td>"&_
		"</tr> -->"&_
		"<!-- <tr>"&_
			"<td align='right'>采集保存非图片文件类型：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='saveFileType' name='saveFileType' size='50' style='width:400px;' value="""& CT_saveFileType &""" />&nbsp;"&_
			"</td>"&_
		"</tr> -->"&_
		"<tr>"&_
			"<td align='right'>允许下载的大小：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='imgMaxSize' name='imgMaxSize' size='50' style='width:60px;' value="""& CT_imgMaxSize &""" />&nbsp;KB"&_
				"<span class='font2_2'>&nbsp;为空或为0，均表示不限制</span>"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>采集超时设置：</td>"&_
			"<td align='left'><input type='text' id='overtime' name='overtime' size='50' style='width:30px;' value="""& CT_overtime &""" />&nbsp;秒</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>排序：</td>"&_
			"<td align='left'><input type='text' id='rank' name='rank' size='50' style='width:30px;' value="""& CT_rank &""" /></td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>状态：</td>"&_
			"<td align='left'>"&_
				"<label><input type='radio' name='state' value='1' "& IsChecked(CT_state,1) &" />显示</label>&nbsp;&nbsp;&nbsp;&nbsp;"&_
				"<label><input type='radio' name='state' value='0' "& IsChecked(CT_state,0) &" />隐藏</label>&nbsp;&nbsp;&nbsp;&nbsp;"&_
			"</td>"&_
		"</tr>"&_
		"</table>"&_
		"")
	Call ShareTableBottom()


	response.write(_
	"<table style='height:16px;' cellpadding='0' cellspacing='0' summary=''><tr><td></td></tr></table>"&_

	"<center><input type='image' src='images/button_"& mudi &".gif' /></center>"&_

	"</form>"&_
	"")

End Sub





Private Sub manage()

	Call ShareTableTop2("share_list.gif","",""& dataTypeCN &"管理")
	Call ShareTableItemTitle("6%,37%,15%,12%,8%,8%,12%","序号,模块名称,下载的图片大小,超时时间,排序,状态,修改　删除")

	Set showrec=server.CreateObject("ADODB.recordset")
	showrec.open "select * from OT_collType order by CT_rank ASC",collObj,3,1

	If Not showrec.EOF Then
		showPageSize=memberexe("MB_itemNum")		'每页条数
		showrec.pagesize=showPageSize	
		showPageCount=showrec.pagecount
		showCount=showrec.recordcount
		page=ToGetInt("page")
		if page < 1 Or page > showPageCount Then page=1
		showrec.absolutepage=page


		response.write(_
		"<tbody class='tabBody padd3'>"&_
		"")
		number=1+(page-1)*showPageSize
		For i=1 To showPageSize
			If showrec.EOF Then Exit For
			If i Mod 2 = 0 Then bgcolor="class='tabColorTr'" Else bgcolor=""

	response.write(_
	"<tr id='data"& showrec("CT_ID") &"' "& bgcolor &">"&_
		"<td align='center'>"& showrec("CT_ID") &"</td>"&_
		"<td align='left'>"& showrec("CT_theme") &"<br /></td>"&_
		"<td align='center'>"& showrec("CT_imgMaxSize") &"&nbsp;KB<br /></td>"&_
		"<td align='center'>"& showrec("CT_overtime") &"&nbsp;秒<br /></td>"&_
		"<td align='center'>"& showrec("CT_rank") &"<br /></td>"&_
		"<td align='center'>"& ItemSwitchColl("OT_collType",showrec("CT_ID"),showrec("CT_state"),"state","") &"<br /></td>"&_
		"<td align='center'>"&_
			"<img src='images/img_rev.gif' class='pointer' onclick=""document.location.href='collType.asp?mudi=rev&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&dataID="& showrec("CT_ID") &"&backURL='+ escape(document.location.href)"" alt='修改' />&nbsp;&nbsp;"&_
			"<img src='images/img_del.gif' class='pointer' onclick=""if(confirm('确定删除？')==true){DataDeal.location.href='collType_deal.asp?mudi=del&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&theme="& server.URLEncode(showrec("CT_theme")) &"&dataID="& showrec("CT_ID") &"'}"" alt='删除' />"&_
		"</td>"&_
	"</tr>"&_
	"")
		showrec.movenext
		number=number+1
		Next
		response.write(_
		"</tbody>"&_
		"")
	End If
	showrec.close
	Set showrec=Nothing

	Call ShareTableBottom2()

End Sub

%>