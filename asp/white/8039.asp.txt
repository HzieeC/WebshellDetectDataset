<!--#include file="conn.asp"--><!--#include file="inc/fun.asp"-->
<link rel="stylesheet" type="text/css" href="inc/css.css"><body leftmargin="0" topmargin="5">
<%
dim lguserid,uploadsize,uploadnum,uploadingnum,maxid,autotable,i,action,uploadtype,savepath,lgname,mymark
action=request("action")
if checknum(application(prefix&"canupload"))<>1 then response.end
lguserid=checknum(session(prefix&"lguserid"))
if isnull(lguserid) or lguserid="" then response.end

if action="headpic" then uploadtype="gif|jpg|bmp":savepath="headpic/upload"
if action="" then uploadtype=lcase(application(prefix&"uploadtype")):savepath="upload"

uploadsize=checknum(application(prefix&"uploadsize"))
uploadnum=checknum(application(prefix&"uploadnum"))
autotable=checknum(application(prefix&"autotable"))

uploadingnum=checknum(session(prefix&"uploadingnum"))
lgname=replace(request.cookies(prefix)("lgname"),"'","")

if uploadingnum>uploadnum-1 and action="" then
response.write "一次只能上传 <b>"&uploadingnum&"</b> 个文件！"
response.end
end if

mymark=conn.execute("select top 1 mark from [user] where userid="&lguserid&"")("mark")
if mymark<10 then
response.write"一次上传需要10点积分，您只有"&mymark&"点积分。"
response.end
end if
%> 

<script>function putinstrHTML(str){
var documentedit;
var browserthis=parent.HtmlGetBrowser();
		if (browserthis== 'IE') {
			documentedit = parent.document.frames("EditForm").document;
		} else {
			documentedit = parent.document.getElementById('EditForm').contentDocument;
		}

documentedit.body.innerHTML+=str;
}
function putinstrUBB(str){
	var documentedit=parent.document.getElementById('CodeForm');
	documentedit.value+=str;
}
</script>

<SCRIPT RUNAT=SERVER LANGUAGE=VBSCRIPT>

dim upfile_k6_Stream

Class upload_k6
  
dim Form,File,Version
  
Private Sub Class_Initialize 
		dim iStart,iFileNameStart,iFileNameEnd,iEnd,vbEnter,iFormStart,iFormEnd,theFile
		dim strDiv,mFormName,mFormValue,mFileName,mFileSize,mFilePath,iDivLen,mStr
		Version=""
		if Request.TotalBytes<1 then Exit Sub
		set Form=CreateObject("Scripting.Dictionary")
		set File=CreateObject("Scripting.Dictionary")
		set upfile_k6_Stream=CreateObject("Adodb.Stream")
		upfile_k6_Stream.mode=3
		upfile_k6_Stream.type=1
		upfile_k6_Stream.open
		upfile_k6_Stream.write Request.BinaryRead(Request.TotalBytes)
		
		vbEnter=Chr(13)&Chr(10)
		iDivLen=inString(1,vbEnter)+1
		strDiv=subString(1,iDivLen)
		iFormStart=iDivLen
		iFormEnd=inString(iformStart,strDiv)-1
		while iFormStart < iFormEnd
		  iStart=inString(iFormStart,"name=""")
		  iEnd=inString(iStart+6,"""")
		  mFormName=subString(iStart+6,iEnd-iStart-6)
		  iFileNameStart=inString(iEnd+1,"filename=""")
		  if iFileNameStart>0 and iFileNameStart<iFormEnd then
		   iFileNameEnd=inString(iFileNameStart+10,"""")
		   mFileName=subString(iFileNameStart+10,iFileNameEnd-iFileNameStart-10)
		   iStart=inString(iFileNameEnd+1,vbEnter&vbEnter)
		   iEnd=inString(iStart+4,vbEnter&strDiv)
		   if iEnd>iStart then
			mFileSize=iEnd-iStart-4
		   else
			mFileSize=0
		   end if
		   set theFile=new FileInfo
		   theFile.FileName=getFileName(mFileName)
		   theFile.FilePath=getFilePath(mFileName)
		   theFile.FileSize=mFileSize
		   theFile.FileStart=iStart+4
		   theFile.FormName=FormName
		   file.add mFormName,theFile
		  else
		   iStart=inString(iEnd+1,vbEnter&vbEnter)
		   iEnd=inString(iStart+4,vbEnter&strDiv)
		
		   if iEnd>iStart then
			mFormValue=subString(iStart+4,iEnd-iStart-4)
		   else
			mFormValue="" 
		   end if
		   form.Add mFormName,mFormValue
		  end if
		
		  iFormStart=iformEnd+iDivLen
		  iFormEnd=inString(iformStart,strDiv)-1
		wend
End Sub

Private Function subString(theStart,theLen)
 dim i,c,stemp
 upfile_k6_Stream.Position=theStart-1
 stemp=""
 for i=1 to theLen
   if upfile_k6_Stream.EOS then Exit for
   c=ascB(upfile_k6_Stream.Read(1))
   If c > 127 Then
    if upfile_k6_Stream.EOS then Exit for
    stemp=stemp&Chr(AscW(ChrB(AscB(upfile_k6_Stream.Read(1)))&ChrB(c)))
    i=i+1
   else
    stemp=stemp&Chr(c)
   End If
 Next
 subString=stemp
End function


Private Function inString(theStart,varStr)
 dim i,j,bt,theLen,str
 InString=0
 Str=toByte(varStr)
 theLen=LenB(Str)
 for i=theStart to upfile_k6_Stream.Size-theLen
   if i>upfile_k6_Stream.size then exit Function
   upfile_k6_Stream.Position=i-1
   if AscB(upfile_k6_Stream.Read(1))=AscB(midB(Str,1)) then
    InString=i
    for j=2 to theLen
      if upfile_k6_Stream.EOS then 
        inString=0
        Exit for
      end if
      if AscB(upfile_k6_Stream.Read(1))<>AscB(MidB(Str,j,1)) then
        InString=0
        Exit For
      end if
    next
    if InString<>0 then Exit Function
   end if
 next
End Function


Private Sub Class_Terminate  
  form.RemoveAll
  file.RemoveAll
  set form=nothing
  set file=nothing
  upfile_k6_Stream.close
  set upfile_k6_Stream=nothing
End Sub
   
 
 Private function GetFilePath(FullPath)
  If FullPath <> "" Then
   GetFilePath = left(FullPath,InStrRev(FullPath, "\"))
  Else
   GetFilePath = ""
  End If
 End  function
 
 Private function GetFileName(FullPath)
  If FullPath <> "" Then
   GetFileName = mid(FullPath,InStrRev(FullPath, "\")+1)
  Else
   GetFileName = ""
  End If
 End  function

 Private function toByte(Str)
   dim i,iCode,c,iLow,iHigh
   toByte=""
   For i=1 To Len(Str)
   c=mid(Str,i,1)
   iCode =Asc(c)
   If iCode<0 Then iCode = iCode + 65535
   If iCode>255 Then
     iLow = Left(Hex(Asc(c)),2)
     iHigh =Right(Hex(Asc(c)),2)
     toByte = toByte & chrB("&H"&iLow) & chrB("&H"&iHigh)
   Else
     toByte = toByte & chrB(AscB(c))
   End If
   Next
 End function
End Class


Class FileInfo
  dim FormName,FileName,FilePath,FileSize,FileStart
  Private Sub Class_Initialize 
    FileName = ""
    FilePath = ""
    FileSize = 0
    FileStart= 0
    FormName = ""
  End Sub
  
 Public function SaveAs(FullPath)
    dim dr,ErrorChar,i
    SaveAs=1
    if trim(fullpath)="" or FileSize=0 or FileStart=0 or FileName="" then exit function
    if FileStart=0 or right(fullpath,1)="/" then exit function
    set dr=CreateObject("Adodb.Stream")
    dr.Mode=3
    dr.Type=1
    dr.Open
    upfile_k6_Stream.position=FileStart-1
    upfile_k6_Stream.copyto dr,FileSize
    dr.SaveToFile FullPath,2
    dr.Close
    set dr=nothing 
    SaveAs=0
  end function
End Class
</SCRIPT>
<%
dim upNum
dim uploadsuc
dim Forumupload
dim ranNum
dim uploadfiletype
dim upload,file,formName,iCount,filename,fileExt
call upload_0()
sub upload_0()
set upload=new upload_k6 ''建立上传对象

response.write "<body leftmargin=5 topmargin=3>"

iCount=0
for each formName in upload.file ''列出所有上传了的文件
 set file=upload.file(formName)  ''生成一个文件对象
 if file.filesize<100 then
 	response.write "请先选择您要上传的文件　[ <a href=# onclick=history.go(-1)>重新上传</a> ]"
	response.end
 end if
 	
 if file.filesize>uploadsize*1000 then
 	response.write "文件大小超过了限制 "&uploadsize&" K　[ <a href=# onclick=history.go(-1)>重新上传</a> ]"
	response.end
 end if

dim sfileExt
 sfileExt=split(file.filename,".")
 fileExt="."&sfileExt(ubound(sfileExt))
 fileExt=lcase(fileExt)
 uploadsuc=false
 Forumupload=split(uploadtype,"|")
 for i=0 to ubound(Forumupload)
	if fileEXT="."&trim(Forumupload(i)) then
	uploadsuc=true
	exit for
	else
	uploadsuc=false
	end if
 next
 '添加禁止上传脚本类型 2005.1.1 lamking
if fileExt=".asp" or fileExt=".php" or fileExt=".jsp" or fileExt=".cer" or fileExt=".htr"  or fileExt=".cdx" or fileExt=".shtml" then uploadsuc=false
 '结束
 if uploadsuc=false then
 	response.write "文件格式不正确　[ <a href=# onclick=history.go(-1)>重新上传</a> ]"
	response.end
 end if
 
 filename=ctime&"-"&lguserid&fileExt
 if action="headpic" then filename=lguserid&fileExt
 if file.FileSize>0 then         ''如果 FileSize > 0 说明有文件数据
  file.SaveAs Server.mappath(savepath&"/"&FileName)   ''保存文件
	for i=0 to ubound(Forumupload)
		if fileEXT="."&trim(Forumupload(i)) then
		if action="" then conn.execute("insert into upload(bbsid,filename,filetype,uploadtime,name,totable) values(0,'"&filename&"','"&fileExt&"',now()+"&timeset&"/24,'"&lgname&"',0)"):conn.execute("update [user] set mark=mark-10 where userid="&lguserid&""):response.write "<script>var pstr='[upload="&Forumupload(i)&"]"&FileName&"[/upload]';putinstrHTML(pstr);putinstrUBB(pstr);</script>"
		if action="headpic" then response.write "<script>parent.form.mypic.value='headpic/upload/"&FileName&"'</script>"
		exit for
		end if
	next
 iCount=iCount+1
 end if
 set file=nothing
next
set upload=nothing
Htmend iCount&" 个文件上传结束!"
end sub


sub HtmEnd(Msg)
if action="" then
session(prefix&"uploadingnum")=uploadingnum+1
response.write "<b>"&session(prefix&"uploadingnum")&"</b> 文件上传成功,请copy下边的文件连接,以备后用！>><a href=upload.asp>继续上传</a>"
response.end
elseif action="headpic" then
response.write"上传成功，<a href=upload.asp?action=headpic>重新上传</a>。"
end if
end sub
function ctime 
  ctime = now()
  ctime = replace(ctime,"-","")
  ctime = replace(ctime," ","") 
  ctime = replace(ctime,":","")
  ctime = replace(ctime,"/","")
  ctime = replace(ctime,"\","")
  ctime = replace(ctime,"PM","")
  ctime = replace(ctime,"AM","")
  ctime = replace(ctime,"上午","")
  ctime = replace(ctime,"下午","")
end function
%></body></html>
