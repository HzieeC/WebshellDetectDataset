<%
'****************************************************
' 零柒。蜀工作室网站管理系统 Power by 07ing.com
' Web: http://www.07ing.net, http://www.07ing.com
' Copyright (C) 2006 零柒。蜀工作室 All Rights Reserved.
'****************************************************
function UBB(Content)
	Content = HTMLEncode(Content)
	dim re
	Set re=new RegExp
	re.IgnoreCase =true
	re.Global=True
	re.Pattern="(javascript)"
	Content=re.Replace(Content,"&#106avascript")
	re.Pattern="(jscript:)"
	Content=re.Replace(Content,"&#106script:")
	re.Pattern="(js:)"
	Content=re.Replace(Content,"&#106s:")
	re.Pattern="(value)"
	Content=re.Replace(Content,"&#118alue")
	re.Pattern="(about:)"
	Content=re.Replace(Content,"about&#58")
	re.Pattern="(file:)"
	Content=re.Replace(Content,"file&#58")
	re.Pattern="(document.cookie)"
	Content=re.Replace(Content,"documents&#46cookie")
	re.Pattern="(vbscript:)"
	Content=re.Replace(Content,"&#118bscript:")
	re.Pattern="(vbs:)"
	Content=re.Replace(Content,"&#118bs:")
	re.Pattern="(on(mouse|exit|error|click|key))"
	Content=re.Replace(Content,"&#111n$2")

	re.Pattern = "\[IMG\](http|https|ftp)://(.[^\[]*)\[\/IMG\]"
	Content = re.Replace(Content,"相关图片:<br><A onfocus=""this.blur()"" href=""$1://$2"" TARGET=_blank><IMG SRC=""$1://$2"" border=""0"" onload=""javascript:if(this.width>screen.width-380){this.width=screen.width-380;}"" onmousewheel=""return img(this)""></A><br>")

	're.Pattern="\[QT=*([0-9]*),*([0-9]*)\](.[^\[]*)\[\/QT]"
	'Content=re.Replace(Content,"<embed src=$3 width=$1 height=$2 autoplay=true loop=false controller=true playeveryframe=false cache=false scale=TOFIT bgcolor=#000000 kioskmode=false targetcache=false pluginspage=http://www.apple.com/quicktime/>")

	're.Pattern="\[MP=*([0-9]*),*([0-9]*)\](.[^\[]*)\[\/MP]"
	'Content=re.Replace(Content,"<object align=middle classid=CLSID:22d6f312-b0f6-11d0-94ab-0080c74c7e95 class=OBJECT id=MediaPlayer width=$1 height=$2 ><param name=ShowStatusBar value=-1><param name=Filename value=$3><embed type=application/x-oleobject codebase=http://activex.microsoft.com/activex/controls/mplayer/en/nsmp2inf.cab#Version=5,1,52,701 flename=mp src=$3 width=$1 height=$2></embed></object>")

	're.Pattern="\[RM=*([0-9]*),*([0-9]*)\](.[^\[]*)\[\/RM]"
	'Content=re.Replace(Content,"<OBJECT classid=clsid:CFCDAA03-8BE4-11cf-B84B-0020AFBBCCFA class=OBJECT id=RAOCX width=$1 height=$2><PARAM NAME=SRC VALUE=$3><PARAM NAME=CONSOLE VALUE=Clip1><PARAM NAME=CONTROLS VALUE=imagewindow><PARAM NAME=AUTOSTART VALUE=-1></OBJECT><br><OBJECT classid=CLSID:CFCDAA03-8BE4-11CF-B84B-0020AFBBCCFA height=60 id=video2 width=$1><PARAM NAME=SRC VALUE=$3><PARAM NAME=_ExtentX VALUE=10054><PARAM NAME=_ExtentY VALUE=1588><PARAM NAME=AUTOSTART VALUE=-1><PARAM NAME=CONTROLS value=ControlPanel,StatusBar><PARAM NAME=CONSOLE VALUE=Clip1></OBJECT>")

	re.Pattern="(\[FLASH\])(.[^\[]*)(\[\/FLASH\])"
	Content= re.Replace(Content,"<IMG SRC=Img/swf.gif border=0><A TARGET=_blank HREF=$2>[全屏欣赏该FLASH]</A>")

	're.Pattern="(\[FLASH\])(.[^\[]*)(\[\/FLASH\])"
	'Content= re.Replace(Content,"<IMG SRC=Img/swf.gif border=0><a href=$4 TARGET=_blank>[全屏欣赏FLASH]</a><br><OBJECT codeBase=http://download.macromedia.com/pub/shockwave/cabs/flash/swflash.cab#version=4,0,2,0 classid=clsid:D27CDB6E-AE6D-11cf-96B8-444553540000><PARAM NAME=movie VALUE=$2><PARAM NAME=quality VALUE=high><embed src=$2 quality=high pluginspage='http://www.macromedia.com/shockwave/download/index.cgi?P1_Prod_Version=ShockwaveFlash' type='application/x-shockwave-flash'>$2</embed></OBJECT>")

	're.Pattern="(\[FLASH=*([0-9]*),*([0-9]*)\])(.[^\[]*)(\[\/FLASH\])"
	'Content= re.Replace(Content,"<IMG SRC=Img/swf.gif border=0><a href=$4 TARGET=_blank>[全屏欣赏FLASH]</a><br><OBJECT codeBase=http://download.macromedia.com/pub/shockwave/cabs/flash/swflash.cab#version=4,0,2,0 classid=clsid:D27CDB6E-AE6D-11cf-96B8-444553540000 width=$2 height=$3><PARAM NAME=movie VALUE=$4><PARAM NAME=quality VALUE=high><embed src=$4 quality=high pluginspage='http://www.macromedia.com/shockwave/download/index.cgi?P1_Prod_Version=ShockwaveFlash' type='application/x-shockwave-flash' width=$2 height=$3>$4</embed></OBJECT>")

	re.Pattern="(\[SOUND\])(.[^\[]*)(\[\/SOUND\])" 
	Content=re.Replace(Content,"<IMG SRC=Images/File/mid.gif border=0><a href=$2 target=_blank>[附背景音乐]</a><bgsound src=$2 loop='-1'>")

	re.Pattern="(\[URL\])(.[^\[]*)(\[\/URL\])"
	Content= re.Replace(Content,"<A HREF=$2 TARGET=_blank>$2</A>")

	re.Pattern="(\[URL=(.[^\[]*)\])(.[^\[]*)(\[\/URL\])"
	Content= re.Replace(Content,"<A HREF=$2 TARGET=_blank>$3</A>")
	
	re.Pattern="(\[EMAIL\])(\S+\@.[^\[]*)(\[\/EMAIL\])"
	Content= re.Replace(Content,"<A HREF=mailto:$2>$2</A>")

	re.Pattern="(\[EMAIL=(\S+\@.[^\[]*)\])(.[^\[]*)(\[\/EMAIL\])"
	Content= re.Replace(Content,"<A target=_blank HREF=mailto:$2>$3</A>")

	re.Pattern = "^((http|https|ftp|rtsp|mms):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+([^<>""])+)"
	Content = re.Replace(Content,"<a target=_blank href=$1>$1</a>")

	re.Pattern = "((http|https|ftp|rtsp|mms):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+)$([^\[]*)"
	Content = re.Replace(Content,"<a target=_blank href=$1>$1</a>")

	re.Pattern = "([^>=""])((http|https|ftp|rtsp|mms):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+)"
	Content = re.Replace(Content,"$1<a target=_blank href=$2>$2</a>")

	re.Pattern = "(^[(http://|http:\\)])((www|cn)[.](\w)+[.]{1,}(net|com|cn|org|cc)(((\/[\~]*|\\[\~]*)(\w)+)|[.](\w)+)*(((([?](\w)+){1}[=]*))*((\w)+){1}([\&](\w)+[\=](\w)+)*)*)"
	Content = re.Replace(Content,"<a target=_blank href=http://$2>$2</a>")

	're.Pattern = "([^(=)])((\w)+[@]{1}((\w)+[.]){1,3}(\w)+)"
	'Content = re.Replace(Content,"<img align=absmiddle src=Images/File/mail.gif border=0><a target=_blank href='mailto:$2'>$2</a>")

	re.Pattern="\[HMTL\](.[^\[]*)\[\/HTML\]"
	Content=re.Replace(Content,"<table width='90%' border='0' cellspacing='0' cellpadding=6><TR><TD width='100%' bgColor='#f3f3f3' class='INDEX_TMALL'>[HTML]---------------------------------------------------<br>$1<br>-----------------------------------------------------[/HTML]</td></tr></table><br>")

	re.Pattern="\[CODE\](.[^\[]*)\[\/CODE\]"
	Content=re.Replace(Content,"<table width='90%' border='0' cellspacing='0' cellpadding=6><TR><TD width='100%' bgColor='#f3f3f3' class='INDEX_TMALL'>[CODE]---------------------------------------------------<br>$1<br>-----------------------------------------------------[/CODE]</td></tr></table><br>")

	re.Pattern="\[FACE=(.[^\[]*)\](.[^\[]*)\[\/FACE\]"
	Content=re.Replace(Content,"<FONT FACE=$1>$2</FONT>")

	re.Pattern="\[COLOR=(.[^\[]*)\](.[^\[]*)\[\/COLOR\]"
	Content=re.Replace(Content,"<FONT COLOR=$1>$2</FONT>")

	re.Pattern="\[ALIGN=(center|left|right)\](.[^\[]*)\[\/ALIGN\]"
	Content=re.Replace(Content,"<DIV ALIGN=$1>$2</DIV>")

	re.Pattern="\[QUOTE\](.*)\[\/QUOTE\]"
	Content=re.Replace(Content,"<table width='90%' border='0' cellspacing='0' cellpadding=6><TR><TD width='100%' bgColor='#f3f3f3' class='INDEX_TMALL'><font color=#006600 size=2>以下是引用文章</font>---------------------------------------------------<br>$1<br>----------------------------------------------------------------</td></tr></table><br>")

	re.Pattern="\[B\](.[^\[]*)(\[\/B\])"
	Content=re.Replace(Content,"<B>$1</B>")

	re.Pattern="\[I\](.[^\[]*)\[\/I\]"
	Content=re.Replace(Content,"<I>$1</I>")

	re.Pattern="\[U\](.[^\[]*)(\[\/U\])"
	Content=re.Replace(Content,"<U>$1</U>")

	re.Pattern="\[SIZE=([1-5])\](.[^\[]*)\[\/SIZE\]"
	Content=re.Replace(Content,"<FONT SIZE=$1>$2</FONT>")
	Content=replace(Content,"<I></I>","")

	if instr(lcase(Content),"[FACE")>0 then
		for FaceCount = 0 to 29
		Content=replace(lcase(Content),"[FACE"&FaceCount&"]","<img src=Face/FACE"&FaceCount&".gif border=0 align=middle>")
		next
	end if
	re.Pattern="(\[FACE(.[^\[]*)\])"
	Content= re.Replace(Content,"<img src=Face/FACE$2.gif border=0 align=middle>")

	set re=Nothing
	UBB=Content
end function

function cutstr(str,strlen,more,url)
if len(str)>strlen then
	 str=left(str,strlen) & "......"
end if
if (len(str)>strlen) and more then
  str=str+"&nbsp;&nbsp;&nbsp;[url="+url+"]点这里查看详情[/url]"
end if
cutstr=str
end function

function strLength(str)
       ON ERROR RESUME NEXT
       dim WINNT_CHINESE
       WINNT_CHINESE  = (len("花香")=2)
       if WINNT_CHINESE then
          dim l,t,c
          dim i
          l=len(str)
          t=l
          for i=1 to l
             c=asc(mid(str,i,1))
             if c<0 then c=c+65536
             if c>255 then
                t=t+1
             end if
          next
          strLength=t
       else 
          strLength=len(str)
       end if
       if err.number<>0 then err.clear
end function

function gotTopic(str,strlen)
	if str="" then
		gotTopic=""
		exit function
	end if
	dim l,t,c, i
	str=replace(replace(replace(replace(str,"&nbsp;"," "),"&quot;",chr(34)),"&gt;",">"),"&lt;","<")
	l=len(str)
	t=0
	for i=1 to l
		c=Abs(Asc(Mid(str,i,1)))
		if c>255 then
			t=t+2
		else
			t=t+1
		end if
		if t>=strlen then
			gotTopic=left(str,i) & ".."
			exit for
		else
			gotTopic=str
		end if
	next
	gotTopic=replace(replace(replace(replace(gotTopic," ","&nbsp;"),chr(34),"&quot;"),">","&gt;"),"<","&lt;")
end function

function AutoUrl(str)
on error resume next
Set url=new RegExp
url.IgnoreCase =True
url.Global=True
url.MultiLine = True
url.Pattern = "(^|[^==""])((http|https|ftp|rtsp|mms|pnm|mmst):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+([^<>""])+)"
str = url.Replace(str,"$1<img align=absmiddle src=/img/url.gif border=0><a target=_blank href=$2>$2</a>")
url.Pattern = "((http|https|ftp|rtsp|mms|pnm|mmst):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+([^<>""])+)$"
str = url.Replace(str,"<img align=absmiddle src=img/url.gif border=0><a target=_blank href=$1>$1</a>")
url.Pattern = "([^>=""])((http|https|ftp|rtsp|mms|pnm|mmst):(\/\/|\\\\)[A-Za-z0-9\./=\?%\-&_~`@[\]\':+!]+([^<>""])+)"
str = url.Replace(str,"$1<img align=absmiddle src=/img/url.gif border=0><a target=_blank href=$2>$2</a>")
url.Pattern = "([^(http://|http:\\)|^<>\@])((www|cn)[.](\w)+[.]{1,}(net|com|cn|org|cc)(((\/[\~]*|\\[\~]*)(\w)+)|[.](\w)+)*(((([?](\w)+){1}[=]*))*((\w)+){1}([\&](\w)+[\=](\w)+)*[^<>""]+)*)"
str = url.Replace(str,"$1<img align=absmiddle src=/img/url.gif border=0><a target=_blank href=http://$2>$2</a>")
set url=Nothing
    AutoUrl=str
end function

function isInteger(para)
       on error resume next
       dim str
       dim l,i
       if isNUll(para) then 
          isInteger=false
          exit function
       end if
       str=cstr(para)
       if trim(str)="" then
          isInteger=false
          exit function
       end if
       l=len(str)
       for i=1 to l
           if mid(str,i,1)>"9" or mid(str,i,1)<"0" then
              isInteger=false 
              exit function
           end if
       next
       isInteger=true
       if err.number<>0 then err.clear
end function

Function MultiPage(Numbers,Perpage,Curpage,Url_Add)
	CurPage=Int(Curpage)
	Dim URL
	URL=Request.ServerVariables("Script_Name")&Url_Add
	MultiPage=""
	Dim Page,Offset,PageI
	If Int(Numbers)>Int(PerPage) Then
		Page=10
		Offset=2
		Dim Pages,FromPage,ToPage
		If Numbers Mod Cint(Perpage)=0 Then
			Pages=Int(Numbers/Perpage)
		Else
			Pages=Int(Numbers/Perpage)+1
		End If
		FromPage=Curpage-Offset
		ToPage=Curpage+Page-Offset-1
		If Page>Pages Then
			FromPage=1
			ToPage=Pages
		Else
			If FromPage<1 Then
				Topage=Curpage+1-FromPage
				FromPage=1
				If (ToPage-FromPage)<Page And (ToPage-FromPage)<Pages Then ToPage=Page
			ElseIF Topage>Pages Then
				FromPage =Curpage-Pages +ToPage
				ToPage=Pages
				If (ToPage-FromPage)<Page And (ToPage-FromPage)<Pages Then FromPage=Pages-Page+1
			End If
		End If
                MultiPage="<a href="""&Url&"page=1"">首页</a> "
		If Curpage>1 Then 
                        MultiPage=MultiPage&"<a href="""&Url&"page="&Curpage-1&""">上一页</a> "
                Else
                        MultiPage=MultiPage&"上一页 "
                End If
		For PageI=FromPage TO ToPage
			If PageI<>CurPage Then
				MultiPage=MultiPage&"<a href="""&Url&"page="&PageI&""">"&PageI&"</a> "
			Else
				MultiPage=MultiPage&"<font color=red><b>"&PageI&"</b></font> "
			End If
		Next
		If Curpage<Pages Then 
                        MultiPage=MultiPage&"<a href="""&Url&"page="&Curpage+1&""">下一页</a> "
                Else
                        MultiPage=MultiPage&"下一页 "
                End If
		        MultiPage=MultiPage&"<a href="""&Url&"page="&Pages&""">尾页</a>"
	                'MultiPage=MultiPage&" 页码："&CurPage&"/"&Pages
        'Else
	  'MultiPage=MultiPage&"页码：1/1"
	End If
End Function

Function Password_GenPass( nNoChars, sValidChars ) 
           ' nNoChars = 密码的长度   
	   ' sValidChars = 有效的字符.如果是空则( "" )   
	   ' 默认为: A-Z 和 a-z 和 0-9    
	   '使用方法NewPassword=Password_GenPass(6,"")
	    Const szDefault = "0123456789abcdefghijklmnopqrstuvxyzABCDEFGHIJKLMNOPQRSTUVXYZ"  
		  Dim nCount 
		  Dim sRet  
		  Dim nNumber 
		Dim nLength  
		Randomize 'init random 
		If sValidChars = "" Then      
		  sValidChars = szDefault        
		End If   
		nLength = Len( sValidChars )       
		For nCount = 1 To nNoChars     
		  nNumber = Int((nLength * Rnd) + 1)       
		  sRet = sRet & Mid( sValidChars, nNumber, 1 ) 
		Next  
	Password_GenPass = sRet 
End Function

Function FormatStr(String)
  on Error resume next
  String = Replace(String, CHR(13), "")
  String = Replace(String, CHR(32), "&nbsp;")
  String = Replace(String, " ", "&nbsp;")
  String = Replace(String, "<", "&lt;")
  String = Replace(String, ">", "&gt;")
  String = Replace(String, CHR(10) & CHR(10), "<BR><BR>")
  String = Replace(String, CHR(10), "<BR>")
  FormatStr = String
End Function

Function CODEStr(String)
  on Error resume next
  String = Replace(String, "&", "&#38;")
  String = Replace(String, "R", "&#82;")
  String = Replace(String, "r", "&#114;")
  String = Replace(String, "&amp;", "&#38;&#97;&#109;&#112;&#59;")
  String = Replace(String, "&quot;", "&#38;&#113;&#117;&#111;&#116;&#59;")
  String = Replace(String, "&lt;", "&#38;&#108;&#116;&#59;")
  String = Replace(String, "&gt;", "&#38;&#103;&#116;&#59;")
  String = Replace(String, "&nbsp;", "&#38;&#110;&#98;&#115;&#112;&#59;")
  String = Replace(String, "<", "&lt;")
  String = Replace(String, ">", "&gt;")
  CODEStr = String
End Function

Function Jencode(byVal iStr)
if isnull(iStr) or isEmpty(iStr) then
Jencode=""
Exit function
end if
dim F,i,E
F=array("ゴ","ガ","ギ","グ","ゲ","ザ","ジ","ズ","ヅ","デ","ド","ポ","ベ","プ","ビ","パ","ヴ","ボ","ペ","ブ","ピ","バ","ヂ","ダ","ゾ","ゼ")
E=array("Jn0;","Jn1;","Jn2;","Jn3;","Jn4;","Jn5;","Jn6;","Jn7;","Jn8;","Jn9;","Jn10;","Jn11;","Jn12;","Jn13;","Jn14;","Jn15;","Jn16;","Jn17;","Jn18;","Jn19;","Jn20;","Jn21;","Jn22;","Jn23;","Jn24;","Jn25;")
F=array(chr(-23116),chr(-23124),chr(-23122),chr(-23120),_
chr(-23118),chr(-23114),chr(-23112),chr(-23110),_
chr(-23099),chr(-23097),chr(-23095),chr(-23075),_
chr(-23079),chr(-23081),chr(-23085),chr(-23087),_
chr(-23052),chr(-23076),chr(-23078),chr(-23082),_
chr(-23084),chr(-23088),chr(-23102),chr(-23104),_
chr(-23106),chr(-23108))
Jencode=iStr
for i=0 to 25
Jencode=replace(Jencode,F(i),E(i))
next
End Function
'=======================================
Function IsObjInstalled(strClassString)
	On Error Resume Next
	IsObjInstalled = False
	Err = 0
	Dim xTestObj
	Set xTestObj = Server.CreateObject(strClassString)
	If 0 = Err Then IsObjInstalled = True
	Set xTestObj = Nothing
	Err = 0
End Function

function JoinChar(strUrl)
	if strUrl="" then
		JoinChar=""
		exit function
	end if
	if InStr(strUrl,"?")<len(strUrl) then 
		if InStr(strUrl,"?")>1 then
			if InStr(strUrl,"&")<len(strUrl) then 
				JoinChar=strUrl & "&"
			else
				JoinChar=strUrl
			end if
		else
			JoinChar=strUrl & "?"
		end if
	else
		JoinChar=strUrl
	end if
end function

function IsValidEmail(email)
	dim names, name, i, c
	IsValidEmail = true
	names = Split(email, "@")
	if UBound(names) <> 1 then
	   IsValidEmail = false
	   exit function
	end if
	for each name in names
		if Len(name) <= 0 then
			IsValidEmail = false
    		exit function
		end if
		for i = 1 to Len(name)
		    c = Lcase(Mid(name, i, 1))
			if InStr("abcdefghijklmnopqrstuvwxyz_-.", c) <= 0 and not IsNumeric(c) then
		       IsValidEmail = false
		       exit function
		     end if
	   next
	   if Left(name, 1) = "." or Right(name, 1) = "." then
    	  IsValidEmail = false
	      exit function
	   end if
	next
	if InStr(names(1), ".") <= 0 then
		IsValidEmail = false
	   exit function
	end if
	i = Len(names(1)) - InStrRev(names(1), ".")
	if i <> 2 and i <> 3 then
	   IsValidEmail = false
	   exit function
	end if
	if InStr(email, "..") > 0 then
	   IsValidEmail = false
	end if
end function

function HTMLEncode(fString)
if not isnull(fString) then
    fString = replace(fString, ">", "&gt;")
    fString = replace(fString, "<", "&lt;")
    fString = Replace(fString, CHR(32), "&nbsp;")
    fString = Replace(fString, CHR(9), "&nbsp;")
    fString = Replace(fString, CHR(34), "&quot;")
    fString = Replace(fString, CHR(39), "&#39;")
    fString = Replace(fString, CHR(13), "")
    fString = Replace(fString, CHR(10) & CHR(10), "</P><P> ")
    fString = Replace(fString, CHR(10), "<BR> ")
    HTMLEncode = fString
end if
end function

function checknum(str)
	if isnull(str) or str=""  then
		exit function
	else
		if not isnumeric(str) then
			response.write"<center>非法操作导致程序中止!</center>"
			response.end
		else
			checknum=int(str)
		end if
	end if
end function

function code_admin(strers,at,acut)
  dim strer
  strer=trim(strers)
  select case int(at)
  case 1
    strer=trim(request.form(strer))
  case 2
    strer=trim(request.querystring(strer))
  end select
  if isnull(strer) or strer="" then
    code_admin=""
    exit function
  end if
  'strer=replace(strer,"'","''")
  if int(acut)>0 then strer=left(strer,acut)
  code_admin=strer
end function

Function post_chk()
	Dim server_v1,server_v2
	post_chk=False 
	server_v1=cstr(Request.ServerVariables("HTTP_REFERER"))
	server_v2=cstr(Request.ServerVariables("SERVER_NAME"))
	If Mid(server_v1,8,len(server_v2))=server_v2 Then post_chk=True 
End Function

function debadstr(str)
	dim badstr,i
	debadstr=str
	badstr=split(hx_In,"|")
	for i=0 to ubound(badstr)
	debadstr=replace(debadstr,badstr(i),"***")
	next
end function

function chk()
    chk=false
    if trim(request.form("chk"))="yes" then
      chk=post_chk()
    end if
    if session("Hx_cms")=false then chk=false
end function

Function CheckStr(byVal ChkStr) 
	Dim Str:Str=ChkStr
	Str=Trim(Str)
	If IsNull(Str) Then
		CheckStr = ""
		Exit Function 
	End If
	Str = Replace(Str,"'","''")
        Str = replace(Str,"&","&amp;")
	Str = replace(Str,chr(34),"&quot;")
        Str = Replace(Str, ">", "&gt;")
        Str = Replace(Str, "<", "&lt;")
    	CheckStr=Str
End Function

Function checkspace(Str)
   If Isnull(Str) Then
      checkspace = ""
      Exit Function 
   End If
      Str = Replace(Str,"execute","[execute]")
      Str = Replace(Str,"request","[request]")
      Str = Replace(Str,"'","''")
      Str = Replace(Str,"--","－－")
      Str = Replace(Str,";","；")
      Str = Replace(Str,",","，")
      Str = Replace(Str,"[","{")
      Str = Replace(Str,"(","（")
      Str = Replace(Str,")","）")
      Str = Replace(Str,"0x","Ox")
      Str = Replace(Str,"%","％")
      Str = Replace(Str,"<","＜")
      Str = Replace(Str,">","＞")
      Str = Replace(Str,"。","")
      Str = Replace(Str,"！","")
      Str = Replace(Str,"!","")
	    checkspace = Str
End Function

function checkname(str)
checkname=true
if Instr(str,"=")>0  or Instr(str,"%")>0 or Instr(str,chr(32))>0  or Instr(str,"?")>0 or Instr(str,"&")>0 or Instr(str,";")>0 or Instr(str,",")>0  or Instr(str,"'")>0 or Instr(str,".")>0 or Instr(str,chr(34))>0 or Instr(str,chr(9))>0  or Instr(str,"")>0 or Instr(str,"$")>0 or Instr(str,chr(255))>0 or Instr(str,":") or instr(str,"|")>0  or instr(str,"#")>0 or instr(str,"`")>0 or instr(str,"\")>0 or instr(str,"(")>0 or instr(str,"[")>0 or instr(str,"-")>0 or instr(str,"~")>0 or Instr(str,"<")>0 or Instr(str,">")>0 then
checkname=false
end if
end function

function checknamecn(str) 
checknamecn=False 
If str <> "" Then 
Dim Re,PassTmp 
set Re = New RegExp 
Re.Global = True 
Re.IgnoreCase = True 
Re.Pattern="[^\u4E00-\u9FA5\uF900-\uFA2D]" 
PassTmp=Re.Test(Str) 
set Re=nothing 
If not PassTmp Then checknamecn=true 
End if 
End function

Function ReplaceBadChar(strChar)
	if strChar="" then
		ReplaceBadChar=""
	else
                ReplaceBadChar=replace(replace(replace(replace(replace(replace(replace(strChar,"'",""),"*",""),"?",""),"(",""),")",""),"<",""),".","")
	end if
End Function

Function Getdj(grade)
  if grade=1 then
    Getdj="<font color=red>(VIP)</font>"
  else 
    Getdj="<font color=gray>(普通)</font>" 
  end if
End Function
Function GetSex(stxt)
  if stxt="男" then
    GetSex="<img src='../img/wd/gg.gif' border=0 align='absmiddle' alt='男'> "
  else 
    GetSex="<img src='../img/wd/mm.gif' border=0 align='absmiddle' alt='女'> " 
  end if
End Function
'日期转换函数
Function DateToStr(DateTime,ShowType)  
	Dim DateMonth,DateDay,DateHour,DateMinute
	DateMonth=Month(DateTime)
	DateDay=Day(DateTime)
	DateHour=Hour(DateTime)
	DateMinute=Minute(DateTime)
	If Len(DateMonth)<2 Then DateMonth="0"&DateMonth
	If Len(DateDay)<2 Then DateDay="0"&DateDay
	Select Case ShowType
	Case "1"  
		DateToStr=Year(DateTime)&"-"&DateMonth&"-"&DateDay
	Case "2"
		DateToStr=Year(DateTime)&"-"&DateMonth
	Case "3"
		If Len(DateHour)<2 Then DateHour="0"&DateHour
		If Len(DateMinute)<2 Then DateMinute="0"&DateMinute
		DateToStr=DateMonth&"-"&DateDay&" "&DateHour&":"&DateMinute
	Case "4"
		DateToStr=DateMonth&"-"&DateDay
	End Select
End Function
'搜索函数
function chsearchstr(tstr,sstr)
  if sstr="" then
    chsearchstr=tstr
  else
    if instr(tstr,sstr)>0 then
      chsearchstr=replace(tstr,sstr,"<span class=""red"">"&sstr&"</span>")
    else
      chsearchstr=tstr
    end if
  end if
end function
%>

