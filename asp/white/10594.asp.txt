<%
''' SDCMS 分页
''' ==================================================================
''' 版权所有 http://www.sdcms.cn
''' -----------------------------------------------------------------
''' 这不是一个自由软件！您只能在不用于商业目的的前提下对程序代码进行修改和使用；
''' 未经授权不允许对程序代码以任何形式任何目的的再发布。
''' ==================================================================
''' 编写: 爱·哲学
''' 修改：IT平民 in 2012.06
class sdcms_pagelist

	private table_,field_,join_,where_,order_,sql_,Conn_,rs_,pagesize_,page_,pagenumber_,htmlurl_,isadminurl_
	private rspage,totalnumber,pagecount
	
	public property let field(str)
		field_=str
	end property
	
	public property let table(str)
		table_=str
	end property
	
	public property let join(str)
		join_=str
	end property
	
	public property let where(str)
		if str<>"" then
			where_=" where "&str
		end if
	end property
	
	public property let order(str)
		if str<>"" then
			order_=" order by "&str
		end if
	end property
	
	public property let pagesize(num)
		if isnumeric(num) then
			pagesize_=cint(num)
			if pagesize_<1 then
				pagesize_=20
			end if
		else
			pagesize_=20
		end if
	end property
	
	public property let page(num)
		if isnumeric(num) then
			page_=cint(num)
			if page_<1 then
				page_=1
			end if
		else
			page_=1
		end if
	end property
	
	public property let pagenumber(num)
		if isnumeric(num) then
			pagenumber_=clng(num)
			if pagenumber_<8 then
				pagenumber_=8
			end if
		else
			pagenumber_=8
		end if
		if pagenumber_ mod 2=0 then
			pagenumber_=8
		end if
	end property
	
	public property let htmlurl(str)
		htmlurl_=str
	end property
	
	public property let isadminurl(str)
		isadminurl_=str
	end property
	
	'初始化类
	private sub class_initialize()
		table_=""
		field_="*"
		join_=""
		where_=""
		order_=""
		pagesize_=20
		pagecount=0
		page_=1
		pagenumber_=6
		htmlurl_=""
		isadminurl_=1
		set rspage=server.createobject("adodb.recordset")
	end Sub
	
	private sub class_terminate()
		set rspage=nothing
	end Sub
	
	public function getrs()
		sql_="select "&field_&" from ["&table_&"]"&join_&where_&order_
		rspage.open sql_,sdcms.db.conn,1,1
		dbquery=dbquery+1
		if not rspage.eof and not rspage.bof then
			rspage.pagesize=pagesize_
			rspage.absolutepage=page_
			totalnumber=rspage.recordcount
			pagecount=abs(int(-abs(totalnumber/pagesize_)))
		end if
		set getrs=rspage
	end function
	
	public function number(byval num)
		number=pagesize_*(page_-1)+num
	end function
	
	public function showpage()
		if totalnumber<=0 then exit function
		dim pagehtml,beginpage,endpage,i
		beginpage=page_
		endpage=page_
		i=pagenumber_
		if page_>pagecount then page_=pagecount
		if endpage>pagecount then endpage=pagecount
		do while true
			if beginpage>1 then
				beginpage=beginpage - 1
				i=i-1
			end if 
			if i>1 and endpage<pagecount then
				endpage=endpage + 1
				i=i-1
			end if
			if (beginpage<=1 and endpage>=pagecount) or i<=1 then exit do
		loop
		pagehtml="<a>&#24635;&#25968; "&totalnumber&"</a>"
		if page_>1 then pagehtml = pagehtml&"<a href="""&geturl(page_-1)&""">&#19978;&#19968;&#39029;</a>"
		if beginpage<>1 then pagehtml = pagehtml&"<a href="""&geturl(1)&""">1...</a>"
		for i = beginpage To endpage
			if i=page_ then
				pagehtml=pagehtml&"<span>"&i&"</span>"
			else
				pagehtml=pagehtml&"<a href="""&geturl(i)&""">"&I&"</a>"
			end if
		next
		if endpage<>pagecount then pagehtml=pagehtml&"<a href="""&geturl(pagecount)&""">..."&pagecount&"</a>"
		if page_<pagecount then pagehtml=pagehtml&"<a href="""&geturl(page_+1)&""">&#19979;&#19968;&#39029;</a>"
		pagehtml=pagehtml&"<a>&#39029;&#27425; "&page_&"/"&pagecount&"</a>"
		pagehtml=pagehtml&"<b><input type=""text"" title=""&#36755;&#20837;&#25968;&#23383;&#65292;&#22238;&#36710;&#36339;&#36716;"" onkeydown=""if(13==event.keyCode){urlgo(this.value,"&webmode&",'"&geturl("")&"',"&pagecount&","&isadminurl_&")}"" value="""&page_&""" /></b>"
		showpage=pagehtml
	end function
	
	public function show_page_num()
		show_page_num=page_
	end function
	
	public function show_page_total()
		show_page_total=pagecount
	end function
	
	
	private function geturl(byval num)
		select case webmode
		case "1"
			geturl="?"&sdcms.geturlparam&"page="&num
		case "2"
			if isadminurl_=1 then
				geturl=replace("list_"&classid&"_[page].html","[page]",num)
				if num="1" then
					geturl=replace(geturl,"_1.html",".html")
				end if
			else
				geturl="?"&sdcms.geturlparam&"page="&num
			end if
		case "3"
			if isadminurl_=1 then
				geturl=replace("index_[page].html","[page]",num)
				if num="1" then
					geturl=replace(geturl,"index_1.html","")
				end if
			else
				geturl="?"&sdcms.geturlparam&"page="&num
			end if
		end select
	end function
	
end class
%>