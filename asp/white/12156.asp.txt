<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


'打开用户表，并检测用户是否登陆
Call OpenMemberexe("","login",0)

Call WebStyleTop("")

response.write(_
"<script language='javascript' type='text/javascript' src='js/inc/list.js'></script>"&_
"<script language='javascript' type='text/javascript' src='js/member.js'></script>"&_
"")



Call IsMenuRight("alertBack","用户管理")


Select Case mudi
	Case Else 
		Call IsMenuRight("alertBack","用户维护")
		Call index()

End Select



Call CloseMemberexe()
Call CloseConobj()
Call WebStyleBottom()




Private Sub index()
	%>
	<script language='javascript' type='text/javascript'>
	//添加用户表单：输入默认密码
	function ResetPwd(){
		alert("默认密码：123456");
		$id('userpwd').value = "123456";
	}

	//检测添加用户表单
	function CheckForm(form){
		if ($id('username').value == ""){alert("用户名不能为空");$id('username').focus();return false}
			if ($id('userMode').value != "rev" || $id('userpwd').value != ""){
		if ($id('userpwd').value.length < 6){alert("密码长度不能小于6位");$id('userpwd').focus();return false}
			}
		if ($id('realname').value == ""){alert("称呼不能为空");$id('realname').focus();return false}
		if ($id('userGroup').value == ""){alert("请选择用户组");$id('userGroup').focus();return false}
			
		if ($id('userMode').value == "rev"){
			if (confirm("您确定修改？")==false){return false}
		}
	}
	</script>


	<%
	refUsername	= ToGetRegExpStr("refUsername","sql")
	refRealname	= ToGetRegExpStr("refRealname","sql")
	refUserGroup= ToGetInt("refUserGroup")
	refState	= ToInt2(ToGetStr("refState"),-1)

	SQLstr="select MB.MB_ID,MB.MB_username,MB.MB_realname,MB.MB_rightStr,MB.MB_time,UG.MG_theme from OT_member as MB LEFT JOIN OT_memberGroup as UG on MB.MB_groupID=UG.MG_ID where MB.MB_ID>1"

	If refUsername<>"" Then SQLstr=SQLstr &" and MB_username like '%"& refUsername &"%'"
	If refRealname<>"" Then SQLstr=SQLstr &" and MB_realname like '%"& refRealname &"%'"
	If refUserGroup>0 Then SQLstr=SQLstr &" and MB_groupID="& refUserGroup
	If refState>-1 Then SQLstr=SQLstr &" and MB_state="& refState


	Call ShareTableTop("share_refer.gif","","查询")
	response.write(_
	"<form name='refForm' method='get' action=''>"&_
	"<input type='hidden' name='mudi' value='"& mudi &"' />"&_
	"<table align='center' cellpadding='0' cellspacing='0' summary=''><tr>"&_
		"<td style='padding:3px' class='font1_2'>用户名：</td>"&_
		"<td style='padding:3px'><input type='text' id='refUsername' name='refUsername' size='10' value='"& refUsername &"' /></td>"&_
		"<td style='padding:3px'>&nbsp;&nbsp;</td>"&_
		"<td style='padding:3px' class='font1_2'>称呼：</td>"&_
		"<td style='padding:3px'><input type='text' id='refRealname' name='refRealname' size='8' value='"& refRealname &"' /></td>"&_
		"<td style='padding:3px'>&nbsp;&nbsp;</td>"&_
		"<td style='padding:3px' class='font1_2'>用户组：</td>"&_
		"<td style='padding:3px'><select id='refUserGroup' name='refUserGroup'>"&_
									"<option value=''>&nbsp;</option>")
									Set readexe=conobj.execute("select MG_ID,MG_theme from OT_memberGroup")
										Do Until readexe.EOF
											response.write("<option value='"& readexe("MG_ID") &"' "& IsSelected(refUserGroup,readexe("MG_ID")) &">"& readexe("MG_theme") &"</option>")
										readexe.movenext
										Loop
									readexe.close
									Set readexe=Nothing
								response.write(_
								"</select>"&_
		"</td>"&_
		"<td style='padding:3px'>&nbsp;&nbsp;</td>"&_
		"<td style='padding:3px' class='font1_2'>"&_
			"状态：<select id='refState' name='refState'>"&_
						"<option value=''>&nbsp;</option>"&_
						"<option value='0' "& IsSelected(refState,0) &">正常</option>"&_
						"<option value='10' "& IsSelected(refState,10) &">冻结</option>"&_
					"</select>"&_
		"</td>"&_
		"<td style='padding:3px'>&nbsp;&nbsp;</td>"&_
		"<td style='padding:3px'><input type='image' src='images/button_refer.gif' /></td>"&_
	"</tr></table>"&_
	"</form>")
	Call ShareTableBottom()

	response.write(_
	"<br />")




	Call ShareTableTop("share_userAdd.gif","formTitle","添加用户")
	%>
		<form name="userform" method='post' action="member_deal.asp?mudi=add" onsubmit="return CheckForm(this)">
		<input type="hidden" id="userMode" name="userMode" value="add" />
		<input type="hidden" id="userID" name="userID" value="0" />
		<table width="90%" align='center' cellpadding='0' cellspacing='0' summary='' class='padd3'><tr>
			<td>
				用户名：<input type="text" id="username" name="username" size='15' onkeyup="if (this.value!=FiltVarStr(this.value)){this.value=FiltVarStr(this.value)}" />
			</td>
			<td>
				密&nbsp;&nbsp;码：<input type="password" id="userpwd" name="userpwd" size='15' />
				<input type="button" onclick="ResetPwd()" value="默认" />
			</td>
			<td>
				称&nbsp;&nbsp;呼：<input type="text" id="realname" name="realname" size='15' />
			</td>
			<td id="formSubmit"><input type="image" src="images/button_submit.gif" /></td>
		</tr>
		<tr>
			<td class='font1_2'>
				状&nbsp;&nbsp;态：<select id="state" name="state">
									<option value="0">正常</option>
									<option value="10">冻结</option>
								</select>
			</td>
			<td colspan="2">
				用户组：<select id="userGroup" name="userGroup">
										<option value="">&nbsp;</option>
										<%
										Set readexe=conobj.execute("select MG_ID,MG_theme,MG_rightStr from OT_memberGroup")
											Do Until readexe.EOF
													If Not (user_rightStr<>"admin" And InStr(readexe("MG_rightStr"),"|用户管理|")<>0) Then
												response.write("<option value='"& readexe("MG_ID") &"'>"& readexe("MG_theme") &"</option>")
													End If
											readexe.movenext
											Loop
										readexe.close
										Set readexe=Nothing
										%>
									</select>
			</td>
		</tr></table>
		</form>
		<span id="userAlertStr" class='font2_2'></span>
	<%
	Call ShareTableBottom()



	response.write(_
	"<br /><br />")



	Call ShareTableTop2("share_userList.gif","","用户列表")
	Call ShareTableItemTitle("6%,22%,22%,15%,20%,15%","序号,用户名,称呼,用户组,加入时间,<!-- 授权　 -->修改　删除")


	Set showrec=server.CreateObject("ADODB.recordset")
	showrec.open SQLstr &" order by MB.MB_ID DESC",conobj,3,1
		If showrec.EOF Then
			response.write("<tr><td colspan='12' class='font1_2' style='padding:8px' align='center'>暂无内容</td></tr></table>")
			response.End
		End If
			showPageSize=memberexe("MB_itemNum")		'每页条数
			showrec.pagesize=showPageSize	
			showPageCount=showrec.pagecount
			showCount=showrec.recordcount
			page=ToGetInt("page")
			if page < 1 Or page > showPageCount Then page=1
			showrec.absolutepage=page


		response.write(_
		"<tbody class='tabBody padd5'>"&_
		"")
		number=1+(page-1)*showPageSize
		For i=1 To showPageSize
			If showrec.EOF Then Exit For
			If i Mod 2 = 0 Then trBg="class='tabColorTr'" Else trBg=""
		response.write(_
		"<tr id='closeData"& showrec("MB_ID") &"' "& trBg &">"&_
			"<td align='center'>"& number &"</td>"&_
			"<td align='left'>"& showrec("MB_username") &"</td>"&_
			"<td align='left'>"& showrec("MB_realname") &"</td>"&_
			"<td align='center'>"& showrec("MG_theme") &"<br /></td>"&_
			"<td align='center'>"& showrec("MB_time") &"<br /></td>"&_
			"<td align='center'>"&_
				"<!-- <img src='images/img_right.gif' style='cursor:pointer' onclick=""document.location.href='memberGroup.asp?mudi=setRight&dataID="& showrec("MB_ID") &"'"" alt='' />&nbsp;&nbsp;&nbsp; -->"&_
				"<img src='images/img_rev.gif' style='cursor:pointer' onclick=""DataDeal.location.href='member_deal.asp?mudi=send&dataID="& showrec("MB_ID") &"'"" alt='' />&nbsp;&nbsp;"&_
				"<img src='images/img_del.gif' style='cursor:pointer' onclick=""if(confirm('你确定要删除？')==true){DataDeal.location.href='member_deal.asp?mudi=del&dataID="& showrec("MB_ID") &"'}"" alt='' />"&_
			"</td>"&_
		"</tr>"&_
		"")
		showrec.movenext
		number=number+1
		Next
		response.write(_
		"</tbody>"&_
		"")
	showrec.close
	Set showrec=Nothing

	Call ShareTableBottom2()

End Sub


%>