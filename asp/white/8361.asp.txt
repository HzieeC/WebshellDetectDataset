<!--#include file="commond.asp" -->
<!--#include file="include/function.asp" -->
<!--#include file="include/md5code.asp" -->
<!--#include file="include/library.asp" -->
<%
Dim log_Year,log_Month,log_Day,logID,cateID,Url_Add
log_Year=CheckStr(Trim(Request.QueryString("log_Year")))
log_Month=CheckStr(Trim(Request.QueryString("log_Month")))
log_Day=CheckStr(Trim(Request.QueryString("log_Day")))
logID=CheckStr(Trim(Request.QueryString("logID")))
cateID=CheckStr(Trim(Request.QueryString("cateID")))
%><!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<META http-equiv=Content-Type content="text/html; charset=utf-8">
<title><%=siteName%>_登陆</title>
<META content="opeasy" name=Keywords>
<META content="open easy,opration easy" name=Description>
<LINK href="images/style.css" type=text/css rel=stylesheet>
<script type="text/javascript" language="javascript" src="common.js"></script>

</head>
<body>
<!--头部开始-->
<!--#include file="header.asp" -->
<!--主体开始-->
<div id="item">
  <%IF Request.QueryString("action")="logout" Then
	Response.Cookies(CookieName)("memName")=""
	Response.Cookies(CookieName)("memPassword")=""
	Response.Cookies(CookieName)("memStatus")=""%>
  <div class="msgtop">退出成功</div>
  <div class="msg"><a href="default.asp">点击返回主页</a></div>
  <%ElseIF Request.QueryString("action")="login" Then
	  Dim Login_Title,Login_Message
	  IF Request.Form("username")=Empty OR Request.Form("password")=Empty Then
	  	  Login_Title="错误信息"
		  Login_Message="请将信息输入完整<br><a href='javascript:history.go(-1);'>请返回重新输入</a>"
	  Else
		 Dim memLogin
		 Set memLogin=Server.CreateObject("ADODB.Recordset")
		 SQL="SELECT mem_Name,mem_Password,mem_Status,mem_LastIP FROM blog_member WHERE mem_Name='"&CheckStr(Request.Form("username"))&"' AND mem_Password='"&md5(CheckStr(Request.Form("password")))&"'"
		 memLogin.Open SQL,Conn,1,3
		 
		 IF memLogin.EOF AND memLogin.BOF Then
		 	Login_Title="错误信息"
		  	Login_Message="用户名与密码错误<br><a href='javascript:history.go(-1);'>请返回重新输入</a>"
		 Else
		 	Response.Cookies(CookieName)("memName")=memLogin("mem_Name")
			Response.Cookies(CookieName)("memPassword")=memLogin("mem_Password")
			Response.Cookies(CookieName)("memStatus")=memLogin("mem_Status")
			Select Case Request.Form("CookieTime")
				Case 1
					Response.Cookies(CookieName).Expires=Date+1
				Case 2
					Response.Cookies(CookieName).Expires=Date+31
				Case 3
					Response.Cookies(CookieName).Expires=Date+365
			End Select
			memLogin("mem_LastIP")=Guest_IP
			memLogin.Update
		 	Login_Title="登陆成功"
		  	Login_Message="<a href='default.asp'>点击返回主页</a>"
		 End IF
		 memLogin.Close
		 Set memLogin=Nothing
	End IF%>
  <div class="msgtop"><%=Login_Title%></div>
  <div class="msg"><%=Login_Message%></div>
  <%Else%>
  <table width="95%" border="0" align="center" cellpadding="4" cellspacing="1" bgcolor="#CCCCCC">
    <form action="logging.asp?action=login" method="post" name="Login" id="Login">
      <tr>
        <td height="25" colspan="2" align="center" bgcolor="#F9FCFE" class="msg_head">用户登陆</td>
      </tr>
      <tr>
        <td align="right" bgcolor="#FFFFFF">用户：</td>
        <td bgcolor="#FFFFFF"><input name="username" type="text" id="username" size="24" maxlength="24" />
        </td>
      </tr>
      <tr>
        <td align="right" bgcolor="#FFFFFF">密码：</td>
        <td bgcolor="#FFFFFF"><input name="password" type="password" id="password" size="16" maxlength="16" />
          &nbsp;</td>
      </tr>
      <tr>
        <td align="right" bgcolor="#FFFFFF"> Cookie选项：<br /></td>
        <td bgcolor="#FFFFFF"><input type="radio" name="CookieTime" value="0" checked="checked" />
          不保存
          <input type="radio" name="CookieTime" value="1" />
          保存一天
          <input type="radio" name="CookieTime" value="2" />
          保存一月
          <input type="radio" name="CookieTime" value="3" />
          保存一年</td>
      </tr>
      <tr>
        <td colspan="2" align="center" bgcolor="#FFFFFF"><input name="Login" type="submit" id="agree" value=" 登 陆 " />
          &nbsp;
          <input name="Register" type="button" id="Register" onclick="javascript:document.location.href='register.asp';" value=" 注 册 " />
        </td>
      </tr>
    </form>
  </table>
  <%End IF%>
</div>
<div class="blank10"></div>
<!--footer-->
<!--#include file="footer.asp" -->
</body>
</html>
