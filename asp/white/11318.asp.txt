<%
'**************************************************
'文件名称：FunctionSite.asp
'摘   要：网站公用函数
'最后更新：Pulb
'最后日期：
'**************************************************

'==================================================
'过程名：SetupSystemDate
'作  用：将系统基本信息存入Application
'参  数：无
'==================================================
Sub SetupSystemDate()
	sqlTemp = "select SystemInfo, CompanyInfo from Pulb_SiteConfig"
	set rsTemp = Conn.Execute(sqlTemp)
	If Not rsTemp.eof And Not rsTemp.bof Then
		Application.Lock
		Application("SystemInfo") = split(rsTemp(0),"|||")
		Application("CompanyInfo") = split(rsTemp(1),"|||")
		Application.UnLock
	End If
	rsTemp.close
	set rsTemp = nothing
End Sub

'==================================================
'过程名：GetSex
'作  用：获取性别
'参  数：ID
'==================================================
Function GetSex(ID)
	if ID = "" then
		Exit Function
	elseif not CheckInteger(ID) then
		Exit Function
	end if
	select case ID
		case 0
			GetSex = "未知"
		case 1
			GetSex = "男"
		case 2
			GetSex = "女"
		case 3
			GetSex = "保密"
		case 5
			GetSex = "不限"
		case else
			GetSex = ""
	end select
End Function

'==================================================
'过程名：GetOrderState
'作  用：订单状态
'参  数：ID
'==================================================
Function GetOrderState(ID)
	if ID = "" then
		Exit Function
	elseif not CheckInteger(ID) then
		Exit Function
	end if
	Select Case Cint(ID)
		Case 0
			GetOrderState = "<font color='#FF0000'>预订</font>"
		Case 1
			GetOrderState = "<font color='#999999'>取消</font>"
		Case 2
			GetOrderState = "<font color='#0000CC'>确认</font>"
		Case 3
			GetOrderState = "<font color='#006600'>完成</font>"
		Case Else
			GetOrderState = ""
	End Select
End Function

'==================================================
'过程名：GetDegree
'作  用：获取学历
'参  数：ID
'==================================================
Function GetDegree(ID)
	if ID = "" then
		Exit Function
	elseif not CheckInteger(ID) then
		Exit Function
	end if
	sqlTemp = "select ClassName from Pulb_Degree where ID="&ID
	set rsTemp = Conn.Execute(sqlTemp)
	If Not rsTemp.eof And Not rsTemp.bof Then
		GetDegree = rsTemp(0)
	Else
		GetDegree = ""
	End If
	rsTemp.close
	set rsTemp = nothing
End Function


'==================================================
'过程名：GetTopicClassStr
'作  用：获取主题
'参  数：Str
'==================================================
Function GetTopicClassStr(Str)
	if Str = "" then
		Exit Function
	end if
	StrLen = Len(Str)
	ThisTemp = MID(Str,2,StrLen-2)
	if ThisTemp = "" then
		Exit Function
	end if
	sqlTemp = "select ID,ClassName from Pulb_TopicClass where ID IN ("&ThisTemp&")"
	set rsTemp = Conn.execute(sqlTemp)
	Do while not rsTemp.eof
		if GetTopicClassStr = "" then
			GetTopicClassStr = rsTemp(1)
		else
			GetTopicClassStr = GetTopicClassStr &" "&rsTemp(1)
		end if
	rsTemp.movenext
	Loop
	rsTemp.close
	set rsTemp = nothing
	ThisTemp = ""
End Function


'==================================================
'过程名：GetProductState
'作  用：产品状态
'参  数：ID
'==================================================
Function GetProductState(IsTop, IsHot, IsSale)
	if IsTop then
		GetProductState = "<img src='/images/tit_state1.gif' width='36' height='15' align='absmiddle' />"
		Exit Function
	end if
	if IsHot then
		GetProductState = "<img src='/images/tit_state2.gif' width='36' height='15' align='absmiddle' />"
		Exit Function
	end if
	if IsSale then
		GetProductState = "<img src='/images/tit_state3.gif' width='36' height='15' align='absmiddle' />"
		Exit Function
	end if
End Function


'==================================================
'过程名：FunTopNews
'作  用：推荐新闻
'参  数：类别,数目
'==================================================
Function FunTopNews(TClass, TNum)
	if TClass = "" then
		TClass = 0
	elseif not CheckInteger(TClass) then
		TClass = 0
	end if
	if TNum = "" then
		TNum = 10
	elseif not CheckInteger(TNum) then
		TNum = 10
	end if
	sqlThis = "select Top "&TNum&" ID, Title from Pulb_News where IsShow"
	if TClass <> 0 then
		sqlThis = sqlThis &" and ClassID IN("&TClass&")"
	end if
	sqlThis = sqlThis &" order by IsTop DESC, UpdateTime DESC"
	set rsThis = server.createobject("adodb.recordset")
	rsThis.open sqlThis,Conn,1,1
	if not rsThis.eof then
		FunTopNews = "<UL class='news_top'>"&chr(10)
		Do while not rsThis.eof
			FunTopNews = FunTopNews &"<LI><a href='/news/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&InterceptString(rsThis(1), 38)&"</a></LI>"&chr(10)
		rsThis.movenext
		loop
		FunTopNews = FunTopNews &"</UL>"
	else
		FunTopNews = "<div class='noinfo'>暂无信息！</div>"
	end if
	rsThis.close
	set rsThis = Nothing
End Function


'==================================================
'过程名：FunTourAll
'作  用：旅游线路幻灯
'参  数：数目
'==================================================
Function FunTourAll(TNum)
	if TNum = "" then
		TNum = 10
	elseif not CheckInteger(TNum) then
		TNum = 10
	end if

	sqlThis = "select Top "&TNum&" ID, Title, TitleColor, TitleSmall, PriceAdult from Pulb_TourRoute where IsShow and IsTop order by UpdateTime DESC"
	set rsThis = server.createobject("adodb.recordset")
	rsThis.open sqlThis,Conn,1,1
	FunTourAll = "<UL class='tour_ftop show'>"&chr(10)
	Do while not rsThis.eof
		if rsThis(3) <> "" then
			TempTitle = rsThis(3)
		else
			TempTitle = rsThis(1)
		end if
		FunTourAll = FunTourAll &"<LI><h5><a href='/tour/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&InterceptString(TempTitle, 30)&"</a></h5><span class='price_s'><em>&yen;</em><font>"&rsThis(4)&"</font></span></LI>"&chr(10)
	rsThis.movenext
	loop
	FunTourAll = FunTourAll &"</UL>"&chr(10)
	rsThis.close
	
	sqlThis = "select Top "&TNum&" ID, Title, TitleColor, TitleSmall, PriceAdult from Pulb_TourRoute where IsShow and IsHot order by UpdateTime DESC"
	rsThis.open sqlThis,Conn,1,1
	FunTourAll = FunTourAll &"<UL class='tour_ftop'>"&chr(10)
	Do while not rsThis.eof
		if rsThis(3) <> "" then
			TempTitle = rsThis(3)
		else
			TempTitle = rsThis(1)
		end if
		FunTourAll = FunTourAll &"<LI><h5><a href='/tour/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&InterceptString(TempTitle, 30)&"</a></h5><span class='price_s'><em>&yen;</em><font>"&rsThis(4)&"</font></span></LI>"&chr(10)
	rsThis.movenext
	loop
	FunTourAll = FunTourAll &"</UL>"&chr(10)
	rsThis.close
	sqlThis = "select Top "&TNum&" ID, Title, TitleColor, TitleSmall, PriceAdult from Pulb_TourRoute where IsShow and IsSale order by UpdateTime DESC"
	rsThis.open sqlThis,Conn,1,1
	FunTourAll = FunTourAll &"<UL class='tour_ftop'>"&chr(10)
	Do while not rsThis.eof
		if rsThis(3) <> "" then
			TempTitle = rsThis(3)
		else
			TempTitle = rsThis(1)
		end if
		FunTourAll = FunTourAll &"<LI><h5><a href='/tour/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&InterceptString(TempTitle, 30)&"</a></h5><span class='price_s'><em>&yen;</em><font>"&rsThis(4)&"</font></span></LI>"&chr(10)
	rsThis.movenext
	loop
	FunTourAll = FunTourAll &"</UL>"
	rsThis.close
	set rsThis = Nothing
End Function


'==================================================
'过程名：FunTourDay
'作  用：几日游旅游线路幻灯
'参  数：数目
'==================================================
Function FunTourDay(TNum)
	if TNum = "" then
		TNum = 10
	elseif not CheckInteger(TNum) then
		TNum = 10
	end if
	set rsThis = server.createobject("adodb.recordset")
	For i = 1 To 4
		sqlThis = "select Top "&TNum&" ID, Title, TitleColor, TitleSmall, PriceAdult from Pulb_TourRoute where IsShow and TourDays="&i&" order by IsTop DESC, IsHot DESC, UpdateTime DESC"
		rsThis.open sqlThis,Conn,1,1
		FunTourDay = FunTourDay &"<UL class='tour_ftop show'>"&chr(10)
		Do while not rsThis.eof
			if rsThis(3) <> "" then
				TempTitle = rsThis(3)
			else
				TempTitle = rsThis(1)
			end if
			FunTourDay = FunTourDay &"<LI><h5><a href='/tour/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&InterceptString(TempTitle, 30)&"</a></h5><span class='price_s'><em>&yen;</em><font>"&rsThis(4)&"</font></span></LI>"&chr(10)
		rsThis.movenext
		loop
		FunTourDay = FunTourDay &"</UL>"&chr(10)
		rsThis.close
	Next
	set rsThis = Nothing
End Function

'==================================================
'过程名：FunTopTour
'作  用：旅游线路
'参  数：类别,数目, 1-推荐 2-热门 3-特价
'==================================================
Function FunTopTour(TClass, TNum, TType)
	if TClass = "" then
		TClass = 0
	elseif not CheckInteger(TClass) then
		TClass = 0
	end if
	if TNum = "" then
		TNum = 10
	elseif not CheckInteger(TNum) then
		TNum = 10
	end if
	if TType = "" then
		TType = 1
	elseif not CheckInteger(TType) then
		TType = 1
	end if
	sqlThis = "select Top "&TNum&" ID, Title, TitleColor, TitleSmall, PriceAdult from Pulb_TourRoute where IsShow"
	if TType = 1 then
		sqlThis = sqlThis &" and IsTop"
	elseif TType = 2 then
		sqlThis = sqlThis &" and IsHot"
	elseif TType = 3 then
		sqlThis = sqlThis &" and IsSale"
	else
		sqlThis = sqlThis &" and IsTop"
	end if
	if TClass <> 0 then
		sqlThis = sqlThis &" and ClassID Like '%,"&TClass&",%'"
	end if
	sqlThis = sqlThis &" order by UpdateTime DESC"
	set rsThis = server.createobject("adodb.recordset")
	rsThis.open sqlThis,Conn,1,1
	if not rsThis.eof then
		FunTopTour = "<UL class='tour_ftop'>"&chr(10)
		Do while not rsThis.eof
			if rsThis(3) <> "" then
				TempTitle = rsThis(3)
			else
				TempTitle = rsThis(1)
			end if
			FunTopTour = FunTopTour &"<LI><h5><a href='/tour/content.asp?id="&rsThis(0)&"' title='"&rsThis(1)&"' target='_blank'>"&TempTitle&"</a></h5><span class='price_s'><em>&yen;</em><font>"&rsThis(4)&"</font></span></LI>"&chr(10)
		rsThis.movenext
		loop
		FunTopTour = FunTopTour &"</UL>"
	else
		FunTopTour = "<div class='noinfo'>暂无信息！</div>"
	end if
	rsThis.close
	set rsThis = Nothing
End Function

'==================================================
'过程名：FunTourClassList
'作  用：旅游线路分类
'参  数：
'==================================================
Function FunTourClassList(pcid)
	if pcid = "" then
		pcid = 0
	elseif not CheckInteger(pcid) then
		pcid = 0
	end if
	dim rsClass,sqlClass,strTemp,tmpDepth
	if pcid > 0 then
		sqlClass="select * From Pulb_TourClass where ParentPath like '0,"&pcid&"%' order by RootID,OrderID"
	else
		sqlClass="select * From Pulb_TourClass order by RootID,OrderID" 
	end if
	set rsClass=Conn.execute(sqlClass) 
	if rsClass.bof and rsClass.eof then 
		strTemp = "暂无分类" 
	else 
		do while not rsClass.eof 
			tmpDepth = rsClass("Depth")
			if rsClass("Child")>0 then
				if strTemp <> "" then
					strTemp=strTemp & "</p>"&chr(10)
				end if
				if tmpDepth > 0 then
					strTemp=strTemp & "<h4><a href='list.asp?pcid="&pcid&"&cid="&rsClass("ClassID")&"'>"&rsClass("ClassName")&"</a></h4>"&chr(10)
				else
					strTemp=strTemp & "<h3><a href='list.asp?pcid="&pcid&"&cid="&rsClass("ClassID")&"'>"&rsClass("ClassName")&"</a></h3>"&chr(10)
				end if
				strTemp=strTemp & "<p>"
			else
				if tmpDepth = 0 then
					strTemp=strTemp & "<b>"&rsClass("ClassName")&"</b>"
				else
					strTemp=strTemp & "<a href='list.asp?pcid="&pcid&"&cid="&rsClass("ClassID")&"'>"& rsClass("ClassName") &"</a>"
				end if
			end if
			rsClass.movenext 
		loop 
	end if 
	rsClass.close 
	set rsClass=nothing 
	strTemp = strTemp & "</p>"
	FunTourClassList = strTemp
End Function

Sub TourClass_Option(ShowType,CurrentID) 
	TableName = "Pulb_TourClass"
	if ShowType=0 then 
		response.write "<option value='0'" 
		if CurrentID=0 then response.write " selected" 
		response.write ">-请选择-</option>" 
	end if 
	dim rsClass,sqlClass,strTemp,tmpDepth,i 
	dim arrShowLine(20) 
	for i=0 to ubound(arrShowLine) 
		arrShowLine(i)=False 
	next 
	sqlClass="select * From "&TableName&" order by RootID,OrderID" 
	set rsClass=Conn.execute(sqlClass) 
	if rsClass.bof and rsClass.eof then 
		response.write "<option value=''>请先添加信息分类</option>" 
	else 
		do while not rsClass.eof 
		tmpDepth=rsClass("Depth") 
		if rsClass("NextID")>0 then 
			arrShowLine(tmpDepth)=True 
		else 
			arrShowLine(tmpDepth)=False 
		end if 
		if ShowType=1 then 
			strTemp="<option value='" & rsClass("ClassID") & "'" 
		elseif ShowType=2 then 
			strTemp="<option value='" & rsClass("ClassID") & "'" 
		elseif ShowType=3 then 
			if rsClass("Child")>0 then 
				strTemp="<option value=''" 
			else 
				strTemp="<option value='" & rsClass("ClassID") & "'" 
			end if 
		elseif ShowType=4 then 
			if rsClass("Child")>0 then 
				strTemp="<option value=''" 
			else 
				strTemp="<option value='" & rsClass("ClassID") & "'" 
			end if 
		else 
			strTemp="<option value='" & rsClass("ClassID") & "'" 
		end if 
		if CurrentID>0 and rsClass("ClassID")=CurrentID then 
			strTemp=strTemp & " selected" 
		end if 
		strTemp=strTemp & ">" 
		
		if tmpDepth>0 then 
			for i=1 to tmpDepth 
				strTemp=strTemp & " " 
				if i=tmpDepth then 
					if rsClass("NextID")>0 then 
						strTemp=strTemp & "├ " 
					else 
						strTemp=strTemp & "└ " 
					end if 
				else 
					if arrShowLine(i)=True then 
						strTemp=strTemp & "│" 
					else 
						strTemp=strTemp & " " 
					end if 
				end if 
			next 
		end if 
		strTemp=strTemp & rsClass("ClassName") 
		strTemp=strTemp & "</option>" 
		response.write strTemp 
		rsClass.movenext 
	loop 
	end if 
	rsClass.close 
	set rsClass=nothing 
End Sub

Sub TourClass_OptionLI()
	TableName = "Pulb_TourClass"
	dim rsClass,sqlClass,strTemp,tmpDepth,i 
	dim arrShowLine(20) 
	for i=0 to ubound(arrShowLine) 
		arrShowLine(i)=False 
	next 
	sqlClass="select * From "&TableName&" order by RootID,OrderID" 
	set rsClass=Conn.execute(sqlClass) 
	if rsClass.bof and rsClass.eof then 
		response.write "<option value=''>请先添加信息分类</option>" 
	else 
		do while not rsClass.eof 
			tmpDepth=rsClass("Depth")
			if rsClass("NextID")>0 then 
				arrShowLine(tmpDepth)=True 
			else 
				arrShowLine(tmpDepth)=False 
			end if
			
			strTemp="<li el='" & rsClass("ClassID") & "'>" 
			
			if tmpDepth>0 then 
				for i=1 to tmpDepth
					if i=tmpDepth then 
						if rsClass("NextID")>0 then 
							strTemp=strTemp & "├ " 
						else 
							strTemp=strTemp & "└ " 
						end if 
					else 
						if arrShowLine(i)=True then 
							strTemp=strTemp & "├ " 
						else 
							strTemp=strTemp & "└ "
						end if 
					end if 
				next 
			end if
			if rsClass("Child")>0 then
				strTemp=strTemp & "<font color='#FF0000'>"
				if tmpDepth = 0 then
					strTemp=strTemp & "<b>"&rsClass("ClassName")&"</b>"
				else
					strTemp=strTemp & rsClass("ClassName")
				end if
				strTemp=strTemp & "</font>" 
			else
				if tmpDepth = 0 then
					strTemp=strTemp & "<b>"&rsClass("ClassName")&"</b>"
				else
					strTemp=strTemp & rsClass("ClassName")
				end if
			end if
			strTemp=strTemp & "</li>"
			response.write strTemp 
			rsClass.movenext 
		loop 
	end if 
	rsClass.close 
	set rsClass=nothing 
End Sub


Function FunHeader()
	Temp = "<div id='top-bar'>"&chr(10)
	Temp = Temp &"	<div id='top'>"&chr(10)
	Temp = Temp &"		<ul class='login' id='TopUser'><script type='text/javascript'>GetUser(1);</script></ul>"&chr(10)
	Temp = Temp &"		<ul class='site'>"&chr(10)
	Temp = Temp &"			<li><a href='/us/content.asp?id=7' target='_blank'>帮助中心</a></li><li><a href=""javascript:window.external.addFavorite('"&SystemInfo(0)&"', '"&SystemInfo(1)&"')"">收藏</a></li>"&chr(10)
	Temp = Temp &"		</ul>"&chr(10)
	Temp = Temp &"	</div>"&chr(10)
	Temp = Temp &"</div>"&chr(10)
	Temp = Temp &"<div class='main'>"&chr(10)
	Temp = Temp &"	<div class='header'><div class='logo'><a href='/'><img src='/images/logo.jpg' width='240' height='60' alt='"&SystemInfo(2)&"' /></a></div><div class='tel'><a title='佳旅通软件官网―商旅企业信息化建设专家' alt='佳旅通旅游网站管理系统,中航信,IBE,网站建设,旅游电子商务' href='http://www.jialvtong.com' target='_blank'><img src='/images/tel.jpg' width='500' height='60' /></a></div></div>"&chr(10)
	Temp = Temp &"	<div class='menu'>"&chr(10)
	Temp = Temp &"		<ul>"&chr(10)
	Temp = Temp &"			<li><a href='/'>首页</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/tour/?pcid=95'>西安旅游</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/tour/?pcid=100'>周边旅游</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/tour/?pcid=1'>国内旅游</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/tour/?pcid=2'>出境旅游</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/news/list.asp?id=6'>签证办理</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/news/'>旅游资讯</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/us/content.asp?id=1'>关于我们</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/us/content.asp?id=6'>支付方式</a></li>"&chr(10)
	Temp = Temp &"			<li><a href='/us/content.asp?id=5'>积分兑换</a></li>"&chr(10)
	Temp = Temp &"		</ul>"&chr(10)
	Temp = Temp &"	</div>"&chr(10)
	Temp = Temp &"</div>"&chr(10)
	if CurrentPage <> "" then
		Temp = Temp &"<div class='main'>"&chr(10)
		Temp = Temp &"	<div class='smenu'>当前位置：<A href='/'>首页</A> -> "&CurrentPage&"</div>"
		Temp = Temp &"</div>"&chr(10)
	end if
	FunHeader = Temp
End Function


Function FunFooter(Scount)
	Temp = "<div class='main'>"&chr(10)
	Temp = Temp &"	<div class='footer'>"&chr(10)
	Temp = Temp &"				<p><a href='/us/content.asp?id=1'>关于我们</a>　<a href='/us/content.asp?id=2' target='_blank'>联系我们</a>　<a href='/us/content.asp?id=6'>支付方式</a>　<a href='/us/content.asp?id=5'>积分兑换</a>　<a href='/us/msg.asp'>在线留言</a><p>"&chr(10)
	Temp = Temp &"		<p>"&CompanyInfo(0)&"　电话："&CompanyInfo(1)&"　传真："&CompanyInfo(2)&"</p>"&chr(10)
	Temp = Temp &"		<p>"&SystemInfo(5)&" <script src='http://s14.cnzz.com/stat.php?id=3965386&web_id=3965386&show=pic' language='JavaScript'></script></p>"&chr(10)
	Temp = Temp &"		<p>Powered by <a title='佳旅通软件-商旅企业信息化建设专家' href='http://www.jialvtong.com/' target='_blank'>佳旅通软件</a></p>"&chr(10)
	Temp = Temp &"	</div>"&chr(10)
	Temp = Temp &"</div>"&chr(10)
	Temp = Temp &"<script type='text/javascript' src='/qqonline/index.js'></script>"
	FunFooter = Temp
End Function


'==================================================
'过程名：FunFocus
'作  用：焦点图
'参  数：
'==================================================
Function FunFocus()
	if ChannelID = "" then
		Exit Function
	end if
	sqlTemp = "select Title,Links,PhotoName from Pulb_SiteAdsFocus where IsShow and ChannelID Like '%,"&ChannelID&",%' order by Orderby"
	set rsTemp = server.createobject("adodb.recordset")
	rsTemp.open sqlTemp,Conn,1,1
	If Not rsTemp.eof Then
		rsTemp.movefirst
		if ChannelID = 0 then
			StyleCName = "focus_in"
		else
			StyleCName = "focus"
		end if
		FunFocus = "<div class='"&StyleCName&"'>"&chr(10)
		if rsTemp.recordcount > 1 then
			FunFocus = FunFocus &"<div id='Slideshow' style='visibility:hidden;'>"&chr(10)
			Do While not rsTemp.eof
				if rsTemp(1) <> "" then
					FunFocus = FunFocus &"<a href='"&rsTemp(1)&"' target='_blank'><img src='/uploadfile/adfile/"&rsTemp(2)&"' /></a>"&chr(10)
				else
					FunFocus = FunFocus &"<a href='#'><img src='/uploadfile/adfile/"&rsTemp(2)&"' /></a>"&chr(10)
				end if
				rsTemp.MoveNext
			loop
			FunFocus = FunFocus &"</div>"
			FunFocus = FunFocus &"<script language='javascript' src='/js/slideshow.js'></script>"&chr(10)
			FunFocus = FunFocus &"<script language='javascript'>		$(function(){$('#Slideshow').KinSlideshow({moveStyle:'down',mouseEvent:'mouseover',isHasTitleBar:false,isHasTitleFont:false,btn:{btn_bgColor:'#666666',btn_bgHoverColor:'#FF6600',btn_fontColor:'#CCCCCC',btn_fontHoverColor:'#000000',btn_fontFamily:'Verdana',btn_borderColor:'#FFFFFF',btn_borderHoverColor:'#FFFFFF',btn_borderWidth:1,btn_bgAlpha:0.8}})})</script>"&chr(10)
		else
			if rsTemp(1) <> "" then
				FunFocus = FunFocus &"<div id='Slideshow'><a href='"&rsTemp(1)&"' target='_blank'><img src='/uploadfile/adfile/"&rsTemp(2)&"' /></a></div>"&chr(10)
			else
				FunFocus = FunFocus &"<div id='Slideshow'><img src='/uploadfile/adfile/"&rsTemp(2)&"' /></div>"&chr(10)
			end if
		end if
		FunFocus = FunFocus &"</div>"&chr(10)
	Else
		FunFocus = ""
	End If
	rsTemp.close
	set rsTemp = nothing
End Function

'==================================================
'过程名：GetGiftState
'作  用：积分兑换状态
'参  数：ID
'==================================================
Function GetSearch(TID, Init)
	TID = 1
%>
				<div class='sear_ty'>旅游搜索</div>
				<div class='sea_form'>
					<form name='FormSeaTour' onSubmit="return sear_Toursubmit();">
					<table width='100%' border='0' cellpadding='0' cellspacing='0'>
						<tr>
							<td width='30%' align='right' height='32'>游玩天数：</td>
							<td width='70%'><SELECT name='xid' autoWidth='true' style='width:120px'>
							<option value='0'>不限</option>
							<option value='1'>1天</option>
							<option value='2'>2天</option>
							<option value='3'>3天</option>
							<option value='3'>4天</option>
							<option value='500'>5天以上</option>
							</SELECT></td>
						</tr>
						<tr>
							<td align='right' height='32'>出发城市：</td>
							<td><INPUT class='searin' id='TourCityFromName' value='西安' name='CityFrom'  /></td>
						</tr>
						<tr>
							<td align='right' height='32'>目的城市：</td>
							<td><INPUT class='searin' id='TourCityToName' value='' name='CityTo' /></td>
						</tr>
						<tr>
							<td height='45' colspan='2' align='center'><input type='submit' value='搜索' name='Submit2' class='input_s in_s'></td>
						</tr>
					</table>
					</form>
				</div>
<%
End Function
%>