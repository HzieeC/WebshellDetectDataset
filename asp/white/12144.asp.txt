<%
If OT_ISOTCMS<>True Then
	response.write("非法访问")
	response.End()
End If


	Dim typeCNarr()
	Set typecnexe=conobj.execute("select IT_ID,IT_theme,IT_level from OT_infoType order by IT_ID DESC")
		If Not typecnexe.EOF Then
			maxTypeID=typecnexe("IT_ID")
			ReDim typeCNarr(maxTypeID)
			Do Until typecnexe.EOF
				typeCNarr(typecnexe("IT_ID"))=typecnexe("IT_theme")
			typecnexe.movenext
			Loop
		End If
	typecnexe.close
	Set typecnexe=Nothing

	Function TypeCN(ID)
		If ID>maxTypeID Then
			TypeCN="[已不存在]"
		Else
			TypeCN=typeCNarr(ID)
		End If
	End Function


' 属性项处理
Function Info_AddiCN(dataID,str)
	addiStr = ""
	addiNum = 0
	addiArr = Array(Array("|audit|","已审"), Array("|img|","滚图"), Array("|thumb|","缩图"), Array("|recom|","推荐"), Array("|marquee|","滚信"), Array("|flash|","幻灯"), Array("|top|","置顶"))
	For n=0 To UBound(addiArr)
		addiNum = addiNum + 1
		If InStr(str,addiArr(n)(0))>0 Then
			addiStr = addiStr & ItemSwitch2("OT_info",dataID,addiArr(n)(1),1,"addition",addiArr(n)(0),addiNum)
		Else
			addiStr = addiStr & ItemSwitch2("OT_info",dataID,addiArr(n)(1),0,"addition",addiArr(n)(0),addiNum)
		End If
	Next
	If addiStr="" Then addiStr = "<br />"

	Info_AddiCN = addiStr
End Function


' 属性项处理
Function Info_AddiBtn(dataID,isAudit,isNew,isHomeThumb,isThumb,isImg,isFlash,isMarquee,isRecom,isTop)
	Info_AddiBtn = ""&_
		ItemSwitchAddi("OT_info",dataID,"已审",isAudit,"isAudit","") &_
		ItemSwitchAddi("OT_info",dataID,"最新",isNew,"isNew","") &_
		ItemSwitchAddi("OT_info",dataID,"首图",isHomeThumb,"isHomeThumb","") &_
		ItemSwitchAddi("OT_info",dataID,"缩图",isThumb,"isThumb","") &_
		ItemSwitchAddi("OT_info",dataID,"滚图",isImg,"isImg","") &_
		ItemSwitchAddi("OT_info",dataID,"幻灯",isFlash,"isFlash","") &_
		ItemSwitchAddi("OT_info",dataID,"滚信",isMarquee,"isMarquee","") &_
		ItemSwitchAddi("OT_info",dataID,"推荐",isRecom,"isRecom","") &_
		ItemSwitchAddi("OT_info",dataID,"置顶",isTop,"isTop","") &_
		""
End Function


' 类别
Function Info_TypeStrCN(str)
	If str="announ" Then
		Info_TypeStrCN = SYS_homeAnnounName
	Else
		newStr = ""
		selStr = 0
		selStr2= 0
		newArr = Split(str,",")
		For n=0 To UBound(newArr)
			newArr(n) = ToInt(newArr(n))
			If newArr(n)>0 Then
				selStr = selStr + 1
				If selStr>1 Then
					selStr2 = selStr2 + 1
					newStr = newStr &"<br />"& String(selStr2,"　") &"┣&nbsp;"& TypeCN(newArr(n)) &""
				Else
					newStr = newStr & TypeCN(newArr(n)) &""
				End If
			End If
		Next
		Info_TypeStrCN = newStr
	End If
End Function
%>