<% 
If OT_ISOTCMS<>True Then
	response.write("非法访问")
	response.End()
End If


'Call File_Write(content,Server.MapPath("../index.htm"))




' 检查文件是否存在
Function File_IsExist(filePath)
    Set fso = CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filePath) then
		   File_IsExist = True	'存在
		Else
		   File_IsExist = False	'不存在
		End if
    Set fso = nothing
End Function

' 读取文件内容
Function File_Read(filePath)
	Dim fso,f
	Set fso=CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filePath) then
			Set f = fso.OpenTextFile(filePath)
				File_Read=f.ReadAll
			f.close
			Set f=Nothing
		Else
		   File_Read = ""
		End if
	Set fso=Nothing
End Function 


' 将内容写入文件里
Function File_Write(content,filePath)
	On Error Resume Next
	Err.Clear

	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		Set f = fso.CreateTextFile(filePath,true)'如果文件名重复将覆盖旧文件
			f.Write content
			If Err Then
				File_Write=False
			Else
				File_Write=True
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function


' 将内容写入文件里，编码为UTF-8
Function File_WriteUTF8(content,filePath)
	On Error Resume Next
	Err.Clear

	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		Set f = fso.CreateTextFile(filePath,true,true)'如果文件名重复将覆盖旧文件
			f.Write content
			If Err Then
				File_WriteUTF8=False
			Else
				File_WriteUTF8=True
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function


' 移动文件
Public Function File_Move(sourFilePath,newFilePath)
	Dim fso
	Set fso = Server.CreateObject("Scripting.FileSystemObject")
	If fso.FileExists(sourFilePath)=True Then
	   fso.MoveFile sourFilePath,newFilePath
	   File_Move = True
	Else
	   File_Move = False
	End if
End Function

' 复制文件
Function File_Copy(TempSource,TempEnd)
	Dim fso
	Set fso = Server.CreateObject("Scripting.FileSystemObject")
'		If fso.FileExists(TempEnd)=False then
'			File_Copy=False
'			Set fso=Nothing
'			Exit Function
'		End IF
		If fso.FileExists(TempSource)=False Then
			File_Copy=False
			Set fso=Nothing
			Exit Function
		End If
    fso.CopyFile TempSource,TempEnd
    Set fso = Nothing 
    File_Copy=True
End Function

' 删除文件
Function File_Del(filename) 
	If filename="" Then
		File_Del = False
	Else
		Set fso=server.CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filename) Then
			fso.DeleteFile filename
			File_Del = True
		Else
			File_Del = False
		End If
	End If
end function

' 删除目录下的所有文件
Function File_DelDir(dirPath)
	Set	fso=server.CreateObject("Scripting.FileSystemObject")
	Set f=fso.GetFolder(dirPath)

	Set fc=f.Files
	For Each f In fc
		If fso.FileExists(f) Then fso.DeleteFile(f)
		'If fso.FolderExists(f) Then fso.DeleteFolder(f)
	Next

	If fso.FolderExists(dirPath) Then fso.DeleteFolder(dirPath)
End Function


' 新建目录,如果存在跳过.
Function File_CreateDir(dirPath)
    Set fsoDir = CreateObject("Scripting.FileSystemObject")
		If Not fsoDir.FolderExists(dirPath) then
		   fsoDir.CreateFolder(dirPath)
		End if
    Set fsoDir = Nothing
End Function


' 检查目录是否存在
Function File_IsDir(folderPath)
    Set fso = CreateObject("Scripting.FileSystemObject")
		If fso.FolderExists(folderPath) then
		   File_IsDir = True	'存在
		Else
		   File_IsDir = False	'不存在
		End if
    Set fso = nothing
End Function


' 获取文件大小
Function File_GetSize(filePath)
    Set fso = CreateObject("Scripting.FileSystemObject")
		If fso.fileexists(filePath) Then
			Set fsoFile=fso.GetFile(filePath)
			File_GetSize = fsoFile.size
			Set fsoFile=Nothing
		Else
		   File_GetSize = -1

		End If
    Set fso = nothing
End Function



' 文件大小（带单位）
Function File_SizeUnit(num)
	sizeNum=num/1048576
	If Int(sizeNum)>=1 Then
		File_SizeUnit=FormatNumber(sizeNum,2,True) &"MB"
	Else
		sizeNum=num/1024
		If Int(sizeNum)>=1 Then
			File_SizeUnit=FormatNumber(sizeNum,2,True) &"KB"
		Else
			File_SizeUnit=num &"bytes"
		End If
	End If
End Function


Function GetFolderList(ByVal dirPath)
	Dim fso,folderObj,subFolderObj,folderI

    Set fso = CreateObject("Scripting.FileSystemObject")
	Set folderObj=fso.GetFolder(dirPath)
	Set subFolderObj=folderObj.Subfolders
	ReDim folderArr(subFolderObj.Count-1)
	folderI=0
	For Each subFolder In subFolderObj
		folderArr(folderI)=subFolder.name &",文件夹,"& subFolder.size &","& subFolder.DateCreated &","& subFolder.DateLastModified &","& subFolder.path
		folderI = folderI + 1 
	Next
	Set folderObj=Nothing
	Set subFolderObj=Nothing

	GetFolderList = folderArr
End Function

Function GetFileList(ByVal dirPath)
	Dim fso,folderObj,fileObj,fileI

	fileI=0
    Set fso = CreateObject("Scripting.FileSystemObject")
	Set folderObj=fso.GetFolder(dirPath)
	Set fileObj=folderObj.Files
	ReDim fileArr(fileObj.Count-1)
	For Each file In fileObj
		fileArr(fileI)=file.name &","& Mid(file.name,InStrRev(file.name,".")+1) &","& file.size &","& file.DateCreated &","& file.DateLastModified &","& file.path
		fileI = fileI + 1 
	Next
	Set fileObj=Nothing
	Set folderObj=Nothing

	GetFileList=fileArr
End Function


Function File_Right_W(filePath)
	If Instr(filePath,":")<=0 Then filePath = server.MapPath(filePath)

	On Error Resume Next
	Err.Clear
	Dim fso,f
	set fso=CreateObject("Scripting.FileSystemObject")
		Set f = fso.CreateTextFile(filePath,true)'如果文件名重复将覆盖旧文件
			f.Write "OTCMS v2.x"
			If Err Then
				Err.Clear
				File_Right_W = False
			Else
				File_Right_W = True
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function


Function File_Right_RW(filePath)
	If Instr(filePath,":")<=0 Then filePath = server.MapPath(filePath)

	On Error Resume Next
	Err.Clear
	Dim fso,f
	File_Right_RW = ""
	readContent = ""
	set fso=CreateObject("Scripting.FileSystemObject")
		set f = fso.OpenTextFile(filePath)
			readContent=f.ReadAll
			If Err Then
				Err.Clear
			Else
				File_Right_RW = File_Right_RW &"R"
			End If
		f.close
		set f=Nothing
		
		Err.Clear
		Set f = fso.CreateTextFile(filePath,true)'如果文件名重复将覆盖旧文件
			f.Write readContent
			If Err Then
				Err.Clear
			Else
				File_Right_RW = File_Right_RW &"W"
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function


Function File_IsDirWrite(ByVal dirPath)
	If Instr(filePath,":")<=0 Then
		dirPath = server.MapPath(dirPath)
	End If
	testPath = dirPath &"/test.txt"

	On Error Resume Next
	Err.Clear
	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		If fso.FolderExists(dirPath)=False Then
			File_IsDirWrite = False
			Exit Function
		End If
		Set f = fso.CreateTextFile(testPath,true)
			f.Write "OTCMS v2.x"
			If Err Then
				Err.Clear
				File_IsDirWrite = False
				Exit Function
			Else
				File_IsDirWrite = True
			End If
		f.Close
		Set f = Nothing
	fso.DeleteFile testPath
	set fso=Nothing
End Function


Function File_Right_DirCW(ByVal dirPath)
	If Instr(filePath,":")<=0 Then
		dirPath = server.MapPath(dirPath &"OTCMS_test_v3.x/")
	End If
	testPath = dirPath &"/test.txt"

	On Error Resume Next
	Err.Clear
	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		If fso.FolderExists(dirPath)=False Then
			fso.CreateFolder(dirPath)
			If Err Then
				Err.Clear
				File_Right_DirCW = False
				Exit Function
			End If
		End If
		Set f = fso.CreateTextFile(testPath,true)
			f.Write "OTCMS v2.x"
			If Err Then
				Err.Clear
				File_Right_DirCW = False
				Exit Function
			Else
				File_Right_DirCW = True
			End If
		f.Close
		Set f = Nothing
	fso.DeleteFile testPath
	fso.deleteFolder dirPath
	set fso=Nothing
End Function

%>
