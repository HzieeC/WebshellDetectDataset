<!-- #include file="codePage.asp" -->
<!---#include file="check.asp"---->
<!-- #include file="inc/file_memberLog.asp" -->
<!-- #include file="inc/file_serverFile.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


Call OpenMemberexe("","login",2)
Call CloseMemberexe()



Select Case mudi
	Case "infoMove"
		Call InfoMove()

	Case "infoMoveSend"
		menuFileID = 13
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call InfoMoveSend()

	Case "infoMoveDel"
		menuFileID = 14
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call InfoMoveDel()

	Case "infoMove3"
		Call InfoMove()

	Case "infoMoveSend3"
		menuFileID = 107
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call InfoMoveSend()

	Case "infoMoveDel3"
		menuFileID = 108
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call InfoMoveDel()

End Select


Call CloseConobj()







'添加、修改滚动信息
Private Sub InfoMove()
	URL			= ToPostStr("URL")
	dataType	= ToPostStr("dataType")
	dataTypeCN	= ToPostStr("dataTypeCN")
	dataID		= ToPostInt("dataID")
	theme		= ToPostStr("theme")
	imgMode		= ToPostStr("imgMode")
	isImg = 0
	img = ""
		If imgMode="upImg" Then
			isImg = 1
			img	= ToPostStr("upImg")
		ElseIf imgMode="URL" Then
			isImg = 2
			img	= ToPostStr("imgURL")
		End If
	webURL		= ToPostStr("webURL")
	alt			= ToPostStr("alt")
	note		= ToPostStr("note")
	startDate	= ToPostStr("startDate")
		If IsDate(startDate)=False Then startDate=Date()
	endDate		= ToPostStr("endDate")
		If IsDate(endDate)=False Then endDate="3000-1-1"
	cost		= ToPostFloat("cost")
	rank		= ToPostInt("rank")
	state		= ToPostInt("state")

	If dataType="" Or theme="" Or webURL="" Then
		Call JS_AlertBackEnd("表单内容接收不全")
	End If
	
	fileAddStr = fileCutStr = ""
	application.lock
		Set dealrec=server.CreateObject("ADODB.recordset")
		dealrec.open "select * from OT_infoMove where IM_ID="& dataID,conobj,0,2
			If dealrec.EOF Then
				If mudi="infoMove3" Then
					menuFileID = 106
					Call IsSecMenuRight("alertBack",menuFileID,dataType)
				Else
					menuFileID = 12
					Call IsSecMenuRight("alertBack",menuFileID,dataType)
				End If
				dealrec.addnew
				alertMode="添加"
				dealrec("IM_type")	= dataType
				dealrec("IM_time")	= Now()
				dealrec("IM_source")= 0
				If imgMode="upImg" And img<>"" Then
					fileAddStr = img
				End If
			Else
				If mudi="infoMove3" Then
					menuFileID = 107
					Call IsSecMenuRight("alertBack",menuFileID,dataType)
				Else
					menuFileID = 13
					Call IsSecMenuRight("alertBack",menuFileID,dataType)
				End If
				alertMode="修改"
				If imgMode="upImg" Or dealrec("IM_imgMode")="upImg" Then
					If imgMode="upImg" And img<>dealrec("IM_img") Then
						fileAddStr = img
					End If
					If dealrec("IM_imgMode")="upImg" And dealrec("IM_img")<>img Then
						fileCutStr = dealrec("IM_img")
					End If
				End If
			End If
			dealrec("IM_imgMode")	= imgMode
			dealrec("IM_isImg")		= isImg
			dealrec("IM_img")		= img
			dealrec("IM_theme")		= theme
			dealrec("IM_URL")		= webURL
			dealrec("IM_alt")		= alt
			dealrec("IM_note")		= note
			dealrec("IM_startDate")	= startDate
			dealrec("IM_endDate")	= endDate
			dealrec("IM_cost")		= cost
			dealrec("IM_rank")		= rank
			dealrec("IM_state")		= state
			dealrec.update
		dealrec.close
		Set dealrec=Nothing
	application.unlock


	Call ServerFile_UseAddMore(fileAddStr)
	Call ServerFile_UseCutMore(fileCutStr)

	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】"& alertMode & alertResult &"！")


	Call JS_AlertHref(""& alertMode &"成功",URL)
End Sub



'发送滚动信息
Private Sub InfoMoveSend()
	dataID=ToGetInt("dataID")
	Set sendexe=conobj.execute("select * from OT_infoMove where IM_ID="& dataID)
		If sendexe.EOF Then
			Call JS_AlertEnd("搜索不到指定记录")
		End If
	response.write(_
	"<script language='javascript' type='text/javascript'>"&_
	"parent.$id('infoTitle').innerHTML='修改';"&_
	"parent.$id('dataID').value='"& sendexe("IM_ID") &"';"&_
	"parent.$id('theme').value="""& ReplaceStr(sendexe("IM_theme"),"js") &""";"&_
	"parent.$id('webURL').value='"& sendexe("IM_URL") &"';"&_
	"parent.$id('rank').value='"& sendexe("IM_rank") &"';"&_
	"")
		If sendexe("IM_imgMode")="upImg" Then
	response.write(_
	"	try {"&_
	"parent.$name('imgMode')[1].checked=true;"&_
	"parent.$id('upImg').value='"& sendexe("IM_img") &"';"&_
	"parent.CheckLogoMode();"&_
	"	}catch (e){}"&_
	"")
		ElseIf sendexe("IM_imgMode")="URL" Then
	response.write(_
	"	try {"&_
	"parent.$name('imgMode')[2].checked=true;"&_
	"parent.$id('imgURL').value='"& sendexe("IM_img") &"';"&_
	"parent.CheckLogoMode();"&_
	"	}catch (e){}"&_
	"")
		Else
	response.write(_
	"	try {"&_
	"parent.$name('imgMode')[0].checked=true;"&_
	"parent.CheckLogoMode();"&_
	"	}catch (e){}"&_
	"")
		End If
	response.write(_
	"	try {"&_
	"parent.$id('alt').value='"& sendexe("IM_alt") &"';"&_
	"parent.$id('startDate').value='"& sendexe("IM_startDate") &"';"&_
	"parent.$id('endDate').value='"& sendexe("IM_endDate") &"';"&_
	"parent.$id('cost').value="""& sendexe("IM_cost") &""";"&_
	"parent.$id('note').value="""& ReplaceStr(sendexe("IM_note"),"js") &""";"&_
	"	}catch (e){}"&_
	"")
		If sendexe("IM_state")=1 Then
	response.write(_
	"parent.$name('state')[0].checked=true;"&_
	"")
		Else
	response.write(_
	"parent.$name('state')[1].checked=true;"&_
	"")
		End If
	response.write(_
	"parent.$id('subButton').src='images/button_rev.gif';"&_
	"</script>")
	sendexe.close
	Set sendexe=Nothing
End Sub



'删除滚动信息
Private Sub InfoMoveDel()
	dataID	= ToGetInt("dataID")
	theme	= ToGetStr("theme")

	If dataID<=0 Then
		Call JS_AlertEnd("指定ID错误")
	End If

	fileCutStr = ""
	Set checkexe=conobj.execute("select IM_imgMode,IM_img from OT_infoMove where IM_ID="& dataID)
		If checkexe.EOF Then
			Call JS_AlertEnd("记录不存在")
		End If
		If checkexe("IM_imgMode")="upImg" Then
			fileCutStr = checkexe("IM_img")
		End If
	checkexe.close
	Set checkexe=Nothing

	conobj.execute("delete from OT_infoMove where IM_ID="& dataID)

		If fileCutStr<>"" Then
	Call ServerFile_UseCutMore(fileCutStr)
		End If

	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】删除"& alertResult &"！")
	
	response.write "<script language='javascript' type='text/javascript'>parent.$id('data"& dataID &"').style.display='none';</script>"
End Sub


%>