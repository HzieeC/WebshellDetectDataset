<%
dbPathPart="../"
%>
<!-- #include file="../check.asp" -->
<!-- #include file="../inc/func_content.asp" -->
<!-- #include file="../inc/func_closeMark.asp" -->
<!-- #include file="../inc_temp/temp_newsList.asp" -->
<!-- #include file="../inc_temp/temp_index.asp" -->
<!-- #include file="../cache/infoSys.asp" -->
<!-- #include file="../file_users.asp" -->
<%
webTypeName	= ""
webTitle	= ""
webTitleAddi= ""
webKey		= "*"
webDesc		= "*"

queryStr=Trim(Request.ServerVariables("QUERY_STRING"))
queryStr=replace(queryStr,".html","")

type1ID	= 0
type1CN = ""
IT_level = 0

If Left(queryStr,5)="list_" Then
	' *** 文章列表页 ***
	webTypeName="list"
	listStr=Right(queryStr,Len(queryStr)-5)

	typeID = 0
	typeStr= ""
	page = 0
	listArr = Split(listStr,"_")
	If IsNum(listArr(0))=True Then
		typeID=ToInt(listArr(0))
	Else
		typeStr=listArr(0)
	End If
	If UBound(listArr)=1 Then
		page = ToInt(listArr(1))
	End If


	If InStr(""& SYS_newsListTitleStr,"|webName|")=0 Then isShowWebTitle=False

'	typeStr	= ToGetStr("typeStr")
	refContent = ""
	areaName = ""
'	typeID	= 0
	newTitle	= "最近更新"
	recomTitle	= "精彩推荐"
	rankTitle	= "点击排行"
	IT_showMode = 1
	IT_showNum	= 10
	Select Case typeStr
		Case "announ"
			webTitle = SYS_homeAnnounName
			areaName = webTitle

		Case "new"
			webTitle = "最新更新"
			areaName = webTitle

		Case Else
			webTitle = "文章栏目"
			areaName = webTitle
'			typeID	= ToGetInt("typeID")
			typeStr = typeID
	'		newTitle	= "本类更新"
			recomTitle	= "本类推荐"
			rankTitle	= "本类排行"
			pointStr = ""
			Set type1exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_fatID,IT_openMode,IT_showMode,IT_showNum,IT_titleAddi,IT_webKey,IT_webDesc from OT_infoType where IT_ID="& typeID)
				If type1exe.EOF Then
					Call JS_AlertHrefEnd("栏目不存在",dbPathPart)
				Else
					type1ID		= type1exe("IT_ID")
					type1CN		= type1exe("IT_theme")
					IT_level	= type1exe("IT_level")
					webTitle	= type1exe("IT_theme")
					webTitleAddi= type1exe("IT_titleAddi")
					webKey		= type1exe("IT_webKey")
					webDesc		= type1exe("IT_webDesc")
					IT_showMode = type1exe("IT_showMode")
					IT_showNum	= type1exe("IT_showNum")
					If type1exe("IT_mode")="url" Then
						type1URL=type1exe("IT_URL")
					ElseIf type1exe("IT_mode")="web" Then
						type1URL=dbPathPart &"dynWeb.asp?dataID="& type1exe("IT_webID")
					Else
						type1URL=Url_NewsListID("",type1exe("IT_ID"),0)
					End If
					pointStr = pointStr &"&nbsp;&gt;&nbsp;<a href='"& type1URL &"' class='font1_2' target='"& type1exe("IT_openMode") &"'>"& type1exe("IT_theme") &"</a>"

					If type1exe("IT_fatID")>0 Then
						Set type2exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_openMode,IT_titleAddi from OT_infoType where IT_ID="& type1exe("IT_fatID"))
							If Not type2exe.EOF Then
								type1ID		= type2exe("IT_ID")
								type1CN		= type2exe("IT_theme")
								If IT_level=0 Then IT_level	= type2exe("IT_level")
								If InStr(""& SYS_newsListTitleStr,"|itemName|")>0 Then webTitle = webTitle & SYS_titleSign & type2exe("IT_theme")
								areaName = type2exe("IT_theme")
								If webTitleAddi="" Then webTitleAddi = type2exe("IT_titleAddi")
								If type2exe("IT_mode")="url" Then
									type2URL=type2exe("IT_URL")
								ElseIf type2exe("IT_mode")="web" Then
									type2URL=dbPathPart &"dynWeb.asp?dataID="& type2exe("IT_webID")
								Else
									type2URL=Url_NewsListID("",type2exe("IT_ID"),0)
								End If
								pointStr = "&nbsp;&gt;&nbsp;<a href='"& type2URL &"' class='font1_2' target='"& type2exe("IT_openMode") &"'>"& type2exe("IT_theme") &"</a>"& pointStr
							End If
						type2exe.close
						Set type2exe=Nothing
					End If
					areaName = type1exe("IT_theme")
				End If
			type1exe.close
			Set type1exe=Nothing
		
	End Select



ElseIf ToInt(Left(queryStr,1))>0 Then
	' *** 文章内容页 ***
	webTypeName="show"
	showArr = Split(queryStr,"_")
	dataID=ToInt(showArr(0))
	If UBound(showArr)=1 Then
		page = ToInt(showArr(1))
	Else
		page = 1
	End If


	If InStr(""& SYS_newsShowTitleStr,"|webName|")=0 Then isShowWebTitle=False

'	dataID	= ToGetInt("dataID")

	areaName = ""
	Set infoexe = conobj.execute("select top 1 IF_type,IF_theme,IF_time,IF_type1ID,IF_type2ID,IF_type3ID,IF_writer,IF_source,IF_themeKey,IF_contentKey,IF_content,IF_pageNum,IF_fileName,IF_file,IF_voteMode,IF_isMarkNews,IF_isReply,IF_addiID,IF_readNum,IF_replyNum,IF_isCheckUser,IF_state from OT_info where IF_ID="& dataID)
		If infoexe.EOF Then
			Call JS_AlertHrefEnd("搜索不到相关记录",dbPathPart)
		End If
		If infoexe("IF_state")=0 Then
			Call JS_AlertHrefEnd("该记录已关闭",dbPathPart)
		End If

		webTitle = infoexe("IF_theme")
		pointStr = ""
	type1ID = infoexe("IF_type1ID")
	type2ID = infoexe("IF_type2ID")
	type3ID = infoexe("IF_type3ID")
	If type1ID=-1 Then
		areaName = SYS_homeAnnounName
		webTitle = webTitle & SYS_titleSign & SYS_homeAnnounName
		pointStr = "&nbsp;&gt;&nbsp;<a href='"& Url_NewsListStr("announ",0) &"' class='font1_2'>"& SYS_homeAnnounName &"</a>"& pointStr
	End If

		If type2ID>0 Then
			Set type2exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_openMode from OT_infoType where IT_ID="& type2ID)
				If Not type2exe.EOF Then
					IT_level = type2exe("IT_level")
					areaName = type2exe("IT_theme")
					If InStr(""& SYS_newsShowTitleStr,"|itemName|")>0 Then webTitle = webTitle & SYS_titleSign & type2exe("IT_theme")
					If type2exe("IT_mode")="url" Then
						type2URL=type2exe("IT_URL")
					ElseIf type2exe("IT_mode")="web" Then
						type2URL=dbPathPart &"dynWeb.asp?dataID="& type2exe("IT_webID")
					Else
						type2URL=Url_NewsListID("",type2exe("IT_ID"),0)
					End If
					pointStr = pointStr &"&nbsp;&gt;&nbsp;<a href='"& type2URL &"' class='font1_2' target='"& type2exe("IT_openMode") &"'>"& type2exe("IT_theme") &"</a>"
				End If
			type2exe.close
			Set type2exe=Nothing
		End If
		If type1ID>0 Then
			Set type1exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_openMode from OT_infoType where IT_ID="& type1ID)
				If Not type1exe.EOF Then
					type1CN		= type1exe("IT_theme")
					If IT_level=0 Then IT_level	= type1exe("IT_level")
					If areaName="" Then areaName=type1exe("IT_theme")
					If InStr(""& SYS_newsShowTitleStr,"|itemName|")>0 Then webTitle = webTitle & SYS_titleSign & type1exe("IT_theme")
					If type1exe("IT_mode")="url" Then
						type1URL=type1exe("IT_URL")
					ElseIf type1exe("IT_mode")="web" Then
						type1URL=dbPathPart &"dynWeb.asp?dataID="& type1exe("IT_webID")
					Else
						type1URL=Url_NewsListID("",type1exe("IT_ID"),0)
					End If
					pointStr = "&nbsp;&gt;&nbsp;<a href='"& type1URL &"' class='font1_2' target='"& type1exe("IT_openMode") &"'>"& type1exe("IT_theme") &"</a>"& pointStr
				End If
			type1exe.close
			Set type1exe=Nothing
		End If
		If areaName="" Then areaName="文章栏目"
		If pointStr="" Then pointStr="&nbsp;"

		IF_type		= infoexe("IF_type")
		IF_theme	= infoexe("IF_theme")
		IF_writer	= infoexe("IF_writer")
		IF_source	= infoexe("IF_source")
		IF_time		= infoexe("IF_time")
		IF_themeKey	= infoexe("IF_themeKey")
		IF_contentKey	= infoexe("IF_contentKey")
		IF_content	= infoexe("IF_content")
		IF_pageNum	= infoexe("IF_pageNum")
		IF_fileName	= infoexe("IF_fileName")
		IF_file		= infoexe("IF_file")
		IF_voteMode	= infoexe("IF_voteMode")
		IF_isMarkNews=infoexe("IF_isMarkNews")
		IF_isReply	= infoexe("IF_isReply")
		IF_addiID	= infoexe("IF_addiID")
		IF_readNum	= infoexe("IF_readNum")
		IF_replyNum	= infoexe("IF_replyNum")
		IF_isCheckUser	= infoexe("IF_isCheckUser")
	infoexe.close
	Set infoexe=Nothing

' If IF_themeKey<>"" Then If IF_contentKey<>"" Then 
	webKey	= IF_themeKey
	webDesc	= ReplaceStr(IF_contentKey,"input")


	content = ""
	If IF_isCheckUser=0 Then
		If IF_pageNum>0 Then
			content	= Content_PageWord(IF_content,dataID,IF_pageNum,"")
		Else
			content	= Content_PageSign(IF_content,dataID,"")
		End If

		Set wordexe = conobj.execute("select KW_theme,KW_URL,KW_useNum from OT_keyWord where KW_isUse=1 order by KW_rank ASC")
			Do Until wordexe.EOF
				If wordexe("KW_useNum")>0 Then
					content = Replace(content,wordexe("KW_theme"),"<a href='"& wordexe("KW_URL") &"' class='keyWord' target='_blank'><strong>"& wordexe("KW_theme") &"</strong></a>",1,wordexe("KW_useNum"))
				Else
					content = Replace(content,wordexe("KW_theme"),"<a href='"& wordexe("KW_URL") &"' class='keyWord' target='_blank'><strong>"& wordexe("KW_theme") &"</strong></a>")
				End If
			wordexe.movenext
			Loop
		wordexe.close
		Set wordexe=Nothing

		content = CloseMark_HTML(AddImgAlt(content,IF_theme))
	'	content = Replace(content,InfoImgAdminDir,InfoImgDir)

		If IF_file<>"" Then
			If IF_fileName<>"" Then
				content = content &_
				"<div style='margin:12px 0 12px 0;'><a href='"& dbPathPart &"deal.asp?mudi=download&dataID="& dataID &"' class='font2_1' target='_blank'><b>附件下载："& IF_fileName &"</b></a></div>"&_
				""
			Else
				content = content &_
				"<div style='margin:12px 0 12px 0;'><a href='"& dbPathPart & DownloadFileDir & IF_file &"' class='font2_1' target='_blank'><b>附件：点击下载</b></a></div>"&_
				""
			End If
		End If

	End If

	newTitle	= "最近更新"
	recomTitle	= "精彩推荐"
	rankTitle	= "点击排行"

	typeID	= 0
	PrevNextWhereStr = ""
	If type2ID>0 Then
		typeID = type2ID
		PrevNextWhereStr = " and IF_type2ID="& typeID &""
	ElseIf type1ID>0 Then
		typeID = type1ID
		PrevNextWhereStr = " and IF_type1ID="& typeID &""
	End If
	If typeID>0 Then
		newTitle	= "本类更新"
		recomTitle	= "本类推荐"
		rankTitle	= "本类排行"
	End If


Else
	Call JS_HrefEnd(dbPathPart)

End If





%>
<!-- #include file="../top.asp" -->
<%

If webTypeName="list" Then
	response.write(_
	"<script language='javascript' type='text/javascript' src='"& dbPathPart &"js/newsList.js'></script>"&_

	"<!-- [OTCMS] -->"&_
	"<div class='height10'></div>"&_
	"<div class='clear'></div>"&_

	"<div id='mainBody'>"&_
		"<div class='areaL' id='newsList'>"&_
		"")

			Call OTCMS_NewsList(areaName,IT_showMode,IT_showNum,typeStr,IT_level)

		response.write(_
		"</div>"&_
		"<div class='areaR'>"&_
		"")
			%>
			<!-- #include file="../news_right.asp" -->
			<%
		response.write(_
		"</div>"&_
		"<div class='clear'></div>"&_
	"</div>"&_
	"<div class='clear'></div>"&_
	"")


Else
	response.write(_
	"<script language='javascript' type='text/javascript' src='"& dbPathPart &"js/newsShow.js'></script>"&_

	"<!-- [OTCMS] -->"&_
	"<div class='height10'></div>"&_
	"<div class='clear'></div>"&_

	"<div id='mainBody'>"&_
		"<div class='areaL'>"&_
			"<div class='pageBox'>"&_
			"<dl>"&_
				"<dt>"& areaName &"</dt>"&_
				"<dd class='webBox'>"&_
					"<input type='hidden' id='dataType' name='dataType' value='"& IF_type &"' />"&_
					"<input type='hidden' id='isReply' name='isReply' value='"& IF_isReply &"' />"&_
					"<input type='hidden' id='infoID' name='infoID' value='"& dataID &"' />"&_
					"<input type='hidden' id='isUserCheck' name='isUserCheck' value='"& IF_isCheckUser &"' />"&_
					"<input type='hidden' id='voteMode' name='voteMode' value='"& IF_voteMode &"' />"&_
					"<input type='hidden' id='pageValue' name='pageValue' value='"& Url_GetShowPageID() &"' />"&_
					"<div class='a'>"&_
						"<h1>"& IF_theme &"</h1>"&_
						"<div class='addi2'>"&_
							"时间："& IF_time &"&nbsp;&nbsp;作者："& IF_writer &"&nbsp;&nbsp;来源："& IF_source &"&nbsp;&nbsp;查看："& IF_readNum &"&nbsp;&nbsp;评论："& IF_replyNum &""&_
						"</div>"&_
						"")

						markStr = ""
						If IF_themeKey<>"" Then
							markStr = "<span class='font2_2'>标签：</span>"
							markArr = Split(IF_themeKey,",")
							For Each mark In markArr
								markStr = markStr &"<a href='"& dbPathPart &"newsList.asp?typeStr=mark&markName="& server.URLEncode(mark) &"' class='font1_2d' target='_blank'>"& mark &"</a>&nbsp;"
							Next
						End If

						response.write(_
				"<script language='javascript' type='text/javascript' src='"& dbPathPart &"deal_js.asp?mudi=newsAddReadNum&dataID="& dataID &"'></script>"&_
						"<div class='clear'></div><div></div>"&_
					"</div>"&_
					"<div class='adClass' style='margin:2px 0 0 0; width:688px; overflow:hidden; text-align:center;'><script type='text/javascript'>OTnews_ads('ot016');</script></div>"&_
					"<div class='clear'></div>"&_
					"<div class='b'>"&_
					"")
							If IS_isContentKey=1 Then
						response.write(_
						"<div class='note'>"&_
							"<b>内容摘要：</b>"& IF_contentKey &""&_
						"</div>"&_
						"")
							End If
						If SYS_isShareNews=1 Then shareNewsStr="<div class='right'>"& SYS_shareNewsCode &"</div>" Else shareNewsStr=""
						response.write(_
						"<div class='adClass' style='float:left; margin:0px; text-align:center;'><script type='text/javascript'>OTnews_ads('ot017');</script></div>"&_
						"<div id='newsContent'>"& content &"</div>"&_
						"<div class='adClass' style='margin:0 auto; width:660px; overflow:hidden; text-align:center;'><script type='text/javascript'>OTnews_ads('ot010');</script></div>"&_
						"<div class='mark'>"&_
							markStr &_
							"<div style='margin-top:8px;'>"& shareNewsStr &"</div><div class='clear'></div>"&_
						"</div>"&_
						"")

						If IF_addiID>0 Then
							Set addiexe=conobj.execute("select top 1 IW_content from OT_infoWeb where IW_ID="& IF_addiID &" and IW_type='news'")
								If Not addiexe.EOF Then
									response.write("<br /><div>"& addiexe("IW_content") &"</div>")
								End If
							addiexe.close
							Set addiexe=Nothing
						End If

						response.write(_
					"</div>"&_
					"")

						If SYS_isNewsVote=1 And IF_voteMode>0 Then
					response.write(_
					"<div id='voteBox' class='d list'>"&_

					"</div>"&_
					"<div class='clear'></div>"&_
					"")
						End If
					
					If IS_prevAndNext>0 Then
						newWeb = ""
						oldWeb = ""
						prevWeb = ""
						nextWeb = ""
						Set checkexe = conobj.execute("select top 1 IF_ID,IF_theme,IF_URL from OT_info where IF_time>"& SqlForTime(IF_time) &" and IF_state=1"& PrevNextWhereStr &" order by IF_time ASC")
							If checkexe.EOF Then
								newWeb = "没有了"
							Else
								If checkexe("IF_URL")<>"" Then webURL=checkexe("IF_URL") Else webURL=Url_NewsShowID(checkexe("IF_ID"),0)
								newWeb = "<a href='"& webURL &"'>"& checkexe("IF_theme") &"</a>"
							End If
						checkexe.close
						Set checkexe = conobj.execute("select top 1 IF_ID,IF_theme,IF_URL from OT_info where IF_time<"& SqlForTime(IF_time) &" and IF_state=1"& PrevNextWhereStr &" order by IF_time DESC")
							If checkexe.EOF Then
								oldWeb = "没有了"
							Else
								If checkexe("IF_URL")<>"" Then webURL=checkexe("IF_URL") Else webURL=Url_NewsShowID(checkexe("IF_ID"),0)
								oldWeb = "<a href='"& webURL &"'>"& checkexe("IF_theme") &"</a>"
							End If
						checkexe.close
						Set checkexe=Nothing

						If IS_prevAndNext=10 Then
							prevWeb = newWeb
							nextWeb = oldWeb
						Else
							prevWeb = oldWeb
							nextWeb = newWeb
						End If
						
						response.write(_
						"<div class='c'>"&_
							"上一篇："& prevWeb &"<br />"&_
							"下一篇："& nextWeb &""&_
						"</div>"&_
						"")
					End If
				response.write(_
				"</dd>"&_
			"</dl>"&_
			"</div>"&_
			"<div class='clear'></div>"&_
			"")

				If SYS_isMarkNews>0 And IF_isMarkNews>0 And Len(IF_themeKey)>0 Then
			response.write(_
			"<div class='markBox'>"&_
			"<dl>"&_
				"<dt>相关文章</dt>"&_
				"<dd class='listArrow3'>"&_
					"<ul>"&_
					"")

						Call OTCMS_MarkNews(dataID,IF_themeKey,10)

					response.write(_
					"</ul>"&_

				"</dd>"&_
			"</dl>"&_
			"</div>"&_
			"<div class='clear'></div>"&_
			"")
				End If

				If SYS_isNewsReply>0 And IF_isReply>0 Then
			username = Users_GetCookies("username")
			OT_userIP	= GetUserIp()
			If username="" Then replyName = "游客" Else replyName = username
			rndMd5 = md5(md5(OT_SiteID & session.sessionID) & OT_SiteID & OT_userIP)
			
			response.write(_
			"<a name='lastReply'></a>"&_
			"<div class='replyBox'>"&_
			"<dl>"&_
				"<dt>相关评论</dt>"&_
				"<dd>"&_
					"<div id='lastReplyBox'></div>"&_
					"<div id='replyList'></div>"&_

					"<div class='height10'></div>"&_
					"<div class='clear'></div>"&_

					"<div id='replyWrite'>"&_
						"<form id='replyForm' name='replyForm' method='post' action='"& dbPathPart &"news_deal.asp?mudi=message' onsubmit='return CheckReplyForm();' onmouseover=""LoadYucmediaJs('replyForm','verCode')"">"&_
						"<script language='javascript' type='text/javascript'>document.write(""<input type='hidden' name='URL' value='""+ document.location.href +""' />"")</script>"&_
						"<input type='hidden' name='dataType' value='"& IF_type &"' />"&_
						"<input type='hidden' id='rndMd5' name='rndMd5' value='"& rndMd5 &"' />"&_
						"<input type='hidden' name='infoID' value='"& dataID &"' />"&_
						"<input type='hidden' name='isReply' value='"& IF_isReply &"' />"&_
						"<table>"&_
						"<tr>"&_
							"<td>"&_
								"<textarea id='replyContent' name='replyContent' style='width:660px; height:80px;'></textarea>"&_
								"<input type='hidden' id='replyContentMaxLen' name='replyContentMaxLen' value='"& SYS_newsReplyMaxLen &"' />"&_
							"</td>"&_
						"</tr>"&_
						"<tr>"&_
							"<td align='left'>"&_
								"<div class='right'><span id='conMaxLenBox' class='font2_2'></span><input type='submit' value='' class='replyBtn button' /></div>"&_
								"评论者：<input type='text' id='replyUser' name='replyUser' value="""& replyName &""" style='width:120px;' maxlength='25' />&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"&_
								"")
									If OT_OpenVerCode=True And SYS_isVerCodeReply=1 Then
								response.write(_
								"验证码："& ShowVerCode("replyForm","") &""&_
								"")
									End If
							response.write(_
							"</td>"&_
						"</tr>"&_
						"</table>"&_
						"</form>"&_
					"</div>"&_
				"</dd>"&_
			"</dl>"&_
			"</div>"&_
			"<div class='clear'></div>"&_
			"")
				End If
		response.write(_
		"</div>"&_
		"<div class='areaR'>"&_
		"")
			%>
			<!-- #include file="../news_right.asp" -->
			<%
		response.write(_
		"</div>"&_
		"<div class='clear'></div>"&_
	"</div>"&_
	"<div class='clear'></div>"&_
	"")


End If
%>

<!-- #include file="../bottom.asp" -->