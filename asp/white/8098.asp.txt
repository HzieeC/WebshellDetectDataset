<%
sub class_menu()
	Response.Write("<a href=""class_list.asp?dbs_class=" & dbs_class & """>类别列表</a> | " &_
		"<a href=""class_add.asp?dbs_class=" & dbs_class & """>添加类别</a>")
end sub

''function class_option(path_root,path_curr)
function class_option(path_curr)
	sql_class="select * from " & dbs_class & " order by RootID,OrderID"
	set rs_class=conn.execute(sql_class)
	temp_class="<option value=""0"">无（作为一级分类）</option>"
	if not rs_class.bof and not rs_class.eof then
		do while not rs_class.eof
			classname=rs_class("classname")
			depth=rs_class("depth")
			for i=1 to depth
				classname="├&nbsp;" & classname
			next
			if rs_class("fullpath")=path_curr then
				temp_class=temp_class & "<option value=""" & rs_class("fullpath") & """ selected>" & classname & "</option>"
			else
				temp_class=temp_class & "<option value=""" & rs_class("fullpath") & """>" & classname & "</option>"
			end if
			rs_class.movenext
		loop
	end if
	class_option=temp_class
end function

function curr_path(path_curr)
	dim arr_classid
	strTemp=""
	arr_classid=split(path_curr,",")
	for i=1 to Ubound(arr_classid)
		classid=trim(arr_classid(i))
		sqlstr="select * from " & dbs_class & " where classid=" & classid
		set rs=conn.execute(sqlstr)
		if i>1 then
			strTemp=strTemp & " &gt;&gt; " & rs("classname")
		else
			strTemp=strTemp & rs("classname")
		end if
	next
	if path_curr=0 then
		strTemp="无（作为一级栏目）"
	end if
	curr_path=strTemp
end function

function get_prev_link(classid)
	sql="select previd,depth from " & dbs_class & " where classid=" & classid
	set rst=conn.execute(sql)
	previd=rst("previd")
	depth=rst("depth")
	if int(depth)=0 then
		link="do/class_move_root_do.asp?dbs_class="&dbs_class&"&rootid="
	else
		link="do/class_move_order_do.asp?dbs_class="&dbs_class&"&orderid="
	end if
	if int(previd)=0 then
		get_prev_link="<font color=""#CCCCCC"">&uarr;</font>"
	else
		get_prev_link="<a href=""" & link & classid & "," & rst("previd") & """>&uarr;</a>"
	end if
end function

function get_next_link(classid)
	sql="select nextid,depth from " & dbs_class & " where classid=" & classid
	set rst=conn.execute(sql)
	nextid=rst("nextid")
	depth=rst("depth")
	if int(depth)=0 then
		link="do/class_move_root_do.asp?dbs_class="&dbs_class&"&rootid="
	else
		link="do/class_move_order_do.asp?dbs_class="&dbs_class&"&orderid="
	end if
	if int(nextid)=0 then
		get_next_link="<font color=""#CCCCCC"">&uarr;</font>"
	else
		get_next_link="<a href=""" & link & classid & "," & rst("nextid") & """>&darr;</a>"
	end if
end function

''功能：重置某级类别下的previd和nextid
''用于：添加、删除类别后重置，或移动修改类别位置排序后重置
''不过：目前添加、删除类别时没用。主要用于修改排序时用到
sub reset_prev_next(sql)
	rs.open sql,conn,3,2
	if not rs.bof and not rs.eof then
		str_pn=""
		i=1
		do while not rs.eof
			str_pn=str_pn & rs("classid") & ","
			rs.movenext
		loop
		str_pn="0," & str_pn & "0"
		arr_pn=split(str_pn,",")
		rs.movefirst
		do while not rs.eof
			rs("previd")=arr_pn(i-1)
			rs("nextid")=arr_pn(i+1)
			rs.update
			i=i+1
			rs.movenext
		loop
	end if
	rs.close
end sub

''重名的错误提示
sub geterror(t_classname,t_fullpath,flag)
	if flag then	''添加
		sql="select count(*) from " & dbs_class & " where classname='" & t_classname & "' and parentpath='" & t_fullpath & "'"
		if get_one(sql,0)>0 then
			call showmessage(3,"错误提示：类别重名！","class_list.asp?dbs_class=" & dbs_class)
		end if
	else	''修改
		sql="select parentpath from " & dbs_class & " where fullpath='" & t_fullpath & "'"
		parentpath=get_one(sql,0)
		sql="select count(*) from " & dbs_class &_
			" where classname='" & t_classname & "' and fullpath<>'" & t_fullpath & "' and parentpath='" & parentpath & "'"
		if get_one(sql,0)>0 then
			call showmessage(3,"错误提示：类别重名！","class_list.asp?dbs_class=" & dbs_class)
		end if
	end if
end sub
%>