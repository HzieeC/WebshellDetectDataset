<!--#include file = "Startup.asp"-->
<!-- #include file="Function.asp" -->
<%
call adminer()
Call Header()
Call ComeUrl()

%>

<%
dim Action,ParentID,SkinID,LayoutID,BrowsePurview,AddPurview,i,FoundErr,ErrMsg
dim SkinCount,LayoutCount
Action=trim(Request("Action"))
ParentID=trim(request("ParentID"))
if ParentID="" then
	ParentID=0
else
	ParentID=CLng(ParentID)
end if
%>
<%
if Action="Add" then
	call AddClass()
	'call Main()
elseif Action="SaveAdd" then
	call SaveAdd()
elseif Action="Del" then
	call DeleteClass()
elseif Action="Modify" then
	call Modify()
	'call Main()
elseif Action="SaveModify" then
	call SaveModify()
elseif Action="UpOrderN" then 
	call UpOrderN() 
elseif Action="DownOrderN" then 
	call DownOrderN() 
elseif Action="OrderN" then
	call OrderN()
elseif Action="Manage" then
	call Main()
else
	'call AddClass()
	call Main()
end if
if FoundErr=True then
	call WriteErrMsg()
end if
%>





<%
Sub AddClass()
%>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312">
<script language="JavaScript" type="text/JavaScript">

function shou(id)
{
if(id==1)
{
document.getElementById("div1").style.display="";
document.getElementById("div2").style.display="none";
}
if(id==2)
{
document.getElementById("div2").style.display="";
document.getElementById("div1").style.display="none";
}
}
</script>
<script language="JavaScript" type="text/JavaScript">


function check()
{
  if (document.form1.ClassName.value=="")
  {
    alert("频道名称不能为空！");
	document.form1.ClassName.focus();
	return false;
  }
}


</script>


<table width="100%" border="0" align=center cellpadding="3" cellspacing="1" bgcolor="#DEDFDE">
  <form name="form1" method="post" action="Channel_Manage.asp">
  	<input type=hidden name=originalfilename>
	<input type=hidden name=savefilename>
	<input type=hidden name=savepathfilename>
    <tr bgcolor="#F7F7F7">
      <td colspan="3" align="center"><STRONG>添 加 非 系 统 频 道 栏 目</STRONG></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">所属频道分类：</td>
      <td><select name="ParentID">
          <%call OtherChannel_Option(0,ParentID)%>
      </select></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">新加频道名称：</td>
      <td><input name="ClassName" type="text" class="input_text" size="30" maxlength="30"></td>
    </tr>

    <tr bgcolor="#FFFFFF">
      <td>网页标题：</td>
      <td><input name="ckey1" type="text" id="ckey1" />
        网页标题内容[可不填，系统直接采用默认内容]</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>频道关键字：</td>
      <td><input name="ckey2" type="text" id="ckey2" size="60" />
        网页关键词[可不填，系统直接采用默认内容]</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>页面描述：</td>
      <td><textarea name="ckey3" cols="30" rows="3" id="ckey3"></textarea>
        网页描述[可不填，系统直接采用默认内容]</td>
    </tr>
			 <tr bgcolor="#FFFFFF">
      <td width="150">排序：</td>
      <td><input name="pai" type="text" class="input_text" value="0" size="3" maxlength="30"></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">频道形式:</td>
      <td><input name="ChannelType" type="radio" onclick="javascript:shou(1)" value="0" checked>
      内容形式
        <input type="radio" name="ChannelType" value="1" onclick="javascript:shou(2)">
      链接形式</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">打开方式:</td>
      <td><input name="OpenType" type="radio" value="0" checked>
      原窗口打开
        <input type="radio" name="OpenType" value="1">
      新窗口打开</td>
    </tr>
    <tr bgcolor="#FFFFFF" id="div2" style="display:none">
      <td width="150">频道链接地址:</td>
      <td><input name="ChannelUrl" type="text" class="input_text" id="ChannelUrl" size="70" maxlength="30"></td>
    </tr>
		 <tr bgcolor="#FFFFFF">
      <td width="150">网页名称：</td>
      <td><input name="url" type="text" class="input_text" size="30" maxlength="30">留空为默认</td>
    </tr>
	 <tr bgcolor="#FFFFFF">
      <td width="150">代码模板：</td>
      <td><input name="moban" type="text" class="input_text" size="30" maxlength="30">生成代码的模板，留空为默认</td>
    </tr>
    <tr bgcolor="#FFFFFF" id="div1">
      <td width="150">频道内容:<br>
        ・换行请按<FONT color=#ff6600>Shift+Enter</FONT><BR>
・另起一段请按<FONT color=#ff6600>Enter</FONT><br></td>
      <td>
			
			<textarea name="Content" style="display:none"></textarea>
			<iframe ID="eWebEditor1" src="aa/ewebeditor.asp?id=Content&style=standardnew&originalfilename=d_originalfilename&savefilename=d_savefilename&savepathfilename=d_savepathfilename" frameborder="0" scrolling="no" width="600" HEIGHT="350"></iframe>
			</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">&nbsp;</td>
      <td><input name="Action" type="hidden" id="Action" value="SaveAdd"><input type="submit" name="Submit" value="　提　交　" class="input_submit" onClick="return check()">　　　
        <input type="reset" name="Submit" value="　重　置　" class="input_submit"></td>
    </tr>
  </form>
</table>
<%
End Sub
%>








<%
Sub Main()
	dim arrShowLine(10)
	for i=0 to ubound(arrShowLine)
		arrShowLine(i)=False
	next
	dim sqlClass,rsClass,i,iDepth
	sqlClass="select * From OtherChannel order by RootID,pai,OrderID"
	set rsClass=server.CreateObject("adodb.recordset")
	rsClass.open sqlClass,oConn,1,1

%>

<table width="100%"  border="0" cellpadding="0" cellspacing="1" bgcolor="#DEDFDE">
  <tr bgcolor="#F7F7F7">
    <td height="25" bgcolor="#F7F7F7"><div align="center"><strong>频道名称</strong></div></td>
    <td width="80"><div align="center"><a href="Channel_Manage.asp?Action=OrderN"><font color="#FF0000">N级频道排序</font></a></div></td>
    <td width="80"><div align="center"><strong>添加子频道</strong></div></td>
    <td width="70"><div align="center"><strong>修改频道</strong></div></td>
    <td width="50"><div align="center"><strong>删除</strong></div></td>
  </tr>

<%
If Not rsClass.Eof Then
Do While Not rsClass.Eof
%>  
  
  <tr bgcolor="#FFFFFF" onmouseout="this.style.backgroundColor=''" onmouseover="this.style.backgroundColor='#F7F8F8'">
    <td><% 
	iDepth=rsClass("Depth")
	if rsClass("NextID")>0 then
		arrShowLine(iDepth)=True
	else
		arrShowLine(iDepth)=False
	end if
	if iDepth>0 then
	  	for i=1 to iDepth 
			if i=iDepth then 
				if rsClass("NextID")>0 then 
					response.write "<img src='images/tree_line1.gif' width='17' height='16' valign='abvmiddle'>" 
				else 
					response.write "<img src='images/tree_line2.gif' width='17' height='16' valign='abvmiddle'>" 
				end if 
			else 
				if arrShowLine(i)=True then
					response.write "<img src='images/tree_line3.gif' width='17' height='16' valign='abvmiddle'>" 
				else
					response.write "<img src='images/tree_line4.gif' width='17' height='16' valign='abvmiddle'>" 
				end if
			end if 
	  	next 
	  end if 
	  if rsClass("Child")>0 then 
	  	response.write "<img src='Images/tree_folder4.gif' width='15' height='15' valign='abvmiddle'>" 
	  else 
	  	response.write "<img src='Images/tree_folder3.gif' width='15' height='15' valign='abvmiddle'>" 
	  end if 
	  if rsClass("Depth")=0 then 
	  	response.write "<b>" 
	  end if 
	  response.write "<a href='Channel_Manage.asp?Action=Modify&ClassID=" & rsClass("ClassID") & "'>" & rsClass("ClassName") & "</a>"
	  if rsClass("Child")>0 then 
	  	response.write "（" & rsClass("Child") & "）" 
	  end if
	  %></td>
	  <td><div align="center"><%=rsClass("pai")%></div></td>
    <td><div align="center"><a href="Channel_Manage.asp?Action=Add&ParentID=<%=rsClass("ClassID")%>">添加子频道</a></div></td>
    <td><div align="center"><a href="Channel_Manage.asp?Action=Modify&ClassID=<%=rsClass("ClassID")%>">修改频道</a></div></td>
    <td><div align="center"><a href="Channel_Manage.asp?Action=Del&ClassID=<%=rsClass("ClassID")%>" onClick="<%if rsClass("Child")>0 then%>return ConfirmDel1();<%else%>return ConfirmDel2();<%end if%>">删除</a></div></td>
  </tr>
  <%
  rsClass.MoveNext
  Loop
  Else
  Response.Write "<tr><td colspan='5' bgcolor='#ffffff'><div align='center'>暂无非系统频道信息！！</div></td></tr>"
  End If
  %>
</table>

<script language="JavaScript" type="text/JavaScript">
function ConfirmDel1()
{
   alert("此频道下还有子频道，必须先删除下属子频道后才能删除此频道！");
   return false;
}

function ConfirmDel2()
{
   if(confirm("确定要删除此频道吗？"))
     return true;
   else
     return false;
	 
}
</script>

<%
End Sub
%>


<%
Sub Modify()
	dim ClassID,sql,rsClass,i
	dim SkinID,LayoutID,BrowsePurview,AddPurview
	ClassID=trim(request("ClassID"))
	if ClassID="" then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>参数不足！</li>"
		exit sub
	else
		ClassID=CLng(ClassID)
	end if
	
	sql="select * From OtherChannel where ClassID=" & ClassID
	set rsClass=server.CreateObject ("Adodb.recordset")
	rsClass.open sql,oConn,1,3
	if rsClass.bof and rsClass.eof then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>找不到指定的频道！</li>"
	else

%>
<script language="JavaScript" type="text/JavaScript">

function shou(id)
{
if(id==1)
{
document.getElementById("div1").style.display="";
document.getElementById("div2").style.display="none";
}
if(id==2)
{
document.getElementById("div2").style.display="";
document.getElementById("div1").style.display="none";
}
}
</script>

<table width="100%" border="0" align=center cellpadding="3" cellspacing="1" bgcolor="#DEDFDE">
  <form name="form1" method="post" action="Channel_Manage.asp">
    <input type=hidden name=originalfilename>
	<input type=hidden name=savefilename>
	<input type=hidden name=savepathfilename>
    <tr bgcolor="#F7F7F7">
      <td colspan="3" align="center"><STRONG>修 改 非 系 统 文 章 栏 目</STRONG></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="200">修改所属频道分类：</td>
      <td><select name="ParentID">
          <%call OtherChannel_Option(0,rsClass("ParentID"))%>
      </select></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>当前所属频道：</td>
      <td><%
	if rsClass("ParentID")<=0 then
	  	response.write "无（作为一级频道）"
	else
    	dim rsParent,sqlParent
		sqlParent="Select * From OtherChannel where ClassID in (" & rsClass("ParentPath") & ") order by Depth"
		set rsParent=server.CreateObject("adodb.recordset")
		rsParent.open sqlParent,oConn,1,1
		do while not rsParent.eof
			for i=1 to rsParent("Depth")
				response.write "&nbsp;&nbsp;&nbsp;"
			next
			if rsParent("Depth")>0 then
				response.write "└"
			end if
			response.write "&nbsp;" & rsParent("ClassName") & "<br>"
			rsParent.movenext
		loop
		rsParent.close
		set rsParent=nothing
	end if
	%></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>修改频道名称：</td>
      <td><input name="ClassName" type="text" class="input_text" size="30" maxlength="30"value="<%=rsClass("ClassName")%>"> <input name="ClassID" type="hidden" id="ClassID" value="<%=rsClass("ClassID")%>"></td>
    </tr>
	
    <tr bgcolor="#FFFFFF">
      <td>网页标题：</td>
      <td><input name="ckey1" type="text" id="ckey1" value="<%=rsClass("ckey1")%>"/>
        网页标题内容[可不填，系统直接采用默认内容]</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>频道关键字：</td>
      <td><input name="ckey2" type="text" id="ckey2" size="60"  value="<%=rsClass("ckey2")%>"/>
        网页关键词[可不填，系统直接采用默认内容]</td>
    </tr>
	 <tr bgcolor="#FFFFFF">
      <td>页面描述：</td>
      <td><textarea name="ckey3" cols="30" rows="3" id="ckey3"><%=rsClass("ckey3")%></textarea>
        网页描述[可不填，系统直接采用默认内容]</td>
    </tr>
				 <tr bgcolor="#FFFFFF">
      <td width="150">排序：</td>
      <td><input name="pai" type="text" class="input_text" value="<%=rsClass("pai")%>" size="3" maxlength="30"></td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">频道形式:</td>
      <td><input name="ChannelType" type="radio" onclick="javascript:shou(1)" value="0" <%If rsClass("ChannelType")=0 Then%>checked<%End If%>>
  内容形式
    <input type="radio" name="ChannelType" value="1" onclick="javascript:shou(2)" <%If rsClass("ChannelType")=1 Then%>checked<%End If%>>
  链接形式</td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td width="150">打开方式:</td>
      <td><input name="OpenType" type="radio" value="0" <%If rsClass("OpenType")=0 Then%>checked<%End If%>>
  原窗口打开
    <input type="radio" name="OpenType" value="1" <%If rsClass("OpenType")=1 Then%>checked<%End If%>>
  新窗口打开</td>
    </tr>
    <tr bgcolor="#FFFFFF" id="div2" <%If rsClass("ChannelType")=0 Then%>style="display:none"<%End If%>>
      <td>频道链接地址:</td>
      <td><input name="ChannelUrl" type="text" class="input_text" id="ChannelUrl" size="70" maxlength="30" value="<%=rsClass("ChannelUrl")%>"></td>
    </tr>
		 <tr bgcolor="#FFFFFF">
      <td width="150">网页名称：</td>
      <td><input name="url" type="text" class="input_text" value="<%=rsClass("url")%>" size="30" maxlength="30">
      留空为默认</td>
    </tr>
	 <tr bgcolor="#FFFFFF">
      <td width="150">代码模板：</td>
      <td><input name="moban" type="text" class="input_text" value="<%=rsClass("moban")%>" size="30" maxlength="30">生成代码的模板，留空为默认</td>
    </tr>
    <tr bgcolor="#FFFFFF" id="div1" <%If rsClass("ChannelType")=1 Then%>style="display:none"<%End If%>>
      <td>频道内容:<br>
・换行请按<FONT color=#ff6600>Shift+Enter</FONT><BR>
・另起一段请按<FONT color=#ff6600>Enter</FONT></td>
      <td>
	  
	  <textarea name="Content" style="display:none"><%
If rsClass("Content")<>"" Then 
	Response.Write Server.HtmlEncode(rsClass("Content"))
End If
%></textarea>
			<iframe ID="eWebEditor1" src="aa/ewebeditor.asp?id=Content&style=standardnew&originalfilename=d_originalfilename&savefilename=d_savefilename&savepathfilename=d_savepathfilename" frameborder="0" scrolling="no" width="600" HEIGHT="350"></iframe>
	  </td>
    </tr>
    <tr bgcolor="#FFFFFF">
      <td>&nbsp;</td>
      <td><input name="Action" type="hidden" id="Action" value="SaveModify"><input type="submit" name="Submit" value="　提　交　" class="input_submit">　　　
        <input type="reset" name="Submit" value="　重　置　" class="input_submit"></td>
    </tr>
  </form>
</table>
<%
end if
rsClass.close
set rsClass=nothing
End Sub
%>







<% 
sub OrderN() 
	dim sqlClass,rsClass,i,iCount,trs,UpMoveNum,DownMoveNum 
	sqlClass="select * From OtherChannel order by RootID,OrderID" 
	set rsClass=server.CreateObject("adodb.recordset") 
	rsClass.open sqlClass,oConn,1,1 
%>
<table width="100%" border="0" align=center cellpadding="3" cellspacing="1" bgcolor="#DEDFDE">
	<tr bgcolor="#F7F7F7">
	  <td colspan="4" align="center" class="title"><strong>N 级 栏 目 排 序</strong></td> 
  </tr> 
  <% 
do while not rsClass.eof 
%> 
  <tr bgcolor="#FFFFFF" class="tdbg" style="padding: 0px 2px;" onmouseover="this.style.backgroundColor='#F7F8F8'" onmouseout="this.style.backgroundColor=''"> 
      <td width="300"> 
    <% 
	for i=1 to rsClass("Depth") 
	  	response.write "&nbsp;&nbsp;&nbsp;" 
	next 
	if rsClass("Child")>0 then 
		response.write "<img src='Images/tree_folder4.gif' width='15' height='15' valign='abvmiddle'>" 
	else 
	  	response.write "<img src='Images/tree_folder3.gif' width='15' height='15' valign='abvmiddle'>" 
	end if 
	if rsClass("ParentID")=0 then 
		response.write "<b>" 
	end if 
	response.write rsClass("ClassName") 
	if rsClass("Child")>0 then 
		response.write "(" & rsClass("Child") & ")" 
	end if 
	%></td> 
<% 
	if rsClass("ParentID")>0 then   '如果不是一级频道，则算出相同深度的频道数目，得到该频道在相同深度的频道中所处位置（之上或者之下的频道数） 
		'所能提升最大幅度应为For i=1 to 该版之上的版面数 
		set trs=oConn.execute("select count(ClassID) From OtherChannel where ParentID="&rsClass("ParentID")&" and OrderID<"&rsClass("OrderID")&"") 
		UpMoveNum=trs(0) 
		if isnull(UpMoveNum) then UpMoveNum=0 
		if UpMoveNum>0 then 
  			response.write "<form action='Channel_Manage.asp?Action=UpOrderN' method='post'><td width='150'>" 
			response.write "<select name=MoveNum size=1 class='input_submit'><option value=0>向上移动</option>" 
			for i=1 to UpMoveNum 
				response.write "<option value="&i&">"&i&"</option>" 
			next 
			response.write "</select>" 
			response.write "<input type=hidden name=ClassID value="&rsClass("ClassID")&">&nbsp;<input type=submit name=Submit value=修&nbsp;改 class='input_submit'>" 
			response.write "</td></form>" 
		else 
			response.write "<td width='150'>&nbsp;</td>" 
		end if 
		trs.close 
		'所能降低最大幅度应为For i=1 to 该版之下的版面数 
		set trs=oConn.execute("select count(ClassID) From OtherChannel where ParentID="&rsClass("ParentID")&" and orderID>"&rsClass("orderID")&"") 
		DownMoveNum=trs(0) 
		if isnull(DownMoveNum) then DownMoveNum=0 
		if DownMoveNum>0 then 
  			response.write "<form action='Channel_Manage.asp?Action=DownOrderN' method='post'><td width='150'>" 
			response.write "<select name=MoveNum size=1 class='input_submit'><option value=0>向下移动</option>" 
			for i=1 to DownMoveNum 
				response.write "<option value="&i&">"&i&"</option>" 
			next 
			response.write "</select>" 
			response.write "<input type=hidden name=ClassID value="&rsClass("ClassID")&">&nbsp;<input type=submit name=Submit value=修&nbsp;改 class='input_submit'>" 
			response.write "</td></form>" 
		else 
			response.write "<td width='150'>&nbsp;</td>" 
		end if 
		trs.close 
	else 
		response.write "<td colspan=2>&nbsp;</td>" 
	end if 
%> 
      <td>&nbsp;</td>
  </tr> 
  <% 
	UpMoveNum=0 
	DownMoveNum=0 
	rsClass.movenext 
loop 
%> 
</table> 
<% 
	rsClass.close 
	set rsClass=nothing 
end sub 
%>








<%
sub SaveAdd()
	dim ClassID,ClassName,IsElite,ShowOnTop,Setting,Readme,ClassMaster,ClassPicUrl,LinkUrl,PrevOrderID
	dim sql,rs,trs
	dim RootID,ParentDepth,ParentPath,ParentStr,ParentName,MaxClassID,MaxRootID
	dim PrevID,NextID,Child
	dim ChannelType,OpenType,ChannelUrl,Content
	dim OriginalFileName,SaveFileName,SavePathFileName,i
	
	If ParentID<>0 Then
		Set oRs=oConn.Execute("Select ChannelType From OtherChannel Where ClassID="&ParentID)
		If Not oRs.Eof Then
			If oRs("ChannelType")=1 Then
				FoundErr=True
				ErrMsg=ErrMsg & "<br><li>父频道为链接形式，不能添加子频道！</li>"
				Exit Sub
			End If
		End If
		oRs.Close
	End If
			

	ClassName=trim(request("ClassName"))'频道名称
	Setting=trim(request("Setting"))'频道设置
	if ClassName="" then'频道名称
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>频道名称不能为空！</li>"
	end if
	
	
	ChannelType=Trim(Request("ChannelType"))
	OpenType=Trim(Request("OpenType"))
	If ChannelType<>"" Then
		If ChannelType=0 Then
			Content=""
			For i = 1 To Request.Form("content").Count
				Content = Content & Request.Form("Content")(i)
			Next
		ElseIf ChannelType=1 Then
			ChannelUrl=Trim(Request("ChannelUrl"))
			If ChannelUrl="" Then
				FoundErr=True
				ErrMsg=ErrMsg&"<br><li>频道链接地址不能为空！</li>"
			End If
		End If
	Else
		FoundErr=True
		ErrMsg=ErrMsg&"<br><li>频道形式参数错误!</li>"
	End If
	'Response.Write ChannelType&OpenType&ChannelUrl&Content
	'Response.Write OriginalFileName&SaveFileName&SavePathFileName
	'Response.end
 

	if FoundErr=True then
		exit sub
	end if
	OriginalFileName = GetSafeStr(Request.Form("OriginalFileName"))
	SaveFileName = GetSafeStr(Request.Form("SaveFileName"))
	SavePathFileName = GetSafeStr(Request.Form("SavePathFileName"))

	
	
	set rs = oConn.execute("select Max(ClassID) From OtherChannel")'ClassID频道ID
	MaxClassID=rs(0)
	if isnull(MaxClassID) then
		MaxClassID=0
	end if
	rs.close
	ClassID=MaxClassID+1
	set rs=oConn.execute("select max(rootid) From OtherChannel")'rootid根频道ID
	MaxRootID=rs(0)
	if isnull(MaxRootID) then
		MaxRootID=0
	end if
	rs.close
	RootID=MaxRootID+1
	
	if ParentID>0 then
		sql="select * From OtherChannel where ClassID=" & ParentID & ""'ParentID父频道ID
		rs.open sql,oConn,1,1
		if rs.bof and rs.eof then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>所属频道已经被删除！</li>"
		end if
		if FoundErr=True then
			rs.close
			set rs=nothing
			exit sub
		else	
			RootID=rs("RootID")'根频道ID
			ParentName=rs("ClassName")'频道名称
			ParentDepth=rs("Depth")'频道层数
			ParentPath=rs("ParentPath")'频道路径
			Child=rs("Child")'子频道数
			ParentPath=ParentPath & "," & ParentID     '得到此频道的父级频道路径
			PrevOrderID=rs("OrderID")'排序ID
			if Child>0 then		
				dim rsPrevOrderID
				'得到与本频道同级的最后一个频道的OrderID排序ID
				set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentID=" & ParentID)
				PrevOrderID=rsPrevOrderID(0)
				set trs=oConn.execute("select ClassID from OtherChannel where ParentID=" & ParentID & " and OrderID=" & PrevOrderID)
				PrevID=trs(0)
				
				'得到同一父频道但比本频道级数大的子频道的最大OrderID排序ID，如果比前一个值大，则改用这个值。
				set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentPath like '" & ParentPath & ",%'")
				if (not(rsPrevOrderID.bof and rsPrevOrderID.eof)) then
					if not IsNull(rsPrevOrderID(0))  then
				 		if rsPrevOrderID(0)>PrevOrderID then
							PrevOrderID=rsPrevOrderID(0)
						end if
					end if
				end if
			else
				PrevID=0
			end if

		end if
		rs.close
	else
		if MaxRootID>0 then
			set trs=oConn.execute("select ClassID from OtherChannel where RootID=" & MaxRootID & " and Depth=0")
			PrevID=trs(0)
			trs.close
		else
			PrevID=0
		end if
		PrevOrderID=0
		ParentPath="0"
	end if

	sql="Select * From OtherChannel Where ParentID=" & ParentID & " AND ClassName='" & ClassName & "'"
	set rs=server.CreateObject("adodb.recordset")
	rs.open sql,oConn,1,1
	if not(rs.bof and rs.eof) then
		FoundErr=True
		if ParentID=0 then
			ErrMsg=ErrMsg & "<br><li>已经存在一级频道：" & ClassName & "</li>"
		else
			ErrMsg=ErrMsg & "<br><li>“" & ParentName & "”中已经存在子频道“" & ClassName & "”！</li>"
		end if
		rs.close
		set rs=nothing
		exit sub
	end if
	rs.close

	sql="Select top 1 * From OtherChannel"
	rs.open sql,oConn,1,3
    rs.addnew
	rs("ClassID")=ClassID'频道ID
   	rs("ClassName")=ClassName'频道名称
	rs("RootID")=RootID'根频道ID

	rs("ParentID")=ParentID'父频道ID
	if ParentID>0 then
		rs("Depth")=ParentDepth+1'Depth频道层数
	else
		rs("Depth")=0
	end if
	rs("ParentPath")=ParentPath'频道路径
	rs("OrderID")=PrevOrderID'排序ID
	rs("Child")=0'子频道数
	rs("PrevID")=PrevID'同级的上一个频道ID
	rs("NextID")=0'同级的下一个频道ID
	
	rs("OriginalFileName") = OriginalFileName
	rs("SaveFileName") = SaveFileName
	rs("SavePathFileName") = SavePathFileName
	rs("ChannelType")=ChannelType
	rs("OpenType")=OpenType
	If ChannelType=0 Then
		rs("Content")=Content
	ElseIf ChannelType=1 Then
		rs("ChannelUrl")=ChannelUrl
	End If
	rs("ckey1")=request("ckey1")
	rs("ckey2")=request("ckey2")
	rs("ckey3")=request("ckey3")
	rs("url")=request("url")
	rs("moban")=request("moban")
	rs("pai")=request("pai")

	
	rs.update
	rs.Close
    set rs=Nothing
	
	'更新与本频道同一父频道的上一个频道的“NextID”字段值
	if PrevID>0 then'PrevID同级的上一个频道ID
		oConn.execute("update OtherChannel set NextID=" & ClassID & " where ClassID=" & PrevID)
	end if
	
	if ParentID>0 then'父频道ID
		'更新其父类的子频道数
		oConn.execute("update OtherChannel set child=child+1 where ClassID="&ParentID)
		
		'更新该频道排序以及大于本需要和同在本分类下的频道排序序号
		oConn.execute("update OtherChannel set OrderID=OrderID+1 where rootid=" & rootid & " and OrderID>" & PrevOrderID)
		oConn.execute("update OtherChannel set OrderID=" & PrevOrderID & "+1 where ClassID=" & ClassID)
	end if
	
	If ParentID=0 Then
		Set rs=Server.CreateObject("Adodb.RecordSet")
		Dim MaxOrderID,MaxClassID1
		set mrs1=oConn.execute("select max(ClassID) from OtherChannel")
		MaxClassID1=mrs1(0)
		set mrs2=oConn.execute("select max(OrderID) from Channel")
		MaxOrderID=mrs2(0)
		'if isnull(MaxOrderID2) then MaxOrderID2=0
		sql="select * from Channel"
		rs.open sql,oconn,1,3
    	rs.addnew
		rs("OrderID")=MaxOrderID+1
    	rs("ChannelName")=ClassName
		rs("ChannelType")=ChannelType
		If ChannelType=1 Then
			rs("LinkUrl")=ChannelUrl
			rs("LinkType")=1
			rs("NotSysChannelID")=MaxClassID1
		ElseIf ChannelType=0 Then
			rs("NotSysChannelID")=MaxClassID1
			rs("LinkType")=0
		End If
		rs("OpenType")=OpenType
		rs("ChannelType")=1
		rs.update
    	rs.Close
	End If

	Response.Write "<p align=center>频道添加成功，3秒后自动返回频道管理页！<script>window.setTimeout(""location.href='Channel_Manage.asp'"",3000);</script></p>"
end sub
%>


<%
sub DeleteClass()
	dim sql,rs,PrevID,NextID,ClassID
	ClassID=trim(Request("ClassID"))
	if ClassID="" then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>参数不足！</li>"
		exit sub
	else
		ClassID=CLng(ClassID)
	end if
	
	sSql="Select ParentID From OtherChannel Where ClassID="&ClassID
	oRs.Open sSql,oConn,1,1
	If Not oRs.Eof Then
		If oRs("ParentID")=0 Then
			sql="Select * From Channel Where NotSysChannelID=" & ClassID
			Set rs=Server.CreateObject("Adodb.RecordSet")
			rs.open sql,oconn,1,3
			if rs.bof and rs.EOF then
				FoundErr=True
				ErrMsg=ErrMsg & "<br><li>找不到指定的频道！</li>"

		    else
				if rs("ChannelType")=0 then
					FoundErr=True
					ErrMsg=ErrMsg & "<br><li>不能删除系统频道</li>"
				end if
			end if
			if FoundErr=True then
				rs.Close
				set rs=Nothing
			else
				rs.delete
				rs.update
				rs.Close
				set rs=Nothing
			end if
		End If
	End If
	oRs.Close


	'删除频道分类下所有新闻中的图片
	Dim sSavePathFileName,aSavePathFileName,i
	sSql = "SELECT SavePathFileName,ClassID FROM OtherChannel WHERE ClassID ="& ClassID
	oRs.Open sSql, oConn, 0, 1
		Do While Not oRs.Eof
		sSavePathFileName = oRs("SavePathFileName")
		' 把带"|"的字符串转为数组
		aSavePathFileName = Split(sSavePathFileName, "|")

		' 删除新闻相关的文件，从文件夹中
		For i = 0 To UBound(aSavePathFileName)
			' 按路径文件名删除文件
			Call DoDelFile(aSavePathFileName(i))
		Next
		oRs.MoveNext
		Loop
	oRs.Close
	
	sql="select ClassID,RootID,Depth,ParentID,Child,PrevID,NextID From OtherChannel where ClassID="&ClassID
	set rs=server.CreateObject ("Adodb.recordset")
	rs.open sql,oConn,1,3
	if rs.bof and rs.eof then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>频道不存在，或者已经被删除</li>"
	else
		if rs("Child")>0 then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>该频道含有子频道，请删除其子频道后再进行删除本频道的操作</li>"
		end if
	end if
	if FoundErr=True then
		rs.close
		set rs=nothing
		exit sub
	end if
	PrevID=rs("PrevID")
	NextID=rs("NextID")
	if rs("Depth")>0 then
		oConn.execute("update OtherChannel set child=child-1 where ClassID=" & rs("ParentID"))'child子频道数,Depth频道层数
	end if
	rs.delete
	rs.update
	rs.close
	set rs=nothing
	
	
	
	'修改上一频道的NextID和下一频道的PrevID
	if PrevID>0 then
		oConn.execute "update OtherChannel set NextID=" & NextID & " where ClassID=" & PrevID
	end if
	if NextID>0 then
		oConn.execute "update OtherChannel set PrevID=" & PrevID & " where ClassID=" & NextID
	end if
	
	
	
	Response.Write "<p align=center>频道删除成功，3秒后自动返回频道管理页！<script>window.setTimeout(""location.href='Channel_Manage.asp'"",3000);</script></p>"
	'response.redirect "Channel_Manage.asp"
		
end sub


' 删除指定的文件
Sub DoDelFile(sPathFile)
	On Error Resume Next
	Dim oFSO
	Set oFSO = Server.CreateObject("Scripting.FileSystemObject")
	oFSO.DeleteFile(Server.MapPath(sPathFile))
	Set oFSO = Nothing
End Sub
%>




<%
sub SaveModify()
	
	dim ClassID,ClassName,sql,rsClass,i
	dim rParentID
	dim trs,rs
	dim ParentID,RootID,Depth,Child,ParentPath,ParentName,iParentID,iParentPath,PrevOrderID,PrevID,NextID
	dim ChannelType,OpenType,ChannelUrl,Content
	dim OriginalFileName,SaveFileName,SavePathFileName
			
	rParentID=trim(request("ParentID"))'父频道ID
	if rParentID="" then
		rParentID=0
	else
		rParentID=CLng(rParentID)
	end if

	If rParentID<>0 Then
		Set oRs=oConn.Execute("Select ChannelType From OtherChannel Where ClassID="&rParentID)
		If Not oRs.Eof Then
			If oRs("ChannelType")=1 Then
				FoundErr=True
				ErrMsg=ErrMsg & "<br><li>父频道为链接形式，不能添加子频道！</li>"
				Exit Sub
			End If
		End If
		oRs.Close
	End If

	ClassName=trim(request("ClassName"))'频道名称
	Setting=trim(request("Setting"))'频道设置
	if ClassName="" then'频道名称
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>频道名称不能为空！</li>"
	end if
	
	
	ChannelType=Trim(Request("ChannelType"))
	OpenType=Trim(Request("OpenType"))
	If ChannelType<>"" Then
		If ChannelType=0 Then
			Content=""
			For i = 1 To Request.Form("content").Count
				Content = Content & Request.Form("Content")(i)
			Next
		ElseIf ChannelType=1 Then
			ChannelUrl=Trim(Request("ChannelUrl"))
			If ChannelUrl="" Then
				FoundErr=True
				ErrMsg=ErrMsg&"<br><li>频道链接地址不能为空！</li>"
			End If
		End If
	Else
		FoundErr=True
		ErrMsg=ErrMsg&"<br><li>频道形式参数错误!</li>"
	End If
	'Response.Write ChannelType&OpenType&ChannelUrl&Content
	'Response.Write OriginalFileName&SaveFileName&SavePathFileName
	'Response.end
 

	if FoundErr=True then
		exit sub
	end if
	OriginalFileName = GetSafeStr(Request.Form("OriginalFileName"))
	SaveFileName = GetSafeStr(Request.Form("SaveFileName"))
	SavePathFileName = GetSafeStr(Request.Form("SavePathFileName"))

	ClassID=trim(request("ClassID"))
	if ClassID="" then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>参数不足！</li>"
		exit sub
	else
		ClassID=CLng(ClassID)
	end if
	
	sql="select * From OtherChannel where ClassID=" & ClassID
	set rsClass=server.CreateObject ("Adodb.recordset")
	rsClass.open sql,oConn,1,3
	if rsClass.bof and rsClass.eof then
		FoundErr=True
		ErrMsg=ErrMsg & "<br><li>找不到指定的频道！</li>"
		rsClass.close
		set rsClass=nothing
		exit sub
	end if
	'更新频道名称
	ClassName=trim(request("ClassName"))
	
	
	If rParentID=0 Then
		sSql="Select * From OtherChannel Where ClassName='"&ClassName&"' And ClassID<>"&ClassID
		oRs.Open sSql,oConn,1,1
		If Not oRs.Eof Then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>重复的频道命名！</li>"
			rsClass.close
			set rsClass=nothing
			exit sub
		End If
		oRs.Close
	End If
	
   	rsClass("ClassName")=ClassName
	
	rsClass("OriginalFileName") = OriginalFileName
	rsClass("SaveFileName") = SaveFileName
	rsClass("SavePathFileName") = SavePathFileName
	rsClass("ChannelType")=ChannelType
	rsClass("OpenType")=OpenType
	If ChannelType=0 Then
		rsClass("Content")=Content
	ElseIf ChannelType=1 Then
		rsClass("ChannelUrl")=ChannelUrl
	End If
	rsclass("ckey1")=request("ckey1")
	rsclass("ckey2")=request("ckey2")
	rsclass("ckey3")=request("ckey3")
	rsclass("url")=request("url")
	rsclass("moban")=request("moban")
	rsclass("pai")=request("pai")
	rsClass.update
	
	
	if rsClass("ParentID")<>rParentID then   '更改了所属频道，则要做一系列检查
		if rParentID=rsClass("ClassID") then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>所属频道不能为自己！</li>"
		end if
		'判断所指定的频道是否为外部频道或本频道的下属频道
		if rsClass("ParentID")=0 then
			if rParentID>0 then
				set trs=oConn.execute("select rootid From OtherChannel where ClassID="&rParentID)'rootid根频道ID
				if trs.bof and trs.eof then
					FoundErr=True
					ErrMsg=ErrMsg & "<br><li>不能指定外部频道为所属频道</li>"
				else
					if rsClass("rootid")=trs(0) then
						FoundErr=True
						ErrMsg=ErrMsg & "<br><li>不能指定该频道的下属频道作为所属频道</li>"
					end if
				end if
				trs.close
				set trs=nothing
			end if
		else
			set trs=oConn.execute("select ClassID From OtherChannel where ParentPath like '"&rsClass("ParentPath")&"," & rsClass("ClassID") & "%' and ClassID="&rParentID)
			if not (trs.eof and trs.bof) then
				FoundErr=True
				ErrMsg=ErrMsg & "<br><li>您不能指定该频道的下属频道作为所属频道</li>"
			end if
			trs.close
			set trs=nothing
		end if
		
	end if

	if FoundErr=True then
		rsClass.close
		set rsClass=nothing
		exit sub
	end if
	
	if rsClass("ParentID")=0 then
		ParentID=rsClass("ClassID")
		iParentID=0
	else
		ParentID=rsClass("ParentID")
		iParentID=rsClass("ParentID")
	end if
	Depth=rsClass("Depth")'频道层数
	Child=rsClass("Child")'子频道数
	RootID=rsClass("RootID")'根频道ID
	ParentPath=rsClass("ParentPath")'频道路径
	PrevID=rsClass("PrevID")'同级的上一个频道ID
	NextID=rsClass("NextID")'同级的下一个频道ID
	rsClass.close
	set rsClass=nothing
	
	
  '假如更改了所属频道
  '需要更新其原来所属频道信息，包括深度、父级ID、频道数、排序、继承版主等数据
  '需要更新当前所属频道信息
  '继承版主数据需要另写函数进行更新--取消，在前台可用ClassID in ParentPath来获得
  dim mrs,MaxRootID
  set mrs=oConn.execute("select max(rootid) From OtherChannel")
  MaxRootID=mrs(0)
  set mrs=nothing
  if isnull(MaxRootID) then
	MaxRootID=0
  end if
  dim k,nParentPath,mParentPath
  dim ParentSql,ClassCount
  dim rsPrevOrderID
  if clng(parentid)<>rParentID and not (iParentID=0 and rParentID=0) then  '假如更改了所属频道
	'更新原来同一父频道的上一个频道的NextID和下一个频道的PrevID
	if PrevID>0 then
		oConn.execute "update OtherChannel set NextID=" & NextID & " where ClassID=" & PrevID
	end if
	if NextID>0 then
		oConn.execute "update OtherChannel set PrevID=" & PrevID & " where ClassID=" & NextID
	end if
	
	if iParentID>0 and rParentID=0 then  	'如果原来不是一级分类改成一级分类
		'得到上一个一级分类频道
		sql="select ClassID,NextID from OtherChannel where RootID=" & MaxRootID & " and Depth=0"
		set rs=server.CreateObject("Adodb.recordset")
		rs.open sql,oConn,1,3
		PrevID=rs(0)      '得到新的PrevID
		rs(1)=ClassID     '更新上一个一级分类频道的NextID的值
		rs.update
		rs.close
		set rs=nothing
		
		MaxRootID=MaxRootID+1
		'更新当前频道数据
		oConn.execute("update OtherChannel set depth=0,OrderID=0,rootid="&maxrootid&",parentid=0,ParentPath='0',PrevID=" & PrevID & ",NextID=0 where ClassID="&ClassID)
		'如果有下属频道，则更新其下属频道数据。下属频道的排序不需考虑，只需更新下属频道深度和一级排序ID(rootid)数据
		if child>0 then
			i=0
			ParentPath=ParentPath & ","
			set rs=oConn.execute("select * From OtherChannel where ParentPath like '%"&ParentPath & ClassID&"%'")
			do while not rs.eof
				i=i+1
				mParentPath=replace(rs("ParentPath"),ParentPath,"")
				oConn.execute("update OtherChannel set depth=depth-"&depth&",rootid="&maxrootid&",ParentPath='"&mParentPath&"' where ClassID="&rs("ClassID"))
				rs.movenext
			loop
			rs.close
			set rs=nothing
		end if
		
		'更新其原来所属频道的频道数，排序相当于剪枝而不需考虑
		oConn.execute("update OtherChannel set child=child-1 where ClassID="&iParentID)
		
	elseif iParentID>0 and rParentID>0 then    '如果是将一个分频道移动到其他分频道下
		'得到当前频道的下属子频道数
		ParentPath=ParentPath & ","
		set rs=oConn.execute("select count(*) From OtherChannel where ParentPath like '%"&ParentPath & ClassID&"%'")
		ClassCount=rs(0)
		if isnull(ClassCount) then
			ClassCount=1
		end if
		rs.close
		set rs=nothing
		
		'获得目标频道的相关信息		
		set trs=oConn.execute("select * From OtherChannel where ClassID="&rParentID)
		if trs("Child")>0 then		
			'得到与本频道同级的最后一个频道的OrderID
			set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentID=" & trs("ClassID"))
			PrevOrderID=rsPrevOrderID(0)
			'得到与本频道同级的最后一个频道的ClassID
			sql="select ClassID,NextID from OtherChannel where ParentID=" & trs("ClassID") & " and OrderID=" & PrevOrderID
			set rs=server.createobject("adodb.recordset")
			rs.open sql,oConn,1,3
			PrevID=rs(0)    '得到新的PrevID
			rs(1)=ClassID     '更新上一个频道的NextID的值
			rs.update
			rs.close
			set rs=nothing
			
			'得到同一父频道但比本频道级数大的子频道的最大OrderID，如果比前一个值大，则改用这个值。
			set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentPath like '" & trs("ParentPath") & "," & trs("ClassID") & ",%'")
			if (not(rsPrevOrderID.bof and rsPrevOrderID.eof)) then
				if not IsNull(rsPrevOrderID(0))  then
			 		if rsPrevOrderID(0)>PrevOrderID then
						PrevOrderID=rsPrevOrderID(0)
					end if
				end if
			end if
		else
			PrevID=0
			PrevOrderID=trs("OrderID")
		end if
		
		'在获得移动过来的频道数后更新排序在指定频道之后的频道排序数据
		oConn.execute("update OtherChannel set OrderID=OrderID+" & ClassCount & "+1 where rootid=" & trs("rootid") & " and OrderID>" & PrevOrderID)
		
		'更新当前频道数据
		oConn.execute("update OtherChannel set depth="&trs("depth")&"+1,OrderID="&PrevOrderID&"+1,rootid="&trs("rootid")&",ParentID="&rParentID&",ParentPath='" & trs("ParentPath") & "," & trs("ClassID") & "',PrevID=" & PrevID & ",NextID=0 where ClassID="&ClassID)
		
		'如果有子频道则更新子频道数据，深度为原来的相对深度加上当前所属频道的深度
		set rs=oConn.execute("select * From OtherChannel where ParentPath like '%"&ParentPath&ClassID&"%' order by OrderID")
		i=1
		do while not rs.eof
			i=i+1
			iParentPath=trs("ParentPath") & "," & trs("ClassID") & "," & replace(rs("ParentPath"),ParentPath,"")
			oConn.execute("update OtherChannel set depth=depth-"&depth&"+"&trs("depth")&"+1,OrderID="&PrevOrderID&"+"&i&",rootid="&trs("rootid")&",ParentPath='"&iParentPath&"' where ClassID="&rs("ClassID"))
			rs.movenext
		loop
		rs.close
		set rs=nothing
		trs.close
		set trs=nothing
		
		'更新所指向的上级频道的子频道数
		oConn.execute("update OtherChannel set child=child+1 where ClassID="&rParentID)
		
		'更新其原父类的子频道数			
		oConn.execute("update OtherChannel set child=child-1 where ClassID="&iParentID)
	else    '如果原来是一级频道改成其他频道的下属频道
		'得到移动的频道总数
		set rs=oConn.execute("select count(*) From OtherChannel where rootid="&rootid)
		ClassCount=rs(0)
		rs.close
		set rs=nothing
		
		'获得目标频道的相关信息		
		set trs=oConn.execute("select * From OtherChannel where ClassID="&rParentID)
		if trs("Child")>0 then		
			'得到与本频道同级的最后一个频道的OrderID
			set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentID=" & trs("ClassID"))
			PrevOrderID=rsPrevOrderID(0)
			sql="select ClassID,NextID from OtherChannel where ParentID=" & trs("ClassID") & " and OrderID=" & PrevOrderID
			set rs=server.createobject("adodb.recordset")
			rs.open sql,oConn,1,3
			PrevID=rs(0)
			rs(1)=ClassID
			rs.update
			set rs=nothing
			
			'得到同一父频道但比本频道级数大的子频道的最大OrderID，如果比前一个值大，则改用这个值。
			set rsPrevOrderID=oConn.execute("select Max(OrderID) From OtherChannel where ParentPath like '" & trs("ParentPath") & "," & trs("ClassID") & ",%'")
			if (not(rsPrevOrderID.bof and rsPrevOrderID.eof)) then
				if not IsNull(rsPrevOrderID(0))  then
			 		if rsPrevOrderID(0)>PrevOrderID then
						PrevOrderID=rsPrevOrderID(0)
					end if
				end if
			end if
		else
			PrevID=0
			PrevOrderID=trs("OrderID")
		end if
	
		'在获得移动过来的频道数后更新排序在指定频道之后的频道排序数据
		oConn.execute("update OtherChannel set OrderID=OrderID+" & ClassCount &"+1 where rootid=" & trs("rootid") & " and OrderID>" & PrevOrderID)
		
		oConn.execute("update OtherChannel set PrevID=" & PrevID & ",NextID=0 where ClassID=" & ClassID)
		set rs=oConn.execute("select * From OtherChannel where rootid="&rootid&" order by OrderID")
		i=0
		do while not rs.eof
			i=i+1
			if rs("parentid")=0 then
				ParentPath=trs("ParentPath") & "," & trs("ClassID")
				oConn.execute("update OtherChannel set depth=depth+"&trs("depth")&"+1,OrderID="&PrevOrderID&"+"&i&",rootid="&trs("rootid")&",ParentPath='"&ParentPath&"',parentid="&rParentID&" where ClassID="&rs("ClassID"))
			else
				ParentPath=trs("ParentPath") & "," & trs("ClassID") & "," & replace(rs("ParentPath"),"0,","")
				oConn.execute("update OtherChannel set depth=depth+"&trs("depth")&"+1,OrderID="&PrevOrderID&"+"&i&",rootid="&trs("rootid")&",ParentPath='"&ParentPath&"' where ClassID="&rs("ClassID"))
			end if
			rs.movenext
		loop
		rs.close
		set rs=nothing
		trs.close
		set trs=nothing
		'更新所指向的上级频道频道数		
		oConn.execute("update OtherChannel set child=child+1 where ClassID="&rParentID)

	end if
  end if
	
	
	If rParentID=0 Then
		Set rs=Server.CreateObject("Adodb.RecordSet")
		Dim MaxOrderID,MaxClassID1
		set mrs1=oConn.execute("select max(ClassID) from OtherChannel")
		MaxClassID1=mrs1(0)
		set mrs2=oConn.execute("select max(OrderID) from Channel")
		MaxOrderID=mrs2(0)
		'if isnull(MaxOrderID2) then MaxOrderID2=0
		sql="select * from Channel Where NotSysChannelID="&ClassID
		rs.open sql,oconn,1,3
		If not rs.eof then
			rs("OrderID")=MaxOrderID+1
    		rs("ChannelName")=ClassName
			rs("ChannelType")=ChannelType
			If ChannelType=1 Then
				rs("LinkUrl")=ChannelUrl
				'rs("NotSysChannelID")=MaxClassID1
				rs("LinkType")=1
			ElseIf ChannelType=0 Then
				'rs("NotSysChannelID")=MaxClassID1
				rs("LinkType")=0
			End If
			rs("OpenType")=OpenType
			rs("ChannelType")=1
			rs.update
		end if
    	rs.Close
	Else
		sSql="Select * From Channel Where NotSysChannelID="&ClassID
		oRs.Open sSql,oConn,1,3
		If Not oRs.Eof Then
			oRs.delete
		End If
		oRs.Close
	End If
	Response.Write "<p align=center>频道修改成功，3秒后自动返回频道管理页！<script>window.setTimeout(""location.href='Channel_Manage.asp'"",3000);</script></p>"
end sub
%>





<%
sub UpOrderN()
	dim sqlOrder,rsOrder,MoveNum,ClassID,i
	dim ParentID,OrderID,ParentPath,Child,PrevID,NextID
	ClassID=Trim(request("ClassID"))
	MoveNum=trim(request("MoveNum"))
	if ClassID="" then
		FoundErr=true
		ErrMsg=ErrMsg & "<br><li>错误参数！</li>"
	else
		ClassID=CLng(ClassID)
	end if
	if MoveNum="" then
		FoundErr=true
		ErrMsg=ErrMsg & "<br><li>错误参数！</li>"
	else
		MoveNum=Cint(MoveNum)
		if MoveNum=0 then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>请选择要提升的数字！</li>"
		end if
	end if
	if FoundErr=True then
		exit sub
	end if

	dim sql,rs,oldorders,ii,trs,tOrderID
	'要移动的频道信息
	set rs=oConn.execute("select ParentID,OrderID,ParentPath,child,PrevID,NextID From OtherChannel where ClassID="&ClassID)
	ParentID=rs(0)
	OrderID=rs(1)
	ParentPath=rs(2) & "," & ClassID
	child=rs(3)
	PrevID=rs(4)
	NextID=rs(5)
	rs.close
	set rs=nothing
	if child>0 then
		set rs=oConn.execute("select count(*) From OtherChannel where ParentPath like '%"&ParentPath&"%'")
		oldorders=rs(0)
		rs.close
		set rs=nothing
	else
		oldorders=0
	end if
	'先修改上一频道的NextID和下一频道的PrevID
	if PrevID>0 then
		oConn.execute "update OtherChannel set NextID=" & NextID & " where ClassID=" & PrevID
	end if
	if NextID>0 then
		oConn.execute "update OtherChannel set PrevID=" & PrevID & " where ClassID=" & NextID
	end if
	
	'和该频道同级且排序在其之上的频道------更新其排序，范围为要提升的数字
	sql="select ClassID,OrderID,child,ParentPath,PrevID,NextID From OtherChannel where ParentID="&ParentID&" and OrderID<"&OrderID&" order by OrderID desc"
	set rs=server.CreateObject("adodb.recordset")
	rs.open sql,oConn,1,3
	i=1
	do while not rs.eof
		tOrderID=rs(1)
		oConn.execute("update OtherChannel set OrderID="&tOrderID+oldorders+i&" where ClassID="&rs(0))
		if rs(2)>0 then
			ii=i+1
			set trs=oConn.execute("select ClassID,OrderID From OtherChannel where ParentPath like '%"&rs(3)&","&rs(0)&"%' order by OrderID")
			if not (trs.eof and trs.bof) then
				do while not trs.eof
					oConn.execute("update OtherChannel set OrderID="&tOrderID+oldorders+ii&" where ClassID="&trs(0))
					ii=ii+1
					trs.movenext
				loop
			end if
			trs.close
			set trs=nothing
		end if
		i=i+1
		if i>MoveNum then
			rs(4)=ClassID
			rs.update
			oConn.execute("update OtherChannel set NextID=" & rs(0) & " where ClassID=" & ClassID)		
			exit do
		end if
		rs.movenext
	loop
	rs.movenext
	if rs.eof then
		oConn.execute("update OtherChannel set PrevID=0 where ClassID=" & ClassID)
	else
		rs(5)=ClassID
		rs.update
		oConn.execute("update OtherChannel set PrevID=" & rs(0) & " where ClassID=" & ClassID)
	end if	
	rs.close
	set rs=nothing
	
	'更新所要排序的频道的序号
	oConn.execute("update OtherChannel set OrderID="&tOrderID&" where ClassID="&ClassID)
	'如果有下属频道，则更新其下属频道排序
	if child>0 then
		i=1
		set rs=oConn.execute("select ClassID From OtherChannel where ParentPath like '%"&ParentPath&"%' order by OrderID")
		do while not rs.eof
			oConn.execute("update OtherChannel set OrderID="&tOrderID+i&" where ClassID="&rs(0))
			i=i+1
			rs.movenext
		loop
		rs.close
		set rs=nothing
	end if
	response.Redirect "Channel_Manage.asp?Action=OrderN"
end sub
%>

<%
sub DownOrderN()
	dim sqlOrder,rsOrder,MoveNum,ClassID,i
	dim ParentID,OrderID,ParentPath,Child,PrevID,NextID
	ClassID=Trim(request("ClassID"))
	MoveNum=trim(request("MoveNum"))
	if ClassID="" then
		FoundErr=true
		ErrMsg=ErrMsg & "<br><li>错误参数！</li>"
		exit sub
	else
		ClassID=Cint(ClassID)
	end if
	if MoveNum="" then
		FoundErr=true
		ErrMsg=ErrMsg & "<br><li>错误参数！</li>"
		exit sub
	else
		MoveNum=Cint(MoveNum)
		if MoveNum=0 then
			FoundErr=True
			ErrMsg=ErrMsg & "<br><li>请选择要下降的数字！</li>"
			exit sub
		end if
	end if

	dim sql,rs,oldorders,ii,trs,tOrderID
	'要移动的频道信息
	set rs=oConn.execute("select ParentID,OrderID,ParentPath,child,PrevID,NextID From OtherChannel where ClassID="&ClassID)
	ParentID=rs(0)
	OrderID=rs(1)
	ParentPath=rs(2) & "," & ClassID
	child=rs(3)
	PrevID=rs(4)
	NextID=rs(5)
	rs.close
	set rs=nothing

	'先修改上一频道的NextID和下一频道的PrevID
	if PrevID>0 then
		oConn.execute "update OtherChannel set NextID=" & NextID & " where ClassID=" & PrevID
	end if
	if NextID>0 then
		oConn.execute "update OtherChannel set PrevID=" & PrevID & " where ClassID=" & NextID
	end if
	
	'和该频道同级且排序在其之下的频道------更新其排序，范围为要下降的数字
	sql="select ClassID,OrderID,child,ParentPath,PrevID,NextID From OtherChannel where ParentID="&ParentID&" and OrderID>"&OrderID&" order by OrderID"
	set rs=server.CreateObject("adodb.recordset")
	rs.open sql,oConn,1,3
	i=0      '同级频道
	ii=0     '同级频道和子频道
	do while not rs.eof
		oConn.execute("update OtherChannel set OrderID="&OrderID+ii&" where ClassID="&rs(0))
		if rs(2)>0 then
			set trs=oConn.execute("select ClassID,OrderID From OtherChannel where ParentPath like '%"&rs(3)&","&rs(0)&"%' order by OrderID")
			if not (trs.eof and trs.bof) then
				do while not trs.eof
					ii=ii+1
					oConn.execute("update OtherChannel set OrderID="&OrderID+ii&" where ClassID="&trs(0))
					trs.movenext
				loop
			end if
			trs.close
			set trs=nothing
		end if
		ii=ii+1
		i=i+1
		if i>=MoveNum then
			rs(5)=ClassID
			rs.update
			oConn.execute("update OtherChannel set PrevID=" & rs(0) & " where ClassID=" & ClassID)		
			exit do
		end if
		rs.movenext
	loop
	rs.movenext
	if rs.eof then
		oConn.execute("update OtherChannel set NextID=0 where ClassID=" & ClassID)
	else
		rs(4)=ClassID
		rs.update
		oConn.execute("update OtherChannel set NextID=" & rs(0) & " where ClassID=" & ClassID)
	end if	
	rs.close
	set rs=nothing
	
	'更新所要排序的频道的序号
	oConn.execute("update OtherChannel set OrderID="&OrderID+ii&" where ClassID="&ClassID)
	'如果有下属频道，则更新其下属频道排序
	if child>0 then
		i=1
		set rs=oConn.execute("select ClassID From OtherChannel where ParentPath like '%"&ParentPath&"%' order by OrderID")
		do while not rs.eof
			oConn.execute("update OtherChannel set OrderID="&OrderID+ii+i&" where ClassID="&rs(0))
			i=i+1
			rs.movenext
		loop
		rs.close
		set rs=nothing
	end if
	response.Redirect "Channel_Manage.asp?Action=OrderN"
end sub
%> 
