<!--#include file="../my/UserChk.asp"-->
<!--#include file="../INC/incuser.asp"-->
<%
TourID = Request.Form("TourID")
OrderNumber = Request.Form("OrderNumber")
InDate = ReplaceBadChar(Request.Form("InDate"))
AdultNum = Request.Form("AdultNum")
ChildNum = Request.Form("ChildNum")

AdultPeople = Replace(ReplaceBadChar(Request.Form("AdultPeople")),", ",",")
AdultCardType = Replace(ReplaceBadChar(Request.Form("AdultCardType")),", ",",")
AdultCardID = Replace(ReplaceBadChar(Request.Form("AdultCardID")),", ",",")

ChildPeople = Replace(ReplaceBadChar(Request.Form("ChildPeople")),", ",",")
'ChildCardType = Replace(ReplaceBadChar(Request.Form("ChildCardType")),", ",",")
ChildCardID = Replace(ReplaceBadChar(Request.Form("ChildCardID")),", ",",")

UserTrueName = ReplaceBadChar(Request.Form("UserTrueName"))
UserMobile = ReplaceBadChar(Request.Form("UserMobile"))
UserTel = ReplaceBadChar(Request.Form("UserTel"))
UserEmail = ReplaceBadChar(Request.Form("UserEmail"))
UserMassage = ReplaceBadChar(Request.Form("UserMassage"))

if InDate = "" then
	Founderr = True
	ErrMsg = "<LI>出发日期不能为空！</LI>"
end if
if not CheckInteger(AdultNum) then
	Founderr = True
	ErrMsg = ErrMsg &"<LI>成人人数必须是整数！</LI>"
end if
if UserTrueName = "" then
	Founderr = True
	ErrMsg = ErrMsg &"<LI>联系人姓名不能为空！</LI>"
end if
if UserMobile = "" then
	Founderr = True
	ErrMsg = ErrMsg &"<LI>联系人手机号码不能为空！</LI>"
end if
If Founderr Then
	Call WriteErrMsg()
	Response.End()
End If

sql="select * from Pulb_TourRoute where IsShow and ID="&TourID
set rs=server.createobject("adodb.recordset")
rs.open sql,Conn,1,1
if not rs.eof then
	ClassID = rs("ClassID")
	Title = rs("Title")
	TourDays = rs("TourDays")
	CityFrom = rs("CityFrom")
	CityTo = rs("CityTo")
	PriceAdultDefault = rs("PriceAdult")
	PriceChildDefault = rs("PriceChild")
	Schedule = rs("Schedule")
	PriceExplain = rs("PriceExplain")
	TopReason = rs("TopReason")
else
	Founderr = True
	ErrMsg = "<LI>没有找到操作对象！</LI>"
end if
rs.close
set rs=nothing
If Founderr Then
	Call WriteErrMsg()
	Response.End()
End If

if IsDate(InDate) then
	OutDate = DateAdd("D", TourDays-1, InDate)
end if

PriceAdult = PriceAdultDefault
PriceChild = PriceChildDefault
OrderPrice = PriceAdult * AdultNum + PriceChild * ChildNum

CurrentPage = "<A href='content.asp?id="&TourID&"'>"& Title &"</a> -> 预订：核对订单"
%><!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312">
<title>核对订单-<%=SystemInfo(1)%></title>
<link rel="stylesheet" href="/css/main.css" type="text/css">
<script language="javascript" src="/js/jquery-1.4.js"></script>
<script language="javascript" src="/js/base.js"></script>
<script language="javascript" src="/js/site.source.js"></script>
</head>

<body>
<%=FunHeader()%>
<div class="main">
	<div class='body'>
		<div class="h_step step2"></div>
	</div>
	<div class='body'>
		<div class="orderb">
			<div class="or_l">
				<h3><a href="content.asp?id=<%=ID%>"><%=Title%></a></h3>
				<p>出游天数：<span style="font-size:16px; color:#FF6600"><%=TourDays%></span>&nbsp;天<br />出发/目的地：<%=CityFrom%><span class='jiantou'>→</span><%=CityTo%><br />班期说明：<%=Schedule%></p>
				<%if PriceExplain <> "" then%>
				<h5>起价说明</h5>
				<p><%=HTMLEncodeStr(PriceExplain)%></p>
				<%end if
				if TopReason <> "" then%>
				<h5>产品经理推荐</h5>
				<p><%=HTMLEncodeStr(TopReason)%></p>
				<%end if%>
			</div>
			<div class="or_main">
				<h3>预订信息</h3>
				<div class="or_su">
					<dl>
						<dt>线路名称：</dt>
						<dd><b><%=Title%></b></dd>
					</dl>
					<dl>
						<dt>出发日期：</dt>
						<dd><b><%=InDate%></b>　成人：<b><span class="price_s"><em>&yen;</em><font><%=PriceAdult%></font></span></b>　儿童：<b><span class="price_s"><em>&yen;</em><font><%=PriceChild%></font></span></b></dd>
					</dl>
					<dl>
						<dt>游客人数：</dt>
						<dd>成人：<%=AdultNum%> 人　　儿童：<%=ChildNum%> 人</dd>
					</dl>
					<dl>
						<dt>成人资料：</dt>
						<dd><%
							tempPeople = Split(AdultPeople,",")
							tempCardType = Split(AdultCardType,",")
							tempCardID = Split(AdultCardID,",")
							set rs=server.createobject("adodb.recordset")
							For j=0 To UBound(tempPeople)
								response.Write tempPeople(j) &"　"
								sql="select ClassName from Pulb_CardType where ID="&Cint(Trim(tempCardType(j)))
								rs.open sql,Conn,1,1
								if not rs.eof then
									response.Write rs(0) &"："
								end if
								rs.close
								response.Write tempCardID(j) &"<br>"
							Next
							set rs=nothing
							%></dd>
					</dl>
					<%if ChildNum > 0 then%>
					<dl>
						<dt>儿童资料：</dt>
						<dd><%
							tempPeople = Split(ChildPeople,",")
							tempCardID = Split(ChildCardID,",")
							For j=0 To UBound(tempPeople)
								response.Write tempPeople(j) &"　出生日期："&tempCardID(j) &"<br>"
							Next
							%></dd>
					</dl>
					<%end if%>
					<dl>
						<dt>订单总价：</dt>
						<dd class="or_p"><span class="price_m2"><em>&yen;</em><font><%=OrderPrice%></font></span></dd>
					</dl>
				</div>
				<h3>联系信息</h3>
				<div class="or_su">
					<dl>
						<dt>联系人姓名：</dt>
						<dd><%=UserTrueName%></dd>
					</dl>
					<dl>
						<dt>手机号码：</dt>
						<dd><%=UserMobile%></dd>
					</dl>
					<dl>
						<dt>联系电话：</dt>
						<dd><%=UserTel%></dd>
					</dl>
					<dl>
						<dt>E-mail：</dt>
						<dd><%=UserEmail%></dd>
					</dl>
					<dl>
						<dt>备注：</dt>
						<dd><%=UserMassage%></dd>
					</dl>
				</div>
				<form name="Form1" method="post" action="orderok.asp">
				<div class="or_but"><a href="#" onClick='Javascript:history.go(-1)'>[返回上一页]</a>　<input type='submit' value='提交订单' name='Submit2' class='input_s orb'></div>
				<input type="hidden" name="OrderNumber" value="<%=OrderNumber%>" />
				<input type="hidden" name="TourID" value="<%=TourID%>" />
				<input type="hidden" name="InDate" value="<%=InDate%>" />
				<input type="hidden" name="OutDate" value="<%=OutDate%>" />
				<input type="hidden" name="AdultNum" value="<%=AdultNum%>" />
				<input type="hidden" name="ChildNum" value="<%=ChildNum%>" />
				
				<input type="hidden" name="AdultPeople" value="<%=AdultPeople%>" />
				<input type="hidden" name="AdultCardType" value="<%=AdultCardType%>" />
				<input type="hidden" name="AdultCardID" value="<%=AdultCardID%>" />
				
				<input type="hidden" name="ChildPeople" value="<%=ChildPeople%>" />
				<input type="hidden" name="ChildCardID" value="<%=ChildCardID%>" />
				
				<input type="hidden" name="UserTrueName" value="<%=UserTrueName%>" />
				<input type="hidden" name="UserMobile" value="<%=UserMobile%>" />
				<input type="hidden" name="UserTel" value="<%=UserTel%>" />
				<input type="hidden" name="UserEmail" value="<%=UserEmail%>" />
				<input type="hidden" name="UserMassage" value="<%=UserMassage%>" />
				<input type="hidden" name="FormOrder" value="true">
				</form>
			</div>
			<div class="clear"></div>
		</div>
	</div>
</div>
<%=FunFooter(0)%>
<%CloseDB%>
</body>
</html>