<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<!-- #include file="inc/file_memberLog.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


Call OpenMemberexe("","login",2)
Call CloseMemberexe()




Select Case mudi
	Case "add","rev"
		Call AddOrRev()

	Case "send"
		menuFileID = 138
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call Send()

	Case "del"
		menuFileID = 139
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call del()

End Select


Call CloseConobj()









'添加与修改
Private Sub AddOrRev()
	URL			= ToPostStr("URL")
	dataType	= ToPostStr("dataType")
	dataTypeCN	= ToPostStr("dataTypeCN")
	dataID		= ToPostInt("dataID")
	theme		= ToPostStr("theme")
	webURL		= ToPostStr("webURL")
	useNum		= ToPostInt("useNum")
	rank		= ToPostInt("rank")
	isUse		= ToPostInt("isUse")

	
	If theme="" Or webURL="" Then
		response.write "<script language='javascript' type='text/javascript'>alert('表单内容接收不全');history.back(-1)</script>"
		response.end
	End If
	
	application.lock
		Set dealrec=server.CreateObject("ADODB.recordset")
		dealrec.open "select top 1 * from OT_keyWord where KW_ID="& dataID,conobj,0,2
			If dealrec.EOF Then
				menuFileID = 137
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				dealrec.addnew
				alertMode="添加"
				dealrec("KW_type")=dataType
			Else
				menuFileID = 138
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				alertMode="修改"
			End If
		dealrec("KW_theme")	= theme
		dealrec("KW_URL")	= webURL
		dealrec("KW_useNum")= useNum
		dealrec("KW_rank")	= rank
		dealrec("KW_isUse")	= isUse
		dealrec.update
		dealrec.close
		Set dealrec=Nothing
	application.unlock


	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】"& alertMode & alertResult &"！")


	Call JS_AlertHref(""& alertMode &"成功！",URL)
End Sub








'数据发送
Private Sub Send()
	numID		= ToGetInt("typeNum")
	dataID		= ToGetInt("dataID")
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")

	Set sendexe=conobj.execute("select top 1 * from OT_keyWord where KW_ID="& dataID)
		If sendexe.EOF Then
			JS_AlertEnd("搜索不到指定记录")
		End If
	response.write(_
	"<script language='javascript' type='text/javascript'>"&_
	"parent.$id('numID').innerHTML='"& numID &"';"&_
	"parent.$id('dataID').value='"& sendexe("KW_ID") &"';"&_
	"parent.$id('theme').value="""& sendexe("KW_theme") &""";"&_
	"parent.$id('webURL').value="""& sendexe("KW_URL") &""";"&_
	"parent.$id('useNum').value='"& sendexe("KW_useNum") &"';"&_
	"parent.$id('rank').value='"& sendexe("KW_rank") &"';"&_
	"parent.$id('isUse').value="""& sendexe("KW_isUse") &""";"&_
	"parent.$id('subButton').src='images/button_rev.gif';"&_
	"</script>")
	sendexe.close
	Set sendexe=Nothing
End Sub









'删除
Private Sub del()
	dataID	= ToGetInt("dataID")
	theme	= ToGetStr("theme")

	If dataID<=0 Then
		Call JS_AlertEnd("指定ID错误！")
	End If

	conobj.execute("delete from OT_keyWord where KW_ID="& dataID)


	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】删除"& alertResult &"！")


	response.write("<script language='javascript' type='text/javascript'>parent.$id('data"& dataID &"').style.display='none'</script>")
End Sub

%>