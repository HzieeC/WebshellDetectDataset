<%@ LANGUAGE="VBSCRIPT" CODEPAGE="65001"%>
<%
'///////////////////////////////////////////////////////////////////////////////
'// 插件应用:    Z-Blog 1.8 Devo 以上
'// 插件制作:    haphic(http://haphic.com)
'// 备    注:    TemplateShortCut
'// 最后修改：   2008-2-6
'// 最后版本:    1.2.SC1
'///////////////////////////////////////////////////////////////////////////////
%>
<% Option Explicit %>
<% On Error Resume Next %>
<% Response.Charset="UTF-8" %>
<% Response.Buffer=True %>
<!-- #include file="../../c_option.asp" -->
<!-- #include file="../../function/c_function.asp" -->
<!-- #include file="../../function/c_function_md5.asp" -->
<!-- #include file="../../function/c_system_lib.asp" -->
<!-- #include file="../../function/c_system_base.asp" -->
<!-- #include file="../../function/c_system_plugin.asp" -->
<!-- #include file="../../function/c_system_event.asp" -->
<!-- #include file="../p_config.asp" -->
<!-- #include file="function.asp" -->
<%

Call System_Initialize()


'检查非法链接
Call CheckReference("")

'检查权限
If BlogUser.Level>1 Then Call ShowError(6) 

If CheckPluginState("TemplateShortCut")=False Then Call ShowError(48)

BlogTitle="TemplateShortCut"

%><!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="<%=ZC_BLOG_LANGUAGE%>" lang="<%=ZC_BLOG_LANGUAGE%>">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta http-equiv="Content-Language" content="<%=ZC_BLOG_LANGUAGE%>" />
	<link rel="stylesheet" rev="stylesheet" href="../../CSS/admin.css" type="text/css" media="screen" />
	<script language="JavaScript" src="../../script/common.js" type="text/javascript"></script>
	<title><%=BlogTitle%></title>
	<style>p{line-height:150%;}textarea{width:99%;font-family:宋体,Verdana;}</style>
</head>
<body>

			<div id="divMain">
<div class="Header">TemplateShortCut - 模板与样式文件列表</div>
<div class="SubMenu">
	<span class="m-left m-now"><a href="TemplateList.asp">[模板与样式文件列表]</a></span>
</div>
<div id="divMain2">
<%Call GetBlogHint()%>
<form id="edit" name="edit" method="post">
<div style="margin:0 10px;float:right;">
	<%If UseCodePress Then%>
		<a href="SaveSetting.asp?type=disable">在编辑时<b>停用 CodePress 代码高亮</b>.(代码高亮只支持IE,FireFox)</a>
	<%Else%>
		<a href="SaveSetting.asp?type=enable">在编辑时<b>启用 CodePress 代码高亮</b>.(代码高亮只支持IE,FireFox)</a>
	<%End If%>
</div>
<div style="margin:0 10px;line-height:150%;">
<p><b>Include 文件夹下的文件:</b></p>
<%
Dim FileList,i,s,t
FileList=LoadIncludeFiles("/THEMES/"& ZC_BLOG_THEME &"/Include/")

t=False

For Each s In FileList

	If Not s="" And Not LCase(s)="index.html" Then
		Response.Write "<strong> ▲ </strong>- <a href=""TemplateEdit.asp?type=include&amp;file="& Server.URLEncode(s) &""">[编辑]</a> - " & s & " - 主题 Include 目录下提供的文件.<br />"
		t=True
	End If

Next

If t=False Then
	Response.Write "<strong> △ </strong>- 主题的 Include 目录为空或不存在.<br />"
End If
%>
<p><b>您当前主题的样式文件:</b></p>
<%
If FileExists(BlogPath & "/THEMES/"& ZC_BLOG_THEME &"/Style/"& ZC_BLOG_CSS & ".asp") Then
	Response.Write "<b style=""color:green""> √ </b>- <a href=""TemplateEdit.asp?type=style&amp;file="& Server.URLEncode(ZC_BLOG_CSS & ".asp") &""">[编辑]</a> - " & ZC_BLOG_CSS & ".asp" & "<br />"
ElseIf FileExists(BlogPath & "/THEMES/"& ZC_BLOG_THEME &"/Style/"& ZC_BLOG_CSS & ".css") Then
	Response.Write "<b style=""color:green""> √ </b>- <a href=""TemplateEdit.asp?type=style&amp;file="& Server.URLEncode(ZC_BLOG_CSS & ".css") &""">[编辑]</a> - " & ZC_BLOG_CSS & ".css" & "<br />"
End If
%>
<p><b>您当前主题的模板文件夹中存在的文件:</b></p>
<%
Response.Flush
FileList=LoadIncludeFiles("/THEMES/"& ZC_BLOG_THEME &"/"& ZC_TEMPLATE_DIRECTORY &"/")

For Each s In FileList
	If GetFileExt(s)="html" Then

		t=False

		For i=0 To 18
			If LCase(aryTemplateFile(i))=LCase(s) Then
				Response.Write "<b style=""color:green""> √ </b>- <a href=""TemplateEdit.asp?type=template&amp;file="& Server.URLEncode(s) &""">[编辑]</a> - " & s & " - " & aryTemplateName(i) & "<br />"
				t=True
			End If
		Next

		If t=False Then
			Response.Write "<b style=""color:red""> ？ </b>- <a href=""TemplateEdit.asp?type=template&amp;file="& Server.URLEncode(s) &""">[编辑]</a> <a href=""SaveSetting.asp?type=del&amp;file="& Server.URLEncode(s) &""" onclick='return window.confirm("""& ZC_MSG058 &""");'>[删除]</a> - " & s & " - " & "自建非默认模板, 请自行确认用途." & "<br />"
		End If

	Else

		If s <> "" Then Response.Write "<b style=""color:red""> × </b>- <a href=""SaveSetting.asp?type=del&amp;file="& Server.URLEncode(s) &""" onclick='return window.confirm("""& ZC_MSG058 &""");'>[删除]</a> - " & s & " - " & "并非模板, 建议删除." & "<br />"

	End If

Response.Flush
Next

Erase FileList
%>
</div>
</form>

<div style="margin:10px;line-height:150%;color:#666;">
<p><b>留言板模板: </b>
留言板模板为可选模板, 如不存在, 则留言板将使用相应的文章页模板.<br/>
您可以自行增加这两个模板以实现对留言板的单独控制, 新增的留言板模板与相应的文章页模板相同.<br/>
<b>但如果原主题含有留言板模板, 请不要随意取消.</b>
</p>
<blockquote>
<%
If FileExists(BlogPath & "/THEMES/"& ZC_BLOG_THEME &"/"& ZC_TEMPLATE_DIRECTORY &"/"& aryTemplateFile(3)) Then
	Response.Write "<strong> ● </strong>- <a href=""TemplateEdit.asp?type=template&amp;file="& Server.URLEncode(aryTemplateFile(3)) &""">[编辑]</a> - <a href=""SaveSetting.asp?type=del&amp;file="& Server.URLEncode(aryTemplateFile(3)) &""" onclick='return window.confirm("""& ZC_MSG058 &""");'>[取消该模板]</a> - " & aryTemplateFile(3) & " - " & aryTemplateName(3) & "<br />"
Else
	Response.Write "<strong> ○ </strong>- <a href=""SaveSetting.asp?type=add&amp;fileNum="& Server.URLEncode(3) &""">[增加该模板]</a> - " & aryTemplateFile(3) & " - " & aryTemplateName(3) & "<br />"
End If

If FileExists(BlogPath & "/THEMES/"& ZC_BLOG_THEME &"/"& ZC_TEMPLATE_DIRECTORY &"/"& aryTemplateFile(18)) Then
	Response.Write "<strong> ● </strong>- <a href=""TemplateEdit.asp?type=template&amp;file="& Server.URLEncode(aryTemplateFile(18)) &""">[编辑]</a> - <a href=""SaveSetting.asp?type=del&amp;file="& Server.URLEncode(aryTemplateFile(18)) &""" onclick='return window.confirm("""& ZC_MSG058 &""");'>[取消该模板]</a> - " & aryTemplateFile(18) & " - " & aryTemplateName(18) & "<br />"
Else
	Response.Write "<strong> ○ </strong>- <a href=""SaveSetting.asp?type=add&amp;fileNum="& Server.URLEncode(18) &""">[增加该模板]</a> - " & aryTemplateFile(18) & " - " & aryTemplateName(18) & "<br />"
End If
%>
</blockquote>
</div>

</div>
</div>
</body>
</html>
<%
Call System_Terminate()

If Err.Number<>0 then
  Call ShowError(0)
End If
%>

