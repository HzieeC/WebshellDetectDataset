<!--#include file="inc/const.asp"-->
<!--#include file="inc/check.asp"-->
<!--#include file="inc/head.inc"-->
<!--#include file="../inc/ubbcode.asp"-->
<!--#include file="../inc/cls_editor.asp"-->
<!--#include file="flash.asp"-->
<%
Call InnerLocation("发布"&NewAsp.ModuleName)

If CInt(GroupSetting(35)) = 0 Then
	ErrMsg = ErrMsg + "<li>对不起！您没有发布动画的权限，如需要该权限请联系管理员。</li>"
	Founderr = True
End If
If CInt(NewAsp.PostGrade) > CInt(NewAsp.membergrade) Then
	ErrMsg = ErrMsg & "<li>对不起！您没有发布动画的权限，如需要该权限请联系管理员。</li>"
	FoundErr = True
End If
Dim Rs,SQL,i,flashid,classid
Dim DefaultShowMode
flashid = NewAsp.ChkNumeric(Request("flashid"))
If ChannelID = 0 Then ChannelID = 5
classid=0
If ChannelID = 5 Then
	DefaultShowMode = 1		'-- 默认显示模式
Else
	DefaultShowMode = 2		'-- 默认显示模式
End If

Dim InstallDir_ChannelDir
InstallDir_ChannelDir = Trim(NewAsp.InstallDir & NewAsp.ChannelDir)
ubb.BasePath = InstallDir_ChannelDir
ubb.setUbbcode = Join(NewAsp.setUserEditor,"|")
ubb.Keyword = NewAsp.KeywordList

Dim Action:Action = LCase(Request("action"))

Select Case Trim(Action)
	Case "save"
		Call SaveNewFlash
	Case "view"
		Call ViewFlash
	Case Else
		Call showmain
End Select
If Founderr = True Then
	Call ToErrors(ErrMsg)
End If

Sub showmain()
	If Founderr = True Then Exit Sub
%>
<script language=JavaScript>
var _maxCount = '<%=CLng(GroupSetting(39))%>';

function doSubmit(){
	var form1 = document.myform;
	var strLen=0;
	try{
		strLen=GetContentLength();
	}
	catch(e){
		strLen=form1.content.value.length;
	}
	if(strLen < 2){
		alert("简介不能小于2个字符！");
		return false;
	}
	if(strLen > _maxCount){
		alert("简介不能超过"+_maxCount+"个字符！");
		return false;
	}

	if (form1.title.value==""){
		alert("标题不能为空！");
		form1.title.focus();
		return false;
	}
	if (form1.showurl.value==""){
		alert("URL不能为空！");
		form1.showurl.focus();
		return false;
	}
	if (form1.classid.value==""){
		alert("该一级分类已经有下属分类，请选择其下属分类！");
		form1.classid.focus();
		return false;
	}
	if (form1.classid.value=="0"){
		alert("该分类是外部连接，不能添加内容！");
		form1.classid.focus();
		return false;
	}
	if (form1.filesize.value==""){
		alert("大小还没有填写！");
		form1.filesize.focus();
		return false;
	}
	<%If CInt(GroupSetting(2)) = 1 Then%>
	if (form1.checkcode.value==""){
		alert("请填写验证码！");
		form1.checkcode.focus();
		return false;
	}
	<%End If%>
}
</script>
<div onkeydown="CtrlEnter()">
<table class="UserTableBorder" cellspacing="1" cellpadding="3" align="center" border="0">
	<tr>
		<th colspan="2">&gt;&gt;发布<%=NewAsp.ModuleName%>&lt;&lt;</th>
	</tr>
	<form action="flashpost.asp" method="post" onsubmit="return doSubmit()" name="myform">
	<input type="Hidden" name="action" value="save"/>
	<input type="hidden" name="ChannelID" value="<%=ChannelID%>"/>
		<tr>
			<td class="UserTableRow2" nowrap="nowrap" align="right" width="15%"><strong>所属分类</strong></td>
			<td class="UserTableRow1" width="85%"><%
	Call selectClasslist(ChannelID,classid)
%></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong><%=NewAsp.ModuleName%>标题</strong></td>
			<td class="UserTableRow1"><input size="60" name="title" type="text" /> <font color="#ff0000">*</font></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>相关<%=NewAsp.ModuleName%></strong></td>
			<td class="UserTableRow1"><input size="60" name="Related" type="text" /></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>文件大小</strong></td>
			<td class="UserTableRow1"><input id="filesize" size="15" onkeyup="if(isNaN(this.value))this.value=''" name="filesize" value="0" type="text" /> <input type="radio" checked="checked" name="SizeUnit" value="KB" /> KB <input type="radio" name="SizeUnit" value="MB" /> MB <strong>推荐星级</strong><select name="star">
			<option value="5">★★★★★</option>
			<option value="4">★★★★</option>
			<option value="3" selected="selected">★★★</option>
			<option value="2">★★</option>
			<option value="1">★</option>
			</select></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong><%=NewAsp.ModuleName%>作者</strong></td>
			<td class="UserTableRow1"><input size="15" name="Author" type="text" /> <select onChange="Author.value=this.value;" name="font2">
			<option value="" selected="selected">选择作者</option>
			<option value="佚名">佚名</option>
			<option value="本站原创">本站原创</option>
			<option value="不详">不详</option>
			<option value="未知">未知</option>
			<option value="<%=MemberName%>"></option>
			</select> <strong><%=NewAsp.ModuleName%>来源</strong> <input name="ComeFrom" type="text" /> <select onChange="ComeFrom.value=this.value;" name="font1">
			<option value="" selected="selected">选择来源</option>
			<option value="本站原创">本站原创</option>
			<option value="本站整理">本站整理</option>
			<option value="不详">不详</option>
			<option value="转载">转载</option>
			</select></td>
		</tr>
		<tr>
			<td class="usertablerow1" colspan="2"><%
	'-- 编辑器设置
	Dim oEditor,EditorCode
	Set oEditor					= New Editor_Cls
	oEditor.UserMode			= 1
	oEditor.setEditMode			= NewAsp.setUserEditor(0)
	oEditor.ChannelID			= ChannelID
	oEditor.Width				= NewAsp.setUserEditor(2)
	oEditor.Height				= NewAsp.setUserEditor(3)
	oEditor.BasePath			= NewAsp.InstallDir & NewAsp.setAdminEditor(4)
	oEditor.ToolbarSet			= NewAsp.setUserEditor(1)
	oEditor.InstanceName		= "content"
	oEditor.Execute()
	Set oEditor = Nothing
%></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>预览图片</strong></td>
			<td class="UserTableRow1"><input id="ImageUrl" size="60" name="miniature" type="text" /></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>上传图片</strong></td>
			<td class="UserTableRow1"><iframe name="frm_image2" src="upload.asp?ChannelID=<%=ChannelID%>" frameborder="0" width="100%" scrolling="no" height="22" allowTransparency="true"></iframe></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>显示模式</strong></td>
			<td class="UserTableRow1"><%
	Dim ShowModeArray

	ShowModeArray = Array("不显示","FLASH","图片","Media","Real","DCR")
	For i = 0 To UBound(ShowModeArray)
		Response.Write "<input type=""radio"" name=""showmode"" value=""" & i & """ "
		If i = DefaultShowMode  Then Response.Write " checked"
		Response.Write ">" & Trim(ShowModeArray(i)) & " "
		If i = 6 Then Response.Write "<br>"
	Next
%></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong><%=NewAsp.ModuleName%>URL</strong></td>
			<td class="UserTableRow1"><input id="filePath" size="60" name="showurl" type="text" /> <font color="#ff0000">*</font></td>
		</tr>
		<tr>
			<td class="UserTableRow2" align="right"><strong>文件上传</strong></td>
			<td class="UserTableRow1"><iframe name="frm_file2" src="upfile.asp?ChannelID=<%=ChannelID%>&amp;type=flash" frameborder="0" width="100%" scrolling="no" height="60" allowTransparency="true"></iframe></td>
		</tr>
				<%
	If CInt(GroupSetting(2)) = 1 Then
%>
		<tr>
			<td class="UserTableRow2" align="right" height="28"><strong>验证码</strong></td>
			<td class="UserTableRow1"><input name="checkcode" type="text" class="logininput" id="checkcode" size="5" maxlength="4" onfocus="get_checkcode();this.onfocus=null;" onkeyup="ajaxcheckcode('check_code','checkcode');" />
				<span id="img_checkcode"><label style="cursor:pointer;" onclick="get_checkcode();">点击获取验证码</label></span><span id="isok_checkcode"></span> <font color="#ff0000">*</font></td>
		</tr>
		<%
	End If
%>
		<tr align="center">
			<td class="UserTableRow2" colspan="4" height="28"><input class="Button" type="button" onclick="javascript:history.go(-1)" name="go_button" value="返回上一页" /> <input class="Button" type="submit" name="submit_button" value="现在发布" /></td>
		</tr>
	</form>
</table>
</div>
<%
End Sub

Sub SaveNewFlash()
	Dim TextContent,isAccept,AlphaTitle
	If CLng(UserToday(5)) => CLng(GroupSetting(38)) And CLng(GroupSetting(38))>0 Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>您每天最多只能发布<font color=red><b>" & GroupSetting(38) & "</b></font>个"& NewAsp.ModuleName &"，如果还要继续发布请明天再来吧！</li>"
	End If
	If NewAsp.CheckPost=False Then
		ErrMsg = ErrMsg + Postmsg
		FoundErr = True
		Exit Sub
	End If
	If CInt(GroupSetting(2)) = 1 Then
		If Not NewAsp.CodeIsTrue() Then
			ErrMsg = ErrMsg + "<meta http-equiv=""refresh"" content=""2;URL="&Request.ServerVariables("HTTP_REFERER")&"""><li>验证码校验失败，请返回刷新页面再试。两秒后自动返回</li>"
			Founderr = True
			Exit Sub
		End If
	End If
	If Trim(Request.Form("title")) = "" Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>标题不能为空！</li>"
	Else
		AlphaTitle = ubb.CheckSpecialChar(Request.Form("title"))
	End If
	If Len(Request.Form("title")) > 200 Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>标题不能超过200个字符！</li>"
	End If
	If Len(Request.Form("Related")) > 200 Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>相关动画不能超过200个字符！</li>"
	End If
	If Not IsNumeric(Request.Form("star")) Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>推荐星级不能为空。</li>"
	End If
	If Not IsNumeric(Request.Form("ClassID")) Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>该一级分类已经有下属分类，不能添加软件！</li>"
		Exit Sub
	End If
	If CLng(Request.Form("ClassID")) = 0 Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>该分类是外部连接，不能添加软件！</li>"
	End If
	
	TextContent = ""
	For i = 1 To Request.Form("content").Count
		TextContent = TextContent & Request.Form("content")(i)
	Next
	If Trim(TextContent) = "" Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>简介不能为空！</li>"
	End If
	If Len(TextContent) > CLng(GroupSetting(39)) And CLng(GroupSetting(39))>0 Then
		ErrMsg = ErrMsg + "<li>简介不能大于" & GroupSetting(39) & "字符！</li>"
		Founderr = True
	End If
	If NewAsp.setUserEditor(0) <> 0 Then
		TextContent = NewAsp.HTMLEncode(TextContent)
	End If
	TextContent = Re_Replace(TextContent, InstallDir_ChannelDir, "[InstallDir_ChannelDir]")
	Dim ChkPostData
	ChkPostData = NewAsp.NeedIsAudit(TextContent, Request.Form("title") & "" & Request.Form("Related"))
	
	If Not IsNumeric(Request.Form("filesize")) Then
		Founderr = True
		ErrMsg = ErrMsg + "<li>文件大小请输入整数！</li>"
	End If

	If Founderr = True Then Exit Sub
	Call PreventRefresh  '防刷新
	Set Rs = NewAsp.CreateAXObject("ADODB.Recordset")
	SQL = "SELECT * FROM NC_FlashList WHERE (flashid is null)"
	Rs.Open SQL,Conn,1,3
	Rs.Addnew
		Rs("ChannelID") = ChannelID
		Rs("ClassID") = NewAsp.ChkNumeric(Request.Form("ClassID"))
		Rs("SpecialID") = 0'NewAsp.ChkNumeric(Request.Form("SpecialID"))
		Rs("title") = NewAsp.RequestForm(Request.Form("title"),200)
		Rs("ColorMode") = 0
		Rs("FontMode") = 0
		Rs("Introduce") = Html2Ubb(TextContent)
		Rs("Related") = NewAsp.RequestForm(Request.Form("Related"),200)
		Rs("Describe") = ""'NewAsp.RequestForm(Request.Form("Describe"),255)
		If UCase(Request.Form("SizeUnit")) = "MB" Then
			Rs("filesize") = CLng(Request.Form("filesize") * 1024)
		Else
			Rs("filesize") = CLng(Request.Form("filesize"))
		End If
		Rs("Author") = NewAsp.RequestForm(Request.Form("Author"),50)
		Rs("ComeFrom") = NewAsp.RequestForm(Request.Form("ComeFrom"),100)
		Rs("star") = Trim(Request.Form("star"))
		Rs("miniature") = NewAsp.RequestForm(Request.Form("miniature"),255)
		Rs("UserGroup") = 0
		Rs("PointNum") = 0
		Rs("UserName") = Trim(MemberName)
		Rs("addTime") = Now()
		Rs("HtmlFileDate") = Trim(NewAsp.HtmlRndFileName)
		Rs("AllHits") = 0
		Rs("DayHits") = 0
		Rs("WeekHits") = 0
		Rs("MonthHits") = 0
		Rs("HitsTime") = Now()
		Rs("grade") = 0
		Rs("showmode") = NewAsp.ChkNumeric(Request.Form("showmode"))
		Rs("isTop") = 0
		Rs("IsBest") = 0
		Rs("downid") = 0'NewAsp.ChkNumeric(Request.Form("downid"))
		Rs("showurl") = NewAsp.RequestForm(Request.Form("showurl"),255)
		Rs("DownAddress") = ""'Trim(Request.Form("DownAddress"))
		Rs("isUpdate") = 1
		If CInt(NewAsp.IsAuditing) = 1 Or ChkPostData = True Then
			Rs("isAccept") = 0
		Else
			Rs("isAccept") = 1
		End If
		Rs("ForbidEssay") = 0
		Rs("AlphaIndex") = NewAsp.ReadAlpha(AlphaTitle)
	Rs.update
	Rs.Close
	Rs.Open "SELECT TOP 1 flashid FROM NC_FlashList WHERE ChannelID=" & ChannelID & " ORDER BY flashid DESC", Conn, 1, 1
	flashid = Rs("flashid")
	Rs.Close:Set Rs = Nothing
	Dim strUserToday
	strUserToday = UserToday(0) &","& UserToday(1) &","& UserToday(2) &","& UserToday(3) &","& UserToday(4) &","& UserToday(5)+1
	UpdateUserToday(strUserToday)
	If CInt(NewAsp.IsAuditing) = 1 Then
		Call ToSucceed("<li>恭喜您！提交成功。请等待管理员验证后正式发布。</li><li><a href=?action=view&ChannelID=" & ChannelID & "&flashid=" & flashid & ">点击此处查看信息</a></li>")
	Else
		 AddUserPointNum MemberName, 1
		 Call ToSucceed("<li>恭喜您！发布" & NewAsp.ModuleName & "成功。</li><li><a href=?action=view&ChannelID=" & ChannelID & "&flashid=" & flashid & ">点击此处查看信息</a></li>")
	End If
End Sub

%>
<!--#include file="inc/foot.inc"-->