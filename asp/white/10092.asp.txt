<!--#include file="inc/conn.asp"-->
<!--#include file="inc/function.asp"-->
<!--#include file="inc/ubbcode.asp"-->
<!--#include file="inc/clsCache.asp"-->
<%
Dim ItemNum,ListNum,ListSuccesNum,ListFalseNum,newsNumAll
Dim RsItem,SqlItem,ItemEnd,ListEnd,ErrMsg

'项目变量
Dim ItemID,ItemName,LoginType,LoginUrl,LoginPostUrl,LoginUser,LoginPass,LoginFalse
Dim ListStr,LsString,LoString,ListPaingType,LPsString,LPoString,ListPaingStr1,ListPaingStr2,ListPaingID1,ListPaingID2,ListPaingStr3,HsString,HoString,HttpUrlType,HttpUrlStr,CollecListNum,CollecnewsNum

'采集相关的变量
Dim Arr_i,newsUrl

'其它变量
Dim LoginData,LoginResult
Dim Arr_Item,CacheTemp,CollecOrder,OrderTemp

'执行时间变量
Dim StartTime,OverTime

'列表
Dim ListUrl,ListCode,newsArrayCode,newsArray,ListArray,ListPaingNext,ListPaingTemp

CacheTemp=Lcase(trim(request.ServerVariables("SCRIPT_NAME")))
CacheTemp=left(CacheTemp,instrrev(CacheTemp,"/"))
CacheTemp=replace(CacheTemp,"\","_")
CacheTemp=replace(CacheTemp,"/","_")
CacheTemp="ansir" & CacheTemp

ItemNum=Clng(Trim(Request("ItemNum")))
ListNum=Clng(Trim(Request("ListNum")))
ListSuccesNum=Clng(Trim(Request("ListSuccesNum")))
ListFalseNum=Clng(Trim(Request("ListFalseNum")))
newsNumAll=Clng(Trim(Request("newsNumAll")))
ListPaingNext=Trim(Request("ListPaingNext"))

FoundErr=False
ItemEnd=False
ListEnd=False
CollecListNum=0
CollecnewsNum=0

Call SetCache()

If ItemEnd<>True Then
   If (ItemNum-1)>Ubound(Arr_Item,2) then
      ItemEnd=True
   Else
      Call SetItems()
   End If
End If

If ItemEnd<>True Then
   If ListPaingType=0 Then
      If ListNum=1 Then
         ListUrl=ListStr
      Else
         ListEnd=True
      End If
   ElseIf ListPaingType=1 Then
      If ListNum=1 Then
         ListUrl=ListStr
      Else
         If ListPaingNext="" or ListPaingNext="$False$" Then
            ListEnd=True
         Else
            ListPaingNext=Replace(ListPaingNext,"{$ID}","&")
            ListUrl=ListPaingNext
         End If
      End If
   ElseIf ListPaingType=2 Then
      If ListPaingID1>ListPaingID2 then
         If (ListPaingID1-ListNum+1)<ListPaingID2 or (ListPaingID1-ListNum+1)<0 Then
            Listend=True
         Else
            ListUrl=Replace(ListPaingStr2,"{$ID}",Cstr(ListpaingID1-ListNum+1))
         End if
      Else
         If (ListPaingID1+ListNum-1)>ListPaingID2 Then
            ListEnd=True
         Else
            ListUrl=Replace(ListPaingStr2,"{$ID}",CStr(ListPaingID1+ListNum-1))
         End If
      End If      
   ElseIf ListPaingType=3  Then
      ListArray=Split(ListPaingStr3,"|")
      If (ListNum-1)>Ubound(ListArray) Then
         ListEnd=True
      Else
         ListUrl=ListArray(ListNum-1)
      End If    
   End If
   If ListNum>CollecListNum And CollecListNum<>0 Then
      ListEnd=True
   End if
End If

If ItemEnd=True Then
   ErrMsg="<br>列表分析完成"
   ErrMsg=ErrMsg & "<br>成功分析： "  &  ListSuccesNum  &  "  页列表,失败： "    &  ListFalseNum  &  "  页,信息：" & newsNumAll & "  条"
   ErrMsg=ErrMsg& "<br>正在整理数据，稍后进行信息的采集..."
   ErrMsg=ErrMsg & "<meta http-equiv=""refresh"" content=""3;url=Admin_ItemCollecnews.asp?ItemNum=1&newsNum=1&newsSuccesNum=0&newsFalseNum=0&ImagesNumAll=0&newsNumAll=" & newsNumAll & """>"
Else
   If ListEnd=True Then
      ItemNum=ItemNum+1
      ListNum=1
      ErrMsg="<br>" & ItemName & "  项目所有列表分析完成，正在整理数据请稍后..."
      ErrMsg=ErrMsg & "<meta http-equiv=""refresh"" content=""3;url=Admin_ItemCollecSteady.asp?ItemNum=" & ItemNum & "&ListNum=" & ListNum & "&ListSuccesNum=" & ListSuccesNum & "&ListFalseNum=" & ListFalseNum & "&newsNumAll=" & newsNumAll & """>"
   End If
End If

Call TopItem()
If ItemEnd<>True And ListEnd<>True Then
   FoundErr=False
   ErrMsg=""
   Call StartCollection()
End  If

Call WriteSucced(ErrMsg)
Response.Flush()
%>

<%
'==================================================
'过程名：StartCollection
'作  用：开始采集
'参  数：无
'==================================================
Sub StartCollection

'第一次采集时登录
If LoginType=1 And ListNum=1 then
   LoginData=UrlEncoding(LoginUser & "&" & LoginPass)
   LoginResult=PostHttpPage(LoginUrl,LoginPostUrl,LoginData)
   If Instr(LoginResult,LoginFalse)>0 Then
      FoundErr=True
      ErrMsg=ErrMsg & "<br><li>在登录网站时发生错误，请确保登录信息的正确性！</li>"
   End If
End If


If FoundErr<>True then
   ListCode=GetHttpPage(ListUrl,unicode)
   Call GetListPaing()
   If ListCode="$False$" Then
      FoundErr=True
      ErrMsg=ErrMsg & "<br><li>在获取列表：" & ListUrl & "网页源码时发生错误！</li>"
   Else
      ListCode=GetBody(ListCode,LsString,LoString,False,False)
      If ListCode="$False$" Or ListCode="" Then
         FoundErr=True
         ErrMsg=ErrMsg & "<br><li>在截取：" & ListUrl & "的信息列表时发生错误！</li>"
      End If
   End If
End If

If FoundErr<>True Then
   newsArrayCode=GetArray(ListCode,HsString,HoString,False,False)
   If newsArrayCode="$False$" Then
      FoundErr=True
      ErrMsg=ErrMsg & "<br><li>在分析：" & ListUrl & "信息列表时发生错误！</li>"
   Else
      newsArray=Split(newsArrayCode,"$Array$")
      For Arr_i=0 to Ubound(newsArray)
         If HttpUrlType=1 Then
            newsArray(Arr_i)=Trim(Replace(HttpUrlStr,"{$ID}",newsArray(Arr_i)))
         Else
            newsArray(Arr_i)=Trim(DefiniteUrl(newsArray(Arr_i),ListUrl))           
         End If
         newsArray(Arr_i)=CheckUrl(newsArray(Arr_i))
      Next
      If CollecOrder=True Then
         For Arr_i=0 to Fix(Ubound(newsArray)/2)
            OrderTemp=newsArray(Arr_i)
            newsArray(Arr_i)=newsArray(Ubound(newsArray)-Arr_i)
            newsArray(Ubound(newsArray)-Arr_i)=OrderTemp
         Next
      End If
   End If
End If

If FoundErr<>True Then
   ErrMsg=ErrMsg & "<br>本次运行 " & Ubound(Arr_Item,2)+1 & " 个项目"
   ErrMsg=ErrMsg & "<br>从第 " & ItemNum  & " 个项目 " & ItemName & " 的第 "  & ListNum & " 页列表分析出 " & Ubound(newsArray) & " 条信息"
   If CollecnewsNum<>0 Then
      ErrMsg=ErrMsg & "，限制 " & CollecnewsNum & " 条。"
      If (CollecnewsNum-1)>Ubound(newsArray) Then
         CollecnewsNum=Ubound(newsArray)+1
      Else
         '保持不变CollecnewsNum
      End If
   Else
      CollecnewsNum=Ubound(newsArray)+1
   End If
   ListSuccesNum=ListSuccesNum+1
   newsNumAll=newsNumAll+CollecnewsNum
   Call SavenewsList()
Else
   ListFalseNum=ListFalseNum+1
End If
ErrMsg=ErrMsg & "<br>" & "<meta http-equiv=""refresh"" content=""3;url=Admin_ItemCollecSteady.asp?ItemNum=" & ItemNum & "&ListNum=" & ListNum+1 & "&ListSuccesNum=" & ListSuccesNum & "&ListFalseNum=" & ListFalseNum & "&newsNumAll=" & newsNumAll & "&ListPaingNext=" & ListPaingNext  & """>"

End Sub


'==================================================
'过程名：SetCache
'作  用：存取缓存
'参  数：无
'==================================================
Sub SetCache()
   Dim myCache
   Set myCache=new clsCache

   '项目信息
   myCache.name=CacheTemp & "items"
   If myCache.valid then 
      Arr_Item=myCache.value
   Else
      ItemEnd=True
   End If
   Set myCache=Nothing
End Sub

Sub SetItems() 
      Dim ItemNumTemp
      ItemNumTemp=ItemNum-1
      ItemID=Arr_Item(0,ItemNumTemp)
      ItemName=Arr_Item(1,ItemNumTemp)
	  
	  LoginType=Arr_Item(6,ItemNumTemp)
      LoginUrl=Arr_Item(7,ItemNumTemp)          '登录
      LoginPostUrl=Arr_Item(8,ItemNumTemp)
      LoginUser=Arr_Item(9,ItemNumTemp)
      LoginPass=Arr_Item(10,ItemNumTemp)
      LoginFalse=Arr_Item(11,ItemNumTemp)
      ListStr=Arr_Item(12,ItemNumTemp)            '列表地址
      LsString=Arr_Item(13,ItemNumTemp)          '列表
      LoString=Arr_Item(14,ItemNumTemp)
      ListPaingType=Arr_Item(15,ItemNumTemp)
      LPsString=Arr_Item(16,ItemNumTemp)          
      LPoString=Arr_Item(17,ItemNumTemp)
      ListPaingStr1=Arr_Item(18,ItemNumTemp)
      ListPaingStr2=Arr_Item(19,ItemNumTemp)
      ListPaingID1=Arr_Item(20,ItemNumTemp)
      ListPaingID2=Arr_Item(21,ItemNumTemp)
      ListPaingStr3=Arr_Item(22,ItemNumTemp)
      HsString=Arr_Item(23,ItemNumTemp)  
      HoString=Arr_Item(24,ItemNumTemp)
      HttpUrlType=Arr_Item(25,ItemNumTemp)
      HttpUrlStr=Arr_Item(26,ItemNumTemp)
	  
      CollecListNum=Arr_Item(77,ItemNumTemp)
      CollecnewsNum=Arr_Item(78,ItemNumTemp)
      CollecOrder=Arr_Item(81,ItemNumTemp)
End Sub

'==================================================
'过程名：GetListPaing
'作  用：获取列表下一页
'参  数：无
'==================================================
Sub GetListPaing()
   If ListPaingType=1 Then
      ListPaingNext=GetPaing(ListCode,LPsString,LPoString,False,False)
      ListPaingNext=FpHtmlEnCode(ListPaingNext)
      If ListPaingNext<>"$False$" And ListPaingNext<>"" Then
         If ListPaingStr1<>""  Then  
            ListPaingNext=Replace(ListPaingStr1,"{$ID}",ListPaingNext)
         Else
            ListPaingNext=DefiniteUrl(ListPaingNext,ListUrl)
         End If
         ListPaingNext=Replace(ListPaingNext,"&","{$ID}")
      End If
   Else
      ListPaingNext="$False$"
   End If
End Sub

'==================================================
'过程名：SavenewsList
'作  用：保存信息
'参  数：无
'==================================================
Sub SavenewsList
   set rs=server.createobject("adodb.recordset")
   sql="select top 1 * from newsList" 
   rs.open sql,connItem,1,3
   For Arr_i=1 To CollecnewsNum
      rs.addnew
      rs("ItemID")=ItemID
      rs("newsUrl")=newsArray(Arr_i-1)
      rs.update
   Next
   rs.close
   set rs=nothing
End Sub

'==================================================
'过程名：TopItem
'作  用：显示导航信息
'参  数：无
'==================================================
Sub TopItem()%>
<html>
<head>
<title>信息采集系统</title>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312">
<link rel="stylesheet" type="text/css" href="../style/Style.css">
</head>
<body>
<table width="98%" align="center" border="0" cellpadding="0" cellspacing="1" bgcolor="#8ED1FF" class="mtab">
  <tr> 
    <td class="td28">采集系统采集理管理</td>
  </tr>
  <tr class="td26" bgcolor="#FFFFFF"> 
    <td><b>管理导航：</b><a href="Admin_ItemStart.asp">管理首页</a> >> 信息采集</td>
  </tr>
</table>
<table width="98%" align="center" border="0" cellpadding="0" cellspacing="1" bgcolor="#8ED1FF" class="mtab">     
  <tr> 
    <td bgcolor="#FFFFFF" aling="center" style="padding:4px; line-height:30px;">采集需要一定的时间，请耐心等待，如果网站出现暂时无法访问的情况这是正常的，采集正常结束后即可恢复。</td>
  </tr>
</table>
<%End Sub%>
</body>         
</html>



