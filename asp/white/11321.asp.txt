<%
'**************************************************
'文件名称：FunctionSystem.asp
'摘   要：系统公用函数
'最后更新：Pulb
'最后日期：
'**************************************************

Function MyCheckAction()
	If Not ChkPost Then
		ErrMsg = "<LI>您不能从外部提交信息！</LI>"
		Call WriteErrMsg()
		Response.End()
	End If
End Function

'**************************************************
'函数名：ChkPost
'作  用：判断提交是否来自外部
'参  数：无
'返回值：True False
'**************************************************
Function ChkPost

	Dim Server_V1,Server_V2
	ChkPost=False
	Server_V1=Cstr(Request.ServerVariables("HTTP_REFERER"))
	Server_V2=Cstr(Request.ServerVariables("SERVER_NAME"))
	If Mid(Server_V1,8,Len(Server_V2))<>Server_V2 Then
		ChkPost=False
	Else
		ChkPost=True
	End If
End Function

'**************************************************
'函数名：GetLocationURL
'作  用：获取当前页地址
'参  数：无
'返回值：链接地址
'**************************************************
Function GetLocationURL()
	Dim Url 
	Dim ServerPort,ServerName,ScriptName,QueryString 
	ServerName = Request.ServerVariables("SERVER_NAME") 
	ServerPort = Request.ServerVariables("SERVER_PORT") 
	ScriptName = Request.ServerVariables("SCRIPT_NAME") 
	QueryString = Request.ServerVariables("QUERY_STRING") 
	Url="http://"&ServerName 
	If ServerPort <> "80" Then Url = Url & ":" & ServerPort 
	Url=Url&ScriptName 
	If QueryString <>"" Then Url=Url&"?"& QueryString 
	GetLocationURL = Url 
End Function 

'**************************************************
'函数名：GetUserIP
'作  用：获取用户IP
'参  数：
'返回值：用户IP
'**************************************************
Public Function GetUserIP()
	GetUserIP = Request.ServerVariables("HTTP_X_FORWARDED_FOR") 
	If GetUserIP = "" Then
		GetUserIP = Request.ServerVariables("REMOTE_ADDR")
	End If
End Function


'**************************************************
'函数名：ReplaceBadChar
'作  用：过滤非法的SQL字符
'参  数：strChar-----要过滤的字符
'返回值：过滤后的字符
'**************************************************
function ReplaceBadChar(strChar)
	if strChar="" then
		ReplaceBadChar=""
	else
		ReplaceBadChar = Trim(replace(replace(replace(replace(replace(replace(replace(strChar,"'",""),"*",""),"?",""),"(",""),")",""),"<",""),"""",""))
	end if
end function

'==================================================
'过程名：CheckInteger
'作  用：判断输入是否为整形
'参  数：para--输入的字符串
'返回值：符合要求返回True
'==================================================
function CheckInteger(para)
	on error resume next
	dim str
	dim l,i
	if isNUll(para) then 
		CheckInteger=false
		exit function
	end if
	str=cstr(para)
	if trim(str)="" then
		CheckInteger=false
		exit function
	end if
	l=len(str)
	for i=1 to l
		if mid(str,i,1)>"9" or mid(str,i,1)<"0" then
			CheckInteger=false 
			exit function
		end if
	next
	CheckInteger=true
	if err.number<>0 then err.clear
end function


'**************************************************
'函数名：CheckString
'作  用：判断输入是否为字母、数字、下划线
'参  数：strng--输入的字符串
'返回值：符合要求返回True
'**************************************************
Function CheckString(strng)
    CheckString = False
    Dim regEx, Match
    Set regEx = New RegExp
    regEx.Pattern = "^[A-Za-z0-9_]+$"
    regEx.IgnoreCase = True
    Set Match = regEx.Execute(strng)
    if match.count then
		CheckString = True
	else
		CheckString = False
	end if
End Function


'**************************************************
'函数名：CheckNumber
'作  用：判断输入是否为数字
'参  数：strng--输入的字符串
'返回值：符合要求返回True
'**************************************************
Function CheckNumber(strng)
    CheckNumber = False
    Dim regEx, Match
    Set regEx = New RegExp
    regEx.Pattern = "^[0-9]+$"
    regEx.IgnoreCase = True
    Set Match = regEx.Execute(strng)
    if match.count then
		CheckNumber = True
	else
		CheckNumber = False
	end if
End Function


'**************************************************
'函数名：strLength
'作  用：求字符串长度。汉字算两个字符，英文算一个字符。
'参  数：str  ----要求长度的字符串
'返回值：字符串长度
'**************************************************
Function strLength(str)
    On Error Resume Next
    Dim WINNT_CHINESE
    WINNT_CHINESE = (Len("中国") = 2)
    If WINNT_CHINESE Then
        Dim l, t, c
        Dim i
        l = Len(str)
        t = l
        For i = 1 To l
            c = Asc(Mid(str, i, 1))
            If c < 0 Then c = c + 65536
            If c > 255 Then
                t = t + 1
            End If
        Next
        strLength = t
    Else
        strLength = Len(str)
    End If
    If Err.Number <> 0 Then Err.Clear
End Function


'**************************************************
'函数名：InterceptString
'作  用：处理字符串
'参  数：txt,length
'**************************************************
function InterceptString(txt,length)
	txt=trim(txt)
	x = len(txt)
	y = 0
	if x >= 1 then
		for ii = 1 to x
			if asc(mid(txt,ii,1)) < 0 or asc(mid(txt,ii,1)) >255 then '如果是汉字 
				y = y + 2
			else
				y = y + 1
			end if
			if y >= length then
				txt = left(trim(txt),ii) '字符串限长
				exit for
			end if
		next
		InterceptString = txt
	else
		InterceptString = ""
	end if
End Function

'**************************************************
'函数名：HTMLEncodeStr
'作  用：将html 标记替换成 能在IE显示的HTML
'参  数：fString ---- 要处理的字符串
'返回值：处理后的字符串
'**************************************************
Public Function HTMLEncodeStr(ByVal fString)
    If IsNull(fString) Or Trim(fString) = "" Then
        HTMLEncodeStr = ""
        Exit Function
    End If
    fString = Replace(fString, ">", "&gt;")
    fString = Replace(fString, "<", "&lt;")

    fString = Replace(fString, Chr(32), "&nbsp;")
    fString = Replace(fString, Chr(9), "&nbsp;")
    fString = Replace(fString, Chr(34), "&quot;")
    fString = Replace(fString, Chr(39), "&#39;")
    fString = Replace(fString, Chr(13), "")
    fString = Replace(fString, Chr(10) & Chr(10), "")
    fString = Replace(fString, Chr(10), "")

    HTMLEncodeStr = fString
End Function

'**************************************************
'函数名：RemoveHTML
'作  用：去除Html格式
'参  数：字符串
'**************************************************
Function RemoveHTML(str)
	Dim RegEx
	Set RegEx = New RegExp
	RegEx.Pattern = "<[^>]*>"
	RegEx.Global = True
	RemoveHTML = RegEx.Replace(str, "")
End Function

'**************************************************
'函数名：GetDateString
'作  用：获取当前时间的字符串
'参  数：
'**************************************************
Function GetDateString()
	CurDate = now()
	Nmonth = month(CurDate)
	Nday = day(CurDate)
	Nhour = hour(CurDate)
	Nminute = minute(CurDate)
	Nsecond = second(CurDate)
	if Len(Nmonth)	= 1 then	Nmonth	= "0"&Nmonth
	if Len(Nday)	= 1 then	Nday	= "0"&Nday
	if Len(Nhour)	= 1 then	Nhour	= "0"&Nhour
	if Len(Nminute)	= 1 then	Nminute	= "0"&Nminute
	if Len(Nsecond)	= 1 then	Nsecond	= "0"&Nsecond
	GetDateString = year(CurDate)&Nmonth&Nday&Nhour&Nminute&Nsecond
End Function


'**************************************************
'函数名：GetCalendarDate
'作  用：获取当前时间的字符串(日历相关使用)
'参  数：
'**************************************************
Function GetCalendarDate(CurDate)
	if Not IsDate(CurDate) then
		CurDate = Date()
	end if
	Nmonth = month(CurDate)
	Nday = day(CurDate)
	if Len(Nmonth)	= 1 then	Nmonth	= "0"&Nmonth
	if Len(Nday)	= 1 then	Nday	= "0"&Nday
	GetCalendarDate = Nmonth&"/"&Nday&"/"&year(CurDate)
End Function


'==================================================
'过程名：getOrderNumber
'作  用：生成订单号
'==================================================
Public Function getOrderNumber()
	randomize
	ranNum = int(90*rnd)+10
	CurDate = Now()
	Nyear = year(CurDate)
	Nmonth = month(CurDate)
	Nday = day(CurDate)
	Nhour = hour(CurDate)
	Nminute = minute(CurDate)
	Nsecond = second(CurDate)
	if Len(Nmonth)	= 1 then	Nmonth	= "0"&Nmonth
	if Len(Nday)	= 1 then	Nday	= "0"&Nday
	if Len(Nhour)	= 1 then	Nhour	= "0"&Nhour
	if Len(Nminute)	= 1 then	Nminute	= "0"&Nminute
	if Len(Nsecond)	= 1 then	Nsecond	= "0"&Nsecond
	'getOrderNumber	= Nyear&Nmonth&Nday&Nhour&Nminute&Nsecond&ranNum
	getOrderNumber	= Right(Nyear, 2)&Nmonth&Nday&Nhour&Nminute&Nsecond&ranNum
End Function

'**************************************************
'函数名：FunDateOne
'作  用：格式化时间
'参  数：dt-----时间
'**************************************************
Function FunFormatDate(DateTime, TypeID)
	if DateTime = "" then
		Exit Function
	end if
	if not IsDate(DateTime) then
		Exit Function
	end if
	Nyear = year(DateTime)
	Nmonth = month(DateTime)
	Nday = day(DateTime)
	if Len(Nmonth)	= 1 then	Nmonth	= "0"&Nmonth
	if Len(Nday)	= 1 then	Nday	= "0"&Nday
	if TypeID = 1 then
		FunFormatDate = Nyear&"-"&Nmonth&"-"&Nday&" "&FormatDateTime(DateTime,4)
	else
		FunFormatDate = Nyear&"-"&Nmonth&"-"&Nday
	end if
End Function


'**************************************************
'函数名：FunDateTime
'作  用：格式化时间
'参  数：dt-----时间
'**************************************************
Function FunDateTime(dt)
	Temp = FormatDateTime(dt,2)
	tempKey = Split(Temp,"-")
	FunDateTime = tempKey(1)&"-"&tempKey(2)
End Function


'**************************************************
'函数名：FunDateOne
'作  用：格式化时间
'参  数：dt-----时间
'**************************************************
Function FunDateOne(dt)
	Temp = FormatDateTime(dt,2)
	tempKey = Split(Temp,"-")
	FunDateOne = Right(tempKey(0),2)&"-"&tempKey(1)&"-"&tempKey(2)
End Function


'**************************************************
'函数名：GetRndString
'作  用：获取随机字符串
'参  数：TID-- 1-全数字, 2-数字加字母；rndStrLen---位数
'**************************************************
Function GetRndString(TID,rndStrLen)
	Dim rndChars
	if TID = 2 then
		rndChars = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"
	else
		rndChars = "0123456789"
	end if
	Dim i
	Dim tmpString
	Dim tmpLen
	tmpLen = Len(rndChars)
	tmpString = ""
	For i=1 To rndStrLen
		tmpString = tmpString & GetCharAt(rndChars,getRndNumber(1,tmpLen))
	Next
	GetRndString = tmpString
End Function
'获得字符串某位置的字符
Function GetCharAt(strChars,index)
     If index<=0 Then index=1
     If index>Len(strChars) Then strChars=Len(strChars)
     GetCharAt = Mid(strChars, index, 1)
End Function
'生成间于lowerbound与upperbound之间的随机数
Function getRndNumber(lowerbound,upperbound)
     Randomize
     getRndNumber = Int((upperbound - lowerbound + 1) * Rnd + lowerbound)
End Function

'**************************************************
'函数名：ProductSmall
'作  用：生成缩略图，固定大小（旅游，酒店产品使用）
'参  数：图片物理路径,图片名称
'**************************************************
Sub ProductSmall(SavePath,FileName)
	Set Jpeg = Server.CreateObject("Persits.Jpeg")
	Jpeg.Open server.mappath(SavePath&"/"&FileName)
	Jpeg.Canvas.Font.Quality = 4
	Jpeg.Quality = 100
	'设定锐化效果
	Jpeg.Sharpen 1, 120
	'指定宽度和高度生成
	Jpeg.Width = 550
	Jpeg.Height = 300
	Jpeg.Save server.mappath(SavePath&"/m_"&FileName)
	Jpeg.Width = 200
	Jpeg.Height = 110
	Jpeg.Save server.mappath(SavePath&"/s_"&FileName)
	Set Jpeg = nothing
End Sub

'================================================
'函数名：DelFiles
'作  用：删除文件
'参  数：filePath   ----路径及文件名
'返回值：无
'================================================
Sub DelFiles(filePath)
	if filePath = "" then
		exit Sub
	end if
	filePath = server.mappath(filePath)
	set fso = Server.CreateObject("Scripting.FileSystemObject")
	if fso.FileExists(filePath) then
		fso.deletefile(filePath)
	end if
	set fso = nothing
end Sub

'删除文件夹
Sub DeleteFolder(strFolder)
	if strFolder = "" then
		exit Sub
	end if
	Dim objFSO
	strFolder = server.mappath(strFolder)
	Set objFSO = CreateObject("Scripting.FileSystemObject")
	If objFSO.FolderExists(strFolder) Then
		objFSO.DeleteFolder(strFolder)
	End If
	Set objFSO = Nothing
End Sub

Function VbsEscape(str) 
    dim i,s,c,a 
    s="" 
    For i=1 to Len(str) 
        c=Mid(str,i,1) 
        a=ASCW(c) 
        If (a>=48 and a<=57) or (a>=65 and a<=90) or (a>=97 and a<=122) Then 
            s = s & c 
        ElseIf InStr("@*_+-./",c)>0 Then 
            s = s & c 
        ElseIf a>0 and a<16 Then 
            s = s & "%0" & Hex(a) 
        ElseIf a>=16 and a<256 Then 
            s = s & "%" & Hex(a) 
        Else 
            s = s & "%u" & Hex(a) 
        End If 
    Next 
    vbsEscape = s 
End Function

Function VbsUnEscape(str) 
    dim i,s,c 
    s="" 
    For i=1 to Len(str) 
        c=Mid(str,i,1) 
        If Mid(str,i,2)="%u" and i<=Len(str)-5 Then 
            If IsNumeric("&H" & Mid(str,i+2,4)) Then 
                s = s & CHRW(CInt("&H" & Mid(str,i+2,4))) 
                i = i+5 
            Else 
                s = s & c 
            End If 
        ElseIf c="%" and i<=Len(str)-2 Then 
            If IsNumeric("&H" & Mid(str,i+1,2)) Then 
                s = s & CHRW(CInt("&H" & Mid(str,i+1,2))) 
                i = i+2 
            Else 
                s = s & c 
            End If 
        Else 
            s = s & c 
        End If 
    Next 
    vbsUnEscape = s 
End Function
%>