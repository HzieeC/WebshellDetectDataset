<!--#include file="const.asp"-->
<!--#include file="../../inc/ubbcode.asp"-->
<%
Admin_header
'=====================================================================
' 软件名称：新云网站管理系统
' 当前版本：NewAsp Content Management System Version 4.0
' 文件名称：admin_list.asp
' 更新日期：2008-06-16
' 官方网站：新云网络(www.newasp.net www.newasp.cn) QQ：94022511
'=====================================================================
' Copyright 2003-2008 newasp.net - All Rights Reserved.
' newasp is a trademark of newasp.net
'=====================================================================
%>
<table class="table1" cellspacing="1" cellpadding="3" align="center" border="0">
	<tr>
		<td class="tableline linetitle" width="80" align="left" noWrap="noWrap"><%=NewAsp.ModuleName%>列表</td>
		<td class="tableline" width="*" align="right"><a href="admin_index.asp?ChannelID=<%=ChannelID%>">管理首页</a>
			 - <a href="admin_post.asp?action=add&ChannelID=<%=ChannelID%>">添加<%=NewAsp.ModuleName%></a>
			 - <a href="../sys/admin_class.asp?ChannelID=<%=ChannelID%>">分类管理</a>
			 - <a href="admin_list.asp?ChannelID=<%=ChannelID%>&order=0">时间排序</a>
			 - <a href="admin_list.asp?ChannelID=<%=ChannelID%>&order=1">ID 排序</a>
			 - <a href="admin_list.asp?ChannelID=<%=ChannelID%>&showmode=1">专题列表</a>
			 - <a href="admin_list.asp?ChannelID=<%=ChannelID%>&isaccept=1">审核列表</a>
			 <%
			 Call selectMenu(ChannelID,NewAsp.ChkNumeric(Request("classid")))
			 %>
		</td>
	</tr>
</table>
<%
Dim maxperpage,totalrec,Pcount,pagelinks,showmode,pagenow,topcount,count
Dim classid,specialid,isaccept,flashidlist,flashid,CurrentPage,TextContent
Dim m_intOrder,m_strOrder,SQLQuery,SQLField
Dim ChildData,ischild,childstr,classname
Dim Action,Inflashid,MainTopic,TopTopic

Const IsDispToplist = True	'--是否显示置顶列表；True=是；False=否

If Not ChkAdmin("List_"&ChannelID) Then
	Call Transfer_error()
End If

Action=LCase(Request("action"))
flashidlist = NewAsp.CheckIDlist(Request("flashid"))
Select Case Trim(Action)
	Case "view":Call viewflash()
	Case Else
		Call showmain()
End Select
If FoundErr=True Then
	ReturnError(ErrMsg)
End If
Admin_footer
SaveLogInfo(AdminName)
NewAsp.PageEnd

Sub showmain()
	Dim i,iCount,lCount,iIndex
	maxperpage=30
	iIndex = 0 : topcount = 0
	pagenow=NewAsp.ChkNumeric(Request("page"))
	If pagenow=0 Then pagenow=1
	showmode=NewAsp.ChkNumeric(Request("showmode"))
	classid=NewAsp.ChkNumeric(Request("classid"))
	specialid=NewAsp.ChkNumeric(Request("specialid"))
	isaccept=NewAsp.ChkNumeric(Request("isaccept"))
	m_intOrder=NewAsp.ChkNumeric(Request("order"))
	count=NewAsp.ChkNumeric(Request("count"))

	If classid>0 Then
		ChildData=NewAsp.GetChildData(ChannelID,classid,0)
		childstr=ChildData(0)
		classname=ChildData(1)
		ischild=CLng(ChildData(2))
	End If
	SQLField = "A.flashid,A.ChannelID,A.classid,A.specialid,A.title,A.ColorMode,A.FontMode,A.[filesize],A.UserName,A.addTime,A.AllHits,A.IsTop,A.isBest,A.isAccept,A.HtmlFileDate,C.ClassName"
%>
<table id="tablehovered" border="0" align="center" cellpadding="3" cellspacing="1" class="tableborder">
<tr>
	<th width="5%" noWrap="noWrap">选择</th>
	<th width="57%"><%=NewAsp.ModuleName%>标题</th>
	<th width="9%" noWrap="noWrap">管理操作</th>
	<th width="8%" noWrap="noWrap">文件大小</th>
	<th width="9%" noWrap="noWrap">用户&评论</th>
	<th width="12%" noWrap="noWrap">整理日期</th>
</tr>
<tr>
	<td class="tablerow1" colspan="6"><form name="search_form" id="search_form" method="get" action="admin_search.asp">
		<span style="position:relative">
		<input type="hidden" name="ChannelID" value="<%=ChannelID%>">
		<input type="text" class="alingmid" id="searchwordbox" name="word" size="45" onfocus="if(this.value=='请输入关键字')this.value=''" />
		<select name="searchfield" class="alingmid">
			<option value="0" selected="selected">标题</option>
			<option value="1">录入者</option>
			<option value="2">作者</option>
			<option value="3">来源</option>
			<option value="9">标签</option>
		</select>
		<input type="submit" value="开始搜索" name="search_submit" class="button alingmid" />
		<input type="checkbox" id="searchmode" name="searchmode" value="1" class="alingmid" /><label for="searchmode">分词搜索</label>
		<div style="display:none" id="sugAutoComplete" onselectstart="return false">&nbsp;</div></span>
		</form>
	</td>
</tr>
<form name="selform" method="post" action="admin_setting.asp?ChannelID=<%=ChannelID%>">
<input type="hidden" name="action" value="batch">
<%
	Call showToplist()
	iCount=2:lCount=2
	If IsArray(TopTopic) Then
		For i=0 To Ubound(TopTopic,2)
			If Not Response.IsClientConnected Then Response.End
			If (iIndex mod 2) = 0 Then
				iCount=2:lCount=1
			Else
				iCount=1:lCount=2
			End If
%>
<tr align="center">
	<td class="tablerow<%=iCount%> hovered"><input type="checkbox" name="flashid" value="<%=TopTopic(0,i)%>"/></td>
	<td class="tablerow<%=iCount%> hovered" align="left">[<a href="?ChannelID=<%=ChannelID%>&classid=<%=TopTopic(2,i)%>"><%=TopTopic(15,i)%></a>]
	<a href="?action=view&ChannelID=<%=ChannelID%>&flashid=<%=TopTopic(0,i)%>"><%=NewAsp.ReadFontMode(TopTopic(4,i),TopTopic(5,i),TopTopic(6,i))%></a>
	<%
	If TopTopic(11,i)>0 Then Response.Write "<font color=""red""><b>顶</b></font>"
	If TopTopic(12,i)>0 Then Response.Write "<font color=""blue""><b>精</b></font>"
	%>
	</td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><a href="admin_post.asp?action=edit&ChannelID=<%=ChannelID%>&flashid=<%=TopTopic(0,i)%>">编辑</a> | <a href="admin_setting.asp?action=del&ChannelID=<%=ChannelID%>&flashid=<%=TopTopic(0,i)%>" onclick="return confirm('<%=NewAsp.ModuleName%>删除后将不能恢复，您确定要删除吗?')">删除</a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap" title="人气：<%=TopTopic(10,i)%>"><%=NewAsp.BytesToString(CLng(TopTopic(7,i))*1024)%></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap" title="点击查看用户评论"><a href="admin_comment.asp?ChannelID=<%=ChannelID%>&flashid=<%=TopTopic(0,i)%>"><%=TopTopic(8,i)%></a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><%=showDateTime(TopTopic(9,i),"yyyy-MM-dd")%></td>
</tr>
<%
			iIndex=iIndex+1 : topcount = topcount + 1
		Next
%>
<tr>
	<td class="tablerow<%=lCount%>" colspan="6" align="center">&nbsp;<b>全部<%=NewAsp.ModuleName%>列表</b></td>
</tr>
<%
		iIndex=iIndex+1
	End If
	TopTopic=Null
%>

<%
	Call showTopiclist()
	iCount=2:lCount=2
	If IsArray(MainTopic) Then
		For i=0 To Ubound(MainTopic,2)
			If Not Response.IsClientConnected Then Response.End
			If (iIndex mod 2) = 0 Then
				iCount=2:lCount=1
			Else
				iCount=1:lCount=2
			End If
%>
<tr align="center">
	<td class="tablerow<%=iCount%> hovered"><input type="checkbox" name="flashid" value="<%=MainTopic(0,i)%>"/></td>
	<td class="tablerow<%=iCount%> hovered" align="left">[<a href="?ChannelID=<%=ChannelID%>&classid=<%=MainTopic(2,i)%>"><%=MainTopic(15,i)%></a>]
	<a href="?action=view&ChannelID=<%=ChannelID%>&flashid=<%=MainTopic(0,i)%>"><%=NewAsp.ReadFontMode(MainTopic(4,i),MainTopic(5,i),MainTopic(6,i))%></a>
	<%
	If MainTopic(11,i)>0 Then Response.Write "<font color=""red""><b>顶</b></font>"
	If MainTopic(12,i)>0 Then Response.Write "<font color=""blue""><b>精</b></font>"
	%>
	</td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><a href="admin_post.asp?action=edit&ChannelID=<%=ChannelID%>&flashid=<%=MainTopic(0,i)%>">编辑</a> | <a href="admin_setting.asp?action=del&ChannelID=<%=ChannelID%>&flashid=<%=MainTopic(0,i)%>" onclick="return confirm('<%=NewAsp.ModuleName%>删除后将不能恢复，您确定要删除吗?')">删除</a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap" title="人气：<%=MainTopic(10,i)%>"><%=NewAsp.BytesToString(CLng(MainTopic(7,i))*1024)%></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap" title="点击查看用户评论"><a href="admin_comment.asp?ChannelID=<%=ChannelID%>&flashid=<%=MainTopic(0,i)%>"><%=MainTopic(8,i)%></a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><%=showDateTime(MainTopic(9,i),"yyyy-MM-dd")%></td>
</tr>
<%
			iIndex=iIndex+1
		Next
	End If
	MainTopic=Null
%>
<tr>
	<td class="tablerow<%=lCount%>" colspan="6"><input class="button" type="button" name="chkall" value="全选" onClick="CheckAll(this.form)"><input class="button" type="button" name="chksel" value="反选" onClick="ContraSel(this.form)">
		管理选项：
		<select name="act">
			<option value="0">请选择操作选项</option>
			<option value="批量删除">批量删除</option>
			<option value="批量置顶">批量置顶</option>
			<option value="取消置顶">取消置顶</option>
			<option value="批量推荐">批量推荐</option>
			<option value="取消推荐">取消推荐</option>
			<option value="批量审核">批量审核</option>
			<option value="取消审核">取消审核</option>
			<option value="更新时间">更新时间</option>
			<option value="生成HTML">生成HTML</option>
		</select>
		<input class="button" type="submit" name="submit_button1" value="执行操作" onclick="return confirm('您确定执行该操作吗?');">
		<input class="button" type="submit" name="submit_button2" value="批量设置" onclick="document.selform.action.value='setting';">
		<input class="button" type="submit" name="submit_button3" value="批量移动" onclick="document.selform.action.value='move';">
	</td>
</tr>
</form>
<tr>
	<td class="tablerow<%=iCount%>" align="right" colspan="6" id="NextPageText"><div style="float:left;">
	<select onchange="if(this.options[this.selectedIndex].value!=''){location=this.options[this.selectedIndex].value;}">
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&showmode=0&order=0">≡选择排序方式≡</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=<%=showmode%>&order=0"<%If m_intOrder=0 Then Response.Write " selected=""selected"""%>>按时间排列</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=<%=showmode%>&order=1"<%If m_intOrder=1 Then Response.Write " selected=""selected"""%>>按 ID 排列</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=<%=showmode%>&order=2"<%If m_intOrder=2 Then Response.Write " selected=""selected"""%>>按人气排列</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=<%=showmode%>&order=3"<%If m_intOrder=3 Then Response.Write " selected=""selected"""%>>按大小排列</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=2&order=0">所有置顶列表</option>
		<option value="admin_list.asp?ChannelID=<%=ChannelID%>&classid=<%=classid%>&specialid=<%=specialid%>&isAccept=<%=isAccept%>&showmode=1&order=0">所有专题列表</option>
	</select>
	</div>
	<var class="morePage"><%=showlistpage(pagenow,Pcount,maxperpage,totalrec,pagelinks)%></var></td>
</tr>
</table>
<%
	Call LoadAutoComplete("search_form")
End Sub

Sub showToplist()
	Dim Rs
	SQLQuery=""
	If pagenow=1 And showmode=0 And IsDispToplist Then
		If classid>0 Then
			If ischild>0 Then
				SQLQuery=" And A.isAccept=1 And A.classid in (" & childstr & ") And A.IsTop>0"
			Else
				SQLQuery=" And A.isAccept=1 And A.classid="&classid&" And A.IsTop>0"
			End If
		Else
			SQLQuery=" And A.isAccept=1 And A.IsTop>0"
		End If

		Set Rs=NewAsp.Execute("SELECT TOP 20 "& SQLField &" FROM [NC_FlashList] A INNER JOIN [NC_Classify] C on A.classid=C.classid WHERE A.ChannelID=" &ChannelID& " "&SQLQuery&" ORDER BY A.IsTop DESC,A.addtime DESC")
		If Not Rs.EOF Then
			TopTopic=Rs.GetRows(-1)
		Else
			TopTopic=Null
		End If
		Set Rs=Nothing
	End If
End Sub

Sub showTopiclist()
	If Not IsObject(Conn) Then ConnectionDatabase
	Dim Rs,SQL,m_isAccept

	If isaccept=0 Then m_isAccept=1 Else m_isAccept=0
	SQLQuery=""
	If showmode=1 Then
		If specialid=0 Then
			SQLQuery=" And A.isAccept="&m_isAccept&" And A.specialID>0"
		Else
			SQLQuery=" And A.isAccept="&m_isAccept&" And A.specialID="&specialid
		End If
	Else
		If showmode=2 Then SQLQuery = " And A.IsTop>0" Else SQLQuery = " And A.IsTop=0"
		If classid>0 Then
			If ischild>0 Then
				SQLQuery=" And A.isAccept="&m_isAccept & " And A.classid in (" & childstr & ")"&SQLQuery
			Else
				SQLQuery=" And A.isAccept="&m_isAccept&" And A.classid="&classid&SQLQuery
			End If
		Else
			SQLQuery=" And A.isAccept="&m_isAccept&SQLQuery
		End If
	End If

	On Error Resume Next

	Select Case m_intOrder
		Case 1	:	m_strOrder="A.flashid"
		Case 2	:	m_strOrder="A.Allhits"
		Case 3	:	m_strOrder="A.Filesize"
		Case Else
			m_strOrder="A.addtime"
	End Select
	If count=0 Then
		totalrec=NewAsp.Execute("SELECT COUNT(flashid) FROM NC_FlashList A WHERE A.ChannelID="&ChannelID&" "&SQLQuery&"")(0)
	Else
		totalrec=count
	End If
	Pcount = CLng(totalrec / maxperpage)
	If Pcount < totalrec / maxperpage Then Pcount = Pcount + 1
	If pagenow>Pcount Then pagenow=1
	Set Rs=NewAsp.CreateAXObject("ADODB.Recordset")
	SQL="SELECT "& SQLField &" FROM [NC_FlashList] A INNER JOIN [NC_Classify] C on A.classid=C.classid WHERE A.ChannelID=" &ChannelID& " "&SQLQuery&" ORDER BY "&m_strOrder&" DESC"
	Rs.Open SQL,Conn,1,1
	If pagenow >1 Then
		Rs.Move (pagenow-1) * maxperpage
	End If
	If Not (Rs.BOF And Rs.EOF) Then
		MainTopic=Rs.GetRows(maxperpage)
	Else
		MainTopic=Null
	End If
	Rs.close()
	Set Rs=Nothing
	pagelinks="admin_list.asp?channelid="& ChannelID &"&classid="& Request("classid")&"&specialid="&Request("specialid")&"&isAccept="&Request("isAccept")&"&showmode="&Request("showmode")&"&order="&Request("order")&"&count="&totalrec&"&"
End Sub

Sub viewflash()
	Dim Rs,SQL,i
	ubb.BasePath = InstallDir_ChannelDir
	ubb.setUbbcode = Join(NewAsp.setUserEditor,"|")
	ubb.Keyword = NewAsp.KeywordList
	ubb.Pagination = False

	flashid=NewAsp.ChkNumeric(Request("flashid"))
	CurrentPage=NewAsp.ChkNumeric(Request("page"))
	SQL = "SELECT * FROM NC_FlashList WHERE ChannelID="& ChannelID &" And flashid="& flashid
	Set Rs = NewAsp.Execute(SQL)
	If Rs.BOF And Rs.EOF Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>Sorry！没有找到相关信息。或者您选择了错误的系统参数！</li>"
		Set Rs = Nothing
		Exit Sub
	End If
	TextContent=Rs("Introduce") & ""
	ubb.CurrentID=Rs("flashid")
	TextContent=ubb.UbbCode(TextContent)
%>
<table border="0" align="center" cellpadding="3" cellspacing="1" class="tableborder">
<tr>
	<th colspan="2">标题：<%=Rs("Title")%></th>
</tr>
<tr>
	<td class="tablerow1" width="50%"><b>动画作者：</b> <%=Rs("Author")%></td>
	<td class="tablerow1" width="50%"><b>动画来源：</b> <%=Rs("ComeFrom")%></td>
</tr>
<tr>
	<td class="tablerow2"><b>文件大小：</b> <%=NewAsp.BytesToString(CLng(Rs("FileSize"))*1024)%></td>
	<td class="tablerow2"><b>推荐星级：</b>
<%
Response.Write "<font color=""red"">"
For i = 1 To Rs("star")
	Response.Write "★"
Next
Response.Write "</font>"
%>
	</td>
</tr>

<tr>
	<td class="tablerow1"><b>更新时间：</b> <%=showDateTime(Rs("addTime"),"yyyy-MM-dd hh:mm:ss")%></td>
	<td class="tablerow1"><b>动画统计：</b> <%=Rs("AllHits")%></td>
</tr>
<tr>
	<td class="tablerow2"><b>发 布 人：</b> <font color="blue"><%=Rs("username")%></font></td>
	<td class="tablerow2"><b>审核状态：</b> <%If Rs("isAccept") > 0 Then%>
	<font color="blue">已审核</font>
	<%Else%>
	<font color="red">未审核</font>
	<%End If%>
	</td>
</tr>
<tr>
	<td class="tablerow1" colspan="2"><b>动画简介</b></td>
</tr>
<tr>
	<td class="tablerow2" colspan="2" style="word-break:normal;word-wrap:normal;">
		<div style="margin:5px;font-size:14px" id="ContentBody" class="ContentBody" style="word-break:normal;word-wrap:normal;"><%=TextContent%></div>
	</td>
</tr>
<tr>
	<td colspan="2" class="tablerow1">
	<%
		Call PreviewMode(Rs("showurl"),Rs("showmode"))
	%>
	</td>
</tr>
<tr>
	<td class="tablerow2" colspan="2">
		<li>上一动画：<%=BackFlash(Rs("flashid"))%></li>
		<li>下一动画：<%=NextFlash(Rs("flashid"))%></li>
	</td>
</tr>
<tr>
	<td class="tablerow1" colspan="2" align="center">
		<input type="button" onclick="{if(confirm('您确定要删除吗?')){location.href='admin_setting.asp?action=del&ChannelID=<%=ChannelID%>&flashid=<%=flashid%>';return true;}return false;}" value=" 删 除 " name="B1" class="button">&nbsp;&nbsp;
		<input type="button" onclick="javascript:history.go(-1)" value="返回上一页" name="B2" class="button">&nbsp;&nbsp;
		<input type="button" name="B3" onclick="javascript:location.href='#'" value="返回顶部" class="button">&nbsp;&nbsp;
		<input type="button" name="B4" onclick="javascript:location.href='admin_post.asp?action=edit&ChannelID=<%=ChannelID%>&flashid=<%=flashid%>'" value=" 编 辑 " class="button">&nbsp;&nbsp;
		<input type="button" onclick="{if(confirm('您确定要审核吗?')){location.href='admin_setting.asp?action=audit&ChannelID=<%=ChannelID%>&flashid=<%=flashid%>';return true;}return false;}" value="直接审核" name="B6" class="button">&nbsp;&nbsp;
	</td>
</tr>
</table>
<%
	Rs.Close()
	Set Rs = Nothing
End Sub

Function BackFlash(flashid)
	Dim Rss, SQL
	SQL = "SELECT TOP 1 flashid,classid,Title FROM NC_FlashList WHERE ChannelID=" & ChannelID & " And isAccept<>0 And flashid<" & flashid & " ORDER BY flashid DESC"
	Set Rss = NewAsp.Execute(SQL)
	If Rss.EOF And Rss.BOF Then
		BackFlash = "已经没有了"
	Else
		BackFlash = "<a href=""?action=view&ChannelID=" & ChannelID & "&flashid=" & Rss("flashid") & """>" & Rss("Title") & "</a>"
	End If
	Rss.Close
	Set Rss = Nothing
End Function
Function NextFlash(flashid)
	Dim Rss, SQL
	SQL = "SELECT TOP 1 flashid,classid,Title FROM NC_FlashList WHERE ChannelID=" & ChannelID & " And isAccept<>0 And flashid>" & flashid & " ORDER BY flashid ASC"
	Set Rss = NewAsp.Execute(SQL)
	If Rss.EOF And Rss.BOF Then
		NextFlash = "已经没有了"
	Else
		NextFlash = "<a href=""?action=view&ChannelID=" & ChannelID & "&flashid=" & Rss("flashid") & """>" & Rss("Title") & "</a>"
	End If
	Rss.Close
	Set Rss = Nothing
End Function

Sub PreviewMode(url,modeid)
	If Len(url) < 3 Then Exit Sub
	If Left(url,1) <> "/" And InStr(url,"://") = 0 Then
		url = NewAsp.ChannelUrl & url
	End If
	Select Case modeid
	Case 1
		Response.Write "<object codebase=""http://download.macromedia.com/pub/shockwave/cabs/flash/swflash.cab#version=4,0,0,0"" height=""400"" width=""550"" classid=""clsid:D27CDB6E-AE6D-11cf-96B8-444553540000"">"& vbCrLf
		Response.Write "	<param name=""movie"" value=""" & url & """>"& vbCrLf
		Response.Write "	<param name=""quality"" value=""high"">"& vbCrLf
		Response.Write "	<param name=""SCALE"" value=""exactfit"">"& vbCrLf
		Response.Write "	<embed src=""" & url & """ quality=""high"" pluginspage=""http://www.macromedia.com/shockwave/download/index.cgi?P1_Prod_Version=ShockwaveFlash"" type=""application/x-shockwave-flash"" width=""550"" height=""400"">"& vbCrLf
		Response.Write "	</embed>"& vbCrLf
		Response.Write "</object>"& vbCrLf
	Case 2
		Response.Write "<img src=""" & url & """ border=""0"" onload=""return imgzoom(this,550)"">"
	Case 3
		Response.Write "<object classid=""CLSID:22D6F312-B0F6-11D0-94AB-0080C74C7E95"" class=""OBJECT"" id=""MediaPlayer"" width=""220"" height=""220"">"& vbCrLf
		Response.Write "	<param name= value=""-1"">"& vbCrLf
		Response.Write "	<param name=""CaptioningID"" value>"& vbCrLf
		Response.Write "	<param name=""ClickToPlay"" value=""-1"">"& vbCrLf
		Response.Write "	<param name=""Filename"" value=""" & url & """>"& vbCrLf
		Response.Write "	<embed src=""" & url & """  width= 220 height=""220"" type=""application/x-oleobject"" codebase=""http://activex.microFlash.com/activex/controls/mplayer/en/nsmp2inf.cab#Version=0,1,1,1"" flename=""mp""></embed>"& vbCrLf
		Response.Write "</object>"& vbCrLf
	Case 4
		Response.Write "<object classid=""clsid:CFCDAA03-8BE4-11cf-B84B-0020AFBBCCFA"" height=""288"" id=""video1"" width=""305"" VIEWASTEXT>"& vbCrLf
		Response.Write "	<param name=""_ExtentX"" value=""5503"">"& vbCrLf
		Response.Write "	<param name=""_ExtentY"" value=""1588"">"& vbCrLf
		Response.Write "	<param name=""AUTOSTART"" value=""-1"">"& vbCrLf
		Response.Write "	<param name=""SHUFFLE"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""PREFETCH"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""NOLABELS"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""SRC"" value=""" & url & """>"& vbCrLf
		Response.Write "	<param name=""CONTROLS"" value=""Imagewindow,StatusBar,ControlPanel"">"& vbCrLf
		Response.Write "	<param name=""CONSOLE"" value=""RAPLAYER"">"& vbCrLf
		Response.Write "	<param name=""LOOP"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""NUMLOOP"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""CENTER"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""MAINTAINASPECT"" value=""0"">"& vbCrLf
		Response.Write "	<param name=""BACKGROUNDCOLOR"" value=""#000000"">"& vbCrLf
		Response.Write "</object>"& vbCrLf
	Case 5
		Response.Write "<object classid=""clsid:166B1BCA-3F9C-11CF-8075-444553540000"" codebase=""http://download.macromedia.com/pub/shockwave/cabs/director/sw.cab#version=8,5,1,0"" width=""100%"" height=""100%"">"& vbCrLf
		Response.Write "	<param name=""src"" value=""" & url & """>"& vbCrLf
		Response.Write "	<param name=""swRemote"" value=""swSaveEnabled='false' swVolume='false' swRestart='false' swPausePlay='false' swFastForward='false' swContextMenu='false' "">"& vbCrLf
		Response.Write "	<param name=""swStretchStyle"" value=""fill"">"& vbCrLf
		Response.Write "	<PARAM name=""bgColor"" value=""#000000"">"& vbCrLf
		Response.Write "	<PARAM name=logo value=""false"">"& vbCrLf
		Response.Write "	<embed src=""" & url & """ bgColor=""#000000"" logo=""FALSE"" width=""550"" height=""400"" swRemote=""swSaveEnabled='false' swVolume='false' swRestart='false' swPausePlay='false' swFastForward='false' swContextMenu='false' "" swStretchStyle=""fill"" type=""application/x-director"" pluginspage=""http://www.macromedia.com/shockwave/download/""></embed>"& vbCrLf
		Response.Write "</object>"& vbCrLf
	
	End Select
End Sub

%>