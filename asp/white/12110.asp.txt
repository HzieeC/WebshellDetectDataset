<%
Const dbPathPart="../"
%>

<!-- #include file="../conobj.asp" -->
<!-- #include file="../inc/md5.asp" -->
<!-- #include file="../inc/func_common.asp" -->
<!-- #include file="../inc/func_encrypt.asp" -->

<%
Response.Buffer = True
Server.ScriptTimeOut=999
Response.Expires = -1
Response.ExpiresAbsolute = Now() - 1
Response.Expires = 0
Response.CacheControl = "no-cache"
installType = "free"
installDb = "access"
updateTime = "20120705"


Dim mdb1,mdb2
Dim dataFailNum,dataMoreNum
Dim configFileState:configFileState=1

mudi=Trim(request.querystring("mudi"))

If File_IsExist(server.mapPath(dbPathPart &"cache/install.lock")) And mudi<>"finish" Then
	response.write("<br /><br /><center>请先删除cache/install.lock文件，再刷新该页面，进行安装向导。</center>")
	response.End()
End If

If Len(""& session("adminDir"))<1 Then session("adminDir")="admin"


Select Case mudi
	Case "setAdminDir"
		Call setAdminDir()

		conobj.close
		Set conobj=Nothing

	Case "check"
		conobj.close
		Set conobj=Nothing

		Call WebTop()
		Call check()
		Call WebBottom()
	
	Case "config"
		Call WebTop()
		Call config()
		Call WebBottom()

		conobj.close
		Set conobj=Nothing

	Case "run"
		Call WebTop()
		Call run()
		Call WebBottom()

	Case "finish"
		Call WebTop()
		Call finish()
		Call WebBottom()

		conobj.close
		Set conobj=Nothing

	Case Else
		Call WebTop()
		Call Default()
		Call WebBottom()

		conobj.close
		Set conobj=Nothing

End Select



Sub WebTop()
	%>
	<!DOCTYPE html PUBLIC "-//W3C//DTD Xhtml 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
	<html xmlns="http://www.w3.org/1999/xhtml">
	<head>
	<meta http-equiv="Content-Type" content="text/html; charset=GB2312" />
	<meta http-equiv="X-UA-Compatible" content="IE=EmulateIE7" />
	<title>欢迎安装 <%=OT_SOFTNAME &" V"& OT_VERSION &" build "& OT_UPDATETIME &" "& OT_SOFTTYPECN%>（安装向导<%=updateTime%>） </title>
	<meta name="keywords" content="网钛科技,asp文章管理系统,新闻发布系统,文章管理系统,文章系统,最好的文章系统,简单好用程序,OTCMS,网钛CMS" />
	<meta name="description" content="网钛科技致力于文章管理系统、淘宝客程序的研发；我们坚持做最简单最好用的系统(ASP/PHP/C#)，傻瓜式的操作,让您在最短的时间内就可以上手并建成一个功能强大的网站." />
	<style type="text/css">
	body	{ margin-top:20px; padding:0px; font-size:13px; line-height:1.7; color:#6d2f2f; }
	a		{ font-weight:normal; text-decoration:underline; color:#9b0108; }
	a:hover	{ color:#cc000a; text-decoration:none; }
	input	{ padding:5px 3px 3px 3px; font-size:14px; color:#333; }
	.select { font-size:13px; color:#333; width:84px; }
	.title	{ font-size:16px; font-weight:bold; height:45px; line-height:45px; padding-left:12px; background:#f4e9e9; color:#6d2f2f; }
	.title2	{ font-size:14px; height:30px; line-height:30px; padding-left:12px; background:#f4e9e9; color:#6d2f2f; }
	.list	{ font-size:14px; height:25px; }
	.desc	{ font-size:12px; color:#9b0108; width:150px; }
	.finish	{ font-size:14px; line-height:150%; font-weight:normal; color:#000000; background-color:#FDFDFD; margin:120px 120px 0px 120px; padding:20px; border:1px solid #B6B6B6; }
	.box1	{ background:#f4eaea; }
	.box2	{ background:#c29494; }
	.border2{ border:1px #c29494 solid; border-left:none;border-bottom:none; }
	.border2 td{ border:1px #c29494 solid; border-top:none;border-right:none; }
	.btnBox	{ font-size:14px; height:55px; background:#ffffff; }
	.border1{ border-bottom:1px #e9d4d4 solid; }
	.td1	{ font-size:14px; height:25px; background:#f9f4f4; }
	.td2	{ font-size:14px; height:25px; background:#ffffff; }
	</style>

	<script language='javascript' type='text/javascript'>
	function $id(str){
		return document.getElementById(str);
	}

	function $name(str){
		return document.getElementsByName(str);
	}
	</script>

	</head>
	<body>
	<%
End Sub


Sub WebBottom()
	response.write(_
	"</body>"&_
	"</html>")
End Sub






Sub Default()
	If installType<>OT_SOFTTYPE Then
		response.write("<center style='color:red;font-size:14px;'>检查到该网站程序不是免费版程序，无法使用该安装向导。<br /><br /><b>安装向导程序下载：</b><a href='http://otcms.com/news/2646.html' target='_blank' title='网钛文章管理系统(TOCMS) 安装向导'>http://otcms.com/news/2646.html</a></center>")
		response.End()
	End If
	If installDb<>OT_Database Then
		response.write("<center style='color:red;font-size:14px;'>检查到该网站程序使用的不是"& installDb &"数据库，无法使用该安装向导<br /><br /><b>安装向导程序下载：</b><a href='http://otcms.com/news/2646.html' target='_blank' title='网钛文章管理系统(TOCMS) 安装向导'>http://otcms.com/news/2646.html</a></center>")
		response.End()
	End If
	If updateTime<OT_INSTALLTIME Then
		response.write("<center style='color:red;font-size:14px;'>检查到该安装向导版本("& updateTime &")不是最新版("& OT_INSTALLTIME &")<br /><br /><b>最新安装向导程序下载：</b><a href='http://otcms.com/news/2646.html' target='_blank' title='网钛文章管理系统(TOCMS) 安装向导'>http://otcms.com/news/2646.html</a></center>")
		response.End()
	End If


%>
<table width="100" border="0" align="center" cellpadding="3" cellspacing="0" class="box1">
<tr>
<td>
	<table width="100" border="0" cellpadding="0" cellspacing="1" class="box2">
	<tr>
	<td>
		<table width="750" border="0" align="center" cellpadding="0" cellspacing="0">
		<tr>
			<td class="title border1">网钛文章管理系统(OTCMS)使用许可协议</td>
		</tr>
		<tr>
			<td style="padding:5px 5px 5px 8px; background:#ffffff;" class="border1">
				<div style='width:100%; height:400px; overflow:auto;'>
					<p>感谢您选择网钛文章管理系统(OTCMS)，本系统基于ASP+ACCESS/MSSQL 技术开发。官方网址：<a href="http://otcms.com/" target="_blank">http://otcms.com</a></p>
					<p>为了使你正确并合法的使用本软件，请你在使用前务必阅读清楚下面的协议条款：</p>
					<p><strong>一、本协议仅适用于网钛文章管理系统(OTCMS)，网钛科技对本协议有最终解释权。</strong></p>
					<p><strong>二、协议许可的权利</strong><br />
					1、您可以在完全遵守本最终用户授权协议的基础上，将本软件应用于商业或非商业用途，而不必支付软件版权授权费用。<br />
					2、您可以在协议规定的约束和限制范围内修改本系统 源代码或界面风格以适应您的网站要求。<br />
					3、您拥有使用本软件构建的网站全部内容所有权，并独立承担与这些内容的相关法律义务。<br />
					4、获得商业授权之后，您可以依据所购买的授权类型中确定的技术支持内容，自购买时刻起，在技术支持期限内拥有通过指定的方式获得指定范围内的技术支持服务。商业授权用户享有反映和提出意见的权力，相关意见将被作为首要考虑，但没有一定被采纳的承诺或保证。</p>

					<p><strong>二、协议规定的约束和限制 </strong><br />
					1、不得将本软件用于国家不允许开设的网站（包括色qing、反dong、含有病毒，赌bo类网站）。<br />
					2、未经官方许可，不得对本软件或与之关联的商业授权进行出租、出售、抵押或发放子许可证。<br />
					3、不管你的网站是否整体使用本系统 ，还是部份栏目使用本软件，<span class="light">在你使用了本软件的网站主页上必须加上本软件官方网址(otcms.com)的链接</span>。<br />
					4、未经官方许可，禁止在本软件的整体或任何部分基础上以发展任何派生版本、修改版本或第三方版本用于重新分发。 <br />
					5、如果您未能遵守本协议的条款，您的授权将被终止，所被许可的权利将被收回，并承担相应法律责任。 </p>

					<p><strong>三、有限担保和免责声明 </strong><br />
					1、本软件及所附带的文件是作为不提供任何明确的或隐含的赔偿或担保的形式提供的。<br />
					2、用户出于自愿而使用本软件，您必须了解使用本软件的风险，在尚未购买产品技术服务之前，我们不承诺对免费用户提供任何形式的技术支持、使用担保，也不承担任何因使用本软件而产生问题的相关责任。<br />
					3、电子文本形式的授权协议如同双方书面签署的协议一样，具有完全的和等同的法律效力。您一旦开始确认本协议并安装本系统，即被视为完全理解并接受本协议的各项条款，在享有上述条款授予的权力的同时，受到相关的约束和限制。协议许可范围以外的行为，将直接违反本授权协议并构成侵权，我们有权随时终止授权，责令停止损害，并保留追究相关责任的权力。<br />
					4、如果本软件带有其它软件的整合API示范例子包，这些文件版权不属于本软件官方，并且这些文件是没经过授权发布的，请参考相关软件的使用许可合法的使用。</p>
					<p>版权所有 (c)2010-<%=Year(Date())%>，网钛科技 保留所有权利。</p>
					<p>协议发布时间：  2011年1月28日 By 网钛科技</p>
				</div>
			</td>
		</tr>
		<tr>
			<td align="center" class="btnBox">
				<input type="button" value="我同意" onclick="document.location.href='index.asp?mudi=check';" />&nbsp;&nbsp;&nbsp;&nbsp;
				<input type="button" value="不同意" onclick="window.close();">
			</td>
		</tr>
		</table>
	</td>
	</tr>
	</table>
</td>
</tr>
</table>
<%
End Sub



Sub setAdminDir()
	newAdminDir = ToGetStr("newAdminDir")
	session("adminDir") = newAdminDir
	response.redirect("index.asp?mudi=check")
End Sub



Sub check()
	dbNameArr = Split(dbName,"/")

	Dim judObjects(15)
	judObjects(1)	= "adodb.connection"			'数据库使用
	judObjects(2)	= "Adodb.Stream"				'数据流读写
	judObjects(3)	= "Scripting.FileSystemObject"	'FSO 文本文件读写
	judObjects(4)	= "JMail.SmtpMail"				'Jmail组件
	judObjects(5)	= "Persits.Jpeg"				'AspJpeg组件
	judObjects(6)	= "Microsoft.XMLHTTP"			'XMLHTTP组件
	judObjects(7)	= "Microsoft.XMLDOM"			'XMLDOM组件
'	judObjects(8)	= "MSXML2.XMLHTTP"				'XML组件
	judObjects(8)	= "MSXML2.ServerXMLHTTP"		'XML组件
	judObjects(9)	= "CDONTS.NewMail"				'CDONTS组件[WIN虚拟SMTP 发信]
	judObjects(10)	= "Persits.MailSender"			'ASPemail 发信
	judObjects(11)	= "SMTPsvg.Mailer"				'SMTPsvg 发信
	judObjects(12)	= "DkQmail.Qmail"				'dkQmail 发信
	judObjects(13)	= "Geocel.Mailer"				'Geocel 发信
	judObjects(14)	= "Scripting.Dictionary"		'IISemail 发信

%>
<script language='javascript' type='text/javascript'>
function CheckAdminDir(){
	if ($id('newAdminDir').value==""){
		alert("请先填写当前后台目录名");$id('newAdminDir').focus();return false;
	}
	document.location.href="index.asp?mudi=setAdminDir&newAdminDir="+ $id('newAdminDir').value;
}
</script>

<table width="750" border="0" align="center" cellpadding="3" cellspacing="0" class="box1">
<tr>
<td>
	<table width="100%" border="0" cellpadding="0" cellspacing="1" class="box2">
	<tr>
	<td>
		<table width="100%" border="0" align="center" cellpadding="5" cellspacing="0" class="border2">
		<colgroup>
			<col class="td1" />
			<col class="td2" />
			<col class="td2" />
			<col class="td2" />
			<col class="td2" />
			<col class="td2" />
		</colgroup>
		<%
		response.write(_
		"<tr>"&_
			"<td colspan='6' class='title'>组件检测</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td width='50%' align='left' colspan='3'>"& judObjects(3) &" （目录文件读写）</td>"&_
			"<td width='50%' align='left' colspan='3'>"& ShowObjInfo(judObjects(3)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='3'>"& judObjects(5) &" （图片缩略图/水印）</td>"&_
			"<td align='left' colspan='3'>"& ShowObjInfo(judObjects(5)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='3'>"& judObjects(2) &" （辅助采集远程图片/文章）</td>"&_
			"<td align='left' colspan='3'>"& ShowObjInfo(judObjects(2)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='3'>"& judObjects(6) &" （采集远程图片）</td>"&_
			"<td align='left' colspan='3'>"& ShowObjInfo(judObjects(6)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='3'>"& judObjects(14) &" （上传文件/验证码）</td>"&_
			"<td align='left' colspan='3'>"& ShowObjInfo(judObjects(14)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='3'>"& judObjects(8) &" （采集远程文章）</td>"&_
			"<td align='left' colspan='3'>"& ShowObjInfo(judObjects(8)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td colspan='6' class='title'>目录、文件权限检查</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td width='33%' align='center' colspan='2'><b>目录/文件名</b></td>"&_
			"<td width='33%' align='center' colspan='2'><b>需要状态</b></td>"&_
			"<td width='33%' align='center' colspan='2'><b>当前状态</b></td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>news/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("news") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>cache/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("cache") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>upFiles/images/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("upFiles/images") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>upFiles/infoImg/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("upFiles/infoImg") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>upFiles/infoMove/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("upFiles/infoMove") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>upFiles/users/</td>"&_
			"<td align='center' colspan='2'>可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirWrite("upFiles/users") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td colspan='6' class='title'>下一步操作（网站配置初始化）需要用到的目录/文件权限</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>config.asp</td>"&_
			"<td align='center' colspan='2'>可读、可写</td>"&_
			"<td align='center' colspan='2'>"& File_Right("config.asp") &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>"& session("adminDir") &"</td>"&_
			"<td align='center' colspan='2'>可改</td>"&_
			"<td align='center' colspan='2'>"&_
			"")
				adResult = File_IsDirRev(session("adminDir"))
				If InStr(adResult,"不存在")>0 Then
					adAddiStr="<br />当前后台目录名：<input type='text' id='newAdminDir' name='newAdminDir' style='width:50px;padding:1px;font-size:12px;' /><input type='button' value='确定' style='padding:1px;font-size:12px;' onclick='CheckAdminDir();' />"
				Else
					adAddiStr=""
				End If
			response.write(_
			adResult & adAddiStr &_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>"& dbNameArr(0) &"</td>"&_
			"<td align='center' colspan='2'>可改</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirRev(dbNameArr(0)) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>"& dbName &"</td>"&_
			"<td align='center' colspan='2'>可改</td>"&_
			"<td align='center' colspan='2'>"& File_IsRev(dbName) &"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='left' colspan='2'>"& dbBackupDir &"</td>"&_
			"<td align='center' colspan='2'>可改、可写</td>"&_
			"<td align='center' colspan='2'>"& File_IsDirRev(dbBackupDir) &"、"& File_IsDirWrite(dbBackupDir) &"</td>"&_
		"</tr>"&_
		"")
		%>
		<tr>
			<td colspan="6" align="center" class="btnBox">
					<input id="updateBtn" type="button" value="上一步" style="margin:10px 0 10px 0;" onclick="document.location.href='index.asp';" />
					&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
				<%If configFileState = 0 Then%>
					<input id="updateBtn" type="button" value="赋予config.asp文件可读写权限后刷新再试" style="margin:10px 0 10px 0;" disabled="true" />
					<br />如不懂设置，请阅读先IIS权限设置：<a href="http://otcms.com/news/3251.html" target="_blank">http://otcms.com/news/3251.html</a>
				<%Else%>
					<input id="updateBtn" type="button" value="下一步" style="margin:10px 0 10px 0;" onclick="document.location.href='index.asp?mudi=config';" />
				<%End If%>
			</td>
		</tr>
		</table>
	</td>
	</tr>
	</table>
</td>
</tr>
</table>
<%
End Sub




Sub config()
	dbNameArr = Split(dbName,"/")
%>
<script language='javascript' type='text/javascript'>
function CheckConfigForm(){
	if ($id('adminName').value==""){
		alert('后台登录帐号不能为空');$id('adminName').focus();return false;
	}
	if ($id('adminPwd').value==""){
		alert('后台登录密码不能为空');$id('adminPwd').focus();return false;
	}
	if ($id('adminDir').value==""){
		alert('后台目录名不能为空');$id('adminDir').focus();return false;
	}
	if ($id('accName').value==""){
		alert('网站数据库名称不能为空');$id('accName').focus();return false;
	}
	var accNameExt = $id('accName').value.substr($id('accName').value.length-4)
	if (accNameExt!=".mdb" && accNameExt!=".asp"){
		alert('网站数据库名称必须以“.mdb”或者“.asp”结尾\n(如 123.mdb 或 123.asp)');$id('accName').focus();return false;
	}
	if ($id('accDir').value==""){
		alert('数据库目录名不能为空');$id('accDir').focus();return false;
	}
	if ($id('accBackupDir').value==""){
		alert('数据库备份目录名不能为空');$id('accBackupDir').focus();return false;
	}
	if ($id('accDir').value==$id('accBackupDir').value){
		alert('数据库目录名与数据库备份目录名不能相同');$id('accDir').focus();return false;
	}
	alertStr = "";
	alertNum = 0;
	if ($id('adminName').value=="admin"){
		alertNum ++;
		alertStr += alertNum +"、后台登录帐号:admin\n";
	}
	if ($id('adminPwd').value=="admin"){
		alertNum ++;
		alertStr += alertNum +"、后台登录密码:admin\n";
	}
	if ($id('adminDir').value=="admin"){
		alertNum ++;
		alertStr += alertNum +"、后台目录名:admin\n";
	}
	if ($id('accName').value=="# OTCMS@!db%22.mdb"){
		alertNum ++;
		alertStr += alertNum +"、网站数据库名称:# OTCMS@!db%22.mdb\n";
	}
	if ($id('accDir').value=="OTCMS%22$d#b@"){
		alertNum ++;
		alertStr += alertNum +"、数据库目录名:OTCMS%22$d#b@\n";
	}
	if ($id('accBackupDir').value=="OTCMS%22$d#b@backup"){
		alertNum ++;
		alertStr += alertNum +"、数据库备份目录名:OTCMS%22$d#b@backup\n";
	}
	if (alertStr != ""){
		if (confirm(alertStr +"\n确定以上这"+ alertNum +"项采用系统默认的？\n（建议修改这些项，以提高网站安全性）")==false){
			return false;
		}
	}
	if ($id('judClearDB').checked){
		if (confirm("确定要初始化(清空)数据库？")==false){
			return false;
		}
	}
}
</script>

<table width="100" border="0" align="center" cellpadding="3" cellspacing="0" class="box1">
<tr>
<td>
	<table width="100" border="0" cellpadding="0" cellspacing="1" class="box2">
	<tr>
	<td>
		<table width="750" border="0" align="center" cellpadding="5" cellspacing="0" class="border2">
		<colgroup>
			<col class="td1" style="width:140px;" />
			<col class="td2" style="width:610px;" />
		</colgroup>
		<form id="configForm" name="configForm" method="post" action="?mudi=run" onsubmit="return CheckConfigForm()">
		<tr>
			<td colspan="2" class="title">网站配置初始化</td>
		</tr>
		<tr>
			<td colspan="2" class="title2">后台帐号信息和路径</td>
		</tr>
		<tr>
			<td align="right">后台登录帐号:</td>
			<td>
				<input type="text" id="adminName" name="adminName" value="admin" style="width:250px;" />
			</td>
		</tr>
		<tr>
			<td align="right">后台登录密码:</td>
			<td>
				<input type="text" id="adminPwd" name="adminPwd" value="admin" style="width:250px;" />
			</td>
		</tr>
		<tr>
			<td align="right">后台目录名:</td>
			<td>
				<input type="text" id="adminDir" name="adminDir" value="<%=session("adminDir")%>" style="width:250px;" />
				<span class="desc">请修改默认后台目录名可提高安全性</span>
			</td>
		</tr>
		<tr>
			<td align="right">数据库类型:</td>
			<td>
				<label><input type="radio" name="dbType" value="access" checked="checked" /> ACCESS</label>
				<label><input type="radio" name="dbType" value="mssql" disabled="true" /> MSSQL（商业版才有）</label>
			</td>
		</tr>
		<tr>
			<td colspan="2" class="title2">设置数据库路径</td>
		</tr>
		<tr>
			<td align="right">网站数据库名称:</td>
			<td>
				<input type="text" id="accName" name="accName" style="width:250px;" value="<%=dbNameArr(1)%>" /> 
				<span class="desc"> 建议修改默认数据库文件名</span>
			</td>
		</tr>
		<tr>
			<td align="right">数据库目录名:</td>
			<td>
				<input type="text" id="accDir" name="accDir" style="width:250px;" value="<%=dbNameArr(0)%>" /> 
				<span class="desc"> 建议修改默认数据库目录名</span>
			</td>
		</tr>
		<tr>
			<td align="right">数据库备份目录名:</td>
			<td>
				<input type="text" id="accBackupDir" name="accBackupDir" style="width:250px;" value="<%=dbBackupDir%>" /> 
				<span class="desc"> 建议修改默认数据库备份目录名</span>
			</td>
		</tr>
		<tr>
			<td colspan="2" class="title2">数据库初始化设置</td>
		</tr>
		<tr>
			<td align="right">清空所有数据和图片:</td>
			<td style="color:#000;">
				<label title="清空的数据有：文章、文章评论、栏目、单篇内容、关键词、留言、来源管理、作者管理、上传文件记录、会员上传文件、会员IP管理、会员、投票"><input type="checkbox" id="judClearDB" name="judClearDB" value="true" />确定清空所有数据<span style="color:#a59ea3;">(清空的数据有：文章、文章评论、栏目、单篇内容、关键词、留言、来源管理、作者管理、上传文件记录、会员上传文件、会员IP管理、会员、投票)</span></label>
				<span class="desc"> </span><br />
				<label><input type="checkbox" id="judClearImg" name="judClearImg" value="true" />确定清空所有上传图片</label>
			</td>
		</tr>
		<tr>
			<td colspan="2" align="center" class="btnBox">
				<input id="updateBtn" type="button" value="上一步" onclick="document.location.href='index.asp?mudi=check';" />
				&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
				<input type="submit" value="确定设置" />
			</td>
		</tr>
		</form>
		</table>
	</td>
	</tr>
	</table>
</td>
</tr>
</table>
<%
End Sub



Sub run()
	adminName	= ToPostStr("adminName")
	adminPwd	= ToPostStr("adminPwd")
	adminDir	= ToPostStr("adminDir")

	dbType		= ToPostStr("dbType")
	accDir		= ToPostStr("accDir")
	accName		= ToPostStr("accName")
	accBackupDir= ToPostStr("accBackupDir")

	judClearDB	= ToPostStr("judClearDB")
	judClearImg	= ToPostStr("judClearImg")

	If adminName="" Or adminPwd="" Or adminDir="" Or dbType="" Or accDir="" Or accName="" Or accBackupDir="" Then
		Call JS_AlertBackEnd("数据接收不全")
	End If
	newUserKey	= RndChar(5)
	adminPwd	= md5(md5(adminPwd) & newUserKey)

	Set adminrec=server.CreateObject("ADODB.recordset")
	adminrec.open "select MB_username,MB_userpwd,MB_userKey from OT_member where MB_ID=1",conobj,0,2
	adminrec("MB_username")	= adminName
	adminrec("MB_userpwd")	= adminPwd
	adminrec("MB_userKey")	= newUserKey
	adminrec.update()
	adminrec.close
	Set adminexe=Nothing

	conobj.execute("update OT_userSys set US_loginKey='"& RndChar(36) &"'")
	conobj.execute("update OT_sysAdmin set SA_adminLoginKey='"& RndChar(36) &"'")

	Call Cache_TableFields("userSys")
	Call Cache_TableFields("infoSys")
	Call Cache_TableFields("sysAdmin")
	Call Cache_TableFields("system")
	Call Cache_TableFields("tbkSystem")
	Call Cache_TableFields("sysImages")
	Call Ad_MakeJs()

	If judClearDB="true" Then
		conobj.execute("delete from OT_info")
		conobj.execute("delete from OT_infoMessage")
'		conobj.execute("delete from OT_infoMove")
		conobj.execute("delete from OT_infoType")
		conobj.execute("delete from OT_infoWeb")
		conobj.execute("delete from OT_keyWord")
		conobj.execute("delete from OT_message")
		conobj.execute("delete from OT_type")
		conobj.execute("delete from OT_upFile")
		conobj.execute("delete from OT_userIp")
		conobj.execute("delete from OT_users")
		conobj.execute("delete from OT_vote")
	End If

		conobj.close
		Set conobj=Nothing

	If judClearDB="true" Then
		Call CompactDB(Server.MapPath(dbPathPart & dbName),"false")
	End If
	If judClearImg="true" Then
		Call File_DelDir(Server.MapPath(dbPathPart &"upFiles/infoImg/"),false,true)
		Call File_CreateDir(Server.MapPath(dbPathPart &"upFiles/infoImg/coll/"))
		Call File_CreateDir(Server.MapPath(dbPathPart &"upFiles/infoImg/laoy/"))
		Call File_CreateDir(Server.MapPath(dbPathPart &"upFiles/infoImg/ok3w/"))
		Call File_DelDir(Server.MapPath(dbPathPart &"upFiles/infoMove/"),false,true)
	End If

	webSiteID		= RndABC(5) &"_"
	webBackupDir	= accBackupDir
	webDatabase		= dbType
	webDbName		= accName

	dbNameArr = Split(dbName,"/")
	alertStr = ""
	alertNum = 0
	isAdminDir		= File_RevDirName(dbPathPart & session("adminDir"),adminDir)
		If isAdminDir=False Then
			alertNum = alertNum + 1
			alertStr = alertStr & alertNum &"、后台目录名重命名（新名称:"& adminDir &"）失败；<br />"
		Else
			session("adminDir") = adminDir		
		End If

	isDatabaseName	= File_RevName(dbPathPart & dbName,accName)
		If isDatabaseName=False Then
			alertNum = alertNum + 1
			alertStr = alertStr & alertNum &"、网站数据库名称重命名（新名称:"& accName &"）失败；<br />"
				webDbName = Split(dbName,"/")(1)
		Else
			isDatabaseDir	= File_RevDirName(dbPathPart & dbNameArr(0),accDir)
				If isDatabaseDir=False Then
					alertNum = alertNum + 1
					alertStr = alertStr & alertNum &"、数据库目录名重命名（新名称:"& accDir &"）失败；<br />"
					accDir = Split(dbName,"/")(0)
				End If
		
		End If


	isBackupDir		= File_RevDirName(dbPathPart & dbBackupDir,accBackupDir)
		If isBackupDir=False Then
			alertNum = alertNum + 1
			alertStr = alertStr & alertNum &"、数据库备份目录名重命名（新名称:"& accBackupDir &"）失败；<br />"
			webBackupDir = dbBackupDir
		End If

	configContent = File_Read(server.mapPath("config2.4.OTtpl"))
	configContent = Replace(configContent,"{%SiteID%}",webSiteID)
	configContent = Replace(configContent,"{%DbDir%}",accDir)
	configContent = Replace(configContent,"{%BackupDir%}",webBackupDir)
	configContent = Replace(configContent,"{%DbName%}",webDbName)
	configContent = Replace(configContent,"{%SiteID%}",webSiteID)
	Call File_Write(configContent,server.mapPath(dbPathPart &"config.asp"))

	Call File_Write("Powered By 网钛科技 Copyright 2010-"& Year(Date()),server.mapPath(dbPathPart &"cache/install.lock"))
	If alertStr<>"" Then
		alertStr = alertStr &"<br />建议重命名以上"& alertNum &"个目录名，然后修改config.asp文件里的相关信息。"
	Else
		alertStr = "<span style='color:green;'>设置成功!</span>"
	End If

	response.write(_
	"<form id='resultForm' name='resultForm' method='post' action='index.asp?mudi=finish'>"&_
	"<textarea id='result' name='result' style='display:none;'>"& alertStr &"</textarea>"&_
	"<input type='submit' id='subBtn' name='subBtn' value='' style='display:none;' />"&_
	"</form>"&_
	"<script language='javascript' type='text/javascript'>"&_
	"document.getElementById('resultForm').submit();"&_
	"</script>"&_
	"")
End Sub


Sub finish()
	result	= ToPostStr("result")

	beforeURL="http://"& request.ServerVariables("Server_NAME") & GetCurrUrlPost() & request.ServerVariables("SCRIPT_NAME")
	beforeURL=Left(beforeURL,InstrRev(beforeURL,"/") - 1)
	beforeURL=Left(beforeURL,InstrRev(beforeURL,"/"))

%>
<table width="100" border="0" align="center" cellpadding="3" cellspacing="0" class="box1">
<tr>
<td>
	<table width="100" border="0" cellpadding="0" cellspacing="1" class="box2">
	<tr>
	<td>
		<table width="750" border="0" align="center" cellpadding="0" cellspacing="0">
		<tr>
			<td class="title border1">
				安装向导初始化结束
			</td>
		</tr>
		<tr>
			<td align="left" class="td1" style="padding:10px 0 10px 0;">
				<table align="center"><tr><td style="line-height:1.8;">
					<%=result%><br />
					<b>后台地址：</b><%="<a href='"& beforeURL & session("adminDir") &"/'>"& beforeURL & session("adminDir") &"/</a>"%><br />
					为了安全，请删除根目录下install(安装向导)文件夹
				</td></tr></table>
			</td>
		</tr>
		<tr>
			<td align="center" class="btnBox">
				<input type="button" value="进入首页" onclick="document.location.href='../';" />
				&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
				<input type="button" value="登录后台" onclick="document.location.href='../<%=session("adminDir")%>';" />
				&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
				<input type="button" value=" 关 闭 " onclick="window.close();">
			</td>
		</tr>
		</table>
	</td>
	</tr>
	</table>
</td>
</tr>
</table>
<%
End Sub










'检查组件是否被支持
Function IsObjInstalled(objName)
	On Error Resume Next
	IsObjInstalled = False
	Err = 0
	Dim checkObj
	Set checkObj = Server.CreateObject(objName)
	If Err = 0 Then IsObjInstalled = True
	Set checkObj = Nothing
	Err = 0
End Function


'检查组件版本
Function GetObjVar(objName)
	On Error Resume Next
	GetObjVar=""
	Err = 0
	Dim checkObj
	Set checkObj = Server.CreateObject(objName)
	If Err = 0 Then GetObjVar="Ver "& checkObj.version
	Set checkObj = Nothing
	Err = 0
End Function


'显示组件信息
Function ShowObjInfo(objName)
	If Not IsObjInstalled(objName) Then 
		ShowObjInfo = "<span style='color:red'><b> ×</b></span>"
	Else
		ShowObjInfo = GetObjVar(objName) & "<span style='color:green'><b> √</b></span>"
			If objName="Persits.Jpeg" Then
				set jpeg=server.createobject("persits.jpeg")
		ShowObjInfo=ShowObjInfo &"(有效期至"& jpeg.expires &")"	
				jpeg.close
				Set jpeg=Nothing
			End If
	End If
End Function


' 检查文件是否存在
Function File_IsExist(filePath)
    Set fso = CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filePath) then
		   File_IsExist = True	'存在
		Else
		   File_IsExist = False	'不存在
		End if
    Set fso = nothing
End Function

' 文件大小（带单位）
Function File_SizeUnit(num)
	sizeNum=num/1048576
	If Int(sizeNum)>=1 Then
		File_SizeUnit=FormatNumber(sizeNum,2,True) &"MB"
	Else
		sizeNum=num/1024
		If Int(sizeNum)>=1 Then
			File_SizeUnit=FormatNumber(sizeNum,2,True) &"KB"
		Else
			File_SizeUnit=num &"bytes"
		End If
	End If
End Function


' 获取文件大小
Function File_GetSize(filePath)
    Set fso = CreateObject("Scripting.FileSystemObject")
		If fso.fileexists(filePath) Then
			Set fsoFile=fso.GetFile(filePath)
			File_GetSize = fsoFile.size
			Set fsoFile=Nothing
		Else
		   File_GetSize = -1

		End If
    Set fso = nothing
End Function

' 读取文件内容
Function File_Read(filePath)
	On Error Resume Next
	Err.Clear
	Dim fso,f
	Set fso=CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filePath) then
			Set f = fso.OpenTextFile(filePath)
				File_Read=f.ReadAll
			f.close
			Set f=Nothing
		Else
		   File_Read = ""
		End if
	Set fso=Nothing
End Function


' 将内容写入文件
Function File_Write(content,filename)
	On Error Resume Next
	Err.Clear
	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		Set f = fso.CreateTextFile(filename,true)'如果文件名重复将覆盖旧文件
			f.Write content
			If Err Then
				Err.Clear
				File_Write = False
			Else
				File_Write = True
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function


' 修改文件名称
Function File_RevName(filePath, newFileName)
	If Instr(filePath,":")<=0 Then
		filePath=Server.MapPath(filePath)
	End If


	On Error Resume Next
	Err.Clear
	Dim fso,fsoFile
	Set fso=Server.CreateObject("Scripting.FileSystemObject")
		Set fsoFile=fso.GetFile(filePath)
		If fsoFile.Name<>newFileName Then fsoFile.Name=newFileName
			If Err Then
'				response.write(filePath &"|"& newFileName)
'				response.end
				Err.Clear
				File_RevName = False
			Else
				File_RevName = True
			End If
		Set fsoFile=nothing
	Set fso=Nothing
End Function


' 修改文件夹名称
Function File_RevDirName(dirPath, newDirName)
	If Instr(dirPath,":")<=0 Then
		dirPath=Server.MapPath(dirPath)
	End If

	On Error Resume Next
	Err.Clear
	Dim fso,fsoDir
	Set fso=Server.CreateObject("Scripting.FileSystemObject")
		Set fsoDir=fso.GetFolder(dirPath)
		If fsoDir.Name<>newDirName Then fsoDir.Name=newDirName
			If Err Then
				Err.Clear
				File_RevDirName = False
			Else
				File_RevDirName = True
			End If
		Set fsoDir=nothing
	Set fso=nothing
End Function


' 复制文件
Function File_Copy(TempSource,TempEnd)
	Dim fso
	Set fso = Server.CreateObject("Scripting.FileSystemObject")
'		If fso.FileExists(TempEnd)=False Then
'			File_Copy=False
'			Set fso=Nothing
'			Exit Function
'		End IF
		If fso.FileExists(TempSource)=False Then
			File_Copy=False
			Set fso=Nothing
			Exit Function
		End If
    fso.CopyFile TempSource,TempEnd
    Set fso = Nothing 
    File_Copy=True
End Function


Function File_IsRev(ByVal filePath)
	filePath=Server.MapPath(dbPathPart & filePath)

	On Error Resume Next
	Err.Clear
	Dim fso,fsoDir
	Set fso=Server.CreateObject("Scripting.FileSystemObject")
		If fso.FileExists(filePath)=False Then
			File_IsRev = "<span style='color:red;'>不存在</span>"
			Exit Function
		End If
		Set fsoFile=fso.GetFile(filePath)
		souDirName = fsoFile.Name
		fsoFile.Name="test_"& souDirName
		fsoFile.Name=souDirName
			If Err Then
				Err.Clear
				File_IsRev = "<span style='color:red;'>不可改</span>"
			Else
				File_IsRev = "<span style='color:green;'>可改</span>"
			End If
		Set fsoFile=nothing
	Set fso=nothing
End Function


Function File_IsDirRev(ByVal dirPath)
	dirPath=Server.MapPath(dbPathPart & dirPath)

	On Error Resume Next
	Err.Clear
	Dim fso,fsoDir
	Set fso=Server.CreateObject("Scripting.FileSystemObject")
		If fso.FolderExists(dirPath)=False Then
			File_IsDirRev = "<span style='color:red;'>不存在</span>"
			Exit Function
		End If
		Set fsoDir=fso.GetFolder(dirPath)
		souDirName = fsoDir.Name
		fsoDir.Name="test_"& souDirName
		fsoDir.Name=souDirName
			If Err Then
				Err.Clear
				File_IsDirRev = "<span style='color:red;'>不可改</span>"
			Else
				File_IsDirRev = "<span style='color:green;'>可改</span>"
			End If
		Set fsoDir=nothing
	Set fso=nothing
End Function


Function File_IsDirWrite(ByVal dirPath)
	testPath = server.MapPath(dbPathPart & dirPath &"/test.txt")
	dirPath = server.MapPath(dbPathPart & dirPath)
	On Error Resume Next
	Err.Clear
	Dim fso,f
	Set fso = Server.CreateObject("Scripting.FileSystemObject") 
		If fso.FolderExists(dirPath)=False Then
			File_IsDirWrite = "<span style='color:red;'>不存在</span>"
			Exit Function
		End If
		Set f = fso.CreateTextFile(testPath,true)
			f.Write "OTCMS v2.0"
			If Err Then
				Err.Clear
				File_IsDirWrite = "<span style='color:red;'>不可写</span>"
				Exit Function
			Else
				File_IsDirWrite = "<span style='color:green;'>可写</span>"
			End If
		f.Close
		Set f = Nothing
	fso.DeleteFile testPath
	set fso=Nothing
End Function

Function File_Right(filePath)
	filePath = server.MapPath(dbPathPart & filePath)
	On Error Resume Next
	Err.Clear
	Dim fso,f
	File_Right = ""
	readContent = ""
	set fso=CreateObject("Scripting.FileSystemObject")
		set f = fso.OpenTextFile(filePath)
			readContent=f.ReadAll
			If Err Then
				Err.Clear
				configFileState = 0
				File_Right = File_Right &"<span style='color:red;'>不可读</span>"
			Else
				File_Right = File_Right &"<span style='color:green;'>可读</span>"
			End If
		f.close
		set f=nothing
		Set f = fso.CreateTextFile(filePath,true)'如果文件名重复将覆盖旧文件
			f.Write readContent
			If Err Then
				Err.Clear
				configFileState = 0
				File_Right = File_Right &"、<span style='color:red;'>不可写</span>"
			Else
				File_Right = File_Right &"、<span style='color:green;'>可写</span>"
			End If
		f.Close
		Set f = Nothing
	set fso=Nothing
End Function

' 表第一行所有字段以常量方式写入文件
Function Cache_TableFields(tabName)
	Select Case tabName
		Case "system"
			tabFieldStr="*"
		Case Else
			tabFieldStr="*"
	End Select

	newStr = "<"& Chr(37) & Chr(10)
	Set fieldexe=conobj.execute("select top 1 "& tabFieldStr &" from OT_"& tabName &"")
		fieldCount = fieldexe.fields.count
		For i=0 To fieldCount-1
			newStr = newStr &"Const "& fieldexe(i).name &"	= "& Cache_Func_FieldStr(fieldexe(i)) & Chr(10)
		Next
	fieldexe.close
	Set fieldexe=Nothing
	newStr = newStr & Chr(37) &">"

	Call File_Write(newStr,Server.MapPath("../cache/"& tabName &".asp"))
End Function



Function Cache_Func_FieldStr(fieldValue)
	If IsNum(fieldValue) Then
		Cache_Func_FieldStr = fieldValue
	Else
		Cache_Func_FieldStr = """"& Replace(Replace(Replace(""& fieldValue,"""",""""""),Chr(10),""),Chr(13),"") &""""
	End If
End Function


Function Ad_MakeJs()
	newStr=""&_
	"var showHiddenAd=false;"& Chr(10) &_
	"function OTnews_ads(str){"& Chr(10) &_
		"switch (str){"& Chr(10) &_
		""
	Set adexe=conobj.execute("select * from OT_ad")
		Do Until adexe.EOF
			AD_code = adexe("AD_code")
			If adexe("AD_num")=17 Then
				AD_code = "<div style='margin:5px;'>"& AD_code &"</div>"
			End If
			newStr=newStr &_
			"case 'ot"& FixIntLen(adexe("AD_num"),3) &"':"& Chr(10) &_
			""
					If adexe("AD_state")=1 Then
				newStr=newStr &_
				HtmlToJs(AD_code) & Chr(10) &_
				"break;"& Chr(10)&Chr(10) &_
				""
					Else
				newStr=newStr &_
				"if (showHiddenAd==true){"& Chr(10) &_
				HtmlToJs(AD_code) & Chr(10) &_
				"}"& Chr(10) &_
				"break;"& Chr(10)&Chr(10) &_
				""
					End If
		adexe.movenext
		Loop
	adexe.close
	Set adexe=Nothing
	newStr=newStr &_
			"}"& Chr(10) &_
		"}"

	Call File_Write(Replace(newStr,"[siteTitle]","""+ escape(document.title) +"""),Server.MapPath("../cache/ads.js"))
End Function

' 获取当前域名端口
Function GetCurrUrlPost()
	Dim Const_SERVER_PORT
	Const_SERVER_PORT = Request.ServerVariables("SERVER_PORT")
	If Const_SERVER_PORT<>80 Then Const_SERVER_PORT=":"& Const_SERVER_PORT Else Const_SERVER_PORT=""
	GetCurrUrlPost = Const_SERVER_PORT
End Function


'压缩数据库函数
Function CompactDB(dbPath, isAccess97)
	Dim fso, Engine, dbPathStr, JET_3X
	dbPathStr = Left(dbPath,InstrRev(dbPath,"\"))

	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FileExists(dbPath) Then
		Set Engine = CreateObject("JRO.JetEngine")
			If isAccess97 = "True" Then
				Engine.Compactdatabase "Provider=Microsoft.Jet.OLEDB.4.0;Data Source="& dbPath, _
				"Provider=Microsoft.Jet.OLEDB.4.0;Data Source="& dbPathStr &"temp.mdb;" _
				& "Jet OLEDB:Engine Type="& JET_3X
			Else
				Engine.Compactdatabase "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" & dbPath, _
				"Provider=Microsoft.Jet.OLEDB.4.0;Data Source="& dbPathStr &"temp.mdb"
			End If
		fso.CopyFile dbPathStr & "temp.mdb",dbPath
		fso.DeleteFile(dbPathStr & "temp.mdb")
			Set fsoFile=fso.GetFile(dbPath)
				newFileSize = fsoFile.size
			Set fsoFile=Nothing
		Set fso = nothing
		Set Engine = nothing

		CompactDB = True
	Else

		CompactDB = False
	End If
End Function

' 新建目录,如果存在跳过.
Function File_CreateDir(dirPath)
    Set fsoDir = CreateObject("Scripting.FileSystemObject")
		If Not fsoDir.FolderExists(dirPath) then
		   fsoDir.CreateFolder(dirPath)
		End if
    Set fsoDir = Nothing
End Function

' 删除目录下的所有文件
Function File_DelDir(ByVal dirPath,ByVal delDir,ByVal delFileDir)
	If Right(dirPath,1)="/" Then dirPath=Left(dirPath,Len(dirPath)-1)
	Set	fso=server.CreateObject("Scripting.FileSystemObject")
	Set f=fso.GetFolder(dirPath)

	' 循环目录下所有子目录
	Set fs=f.SubFolders
	For Each folder In fs
		If fso.FolderExists(folder) Then Call File_DelDir(folder,delFileDir,delFileDir)
	Next

	' 循环目录下所有文件
	Set fc=f.Files
	For Each file In fc
		If fso.FileExists(file) Then fso.DeleteFile(file)
		'If fso.FolderExists(file) Then fso.DeleteFolder(file)
	Next

	If fso.FolderExists(dirPath) And delDir=True Then fso.DeleteFolder(dirPath)
End Function

%>