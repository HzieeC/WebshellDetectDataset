<%
function clearFolderDot(path)
	do while left(path,1)="."
		path = mid(path,2)
	loop
	clearFolderDot = path
end function
function writefile(name,content,charset)
	Dim tmpStrm
	Set tmpStrm =server.CreateObject("ADODB.Stream")
	tmpStrm.Mode = 3
	tmpStrm.Type = 2
	tmpStrm.charset=charset
	tmpStrm.Open
	tmpStrm.writetext content
	tmpStrm.SaveToFile name, 2
	tmpStrm.Close
	Set tmpStrm = Nothing
end function
Function CheckIn(ary,v)
	CheckIn = false
	Dim i
	For i=0 to ubound(ary)
		If ary(i) = v Then 
			CheckIn = true
			exit for
		End If
	Next
End Function
Function instr2(source,search)
	instr2 = (instr("," & source & ",","," & search & ",")>0)
End Function
Function Rnd_(strSeed,intLength)
    Dim seedLength, pos, Strs, i
    seedLength = Len(strSeed)
    Strs = ""
    For i = 1 To intLength
		randomize timer*1000
        Strs = Strs & Mid(strSeed, Int(seedLength * Rnd) + 1, 1)
    Next
    Rnd_ = Strs
end function
'生成随机字符串
Function RndUCase(intLength)
    RndUCase = Rnd_("IJKLMNOPQRSTUVWXYZABCDEFGH",intLength)
End Function

'生成随机字符串
Function RndLCase(intLength)
    RndLCase = Rnd_("abcdefghijklmnopqrstuvwxyz",intLength)
End Function

'生成随机字符串
Function RndStr(intLength)
    RndStr = Rnd_("abcdefghiIJKLMNOPQRSTUVWXYZjklmnopqrstuvwxyzABCDEFGH",intLength)
End Function

'生成随机字符串
Function RndNumber(intLength)
    RndNumber = Rnd_("12345678906789012678901234534567890126789012345345",intLength)
End Function

'生成随机字符串
Function RndStr1(intLength)
    RndStr1 = Rnd_("abcdefghiIJKLMNOPQRSTUVWXYZjklmnopqrstuvwxyzABCDEFGH12345678906789012678901234534567890126789012345345",intLength)
End Function

Function GetMatch(ByVal Str, ByVal Rex)
	Dim Reg, Mag
	Set Reg = New RegExp
	With Reg
		.IgnoreCase = True
		.Global = True
		.Pattern = Rex
		Set Mag = .Execute((str))
		If Mag.Count > 0 Then
			Set GetMatch = Mag
		Else
			Set GetMatch = Server.CreateObject("Scripting.Dictionary")
		End If
	End With
	Set Reg = nothing
End Function

Function RegTest(Str,RegStr)
	Dim Reg, Mag
	Set Reg = New RegExp
	With Reg
		.IgnoreCase = True
		.Global = True
		.Pattern = RegStr
		RegTest = .test((str))
	End With
	Set Reg = nothing
End Function
Function ReplaceEx(sourcestr, regString, str)
    dim re
	Set re = new RegExp
	re.IgnoreCase = true
	re.Global = True
	re.pattern = "" & regString & ""
	str = re.replace(sourcestr, str)
    set re = Nothing
    ReplaceEx = str
End Function
Function formatDateEx(Byval vDate, Byval formatStr)
	If not isdate(vDate) Then vDate = now()
	Dim y(1),m(1),d(1),h(1),n(1),s(1),w,ws(1)
	y(0) = year(vDate)
	m(0) = month(vDate)
	d(0) = day(vDate)
	h(0) = hour(vDate)
	n(0) = minute(vDate)
	s(0) = second(vDate)
	y(1) = right(year(vDate),2)
	m(1) = right("0" & month(vDate),2)
	d(1) = right("0" & day(vDate),2)
	h(1) = right("0" & hour(vDate),2)
	n(1) = right("0" & minute(vDate),2)
	s(1) = right("0" & second(vDate),2)
	ws(0)=Array("日","一","二","三","四","五","六")
	ws(1)=Array("Sun","Mon","Tues","Wed","Thur","Fri","Sat")
	w=weekday(vDate)-1
	
	Dim returnStr
	returnStr = replace(formatStr,"yyyy",y(0))
	returnStr = replace(returnStr,"y",y(1))
	returnStr = replace(returnStr,"mm",m(1))
	returnStr = replace(returnStr,"m",m(0))
	returnStr = replace(returnStr,"dd",d(1))
	returnStr = replace(returnStr,"d",d(0))
	returnStr = replace(returnStr,"hh",h(1))
	returnStr = replace(returnStr,"h",h(0))
	returnStr = replace(returnStr,"nn",n(1))
	returnStr = replace(returnStr,"n",n(0))
	returnStr = replace(returnStr,"ss",s(1))
	returnStr = replace(returnStr,"s",s(0))
	returnStr = replace(returnStr,"W",ws(0)(w))
	returnStr = replace(returnStr,"w",ws(1)(w))
	formatDateEx = returnStr
End Function
Function AppendToFile(Byval FileName,Byval Content,byval charset)
	Dim Stream
	Set stream = server.CreateObject("ADODB.STREAM")
	stream.mode=3
	stream.type=2
	stream.charset = charset
	stream.open
	if isExists(FileName) Then
		stream.loadfromfile FileName
		stream.position = stream.size
		stream.writetext Content
	Else
		stream.writetext Content
	End If
	stream.savetofile FileName,2
	stream.close
	set stream = nothing
End Function

Function ReadFromFile(Byval FileName,Byval Charset)
	Dim Stream
	Set stream = server.CreateObject("ADODB.STREAM")
	stream.mode=3
	stream.type=2
	stream.charset = Charset
	stream.open
	if isExists(FileName) Then
		stream.loadfromfile FileName
		ReadFromFile = Stream.readtext()
	Else
		ReadFromFile = ""
	End If
	stream.close
	set stream = nothing
End Function

Function ReadBinFromFile(Byval FileName)
	Dim Stream
	Set stream = server.CreateObject("ADODB.STREAM")
	stream.mode=3
	stream.type=1
	stream.open
	if isExists(FileName) Then
		stream.loadfromfile FileName
		ReadBinFromFile = Stream.read()
	Else
		ReadBinFromFile = ""
	End If
	stream.close
	set stream = nothing
End Function

Function isExists(Byval FileName)
	Dim fso
	set fso = server.CreateObject("scripting.filesystemobject")
	isExists = fso.fileexists(FileName)
	Set fso = nothing
End Function
Function IIF(Byval Comf,Byval retTrue, Byval retFalse)
	If Comf Then
		IIF = retTrue
	Else
		IIF = retFalse
	End If
End Function
%>