<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->

<%
Call CheckIframe()


' **** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 **** */


' 打开用户表，并检测用户是否登陆
Call OpenMemberexe("","login",0)

Call WebStyleTop("")


response.write(_
"<script language='javascript' type='text/javascript' src='js/inc/list.js'></script>"&_
"<script language='javascript' type='text/javascript' src='js/memberLog.js'></script>"&_
"")


dataTypeCN = "后台人员操作记录"


Select Case mudi
	Case "manage":
		Call IsAdminRight("alertBack")
		Call manage()

End Select


Call CloseMemberexe()
Call CloseConobj()
Call WebStyleBottom()









function manage()
	refRealName	= ToGetRegExpStr("refRealName","sql")
	refNote		= ToGetRegExpStr("refNote","sql")
	refDate1	= ToGetStr("refDate1")
		If IsDate(refDate1)=False Then refDate1=""
	refDate2	= ToGetStr("refDate2")
		If IsDate(refDate2)=False Then refDate2=""

	SQLstr="select * from OT_memberLog where (1=1)"

	If refRealName<>"" Then SQLstr = SQLstr &" and ML_realname like '%"& refRealName &"%'"
	If refNote<>"" Then SQLstr = SQLstr &" and ML_note like '%"& refNote &"%'"
	If refDate1<>"" Then SQLstr = SQLstr &" and ML_time>="& SqlForTime(refDate1)
	If refDate2<>"" Then SQLstr = SQLstr &" and ML_time<"& SqlForTime(DateAdd("d",1,refDate2))

	orderName=ToGetStr("orderName")
		If orderName<>"realname" And orderName<>"note" And orderName<>"ipCN" And orderName<>"readNum" And orderName<>"time" Then orderName="time"
	orderSort=ToGetStr("orderSort")
		If orderSort<>"ASC" Then orderSort="DESC"




	Call ShareTableTop("share_refer.gif","",dataTypeCN &"查询")
	response.write(_
	"<form name='refForm' method='get' action=''>"&_
	"<input type='hidden' name='mudi' value='"& mudi &"' />"&_
	"<input type='hidden' name='dataType' value='"& dataType &"' />"&_

	"<table align='center' border='0' cellSpacing='0' cellPadding='0' summary=''>"&_
	"<tr>"&_
		"<td width='30%' style='padding:5px' class='font1_2'>"&_
			"&nbsp;&nbsp;&nbsp;&nbsp;称呼："&_
			"<input type='text' name='refRealName' size='14' value='"& refRealName &"' />"&_
		"</td>"&_
		"<td width='30%' style='padding:5px' class='font1_2'>"&_
			"&nbsp;&nbsp;&nbsp;&nbsp;记录："&_
			"<input type='text' name='refNote' size='14' value='"& refNote &"' />"&_
		"</td>"&_
		"<td width='40%' style='padding:5px' class='font1_2'>"&_
			"&nbsp;&nbsp;操作日期：<input type='text' name='refDate1' size='10' value='"& refDate1 &"' onfocus=""WdatePicker()"" />&nbsp;"&_
			"至&nbsp;<input type='text' name='refDate2' size='10' value='"& refDate2 &"' onfocus=""WdatePicker()"" />"&_
		"</td>"&_
	"</tr>"&_
	"<tr>"&_
		"<td align='center' style='padding:5px;padding-top:20px' colspan='3'>"&_
			"<input type='image' src='images/button_refer.gif' />"&_
			"&nbsp;&nbsp;&nbsp;&nbsp;"&_
			"<img src='images/button_reset.gif' onclick=""document.location.href='?mudi="& mudi &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"'"" style='cursor:pointer' alt='' />"&_
		"</td>"&_
	"</tr>"&_
	"</table>"&_
	"</form>"&_
	"")

	Call ShareTableBottom()


	response.write(_
	"<br />"&_
	"")

	Call ShareTableTop2("share_list.gif","",dataTypeCN &"列表")
	Call ShareTableItemTitle("6%,14%,55%,7%,19%","序号,"& ShowArrow("称呼","realname",orderName,orderSort) &","& ShowArrow("记录","note",orderName,orderSort) &","& ShowArrow("次数","readNum",orderName,orderSort) &","& ShowArrow("操作时间","time",orderName,orderSort) &"")


	Set showrec=server.CreateObject("ADODB.recordset")
	showrec.open SQLstr &" order by ML_"& orderName &" "& orderSort,conobj,3,1
		If showrec.EOF Then
			response.write("</table><br /><center class='font1_1'>无内容</center>")
			response.End
		End If
			showPageSize=memberexe("MB_itemNum")		'每页条数
			showrec.pagesize=showPageSize	
			showPageCount=showrec.pagecount
			showCount=showrec.recordcount
			page=ToGetInt("page")
			if page < 1 Or page > showPageCount Then page=1
			showrec.absolutepage=page


		response.write(_
		"<tbody class='tabBody padd5'>"&_
		"")
		number=1+(page-1)*showPageSize
		For i=1 To showPageSize
			If showrec.EOF Then Exit For
			If i Mod 2 = 0 Then bgcolor="class='tabColorTr'" Else bgcolor=""
		response.write(_
		"<tr "& bgcolor &">"&_
			"<td align='center'>"& number &"</td>"&_
			"<td align='center'>"& showrec("ML_realname") &"<br /></td>"&_
			"<td align='left'>"& showrec("ML_note") &"<br /></td>"&_
			"<td align='center'>"& showrec("ML_readNum") &"<br /></td>"&_
			"<td align='center'>"& showrec("ML_time") &"<br /></td>"&_
		"</tr>"&_
		"")
		showrec.MoveNext()
		number = number + 1
		Next
		response.write(_
		"</tbody>"&_
		"")

	response.write(_
	"<tr class='tabColorB'>"&_
		"<td align='left' class='padd5' colspan='20'>"&_
			"<input type='button' value='3天前记录删除' onclick=""if(confirm('你确定要删除3天前记录？')==true){document.location.href='memberLog_deal.asp?mudi=monthDel&day=3&URL='+ escape(document.location.href)}"" />"&_
			"<input type='button' value='7天前记录删除' onclick=""if(confirm('你确定要删除1周前记录？')==true){document.location.href='memberLog_deal.asp?mudi=monthDel&day=7&URL='+ escape(document.location.href)}"" />"&_
			"<input type='button' value='14天前记录删除' onclick=""if(confirm('你确定要删除2周前记录？')==true){document.location.href='memberLog_deal.asp?mudi=monthDel&day=14&URL='+ escape(document.location.href)}"" />"&_
			"<input type='button' value='30天前记录删除' onclick=""if(confirm('你确定要删除一个月前记录？')==true){document.location.href='memberLog_deal.asp?mudi=monthDel&day=30&URL='+ escape(document.location.href)}"" />"&_
		"</td>"&_
	"</tr>"&_
	"")
	showrec.close
	Set showrec=Nothing

	Call ShareTableBottom2()

End Function

%>