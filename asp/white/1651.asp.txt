<%
Dim objXML
'装载XML文档
Function LoadXML(FilePath)
	Dim IsLoad
	Set objXML = Server.CreateObject("Microsoft.XMLDOM")
	objXML.async = False
	IsLoad = objXML.load(filepath) 
	If Not IsLoad then
		ErrMsg = "装载XML文档失败"
		Call ShowErrMsg(0,0)
	End If
End Function
'关闭XML文档对象
Sub CloseXML()
	Set objXML		= Nothing
	'Set objXMLHttp	= Nothing
	'Set objStream	= Nothing
End Sub
'创建XML文档
Function CreateFile(FilePath)
	Dim fso,WebConfig
	Set fso = CreateObject("Scripting.FileSystemObject")
	Set WebConfig = fso.CreateTextFile(FilePath,True)
	WebConfig.WriteLine("<?xml version=""1.0"" encoding=""utf-8"" ?>")
	WebConfig.WriteLine("<webconfig></webconfig>")
	WebConfig.Close
	Set WebConfig=nothing
	Set fso=nothing
End Function
'检测文件是否存在
Function IsFile(FilePath)
   Dim fso
   Set fso = CreateObject("Scripting.FileSystemObject")
   If (fso.FileExists(FilePath)) Then
      IsFile = True
   Else
      IsFile = False
   End If
   Set fso=nothing
End Function
'-----------------------
'参数说明:
'	1.PathValue:文件夹相对路径
'	2.Auto:是否以年月自动创建文件夹(0:否,1:是)
'-----------------------
Function CreatePath(PathValue,Auto)
	Dim objFSO,Folder
	'以年月创建上传文件夹，格式：2003－8
	If Auto=0 then
		Folder = ""
	Else
		Folder = year(now) & "-" & month(now)
	End If
	If Right(PathValue,1)<>"/" Then PathValue = PathValue&"/"
	On Error Resume Next
	Set objFSO = Server.CreateObject("Scripting.FileSystemObject")
	If objFSO.FolderExists(Server.MapPath(PathValue & Folder))=False Then
		objFSO.CreateFolder Server.MapPath(PathValue & Folder)
	End If
	If Err.Number = 0 Then
		CreatePath = PathValue & Folder & "/"
	Else
		CreatePath = PathValue
	End If
	Set objFSO = Nothing
End Function
%>