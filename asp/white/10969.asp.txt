<%
function UBBCode(strContent)
    dim objRegExp
    Set objRegExp=new RegExp
    objRegExp.IgnoreCase =true
    objRegExp.Global=True

    '换行
    objRegExp.Pattern="(\[br\])"
    strContent=objRegExp.Replace(strContent,"<br/>")
    '下划线
	objRegExp.Pattern="(\[u\])(.+?)(\[\/u\])"
    strContent=objRegExp.Replace(strContent,"<u>$2</u>")
	'斜体
    objRegExp.Pattern="(\[i\])(.+?)(\[\/i\])"
    strContent=objRegExp.Replace(strContent,"<i>$2</i>")
    '加粗
    objRegExp.Pattern="(\[b\])(.+?)(\[\/b\])"
    strContent=objRegExp.Replace(strContent,"<b>$2</b>")
    '双横线
    objRegExp.Pattern="(\[QUOTE\])(.+?)(\[\/QUOTE\])"
    strContent=objRegExp.Replace(strContent,"<BLOCKQUOTE><font size=2 face=""Verdana, Arial"">引用:</font><HR>$2<HR></BLOCKQUOTE>")
    '红色
    objRegExp.Pattern="(\[red\])(.+?)(\[\/red\])"
    strContent=objRegExp.Replace(strContent,"<FONT COLOR=""#ff0000"">$2</FONT>")
    '灰色
    objRegExp.Pattern="(\[gray\])(.+?)(\[\/gray\])"
    strContent=objRegExp.Replace(strContent,"<FONT COLOR=""#77ACAC"">$2</FONT>")
    '绿色
    objRegExp.Pattern="(\[green\])(.+?)(\[\/green\])"
    strContent=objRegExp.Replace(strContent,"<FONT COLOR=""#009933"">$2</FONT>")
    '蓝色
    objRegExp.Pattern="(\[blue\])(.+?)(\[\/blue\])"
    strContent=objRegExp.Replace(strContent,"<FONT COLOR=""#0055ff"">$2</FONT>")
    '任何颜色字
    objRegExp.Pattern="(\[color\=)(.+?)(\])(.+?)(\[\/color\])"
    strContent=objRegExp.Replace(strContent,"<FONT COLOR=""$2"">$4</FONT>")
	'电子邮件
	objRegExp.Pattern="(\[EMAIL\])(\S+\@\S+?)(\[\/EMAIL\])"
    strContent= objRegExp.Replace(strContent,"<A HREF=""mailto:$2"">$2</A>")
	'带链接的文字
	objRegExp.Pattern="(\[URL\])(http:\/\/\S+?)(\[\/URL\])"
    strContent= objRegExp.Replace(strContent,"<A HREF=""$2"" TARGET=_blank>$2</A>")
    objRegExp.Pattern="(\[URL\])(\S+?)(\[\/URL\])"
    strContent= objRegExp.Replace(strContent,"<A HREF=""http://$2"" TARGET=_blank>$2</A>")
	'滚动
	 objRegExp.Pattern="(\[marquee\])(.+?)(\[\/marquee\])"
    strContent=objRegExp.Replace(strContent,"<marquee scrollamount='3' id=xxskybbs onmouseover=xxskybbs.stop() onmouseout=xxskybbs.start()>$2</marquee>")
    '回滚
    objRegExp.Pattern="(\[marqueea\])(.+?)(\[\/marqueea\])"
    strContent=objRegExp.Replace(strContent,"<marquee behavior=""alternate"" scrollamount='3' id=xxskybbs onmouseover=xxskybbs.stop() onmouseout=xxskybbs.start()>$2</marquee>")
    '带链接的图片
    objRegExp.Pattern="(\[IMGurl\=)(http:\/\/\S+?)(\])(http:\/\/\S+?)(\[\/IMGurl\])"
    strContent=objRegExp.Replace(strContent,"<a href=""$2"" target=_blank><IMG SRC=""$4"" border=0 onload=""javascript:if(this.width>screen.width-366)this.width=screen.width-366""></a>")
    objRegExp.Pattern="(\[IMGurl\=)(\S+?)(\])(\S+?)(\[\/IMGurl\])"
    strContent=objRegExp.Replace(strContent,"<a href=""http://$2"" target=_blank><IMG SRC=""http://$4"" border=0 onload=""javascript:if(this.width>screen.width-366)this.width=screen.width-366""></a>")
    '图片
    objRegExp.Pattern="(\[IMG\])(\S+?)(\[\/IMG\])"
    strContent=objRegExp.Replace(strContent,"<IMG SRC=""$2"">")


	set objRegExp=Nothing
    UBBCode=strContent
end function
%>
