<%@ LANGUAGE = VBScript CodePage = 936%>
<!--#include file="../INC/incuser.asp"-->
<!--#include file="../INC/md5.asp"-->
<%
'if not ChkPost then
'	response.end
'end if

'on error resume next
Dim Action,ID
Action = ReplaceBadChar(request.QueryString("Action"))
Select Case Action
	Case "Login":			Call DoLogin()
	Case "Register":		Call DoRegister()
	Case "Logout":			Call DoLogout()
End Select

Class mydata
	public UID
	public UserName
End Class
Function UserCookie()
	set UserCookie = new mydata
	CookieUID = Request.Cookies("MemberAuth")("UID")
	CookieUserName = Request.Cookies("MemberAuth")("UserName")
	CookieUserScore = Request.Cookies("MemberAuth")("UserScore")
	If CookieUID <> "" and CookieUserName <> "" Then
		UserCookie.UID = CookieUID
		UserCookie.UserName = CookieUserName
	Else
		UserCookie.UID = 0
		UserCookie.UserName = ""
	End If
End Function



Sub DoLogin()
	UserName = ReplaceBadChar(request.QueryString("UserName"))
	Password = ReplaceBadChar(request.QueryString("Password"))
	SavePass = request.QueryString("SavePass")
	BackUrl = Request.QueryString("BackUrl")
	If SavePass = "" Then
		SavePass = False
	End If
	Password = MD5(Password, 32)
	CookieUID = 0
	CookieUserName = ""
	isPass = 0
	LevelID = 0
	CurrentLevelID = 0
	sql="select UID, UserName, PassWord, LevelID from Pulb_Member where UserName='"&UserName&"' or Mobile='"&UserName&"' or Email='"&UserName&"'"
	set rs=server.createobject("adodb.recordset")
	rs.open sql,Conn,1,1
	if not rs.eof and not rs.bof then
		if Password = rs(2) then
			isPass = 1
			CookieUID = rs(0)
			CookieUserName = rs(1)
			LevelID = rs(3)
		else
			isPass = 3
		end if
	else
		isPass = 4
	end if
	rs.close
	set rs = nothing
	
	if isPass = 1 then
		CookieUserScore = GetUserScore(CookieUID)		'获取会员积分
		CurrentLevelID = GetUserLevel(CookieUserScore)	'获取会员等级ID
		sql="select LevelID, LoginTimes, LastLoginTime, ThisLoginTime from Pulb_Member where UID="&CookieUID
		set rs=server.createobject("adodb.recordset")
		rs.open sql,Conn,1,3
		if not rs.eof and not rs.bof then
			if CurrentLevelID > LevelID then
				rs(0) = CurrentLevelID
			end if
			rs(1) = rs(1) + 1
			rs(2) = rs(3)
			rs(3) = Now()
			rs.Update
		end if
		rs.close
		set rs = nothing
	
		if SavePass then
			Response.Cookies("MemberAuth").Expires = Date + 1000
		end if
		Response.Cookies("MemberAuth")("UserScore") = CookieUserScore
		Response.Cookies("MemberAuth")("UserName") = VbsEscape(CookieUserName)
		Response.Cookies("MemberAuth")("UID") = CookieUID
		Response.Write "succeed"
		Exit Sub
	elseif isPass = 3 then
		Response.Write VbsEscape("密码错误！")
	else
		Response.Write VbsEscape("用户不存在！")
	end if
End Sub


Sub DoRegister()
	UserName = ReplaceBadChar(Request.QueryString("UserName"))
	PassWord = ReplaceBadChar(Request.QueryString("PassWord"))
	TrueName = ReplaceBadChar(Request.QueryString("TrueName"))
	Mobile = ReplaceBadChar(Request.QueryString("Mobile"))
	Email = ReplaceBadChar(Request.QueryString("Email"))
	CodeStr = Request.QueryString("CodeStr")
	If Session("SysCode") <> CodeStr Then
		Response.Write VbsEscape("请输入正确的验证码！")
		Exit Sub
	End IF
	Session("SysCode") = ""
	if UserName = "" then
		Response.Write VbsEscape("用户名不能为空！")
		Exit Sub
	end if
	if PassWord = "" then
		Response.Write VbsEscape("密码不能为空！")
		Exit Sub
	end if
	if TrueName = "" then
		Response.Write VbsEscape("姓名不能为空！")
		Exit Sub
	end if
	if Mobile = "" then
		Response.Write VbsEscape("手机号码不能为空！")
		Exit Sub
	end if
	if UserNameRegDisabled(UserName) then
		Response.Write VbsEscape("用户名 "& UserName &" 被禁止使用，请重新输入！")
		Exit Sub
	end If
	if UserNameIsUse(UserName) then
		Response.Write VbsEscape("用户名 "& UserName &" 已被使用，请重新输入！")
		Exit Sub
	end if
	if EmailIsUse(Email) then
		Response.Write VbsEscape("E-Mail "& Email &" 已被使用，请重新输入！")
		Exit Sub
	end if
	UserIP = GetUserIP()
	UserScoreDefault = 0  '默认赠送积分
	sql="select UserScoreDefault from Pulb_SiteConfig"
	set rs=server.createobject("adodb.recordset")
	rs.open sql,Conn,1,1
	if not rs.eof then
		UserScoreDefault = rs(0)
	end if
	rs.close
	sql="select * from Pulb_Member"
	rs.open sql,Conn,1,3
	rs.addnew
	rs("UserName") = UserName
	rs("PassWord") = MD5(PassWord, 32)
	rs("TrueName") = TrueName
	rs("Mobile") = Mobile
	rs("Email") = Email
	rs("LoginTimes") = 1
	rs("RegisterIP") = UserIP
	rs("RegisterTime") = now()
	rs.update
	rs.close
	sql = "select @@identity AS UID"
	rs.open sql,Conn,1,1
	UID = rs(0)
	rs.close
	set rs=nothing
	'写积分记录
	if UserScoreDefault > 0 then
		Call SaveUserScore(UID, 10, UserScoreDefault, FunFormatDate(now(), 2)&"注册会员")
	end if
	CloseDB
	Response.Cookies("MemberAuth")("UserScore") = UserScoreDefault
	Response.Cookies("MemberAuth")("UserName") = VbsEscape(UserName)
	Response.Cookies("MemberAuth")("UID") = UID
	Response.Write "succeed"
End Sub


Sub DoLogout()
	Response.Cookies("MemberAuth")("UID") = Empty
	Response.Cookies("MemberAuth")("UserName") = Empty
	Response.Cookies("MemberAuth")("UserScore") = Empty
	ComeUrl = request.servervariables("http_referer")
	if ComeUrl <> "" then
		Response.Redirect ComeUrl
	else
		Response.Redirect "/"
	end if
End Sub
%>