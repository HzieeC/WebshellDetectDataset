<%
	 

		Function SaveFile(Templetcontent,FileName)
			on error resume next 
 			Dim FileFSO,FileType
			 Set FileFSO = Server.CreateObject("ADODB.Stream")
				With FileFSO
				.Type = 2:.Mode = 3:.Open:
 				.Charset = "utf-8":.Position = FileFSO.Size
 				.WriteText  Templetcontent&vbcrlf & "<!-- Created Page at " & Now() & " ,By fycms.com 合肥安川网络-->" & vbCrLf
				.SaveToFile Server.MapPath(FileName),2
 				if err then err.clear: SaveFile = false else SaveFile = true
 				.Close
				End With
			Set FileType = nothing
			Set FileFSO = nothing
		End Function

		Sub echo(str)
			response.write str 
		End Sub 
		Sub die(str)
			  response.write str : Response.End
		End Sub 
  	    Public Function GetStrValue(ByVal strs, ByVal strlen,ns)
			If strs = "" Then GetStrValue = "":Exit Function
			If strlen=0 Then GetStrValue=strs:Exit Function
			Dim l, T, c, I, strTemp
			Dim str
			str=closeHTML(strs)
			l = Len(Str)
			T = 0
			strTemp = Str
			strlen = CLng(strlen)
			For I = 1 To l
				c = Abs(Asc(Mid(Str, I, 1)))
				If c > 255 Then
					T = T + 2
				Else
					T = T + 1
				End If
				If T >= strlen Then
					strTemp = Left(Str, I)
					Exit For
				End If
			Next
			If strTemp <> Str Then
				If ns="1" Then 
					strTemp = strTemp&"..."
				Else 
					strTemp = strTemp
				End If 
			End If 
			GetStrValue=Replace(strs,str,strTemp)
	    End Function

		Function Rep(ByVal ReContent,ByVal ReKey,ByVal ReStr)
			On Error Resume Next
			IF IsNull(ReContent) Or Len(ReContent)=0 Then ReContent=""
			IF IsNull(ReKey) Or Len(ReKey)=0 Then ReKey=""
			IF IsNull(ReStr) Or Len(ReStr)=0 Then ReStr=""
			Rep=Replace(ReContent,ReKey,ReStr,1,-1,1)
		End Function
 


		Function InStrRep(ByVal ReContent,ByVal ReKey,ByVal ReStr)
		     if isdebug=0 then On Error Resume Next
			If InStr(1,ReContent, ReKey,1) > 0  Then
 				InStrRep=rep(ReContent,ReKey,ReStr)
			Else 
  				InStrRep=ReContent
			End If 
		End Function
 







		Public Function delHtml(ContentStr)
			On Error Resume Next
			Dim TempLoseStr, regEx
			If Trim(ContentStr)="" Then Exit Function
			TempLoseStr = CStr(ContentStr)
			Set regEx = New RegExp
			regEx.Pattern = "<\/*[^<>]*>"
			regEx.IgnoreCase = True
			regEx.Global = True
			TempLoseStr = regEx.Replace(TempLoseStr, "")
			TempLoseStr=rep(TempLoseStr,"""","")
			delHtml = TempLoseStr
		End Function
		Function URLEncode(strs)
 			URLEncode=server.URLEncode(strs)
		End Function 
		Function URLDecode(enStr)
			On Error Resume Next
			Dim deStr,c,i,v:deStr=""
			For i=1 to len(enStr)
				c=Mid(enStr,i,1)
				If c="%" Then
					v=eval("&h"+Mid(enStr,i+1,2))
					If v<128 Then
						deStr=deStr&Chr(v)
						i=i+2
					Else
						If isvalidhex(Mid(enstr,i,3)) Then
							If isvalidhex(Mid(enstr,i+3,3)) Then	'这个判断检测是否双字节--不是
								v=eval("&h"+Mid(enStr,i+1,2)+Mid(enStr,i+4,2))
								deStr=deStr&Chr(v)
								i=i+5
							Else
								v=eval("&h"+Mid(enStr,i+1,2)+Cstr(Hex(Asc(Mid(enStr,i+3,1)))))	'--是
								deStr=deStr&Chr(v)
								i=i+3 
							End If
						Else 
							destr=destr&c
						End If
					End If
				Else
					If c="+" Then
						deStr=deStr&" "
					Else
						deStr=deStr&c
					End If
				End If
			Next
			URLDecode=deStr
		End Function

		'==================================================
		'函数名：ScriptHtml
		'作  用：过滤html标记
		'参  数：ConStr ------ 要过滤的字符串
		'==================================================
		Function ScriptHtml(ByVal Constr, TagName, FType)
 				Dim re
				Set re = New RegExp
				re.IgnoreCase = True
				re.Global = True
				Select Case FType
				Case 1
				   re.Pattern = "<" & TagName & "([^>])*>"
				   Constr = re.Replace(Constr, "")
				Case 2
				   re.Pattern = "<" & TagName & "([^>])*>.*?</" & TagName & "([^>])*>"
				   Constr = re.Replace(Constr, "")
				Case 3
				   re.Pattern = "<" & TagName & "([^>])*>"
				   Constr = re.Replace(Constr, "")
				   re.Pattern = "</" & TagName & "([^>])*>"
				   Constr = re.Replace(Constr, "")
				End Select
				ScriptHtml = Constr
				Set re = Nothing
		End Function
	
		Function isvalidhex(str)
			Dim c
			isvalidhex = True
			str = UCase(str)
			If Len(str) <> 3 Then isvalidhex = False: Exit Function
			If Left(str, 1) <> "%" Then isvalidhex = False: Exit Function
			c = Mid(str, 2, 1)
			If Not (((c >= "0") And (c <= "9")) Or ((c >= "A") And (c <= "Z"))) Then isvalidhex = False: Exit Function
			c = Mid(str, 3, 1)
			If Not (((c >= "0") And (c <= "9")) Or ((c >= "A") And (c <= "Z"))) Then isvalidhex = False: Exit Function
		End Function
 		Function closeHTML(strContent)
		  CloseHTML=strContent:Exit Function 
 		    Dim arrTags,i,OpenPos,ClosePos,re,strMatchs,j,Match
			Set re=new RegExp
			re.IgnoreCase =True
			re.Global=True
			arrTags=array("strong","em","strike","b","u","i","font","span","a", "h1","h2","h3","h4","h5","h6","p","li","ol","ul","td","tr","tbody","table","blockquote","pre","cite","div")
			  For i=0 To ubound(arrTags)
				   OpenPos=0:ClosePos=0
				   re.Pattern="\<"+arrTags(i)+"( [^\<\>]+|)\>"
				   Set strMatchs=re.Execute(strContent)
				   For Each Match In strMatchs
					OpenPos=OpenPos+1
				   Next
				   re.Pattern="\</"+arrTags(i)+"\>"
				   Set strMatchs=re.Execute(strContent)
				   For Each Match In strMatchs
					ClosePos=ClosePos+1
				   Next
				   For j=1 To OpenPos-ClosePos
					  strContent=strContent+"</"+arrTags(i)+">"
				   Next
			  Next
			  CloseHTML=strContent
 		End Function 

		Function RemAsp(str)
			RemAsp=rep(str,"""","""""")
			RemAsp=rep(RemAsp,vbCrLf,"")
 		End Function 
		Function delAsp(str)
 			delAsp=rep(str,"<%","")
			delAsp=rep(delAsp,"%" & ">","")
  		End Function 

	
   		Public Function ChkNumeric(ByVal CheckID)
			If CheckID <> "" And IsNumeric(CheckID) Then
				CheckID = CDbl(CheckID)
				If CheckID < 0 Then CheckID = 0
			Else
				CheckID = 0
			End If
			ChkNumeric = CheckID
		End Function
		'过滤非法的SQL字符
		Public Function RSQL(strChar)
			If strChar = "" Or IsNull(strChar) Then RSQL = "":Exit Function
			Dim strBadChar, arrBadChar, tempChar, I
			strBadChar = "$,#,',%,^,&,?,(,),<,>,[,],{,},/,\,;,:," & Chr(34) & "," & Chr(0) & ""
			arrBadChar = Split(strBadChar, ",")
			tempChar = strChar
			For I = 0 To UBound(arrBadChar)
				tempChar = Replace(tempChar, arrBadChar(I), "")
			Next
			RSQL = tempChar
		End Function
		Public Function GetIP() 
			Dim strIPAddr 
			If Request.ServerVariables("HTTP_X_FORWARDED_FOR") = "" Or InStr(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), "unknown") > 0 Then 
				strIPAddr = Request.ServerVariables("REMOTE_ADDR") 
			ElseIf InStr(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), ",") > 0 Then 
				strIPAddr = Mid(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), 1, InStr(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), ",")-1) 
			ElseIf InStr(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), ";") > 0 Then 
				strIPAddr = Mid(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), 1, InStr(Request.ServerVariables("HTTP_X_FORWARDED_FOR"), ";")-1)
			Else 
				strIPAddr = Request.ServerVariables("HTTP_X_FORWARDED_FOR") 
			End If 
			getIP = Replace(Trim(Mid(strIPAddr, 1, 30)), "'", "")
			getIP = Replace(getIP,";","")
			getIP = Replace(getIP,"-","")
			getIP = Replace(getIP,"(","")
			getIP = Replace(getIP,")","")
			getIP = Replace(getIP,">","")
			getIP = Replace(getIP,"<","")
			getIP = Replace(getIP,"=","")
			getIP = Replace(getIP,"*","")
	   End Function
  	 
		Function  LoadTemplate(TempString) 
				on error resume next
				Dim  Str,A_W
				set A_W=server.CreateObject("adodb.Stream")
				A_W.Type=2:A_W.mode=3:A_W.charset="utf-8":A_W.open
				A_W.loadfromfile server.MapPath(actcms.ThisTheme&"/"&TempString)
				If Err.Number<>0 Then Err.Clear:LoadTemplate=actcms.ThisTheme&"/"&TempString&"模板没有找到 <br> by <a href=""http://www.fycms.com"" title=""aspcms"">fycms</a>":Exit Function
				Str=A_W.readtext
				A_W.Close
				Set  A_W=nothing
				LoadTemplate=Str
		End  Function
		Function  LTemplate(temppath) 
			on error resume next
			Dim  Str,A_W
			set A_W=server.CreateObject("adodb.Stream")
			A_W.Type=2 
			A_W.mode=3 
			A_W.charset="utf-8"
			A_W.open
			A_W.loadfromfile server.MapPath(temppath)
			If Err.Number<>0 Then Err.Clear:LTemplate="":Exit Function
			Str=A_W.readtext
			A_W.Close
			Set  A_W=nothing
			LTemplate=Str
		End  function
	 
	 
		Function CheckXSS(ByVal strCode)
			Dim Re
			Set re=new RegExp
			re.IgnoreCase =True
			re.Global=True
			re.Pattern="<.[^>]*(style).>"
			strCode = re.Replace(strCode, "")
			re.Pattern="<(a.[^>]*|\/a|li|br|B|\/li|\/B|font.[^>]*|\/font)>"
			strCode=re.Replace(strCode,"[$1]")
			strCode=Replace(Replace(strCode, "<", "&lt;"), ">", "&gt;")
			re.Pattern="\[(a.[^\]]*|\/a|li|br|B|\/li|\/B|font.[^\]]*|\/font)\]"
			strCode=re.Replace(strCode,"<$1>")
			re.Pattern="<.[^>]*(on(load|click|dbclick|mouseover|mouseout|mousedown|mouseup|mousewheel|keydown|submit|change|focus)).>"
			strCode = re.Replace(strCode, "")
			Set Re=Nothing
			CheckXSS=strCode
	   End Function




		Function languagerlist(Modeid,str)
			Dim arr,li,languagc
			languagc=ACTCMS.ACT_C(Modeid,17)
			If languagc<>"0" Then 
 				arr=Split(ACTCMS.ACT_C(Modeid,17),chr(10))
			Else 
				arr=Split(LTemplate(actcms.actsys&"act_inc/lan.txt"),vbCrLf)
			End If 
			
			For li=0 To ubound(arr)
				If checkarr(arr(li),str)<>"Lack of language pack" Then languagerlist=checkarr(arr(li),str):Exit For 
			Next 
			
		End Function 
	  
		 Function checkarr(arrstr,str)
				Dim arrs
				arrs=Split(arrstr,"=")
				If ubound(arrs)=1 Then 
					If Trim(arrs(0))=Str Then 
						checkarr=arrs(1) 
					Else 
						checkarr="Lack of language pack"  
					End If 
				Else 
						checkarr="Lack of language pack"  
				End If 
		 End Function 



		Function noSaveFile(Templetcontent,FileName)
			on error resume next 
 			Dim FileFSO,FileType
			 Set FileFSO = Server.CreateObject("ADODB.Stream")
				With FileFSO
				.Type = 2:.Mode = 3:.Open:
 				.Charset = "utf-8":.Position = FileFSO.Size
 				.WriteText  Templetcontent
				.SaveToFile Server.MapPath(FileName),2
 				if err then err.clear: noSaveFile = false else noSaveFile = true
 				.Close
				End With
			Set FileType = nothing
			Set FileFSO = nothing
		End Function

		Function tojs(jscontent)
			If jscontent="" Then tojs="":Exit Function
			jscontent=Replace(jscontent,Chr(39), "\'")
			jscontent=Replace(jscontent,"""","\""")
			jscontent=Replace(jscontent,vbCrLf, "")
			'jscontent=Replace(jscontent,"'", "\'")
			jscontent=Replace(jscontent,"/", "\/")
 			
			tojs=jscontent
		End Function 

 		Function FileExists(fileName)
			On Error Resume Next
			Dim objFSO
			FileExists = False
			Set objFSO = CreateObject(ACTCMS.FsoName)
			If objFSO.FileExists(fileName) Then
				FileExists = True
			End If
			Set objFSO = Nothing
			Err.Clear
		End Function
		
		Function getcontentxml(modeid,id,addtime)
			getcontentxml=LTemplate(ACTCMS.ActSys&"cache/"&ModeID&"/"&md5(AcTCMSN&addtime&id,32)&".txt")
 		End Function 

		Public Function Userface(userid)
			If ChkNumeric(userid)="0"  Then Userface=actcms.actsys&"user/images/nophoto.gif":Exit Function 
			Dim rs3
			Set rs3=actcms.actexe("select userid,myface  from User_Act where userid=" & userid & "")	
			If Not rs3.eof Then 
			If rs3("myface")<>"" Then 
				Userface=rs3("myface")
			Else 
				Userface=actcms.actsys&"user/images/nophoto.gif"
			End If 
			rs3.Close:Set rs3=Nothing
			End If 
		End Function 

 		Public Function UserLink(userid)
			If ChkNumeric(userid)="0"  Then UserLink="#":Exit Function 
			Dim rs3
			Set rs3=actcms.actexe("select userid,UmodeID from User_Act where userid=" & userid & "")	
			If Not rs3.eof Then 
				UserLink=actcms.actsys&"space/?"&actcms.ACT_U(rs3("UmodeID"),5)&"-"&rs3("userid")
			rs3.Close:Set rs3=Nothing
			End If 
		End Function 

	    Function  rewriteurlstr(rewriteurl,ID)
		  Dim rs2,strs,i,sql,strs3,tempstrs
			Set rs2=actcms.actexe("Select id,entitle From plusget_act Where cID="&ID)
		  If Not  rs2.eof Then 
		  sql=rs2.getrows(-1)
		  for i=0 to ubound(sql,2)
			tempstrs= server.URLEncode(rsql(actcms.s(sql(1,i))))
			If Trim(tempstrs)="" Then tempstrs="fycms.com"
			rewriteurl=rep(rewriteurl,"{"&sql(1,i)&"}",tempstrs)
		  Next
		End If 
		rewriteurlstr= rewriteurl
	   End  Function 
		Public Function UName(userid)
			If ChkNumeric(userid)="0"  Then UName="匿名":Exit Function 
			Dim rs3
			Set rs3=actcms.actexe("select [UserName] from User_Act where userid=" & userid & "")	
			If Not rs3.eof Then 
				UName=rs3("UserName")
			rs3.Close:Set rs3=Nothing
			End If 
		End Function 


		Function getlink(cid,id,f)
			Dim gModeID,rsk
 			gModeID=actcms.ACT_L(cid,10) 
			If gModeID=0 Then gModeID=1
			Set rsk=actcms.actexe("select * from "&ACTCMS.ACT_C(gModeID,2)&" where id="&id)
			If Not rsk.eof Then 
				Select Case f
					Case "link"
						getlink=ACTCMS.GetInfoUrl(gModeID,rsk("ClassID"),rsk("ID"),rsk("ActLink"),rsk("FileName"),rsk("infopurview"),rsk("readpoint"),rsk("pinyin"))
					Case Else 
						getlink=rsk(f)
 				End Select 
 			rsk.Close:Set rsk=Nothing
			End If 
 		End Function 
		
		
		Function getlinkM(gModeID,id,f)
			If gModeID=0 Then gModeID=1
			Dim rsm
			Set rsm=actcms.actexe("select * from "&ACTCMS.ACT_C(gModeID,2)&" where id="&id)
			If Not rsm.eof Then 
				Select Case f
					Case "link"
						getlinkM=ACTCMS.GetInfoUrl(gModeID,rsm("ClassID"),rsm("ID"),rsm("ActLink"),rsm("FileName"),rsm("infopurview"),rsm("readpoint"),rsm("pinyin"))
					Case Else 
						getlinkM=rsm(f)
 				End Select 
 			rsm.Close:Set rsm=Nothing
			End If 
 		End Function 


%>