<%

'#################################################################################
'#	SetSafeStr 将字符串转换成用于网页显示和SQL查询的安全字符串
'#		参数：str {即将转换的字符串}
'#
'#		返回：字符串
'#################################################################################
Function SetSafeStr(str)

	Dim strResult,iLen,i

	If IsNULL(str) Then
		SetSafeStr = ""
		Exit Function
	End If

	iLen = Len(str)
	strResult = ""

	For i = 1 To iLen
		Select Case Mid(str,i,1)
			Case "<"
				strResult = strResult & "&lt;"
			Case ">"
				strResult = strResult & "&gt;"
			Case Chr(13)
				strResult = strResult & "<br>"
			Case Chr(34)
				strResult = strResult & "&quot;"
			Case "&"
				strResult = strResult & "&amp;"
			Case Chr(32)
				strResult = strResult & "&nbsp;"
			Case Chr(9)
				strResult = strResult & "	"
			Case Chr(39)
				strResult = strResult & "'" + "'"
			Case Else
				strResult = strResult & Mid(str,i,1)

		End Select
	Next 'i

	SetSafeStr = strResult

End Function
'#################################################################################


'#################################################################################
'#	GetSafeStr 从数据库中得到字符串用于在网页控件中还原显示，适应多行文本
'#		参数：str {数据库里的字符串}
'#
'#		返回：字符串
'#################################################################################
Function GetSafeStr(str)

	Dim strResult
	strResult = str

	strResult = Replace (strResult,"<br>",chr(13))
	strResult = Replace (strResult,"''",chr(39))

	GetSafeStr = strResult

End Function
'#################################################################################


'#################################################################################
'#	SplitPage 记录集分页显示
'#		参数：unitid {类型ID}
'#			  buf {提示字符串}
'#			  rscount {记录总数}
'#			  curpage {当前页号}
'#			  pagecount {总页数}
'#
'#		返回：字符串
'#################################################################################
Function SplitPage(unitid,buf,rscount,curpage,pagecount)

	Dim strResult
	strResult = "<table width=100% border=0 cellspacing=0 cellpadding=0><tr><td>"
	strResult = strResult & buf & "　　共 " & rscount & " 条"
	strResult = strResult & "</td></tr><tr><td height=4></td></tr><tr><td>页号:　"

	For i = 1 To pagecount
		If i = curpage Then
			strResult = strResult & "<font color=red>" & i & "</font>　"
		Else
			strResult = strResult & "<a href='" & Request.ServerVariables("SCRIPT_NAME") & "?id=" & unitid & "&pn=" & i & "'>" & i & "</a>" & "　"
		End If
	Next 'i

	strResult = strResult & "</td></tr></table>"

	SplitPage = strResult
	
End Function
'#################################################################################

'############################################################
'#	DivisionPage() 分页过程
'#		intCurPageNum 当前页号
'#		intRSPages 记录集页数 -- rs.PageCount
'############################################################
Sub DivisionPage(intRSPages,intPageNum)
	Response.Write "页码:"
	For i = 1 To intRSPages
		If i = intPageNum Then
			Response.Write " <font color=red>" & i & "</font>　"
		Else
			If ftype = "" Then
				Response.Write " <a href='" & Request.ServerVariables("SCRIPT_NAME") & "?pn=" & i & "'>" & i & "</a>　"
			Else
				Response.Write " <a href='" & Request.ServerVariables("SCRIPT_NAME") & "?pn=" & i & "&ftype=" & ftype & "&fclass="&fclass&"'>" & i & "</a>　"
			End if
		End If
	Next ' i

End Sub
'#################################################################################
%>