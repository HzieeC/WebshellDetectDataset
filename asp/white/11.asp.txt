<%
'****************************************************
'函数名：CheckAdmin
'作  用：检查管理员是否登录
'参  数：True/False
'****************************************************
Function CheckAdmin()
	'检查管理员是否登录
	'Site_Sn=Replace(Replace(LCase(Request.ServerVariables("SERVER_NAME") & GetScriptPath(Trim(Request.ServerVariables("SCRIPT_NAME")), 1)), "/", ""), ".", "")
	Site_Sn=Replace(LCase(Request.ServerVariables("Server_Name")), ".", "")
	AdminName = NoSqlHack(Trim(Request.Cookies(Site_Sn)("AdminName")))
	AdminPassword = NoSqlHack(Trim(Request.Cookies(Site_Sn)("AdminPassword")))
	RndPassword = NoSqlHack(Trim(Request.Cookies(Site_Sn)("RndPassword")))
	AdminLoginCode = NoSqlHack(Trim(Request.Cookies(Site_Sn)("AdminLoginCode")))	
	If AdminName = "" Or AdminPassword = "" Or RndPassword = "" Or AdminLoginCode <> Config(7) Then
		CheckAdmin=False
	Else
	    CheckAdmin=True
	End If
End Function

'**************************************************
'函数名：ReplaceBadChar
'作  用：过滤非法的SQL字符
'参  数：strChar-----要过滤的字符
'返回值：过滤后的字符
'**************************************************
Function ReplaceBadChar(strChar)
    If strChar = "" Or IsNull(strChar) Then
        ReplaceBadChar = ""
        Exit Function
    End If
    Dim strBadChar, arrBadChar, tempChar, i
    strBadChar = "+,',--,%,^,&,?,(,),<,>,[,],{,},/,\,;,:," & Chr(34) & "," & Chr(0) & ""
    arrBadChar = Split(strBadChar, ",")
    tempChar = strChar
    For i = 0 To UBound(arrBadChar)
        tempChar = Replace(tempChar, arrBadChar(i), "")
    Next
    tempChar = Replace(tempChar, "@@", "@")
    ReplaceBadChar = tempChar
End Function

'****************************************************
'函数名：Replacehtml
'作  用：清理html
'参  数：tstr ----字符串
'****************************************************
Function ReplaceHtml(str)
	Dim re
	Set re=new RegExp
	re.IgnoreCase =True
	re.Global=True
	re.Pattern="<(p|\/p|br)>"
	str=re.Replace(str,vbNewLine)
	re.Pattern="<img.[^>]*src(=| )(.[^>]*)>"
	str=re.replace(str,"[img]$2[/img]")
	re.Pattern="<(.[^>]*)>"
	str=re.Replace(str,"")
	Set Re=Nothing
	ReplaceHtml=str
End Function

Function NoSqlHack(content)
	Dim f_NoSqlHack_AllStr,f_NoSqlHack_Str,f_NoSqlHack_i,Str_InputStr
	If content = "" Or IsNull(content) Then Exit Function
	Str_InputStr=content
	f_NoSqlHack_AllStr="%27|;|*|dbcc|alter|drop|and|exec|insert|select|delete|update|count|master|truncate|declare|char|mid(|chr|set |where|xp_cmdshell|tab|union"
	f_NoSqlHack_Str = Split(f_NoSqlHack_AllStr,"|")
	For f_NoSqlHack_i=LBound(f_NoSqlHack_Str) To Ubound(f_NoSqlHack_Str)
		If Instr(LCase(Str_InputStr),f_NoSqlHack_Str(f_NoSqlHack_i))<>0 Then
			If f_NoSqlHack_Str(f_NoSqlHack_i)="'" Then f_NoSqlHack_Str(f_NoSqlHack_i)=" \' "
			Response.Write "<html><title>警告</title><body bgcolor=""EEEEEE"" leftmargin=""60"" topmargin=""30""><font style=""font-size:16px;font-weight:bolder;color:blue;""><li>您提交的数据有恶意字符"&f_NoSqlHack_Str(f_NoSqlHack_i)&"</li></font><font style=""font-size:14px;font-weight:bolder;color:red;""><br><li>您的数据已经被记录!</li><br><li>您的IP："&Request.ServerVariables("Remote_Addr")&"</li><br><li>操作日期："&Now&"</li></font></body></html>"
			Response.End
		End if
	Next
	NoSqlHack = Replace(Replace(Str_InputStr,"'","''"),"%27","''")
End Function

Function Bad_Url(strValue)
	Set regEx = New RegExp
	regEx.IgnoreCase = True
	regEx.Global = True
    Dim strCustom, strParameter,strArray
	strCustom = strValue
	regEx.Pattern = "(\w+\.|\s(com.cn|net.cn|com|cn|cc|net|org|la))"
    Set Matches = regEx.Execute(strCustom)
    For Each Match In Matches
		strCustom=Replace(strCustom,Match.value,"^v^")
    Next
	'过滤脏话
	strArray=Split(unescape("%u5C3B%2C%u5C4C%2C%u64CD%u4F60%2C%u5E72%u6B7B%u4F60%2C%u8D31%u4EBA%2C%u72D7%u5A18%2C%u5A4A%u5B50%2C%u8868%u5B50%2C%u9760%u4F60%2C%u53C9%u4F60%2C%u53C9%u6B7B%2C%u63D2%u4F60%2C%u63D2%u6B7B%2C%u5E72%u4F60%2C%u5E72%u6B7B%2C%u65E5%u4F60%2C%u65E5%u6B7B%2C%u9E21%u5DF4%2C%u777E%u4E38%2C%u5305%u76AE%2C%u9F9F%u5934%2C%u5C44%2C%u8D51%2C%u59A3%2C%u808F%2C%u5976%u5B50%2C%u5976%u5934%2C%u9634%u6BDB%2C%u9634%u9053%2C%u9634%u830E%2C%u53EB%20%u5E8A%2C%u5F3A%u5978%2C%u7231%u6DB2%2C%u6027%u9AD8%u6F6E%2C%u505A%u7231%2C%u6027%u4EA4%2C%u53E3%u4EA4%2C%u809B%u4EA4%2C%u65E5%u76AE%2C%u80E1%u9526%u6D9B%2C%u6E29%u5BB6%u5B9D%2C%u6C5F%u6CFD%u6C11"),",")
	For i=0 to ubound(strArray)
		strCustom=replace(strCustom,strArray(i),"^v^",1,-1,1)
	Next
    Bad_Url = strCustom
End Function

Function EncodeHtml(Byval str)
	IF len(str)=0 OR Trim(str)="" then exit function
		str=replace(str,"<","&lt;")
		str=replace(str,">","&gt;")
		str=replace(str,CHR(34),"&quot;")
		str=replace(str,CHR(39),"&apos;")
		encodeHtml=str
End Function

Function DecodeHtml(Byval str)
	IF len(str)=0 OR Trim(str)="" or isNull(str) then exit function
		str=replace(str,"&lt;","<")
		str=replace(str,"&gt;",">")
		str=replace(str,"&quot;",CHR(34))
		str=replace(str,"&apos;",CHR(39))
		decodeHtml=str
End Function
%>