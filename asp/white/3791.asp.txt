<!--#include file="const.asp"-->
<!--#include file="../../inc/cls_keyword.asp"-->
<%
Admin_header
'=====================================================================
' 软件名称：新云网站管理系统
' 当前版本：NewAsp Content Management System Version 4.0
' 文件名称：admin_search.asp
' 更新日期：2008-10-16
' 官方网站：新云网络(www.newasp.net www.newasp.cn) QQ：94022511
'=====================================================================
' Copyright 2003-2008 newasp.net - All Rights Reserved.
' newasp is a trademark of newasp.net
'=====================================================================
Dim maxperpage,totalrec,Pcount,pagelinks,pagenow
Dim classid,SQLQuery,SQLField,Topiclist,searchmode,searchfield,fieldName
Dim ChildData,ischild,childstr,Keyword,searchMaxResult,ArryWords()
Keyword = Trim(Request("word"))
searchmode = NewAsp.ChkNumeric(Request("searchmode"))
searchfield = NewAsp.ChkNumeric(Request("searchfield"))
searchMaxResult = 1000
%>
<table class="table1" cellspacing="1" cellpadding="3" align="center" border="0">
	<tr>
		<td class="tableline"><form name="search_form" id="search_form" method="get" action="admin_search.asp">
			<input type="hidden" name="ChannelID" value="<%=ChannelID%>">
			<input type="text" class="alingmid" id="word" name="word" size="45" onfocus="if(this.value=='请输入关键字')this.value=''" value="<%=Server.HTMLEncode(Keyword)%>" />
			<select name="searchfield" class="alingmid">
				<option value="0">不限</option>
				<option value="1"<%If searchfield=1 Then Response.Write " selected=""selected"""%>>标题</option>
				<option value="2"<%If searchfield=2 Then Response.Write " selected=""selected"""%>>用户</option>
			</select>
			<input type="submit" value="开始搜索" name="search_submit" class="button alingmid" />
			<input type="checkbox" id="searchmode" name="searchmode" value="1" class="alingmid"<%If searchmode=1 Then Response.Write " checked=""checked"""%> /><label for="searchmode">分词搜索</label>
		</form>
		</td>
		<td class="tableline" id="searchInfo" align="right">&nbsp;</td>
	</tr>
</table>
<%
If Len(Keyword)>1 Then
	Call showmain()
End If
Admin_footer
NewAsp.PageEnd
Ask_CloseConn

Sub showmain()
	Dim i,iCount,lCount
	maxperpage=30
	pagenow=NewAsp.ChkNumeric(Request("page"))
	If pagenow=0 Then pagenow=1
	classid=NewAsp.ChkNumeric(Request("classid"))

	SQLField = "TopicID,classid,userid,classname,title,PostUsername,Expired,Closed,PostTable,DateAndTime,LastPostTime,LockTopic,PostNum,TopicMode,Anonymous,IsTop"
%>
<form name="selform" id="selform" method="post" action="admin_list.asp?ChannelID=<%=ChannelID%>">
<input type="hidden" name="action" value="del">
<table id="tablehovered" border="0" align="center" cellpadding="3" cellspacing="1" class="tableborder">
<tr>
	<th width="5%" noWrap="noWrap">选择</th>
	<th width="56%">标题</th>
	<th width="12%" noWrap="noWrap">用户名</th>
	<th width="6%" noWrap="noWrap">状态</th>
	<th width="12%" noWrap="noWrap">日期</th>
	<th width="9%" noWrap="noWrap">管理操作</th>
</tr>
<tr>
	<td class="tablerow1" colspan="6" align="left" id="showNextPage">&nbsp;</td>
</tr>
<%
	Call searchAskedlist()
	iCount=2:lCount=2
	If IsArray(Topiclist) Then
		For i=0 To Ubound(Topiclist,2)
			If Not Response.IsClientConnected Then Response.End
			If (i mod 2) = 0 Then
				iCount=2:lCount=1
			Else
				iCount=1:lCount=2
			End If
%>
<tr align="center">
	<td class="tablerow<%=iCount%> hovered"><input type="checkbox" name="topicid" value="<%=Topiclist(0,i)%>"/></td>
	<td class="tablerow<%=iCount%> hovered" align="left">[<a href="<%=AskInstalDir%>showlist.asp?classid=<%=Topiclist(1,i)%>" target="_blank"><%=Topiclist(3,i)%></a>]
	<a href="<%=AskInstalDir%>question.asp?topicid=<%=Topiclist(0,i)%>" target="_blank"><%=Trim(Topiclist(4,i))%></a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><a href="<%=AskInstalDir%>usertopic.asp?userid=<%=Topiclist(2,i)%>&topicmode=<%=Topiclist(13,i)%>" target="_blank"><%=Topiclist(5,i)%></a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap" title="查看评论"><a href="admin_comment.asp?ChannelID=<%=ChannelID%>&topicid=<%=Topiclist(0,i)%>"><img src="../images/icons/icon_0<%=Topiclist(13,i)%>.gif" border="0"/></a></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><%=showDateTime(Topiclist(10,i),"yyyy-MM-dd")%></td>
	<td class="tablerow<%=iCount%> hovered" noWrap="noWrap"><a href="admin_list.asp?action=asked&ChannelID=<%=ChannelID%>&topicid=<%=Topiclist(0,i)%>">编辑</a> | <a href="admin_list.asp?action=del&ChannelID=<%=ChannelID%>&topicid=<%=Topiclist(0,i)%>" onclick="return confirm('删除后将不能恢复，您确定要删除吗?')">删除</a></td>
</tr>
<%
		Next
	End If
	Topiclist=Null
%>
<tr>
	<td class="tablerow<%=lCount%>" colspan="6"><input class="button" type="button" name="chkall" value="全选" onClick="CheckAll(this.form)"><input class="button" type="button" name="chksel" value="反选" onClick="ContraSel(this.form)">

		<input class="button" type="submit" name="submit_button1" value="批量删除" onclick="return confirm('您确定执行该操作吗?');">
	</td>
</tr>
<tr>
	<td class="tablerow<%=iCount%>" align="right" colspan="6" id="NextPageText">
	<var class="morePage"><%=showlistpage(pagenow,Pcount,maxperpage,totalrec,pagelinks)%></var></td>
</tr>
</table>
</form>
<script type="text/javascript">
document.getElementById("showNextPage").innerHTML = document.getElementById("NextPageText").innerHTML;
var searchEl=document.getElementById("searchInfo");
if(searchEl)
searchEl.innerHTML = "找到相关结果约<%=totalrec%>条，用时<%=FormatNumber((Timer()-startime),3, -1)%>秒";
</script>
<%
End Sub

Sub searchAskedlist()
	Dim Rs,SQL,i,iCount,ArryKeyword
	totalrec=0
	Select Case searchfield
		Case 1:fieldName="title"
		Case 2:fieldName="PostUsername"
		Case Else
			fieldName="title+PostUsername"
	End Select
	pagelinks="admin_search.asp?channelid="&ChannelID&"&classid="&classid&"&word="&Keyword&"&searchmode="&searchmode&"&searchfield="&searchfield&"&"
	If Len(Keyword)>1 Then
		If searchmode=1 Then
			iCount=0
			SQLQuery=""
			ArryKeyword = cmWords.ParseKeyword(Keyword)
			For i = 0 To UBound(ArryKeyword)
				If Len(ArryKeyword(i)) > 1 Then
					ReDim Preserve ArryWords(iCount)
					ArryWords(iCount)=ArryKeyword(i)
					If iCount=0 Then
						If AskIsSqlDataBase=1 Then
							SQLQuery=""&fieldName&" like '%"&ArryKeyword(i)&"%'"
						Else
							SQLQuery="InStr(1,LCase("&fieldName&"),LCase('"&ArryKeyword(i)&"'),0)>0"
						End If
					Else
						If AskIsSqlDataBase=1 Then
							SQLQuery=SQLQuery & " Or "&fieldName&" like '%"&ArryKeyword(i)&"%'"
						Else
							SQLQuery=SQLQuery & " Or InStr(1,LCase("&fieldName&"),LCase('"&ArryKeyword(i)&"'),0)>0"
						End If
					End If
					iCount=iCount+1
				End If
			Next
			If iCount=0 Then SQLQuery="":ReDim Preserve ArryWords(0):ArryWords(0)=""
		Else
			If cmWords.CheckKeyword(Keyword) Then
				If AskIsSqlDataBase=1 Then
					SQLQuery = ""&fieldName&" like '%"&Keyword&"%'"
				Else
					SQLQuery = "InStr(1,LCase("&fieldName&"),LCase('"&Keyword&"'),0)>0"
				End If
				ReDim Preserve ArryWords(0):ArryWords(0)=Keyword
			Else
				SQLQuery="":ReDim Preserve ArryWords(0):ArryWords(0)=""
			End If
		End If

		If Len(SQLQuery) >10 Then SQLQuery=" And ("&SQLQuery&")" Else SQLQuery=""
		If classid>0 Then
			SQLQuery=" And classid="&classid&" "&SQLQuery
		Else
			SQLQuery=SQLQuery
		End If
		
		Set Rs=NewAsp.CreateAXObject("ADODB.Recordset")
		SQL="SELECT TOP "&searchMaxResult&" "& SQLField &" FROM [NC_Ask_Topic] WHERE LockTopic=0 "&SQLQuery&" ORDER BY LastPostTime DESC"
		Rs.Open SQL,Ask_Conn,1,1
		If pagenow >1 Then
			Rs.Move (pagenow-1) * maxperpage
		End If
		If Not Rs.EOF Then
			totalrec = CLng(Rs.Recordcount) '###记录总数
			Topiclist=Rs.GetRows(maxperpage)
		Else
			Topiclist=Null
		End If
		Rs.close()
		Set Rs=Nothing
	End If
	Pcount = CLng(totalrec / maxperpage)
	If Pcount < totalrec / maxperpage Then Pcount = Pcount + 1
	If pagenow>Pcount Then pagenow=1
End Sub

Function HighlightWord(str)
	Dim i
	For i=0 To UBound(ArryWords)
		If ArryWords(i)<>"" Then str=Replace(str, ArryWords(i), "<font color=""red"">"& ArryWords(i) &"</font>")
	Next
	HighlightWord=str
End Function

%>