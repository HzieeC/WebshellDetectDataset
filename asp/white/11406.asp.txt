<!--#include file="AspCms_CommonFileFun.asp" -->

<%
'弹出对话框
'str 提示信息
'url 跳转地址
Sub alertMsgAndGo(str,url)
	dim urlstr 
	if url<>"" then urlstr="location.href='"&url&"';"
	if url="-1" then urlstr="history.go(-1);"
	if not isNul(str) then str ="alert('"&str&"');"
	echo("<script>"&str&urlstr&"</script>")
	response.End()
End Sub

'输出信息
'
'
Sub echoMsgAndGo(str,timenum)
	echo str&",稍后将自动返回<script language=""javascript"">setTimeout(""goLastPage()"","&timenum*1000&");function goLastPage(){history.go(-1);}</script>&nbsp;&nbsp;<a href='/"&sitePath&"' target='_self'>进入网站首页</a>"&" Powered By "&siteTitle
	response.End()	
End Sub

'选择跳转
'str 提示信息
'url1 
'url2 
Sub selectMsg(str,url1,url2)
	echo("<script>if(confirm('"&str&"')){location.href='"&url1&"'}else{location.href='"&url2&"'}</script>") 
End Sub


'输出
Sub echo(str)
	response.write(str)
	response.Flush()
End Sub

'输出后停止，调试用
Sub die(str)
	if not isNul(str) then
		echo str
	end if	 
	response.End()
End Sub

'读cookies
Function rCookie(cookieName)
	rCookie=request.cookies(cookieName)
End Function

'写cookies
Sub wCookie(cookieName,cookieValue)
	response.cookies(cookieName)=cookieValue
End Sub

'写cookies写义过期时间
Sub wCookieInTime(cookieName,cookieValue,dateType,dateNum)
	Response.Cookies(cookieName).Expires=DateAdd(dateType,dateNum,now())
	response.cookies(cookieName)=cookieValue
End Sub

'是否为空
Function isNul(str)
	if isnull(str) or str=""  then isNul=true else isNul=false
End Function

'是否为数字
Function isNum(str)
	if not isNul(str) then  isNum=isnumeric(str) else isNum=false
End Function

'是否为URL
Function isUrl(str)
	if not isNul(str) then
		if left(str,7)="http://" then isUrl=true else isUrl=false
	else
		isUrl=false
	end if
End Function

'获取扩展名
Function getFileFormat(str)
	dim ext:str=trim(""&str):ext=""
	if str<>"" then
		if instr(" "&str,"?")>0 then:str=mid(str,1,instr(str,"?")-1):end if
		if instrRev(str,".")>0 then:ext=mid(str,instrRev(str,".")):end if
	end if
	getFileFormat=ext
End Function

'全角转换成半角
Function convertString(Str)
	Dim strChar,intAsc,strTmp,i
	For i = 1 To Len(Str)
      strChar = Mid(Str, i, 1)
      intAsc = Asc(strChar)
      If (intAsc>=-23648 And intAsc<=-23553) Then 
         strTmp = strTmp & Chr(intAsc+23680)
      Else
         strTmp = strTmp & strChar 
      End if    
    Next
	ConvertString=strTmp
End Function

'获取参数值
Function getForm(element,ftype)
	Select case ftype
		case "get"
			getForm=trim(request.QueryString(element))
		case "post"
			getForm=trim(request.Form(element))
		case "both"
			if isNul(request.QueryString(element)) then getForm=trim(request.Form(element)) else getForm=trim(request.QueryString(element))
	End Select
End Function

'是否为已安装对象
Function isInstallObj(objname)
	dim isInstall,obj
	On Error Resume Next
	set obj=server.CreateObject(objname)
	if Err then 
		isInstallObj=false : err.clear 
	else 
		isInstallObj=true:set obj=nothing
	end if
End Function

Sub setStartTime()
	starttime=timer()
End Sub

Sub echoRunTime()
	endtime=timer()      
	echo pageRunStr(0)&FormatNumber((endtime-starttime),4,-1)&pageRunStr(1)&conn.queryCount&pageRunStr(2)
End Sub

Function getRunTime()
	endtime=timer()
	getRunTime=pageRunStr(0)&FormatNumber((endtime-starttime),4,-1)&pageRunStr(1)&conn.queryCount&pageRunStr(2)
End Function

Function getKeywordsList(key,span)
	dim keyWordsArray,i,keyWordsStr,keystr
	keystr=replaceStr(key,"，",",")
	if instr(keystr,",")>0 then keyWordsArray=split(keystr,",") else keyWordsArray=split(keystr," ")
	for i=0 to ubound(keyWordsArray)
		keyWordsStr=keyWordsStr&"<a href='/"&sitePath&"search.asp?key="&server.URLEncode(keyWordsArray(i))&"'>"&keyWordsArray(i)&"</a>"&span
	next
	getKeywordsList=keyWordsStr
End Function


Function replaceStr(Byval str,Byval finStr,Byval repStr)
	on error resume next
	if isNull(repStr) then repStr=""
	replaceStr=replace(str,finStr,repStr)
	if err then replaceStr="" : err.clear
End Function

Function getArrayElementID(Byval parray,Byval itemid,Byval compareValue)
	dim i 
	for  i=0 to ubound(parray,2)
		if trim(parray(itemid,i))=trim(compareValue) then
			getArrayElementID=i
			Exit Function
		end if
	next
End Function

Function trimOuter(Byval str)
	dim vstr : vstr=str
	if left(vstr,1)=chr(32) then vstr=right(vstr,len(vstr)-1) 
	if right(vstr,1)=chr(32) then  vstr=left(vstr,len(vstr)-1)
	trimOuter=vstr
End Function

Function trimOuterStr(Byval str,Byval flag)
	dim vstr,m : vstr=str : m=len(flag)
	if left(vstr,m)=flag then vstr=right(vstr,len(vstr)-m) 
	if right(vstr,m)=flag then  vstr=left(vstr,len(vstr)-m)
	trimOuterStr=vstr
End Function

Function getPageSize(Byval str,Byval ptype)
	dim regObj,matchChannel,matchesChannel,sizeValue
	set regObj=New RegExp
	regObj.Pattern="\{aspcms:"&ptype&"list[\s\S]*size=([\d]+)[\s\S]*\}"	
	set matchesChannel=regObj.Execute(str)
	for each matchChannel in matchesChannel
		sizeValue=matchChannel.SubMatches(0) : if isNul(sizeValue) then sizeValue=10
		set regObj=nothing
		set matchesChannel=nothing
		getPageSize=sizeValue
		Exit Function
	next
End Function

Function dropHtml(Byval strHTML) 
	Dim objRegExp, Match, Matches 
	Set objRegExp = New Regexp 
	objRegExp.IgnoreCase = True 
	objRegExp.Global = True 
	'取闭合的<> 
	objRegExp.Pattern = "<.+?>" 
	'进行匹配 
	Set Matches = objRegExp.Execute(strHTML) 
	' 遍历匹配集合，并替换掉匹配的项目 
	For Each Match in Matches 
	strHtml=Replace(strHTML,Match.Value,"") 
	Next 
	dropHtml=strHTML 
	Set objRegExp = Nothing 
End Function

Function filterStr(Byval str,Byval filtertype)
	if isNul(str) then  filterStr="" : Exit Function
	dim regObj, outstr,rulestr : set regObj=New Regexp
	regObj.IgnoreCase=true : regObj.Global=true
	Select case filtertype
		case "html"	
			rulestr="(<[a-zA-Z].*?>)|(<[\/][a-zA-Z].*?>)"
		case "jsiframe"
			rulestr="(<(script|iframe).*?>)|(<[\/](script|iframe).*?>)"
	end Select
	regObj.Pattern=rulestr
	outstr=regObj.Replace(str, "")
	set regObj=Nothing : filterStr=outstr
End Function

Function getAgent()
	getAgent=request.ServerVariables("HTTP_USER_AGENT")
End Function

Function getRefer()
	getRefer=request.ServerVariables("HTTP_REFERER")
End Function

Function getServername()
	getServername=request.ServerVariables("server_name")
End Function

Function isOutSubmit()
	dim server1, server2
	server1=getRefer
	server2=getServername
	if Mid(server1, 8, len(server2)) <> server2 then
		isOutSubmit=true
	else
		isOutSubmit=false
	end if
End Function

Function getIp()
	dim forwardFor
	getIp=request.servervariables("Http_X_Forwarded_For")
	if getIp="" then getIp=request.servervariables("Remote_Addr") 	
	getIp=replace(getIp, chr(39), "")
End Function


Function urldecode(ByVal sUrl)
	Dim i,c,ts,b,lc,t,n:ts="":b=false:lc=""
	for i=1 to len(sUrl)
		c=mid(sUrl,i,1)
		if c="+" then
			ts=ts & " "
		elseif c="%" then
			t=mid(sUrl,i+1,2):n=cint("&H" & t)
			if b then
				b=false:ts=ts & chr(cint("&H" & lc & t))
			else
				if abs(n)<=127 then
					ts=ts & chr(n)
				else
					b=true:lc=t
				end if
			end if
			i=i+2
		else
			ts=ts & c
		end if
	next
	urldecode=ts
End Function

Function urlencode(ByVal sUrl)
	if InStr(" "&sUrl,"?")>0 then
		dim ts,i,l,s:ts=Split(Mid(sUrl,InStr(sUrl,"?")+1),"&"):l=UBound(ts)
		for i=0 to l
			if InStr(" "&ts(i),"=")>0 then
				s=Split(ts(i),"=")
				if s(1)<>"" then
					if InStr(" "&s(1),"%") then:s(1)=urldecode(s(1)):end if
					s(1)=Server.urlencode(s(1)):ts(i)=Join(s,"=")
				end if
			end if
		next
		urlencode=Mid(sUrl,1,InStr(sUrl,"?"))&Join(ts,"&")
	else
		urlencode=sUrl
	end if
End Function

dim gXmlHttpVer
Function getXmlHttpVer()
	dim i,xmlHttpVersions,xmlHttpVersion
	getXmlHttpVer=false
	xmlHttpVersions=Array("Microsoft.XMLHTTP", "MSXML2.XMLHTTP", "MSXML2.XMLHTTP.3.0","MSXML2.XMLHTTP.4.0","MSXML2.XMLHTTP.5.0")
	for i=0 to ubound(xmlHttpVersions)
		xmlHttpVersion=xmlHttpVersions(i)
		if isInstallObj(xmlHttpVersion) then getXmlHttpVer=xmlHttpVersion:gXmlHttpVer=xmlHttpVersion: Exit Function
	next
End Function

Function tryXmlHttp()
	dim i,ah:ah=array("MSXML2.ServerXMLHTTP.5.0","MSXML2.ServerXMLHTTP","MSXML2.ServerXMLHTTP.2.0","MSXML2.ServerXMLHTTP.3.0","MSXML2.ServerXMLHTTP.4.0","MSXML2.ServerXMLHTTP.6.0","Microsoft.XMLHTTP", "MSXML2.XMLHTTP", "MSXML2.XMLHTTP.3.0","MSXML2.XMLHTTP.4.0","MSXML2.XMLHTTP.5.0")
	On Error Resume Next
	for i=0 to UBound(ah)
		SET tryXmlHttp=Server.CreateObject(ah(i))
		if err.number=0 then:gXmlHttpVer=ah(i):tryXmlHttp.setTimeouts 2000,20000,20000,180000:err.clear:Exit Function:else:err.clear:end if
	next
End Function

dim gXmlHttpObj
Function getRemoteContent(Byval url,Byval returnType)
	if not isObject(gXmlHttpObj) then:set gXmlHttpObj=tryXmlHttp():end if
	url=urlencode(url):gXmlHttpObj.open "GET",url,False
	On error resume next
	gXmlHttpObj.send()
	if err.number = -2147012894 then
		dim des
		select case gXmlHttpObj.readyState
			Case 1:des="解析域名或连接远程服务器"
			Case 2:des="发送请求"
			Case 3:des="接收数据"
			Case else:des="未知阶段"
		end select
		die gXmlHttpVer&"组件<br />在请求 “"&url&"”时<br />发生" + des + " 超时错误,请重试.如果问题还没解决，请联系你的服务商"
	else
		select case returnType
			case "text"
				getRemoteContent=gXmlHttpObj.responseText
			case "body"
				getRemoteContent=gXmlHttpObj.responseBody
		end select
	end if
End Function


Function bytesToStr(Byval responseBody,Byval strCharSet)
	with objStream
		.Type=1
		.Mode =3
		.Open
		.Write responseBody
		.Position=0
		.Type=2
		.Charset=strCharSet
		bytesToStr=objstream.ReadText
		objstream.Close
	End With
End Function

Function computeStrLen(Byval str)
	dim strlen,charCount,i,an
	str=trim(str)   
	charCount=len(str)   
	strlen=0   
	for i=1 to charCount
		an=asc(mid(str,i,1))
		if an < 0 or an >255 then   
			strlen=strlen + 2
		else   
			strlen=strlen + 1
		end if
	next
	computeStrLen=strlen
End Function

Function getStrByLen(Byval str, Byval strlen)
	dim vStrlen,charCount,i,an
	str=trim(str)
	if isNul(str) then Exit Function   
	charCount=len(str)  
	vStrlen=0   
	for i=1 to charCount
		an=asc(mid(str,i,1))
		if an < 0 or an >255 then
			vStrlen=vStrlen + 2
		else   
			vStrlen=vStrlen + 1
		end if
		if vStrlen >= strlen then getStrByLen=left(str,i):Exit Function
	next
	getStrByLen=str
End Function

Function encodeHtml(Byval str)
	IF len(str)=0 OR Trim(str)="" then exit function
		str=replace(str,"<","&lt;")
		str=replace(str,">","&gt;")
		str=replace(str,CHR(34),"&quot;")
		str=replace(str,CHR(39),"&apos;")
		encodeHtml=str
End Function

Function decodeHtml(Byval str)
	IF len(str)=0 OR Trim(str)="" or isNull(str) then exit function
		str=replace(str,"&lt;","<")
		str=replace(str,"&gt;",">")
		str=replace(str,"&quot;",CHR(34))
		str=replace(str,"&apos;",CHR(39))
		decodeHtml=str
End Function


Function decode(str)
	decode=replace(str,"<br>",chr(13)&chr(10))
End Function

Function encode(str)
	encode=replace(replace(str,chr(10),""),chr(13),"<br>")
End Function


Function timeToStr(Byval t)
	t=Replace(Replace(Replace(Replace(t,"-",""),":","")," ",""),"/","") : timeToStr=t
End Function

'分页中部
Function makePageNumber(Byval currentPage,Byval pageListLen,Byval totalPages,Byval linkType,Byval sortid)
	dim beforePages,pagenumber,page
	dim beginPage,endPage,strPageNumber
	if pageListLen mod 2 = 0 then beforePages = pagelistLen / 2 else beforePages = clng(pagelistLen / 2) - 1
	if  currentPage < 1  then currentPage = 1 else if currentPage > totalPages then currentPage = totalPages
	if pageListLen > totalPages then pageListLen=totalPages
	if currentPage - beforePages < 1 then
		beginPage = 1 : endPage = pageListLen
	elseif currentPage - beforePages + pageListLen > totalPages  then
		beginPage = totalPages - pageListLen + 1 : endPage = totalPages
	else 
		beginPage = currentPage - beforePages : endPage = currentPage - beforePages + pageListLen - 1
	end if	
'	die currentPage
	for pagenumber = beginPage  to  endPage
		if pagenumber=1 then page = "" else page = pagenumber
		if pagenumber=currentPage then
			if linkType="commentlist" then
				strPageNumber=strPageNumber&"<a class=""selectpage"" href=""javascript:pager("&sortid&","&pagenumber&")"">"&pagenumber&"</a>"
			else				
				strPageNumber=strPageNumber&"<span><font color=red>"&pagenumber&"</font></span>"&"&nbsp;"
			end if			
		else
			if linkType="newslist" or linkType="downlist" or linkType="productlist" or linkType="piclist" then		
					if pagenumber>1 then 
						strPageNumber=strPageNumber&"<a href="""&runstr&sortid&"_"&pagenumber&FileExt&""">"&pagenumber&"</a>&nbsp;" 
					else 
						strPageNumber=strPageNumber&"<a href="""&runstr&sortid&"_1"&FileExt&""">"&pagenumber&"</a>&nbsp;"
					end if
			elseif linkType="about" then
				if pagenumber>1 then 
					strPageNumber=strPageNumber&"<a href="""&runstr&sortid&"_"&pagenumber&FileExt&""">"&pagenumber&"</a>&nbsp;"
				else
					strPageNumber=strPageNumber&"<a href="""&runstr&sortid&""&FileExt&""">"&pagenumber&"</a>&nbsp;"					
				end if
			elseif linkType="gbooklist" then
				strPageNumber=strPageNumber&"<a href=""?"&pagenumber&FileExt&""">"&pagenumber&"</a>&nbsp;"	
			elseif linkType="searchlist" then
				strPageNumber=strPageNumber&"<a href=""?page="&pagenumber&"&key="&keys&"&searchstyle="&searchstyle&""">"&pagenumber&"</a>&nbsp;"						
			elseif linkType="commentlist" then
				strPageNumber=strPageNumber&"<a href=""javascript:pager("&sortid&","&pagenumber&")"">"&pagenumber&"</a>"			
			else
				if sortid="" then
					strPageNumber=strPageNumber&"<a href=""?"&linkType&"_"&pagenumber&FileExt&""">"&pagenumber&"</a>&nbsp;"
				else
					if pagenumber>1 then 
						strPageNumber=strPageNumber&"<a href="""&runstr&sortid&"_"&linkType&"_"&pagenumber&FileExt&""">"&pagenumber&"</a>&nbsp;"
					else
						strPageNumber=strPageNumber&"<a href="""&runstr&sortid&"_"&linkType&FileExt&""">"&pagenumber&"</a>&nbsp;"					
					end if
				end if
			end if
		end if	
	next
	makePageNumber=strPageNumber
End Function

'分页中部
Function makePageNumber_(Byval currentPage,Byval pageListLen,Byval totalPages,Byval linkType,Byval sortid)
	dim beforePages,pagenumber,page
	dim beginPage,endPage,strPageNumber
	if pageListLen mod 2 = 0 then beforePages = pagelistLen / 2 else beforePages = clng(pagelistLen / 2) - 1
	if  currentPage < 1  then currentPage = 1 else if currentPage > totalPages then currentPage = totalPages
	if pageListLen > totalPages then pageListLen=totalPages
	if currentPage - beforePages < 1 then
		beginPage = 1 : endPage = pageListLen
	elseif currentPage - beforePages + pageListLen > totalPages  then
		beginPage = totalPages - pageListLen + 1 : endPage = totalPages
	else 
		beginPage = currentPage - beforePages : endPage = currentPage - beforePages + pageListLen - 1
	end if	
'	die currentPage
	for pagenumber = beginPage  to  endPage
		if pagenumber=1 then page = "" else page = pagenumber
		if pagenumber=currentPage then
			strPageNumber=strPageNumber&"<span><font color=red>"&pagenumber&"</font></span>"&"&nbsp;"
		else
			if linkType="newslist" or linkType="downlist" or linkType="productlist" or linkType="piclist" then
					strPageNumber=strPageNumber&"<a href='?sort="&sortid&"&page="&pagenumber&"&order="&order&"&pic="&pic&"&keyword="&keyword&"'>"&pagenumber&"</a>&nbsp;"

			elseif linkType="guestlist" then
				strPageNumber=strPageNumber&"<a href='?page="&pagenumber&"'>"&pagenumber&"</a>&nbsp;"						
			else
				if sortid="" then
					strPageNumber=strPageNumber&"<a href='?Sort="&linkType&"&Page="&pagenumber&"'>"&pagenumber&"</a>&nbsp;"
				else
					strPageNumber=strPageNumber&"<a href='?Sort="&linkType&"&ID="&sortid&"&Page="&pagenumber&"'>"&pagenumber&"</a>&nbsp;"
				end if
			end if
		end if	
	next
	makePageNumber_=strPageNumber
End Function


'分页两侧
Function pageNumberLinkInfo(Byval currentPage,Byval pageListLen,Byval totalPages,Byval linkType,Byval sortid)
	dim pageNumber,pagesStr,i,pageNumberInfo,firstPageLink,lastPagelink,nextPagelink,finalPageLink
	pageNumber=makePageNumber(currentPage,pageListLen,totalPages,linkType,sortid)
	if currentPage=1 then  
		firstPageLink="<span class='nolink'>首页</span>" : lastPagelink="<span class='nolink'>上一页</span>"
	else
	    if linkType="gbooklist" then
		    firstPageLink="<a href='?1"&FileExt&"'>首页</a>" : lastPagelink="<a href='?"&currentPage-1&FileExt&"'>上一页</a>"
		elseif linkType="searchlist" then
		    firstPageLink="<a href='?page=1&key="&keys&"&searchstyle="&searchstyle&"'>首页</a>" : lastPagelink="<a href='?page="&currentPage-1&"&key="&keys&"&searchstyle="&searchstyle&"'>上一页</a>"
		else
			firstPageLink="<a href='"&runstr&sortid&"_1"&FileExt&"'>首页</a>" : if currentPage>2 then lastPagelink="<a href='"&runstr&sortid&"_"&currentPage-1&FileExt&"'>上一页</a>" : else lastPagelink="<a href='"&runstr&sortid&"_1"&FileExt&"'>上一页</a>"
		end if
	end if 
	if currentPage=totalPages then 
		nextPagelink="<span class='nolink'>下一页</span>" : finalPageLink="<span class='nolink'>尾页</span>"
	else
	    if linkType="gbooklist" then
		    nextPagelink="<a href='?"&currentPage+1&FileExt&"'>下一页</a>" : finalPageLink="<a href='?"&totalPages&FileExt&"'>尾页</a>"
		elseif linkType="searchlist" then
		    nextPagelink="<a href='?page="&currentPage+1&"&key="&keys&"&searchstyle="&searchstyle&"'>下一页</a>" : finalPageLink="<a href='?page="&totalPages&"&key="&keys&"&searchstyle="&searchstyle&"'>尾页</a>"
		else
			nextPagelink="<a href='"&runstr&sortid&"_"&currentPage+1&FileExt&"'>下一页</a>" : finalPageLink="<a href='"&runstr&sortid&"_"&totalPages&FileExt&"'>尾页</a>"
		end if	
	end if 
	pageNumberInfo="<span>共"&totalPages&"页 页次:"&currentPage&"/"&totalPages&"页</span>&nbsp;"&firstPageLink&lastPagelink&pageNumber&""&nextPagelink&""&finalPagelink
	pageNumberLinkInfo=pageNumberInfo
End Function


Sub checkRunMode
	if runMode<>"1" then  die "<div style='width:50%;margin-top:50px;background:#66CCCC;font-size:13px;'><br><font color='red'>网站不是静态模式，不允许生成</font><br><br></div>"
End Sub

Function filterDirty_(content)
	dim dirtyStr,dirtyStrArray,i : dirtyStr="%u80E1%u9526%u6D9B%2C%u6E29%u5BB6%u5B9D%2C%u5C3B%2C%u5C4C%2C%u64CD%u4F60%2C%u5E72%u6B7B%u4F60%2C%u8D31%u4EBA%2C%u72D7%u5A18%2C%u5A4A%u5B50%2C%u8868%u5B50%2C%u9760%u4F60%2C%u53C9%u4F60%2C%u53C9%u6B7B%2C%u63D2%u4F60%2C%u63D2%u6B7B%2C%u5E72%u4F60%2C%u5E72%u6B7B%2C%u65E5%u4F60%2C%u65E5%u6B7B%2C%u9E21%u5DF4%2C%u777E%u4E38%2C%u5305%u76AE%2C%u9F9F%u5934%2C%u5C44%2C%u8D51%2C%u59A3%2C%u808F%2C%u5976%u5B50%2C%u5976%u5934%2C%u9634%u6BDB%2C%u9634%u9053%2C%u9634%u830E%2C%u53EB%u5E8A%2C%u5F3A%u5978%2C%u7231%u6DB2%2C%u6027%u9AD8%u6F6E%2C%u505A%u7231%2C%u6027%u4EA4%2C%u53E3%u4EA4%2C%u809B%u4EA4"
	dirtyStrArray=split(unescape(dirtyStr),",")
	for i=0 to ubound(dirtyStrArray)
		content=replace(content,dirtyStrArray(i),"***",1,-1,1)
	next
	filterDirty=content
End Function

'在str中是否存在findstr
Function isExistStr(str,findstr)
	if isNul(str) or isNul(findstr) then isExistStr=false:Exit Function
	if instr(str,findstr)>0 then isExistStr=true else isExistStr=false
End Function

Function getSubStrByFromAndEnd(str,startStr,endStr,operType)
	dim location1,location2
	select case operType
		case "start"
			location1=instr(str,startStr)+len(startStr):location2=len(str)+1
		case "end"
			location1=1:location2=instr(location1,str,endStr)
		case else
			location1=instr(str,startStr)+len(startStr):location2=instr(location1,str,endStr)
	end select
	getSubStrByFromAndEnd=mid(str,location1,location2-location1) 
End Function

'转换时间
Function FormatDate(Byval t,Byval ftype)
    select case cint(ftype)
	case 1
	    FormatDate = year(t)&"-"&month(t)&"-"&day(t)
	case 2
	    FormatDate = right(year(t),2)&"-"&month(t)&"-"&day(t)
	case 3
	    FormatDate = month(t)&"-"&day(t)
	end select
End Function

'过滤参数
Function filterPara(byVal Para)
    filterPara=preventSqlin(Checkxss(Para))
End Function

Function preventSqlin(content)
	dim sqlStr,sqlArray,i,speStr
	sqlStr="<|>|%|%27|'|''|;|*|and|exec|dbcc|alter|drop|insert|select|update|delete|count|master|truncate|char|declare|where|set|declare|mid|chr"
	if isNul(content) then Exit Function
	sqlArray=split(sqlStr,"|")
	for i=lbound(sqlArray) to ubound(sqlArray)
		if instr(lcase(content),sqlArray(i))<>0 then
				select case sqlArray(i)
					case "<":speStr="&lt;"
					case ">":speStr="&gt;"
					case "'","""":speStr="&quot;"
					'case ";":speStr="；"
					case else:speStr=""
				end select
				content=replace(content,sqlArray(i),speStr,1,-1,1)		
		end if
	next
	preventSqlin=content
End Function

'过滤xss注入
Function Checkxss(byVal ChkStr)
    dim Str,re
    Str = ChkStr
    if IsNull(Str) then  Checkxss = "" : Exit Function
    Str = Replace(Str, "&", "&amp;") : Str = Replace(Str, "'", "&acute;") : Str = Replace(Str, """", "&quot;") : Str = Replace(Str, "<", "&lt;") : Str = Replace(Str, ">", "&gt;") : Str = Replace(Str, "/", "&#47;") : Str = Replace(Str, "*", "&#42;")
    Set re = New RegExp
    re.IgnoreCase = True : re.Global = True
    re.Pattern = "(w)(here)" : Str = re.Replace(Str, "$1h&#101;re")
	re.Pattern = "(s)(elect)" : Str = re.Replace(Str, "$1el&#101;ct")
	re.Pattern = "(i)(nsert)" : Str = re.Replace(Str, "$1ns&#101;rt")
	re.Pattern = "(c)(reate)" : Str = re.Replace(Str, "$1r&#101;ate")
	re.Pattern = "(d)(rop)" : Str = re.Replace(Str, "$1ro&#112;")
	re.Pattern = "(a)(lter)" : Str = re.Replace(Str, "$1lt&#101;r")
	re.Pattern = "(d)(elete)" : Str = re.Replace(Str, "$1el&#101;te")
	re.Pattern = "(u)(pdate)" : Str = re.Replace(Str, "$1p&#100;ate")
	re.Pattern = "(\s)(or)" : Str = re.Replace(Str, "$1o&#114;")
	re.Pattern = "(java)(script)" : Str = re.Replace(Str, "$1scri&#112;t")
	re.Pattern = "(j)(script)" : Str = re.Replace(Str, "$1scri&#112;t")
	re.Pattern = "(vb)(script)" : Str = re.Replace(Str, "$1scri&#112;t")
	If Instr(Str, "expression") > 0 Then Str = Replace(Str, "expression", "e&#173;xpression", 1, -1, 0)
    Set re = Nothing
    Checkxss = Str
End Function

'获取SortID分类的顶级分类ID
Function GetTopId(byval SortID)
    dim sqlStr,rsObj,ChildArray,i
	sqlStr= "select SortID,SortLevel from Aspcms_NewsSort where ParentID=0"

	set rsObj = conn.Exec(sqlStr,"r1")
	do while not rsObj.eof
	    ChildArray=split(rsObj(1),",")
		for i=0 to ubound (ChildArray)
		    if cint(ChildArray(i))=cint(SortID) then GetTopId=rsObj(0) : exit for : exit do
		next
	rsObj.movenext
	loop
	rsObj.close
	set rsObj = nothing
End Function


'前台栏目类别目录
Function Dir(byval webType)
    select case cint(WebType)
		case 0 : Dir="news"
		case 1 : Dir="pic"
		case 2 : Dir="about"
		case 3 : Dir="product"
		case 4 : Dir="down"
		case 5 : Dir="url"
	end select
End Function


'前台类别
Function makeqtType(topId,separateStr,classname)
	dim sqlStr,rsObj,selectedStr,qtstr,qtspan
	sqlStr= "select SortID,SortName,SortStyle from Aspcms_NewsSort where SortStatus and ParentID="&topId&" order by SortID asc"
	set rsObj = conn.Exec(sqlStr,"r1")
	do while not rsObj.eof
	  
			if runMode="0" then
				qtstr = qtstr + "<div class='"&classname&"'>"&qtspan&"<a href='/"&sitePath&"productlist/?"&rsObj("SortID")&"_1.html'>"&rsObj("SortName")&"</a></div>"
			elseif runMode="1" then
				qtstr = qtstr + "<div class='"&classname&"'>"&qtspan&"<a href='/"&sitePath&"productlist/"&rsObj("SortID")&".html'>"&rsObj("SortName")&"</a></div>"
			end if
		qtspan=qtspan&separateStr
		makeqtType rsObj("SortID"),separateStr,classname
		rsObj.movenext
	loop
	if not isNul(qtspan) then qtspan = left(qtspan,len(qtspan)-len(separateStr))
	rsObj.close
	set rsObj = nothing
	makeqtType=qtstr
End Function

'所有类别
Sub makeTypeOption(topId,separateStr,compareValue,sortid)
	dim sqlStr,rsObj,selectedStr
	sqlStr= "select ID,SortName from Aspcms_NewsSort where ParentID="&topId&" and IsOut=0 order by ID asc"
	set rsObj = conn.Exec(sqlStr,"r1")
	do while not rsObj.eof
	    if rsObj("ID")=compareValue then selectedStr=" selected" else selectedStr=""
		print "<option value='"&rsObj("ID")&"' "&selectedStr&">"&span&"&nbsp;|―"&rsObj("SortName")&"</option>"
		span=span&separateStr
		makeTypeOption rsObj("ID"),separateStr,compareValue,sortid
		rsObj.movenext
	loop
	if not isNul(span) then span = left(span,len(span)-len(separateStr))
	rsObj.close
	set rsObj = nothing
End Sub

'判断一个类别是否有子类
Function HasChild(TableName,ClassID)
	Dim HasChild_SQL	:	HasChild_SQL="SELECT COUNT(*) FROM ["&TableName&"] WHERE [ParentID]="&ClassID
	Dim HasChild_Rs		:	Set HasChild_Rs=conn.Exec(HasChild_SQL,"r1")
	Dim Has
	IF HasChild_Rs(0)>0 Then
		Has=True
	Else
		Has=False
	End IF
	HasChild_Rs.Close	:	Set HasChild_Rs=Nothing
	HasChild=Has
End Function

'获取某个类别表的某个类别的最小子类列表
Function GetSmallestChild(TableName,ClassID)
	Dim Str
	IF HasChild(TableName,ClassID) Then
		Str=GetSmallestChild_Sub(TableName,ClassID,"")
	Else
		Str=ClassID&","
	End IF
	GetSmallestChild=Left(Str,Len(Str)-1)
End Function

'获取某个类别表的某个类别的最小子类列表,GetSmallestChild函数调用的递归函数
Function GetSmallestChild_Sub(TableName,ClassID,TmpStr)
	IF HasChild(TableName,ClassID) Then
		Dim GetSmallestChild_Sub_SQL	:	GetSmallestChild_Sub_SQL="SELECT [SortID] FROM ["&TableName&"] WHERE [ParentID]="&ClassID
		Dim GetSmallestChild_Sub_Rs		:	Set GetSmallestChild_Sub_Rs=conn.Exec(GetSmallestChild_Sub_SQL,"r1")
		While Not (GetSmallestChild_Sub_Rs.Eof Or GetSmallestChild_Sub_Rs.Bof)
			Dim TmpClassID	:	TmpClassID=GetSmallestChild_Sub_Rs(0)
			IF HasChild(TableName,TmpClassID) Then
				TmpStr=GetSmallestChild_Sub(TableName,TmpClassID,TmpStr)
			Else
				TmpStr=TmpStr&TmpClassID&","
			End IF
			GetSmallestChild_Sub_Rs.MoveNext
		Wend
	Else
		TmpStr=TmpStr&ClassID&","
	End IF
	GetSmallestChild_Sub=TmpStr
End Function


	
'获取checkbox的值,选中为1，选为0
function getCheck(cValue)
	if isnul(cValue) then 
		getCheck=0
	elseif cValue="1" then
		getCheck=1	
	end if	
end function

'将null替换成空
Function repnull(str)
	repnull=str
	if isnul(str) then repnull=""
End Function

Function getStr(Stat,str1,str2)
	if Stat then
		getStr=str1
	else
		getStr=str2
	end if
End Function

'获取当前页面名称
Function getPageName()
	Dim fileName,arrName,postion
	fileName=Request.ServerVariables("script_name")
	postion=InstrRev(fileName,"/")+1
	fileName=Mid(fileName,postion)
	If InStr(fileName,"?")>0 Then
		arrName=fileName
		arrName=Split(arrName,"?")
		filename=arrName(0)
	End If
	getPageName=filename
End Function


Function CheckAdmin()
	if isnul(rCookie("adminName")) then 
		alertMsgAndGo"您还没有登陆","/"
	else
		Dim Permissions
		Permissions=rCookie("Permissions")
		if Permissions<>"all" and isnul(Permissions) then
			alertMsgAndGo"您没有访问权限","-1"
		else			
		'die not instr(lcase(Permissions),lcase(getPageName))>0 and Permissions<>"all" and not instr(lcase(getPageName),lcase("AspCms_ManagerEditPass"))>0
		'die lcase(Permissions)&"<br>"&lcase(getPageName)
			if not instr(lcase(Permissions),lcase(getPageName))>0 and Permissions<>"all" and not instr(lcase(getPageName),lcase("AspCms_ManagerEditPass"))>0 then
				alertMsgAndGo"您的访问权限不够","-1"
			end if
		end if
	end if
End Function

Function CheckLogin()
	if isnul(rCookie("adminName")) or rCookie("adminName")="" then 
		alertMsgAndGo"您还没有登陆","/"
	else
		Dim Permissions
		Permissions=rCookie("Permissions")
		if Permissions<>"all" and isnul(Permissions) then
			alertMsgAndGo"您没有访问权限","-1"
		end if
	end if
End Function

'从内容里面提取图片
Function getImgFromText(strng) 
	Dim regEx, Match, Matches '建立变量。 
	Set regEx = New RegExp '建立正则表达式。	
	regEx.Pattern = "(<)(.[^<]*)(src=)('|"&CHR(34)&"| )?(.[^'|\s|"&CHR(34)&"]*)(\.)(jpg|gif|png|bmp|jpeg)('|"&CHR(34)&"|\s|>)(.[^>]*)(>)" '设置模式。 	
	regEx.IgnoreCase = true '设置是否区分字符大小写。 
	regEx.Global = True '设置全局可用性。 
	Set Matches = regEx.Execute(strng) '执行搜索。 
	For Each Match in Matches '遍历匹配集合。 
	values=values&Match.Value&"{|LDIV|}" 
	Next 
	RegExpExecute = values 
End Function 

Function getDataCount(sqlStr)
	getDataCount=conn.Exec(sqlStr,"exe")(0)
End Function


Function LoadSelect(selName,tableName,fieldText,fieldValue,selected, ParentID,strOrder,topText)
	echo "<select name="""& selName &""" id="""& selName &""">" & vbcr & "<option value=""0"">"&topText&"</option>"& vbcr 
	makeOption tableName,fieldText,fieldValue,selected,strOrder,ParentID
	echo "</select>" & vbcr
End Function

Function makeOption(tableName,fieldText,fieldValue,selected,strOrder,ParentID)
	Dim rs ,sel
	sel=""
	set rs =Conn.Exec ("select ["&fieldValue&"],["&fieldText&"],ParentID,SortLevel,(select count(*) from AspCms_NewsSort where ParentID=t.SortID) as c from "&tableName&" as t where ParentID="&ParentID&" "&strOrder,"r1")		
	Do While Not rs.Eof	
		IF CSTR(selected)=CSTR(rs(0)) Then sel = "selected=""selected""" else sel="" end if
		'echo selected&"="&rs(0)&"<br>"
		echo "<option value="""& rs(0) &""" "&sel&">"&getLevel(rs(3))&rs(1) &"</option>" & vbcr
		if rs(4)>0 then 
			makeOption = makeOption & makeOption(tableName,fieldText,fieldValue,selected,strOrder,rs(0))
		end if
		rs.MoveNext
	Loop
	rs.Close	:	Set rs=Nothing
End Function

function getLevel(num)
	if not isnum(num) then  exit Function
	dim i
	getLevel=""
	for i=1 to num
		getLevel=getLevel&"&nbsp;&nbsp;"		
	next
	if num<>"1" then getLevel=getLevel&"&nbsp;├&nbsp;"	
end function


Sub echoList(str)
	dim page,keys,id,SortAndID,ParentID,topsortid
	SortAndID=split(replaceStr(request.QueryString,FileExt,""),"_")
	if isnul(request.QueryString) then  echoMsgAndGo "页面不存在",3
	SortID = SortAndID(0)
	page = SortAndID(1)
	keys=filterPara(getForm("keys","both"))
	
	if not isNul(SortID) and isNum(SortID) then SortID=clng(SortID) else echoMsgAndGo "页面不存在",3 end if
	if not isNul(page) and isNum(page) then page=clng(page) else echoMsgAndGo "页面不存在",3 end if
	
	dim templateobj,TemplatePath : set templateobj = mainClassobj.createObject("MainClass.template")
	dim typeIds,rsObj,rsObjtid,Tid,rsObjSmalltype,rsObjBigtype,channelTemplateName,tempStr,channelStr,PageKey,PageDesc
	dim templateFile
	templateFile=getTemplateFile(SortID,str,2)
	if not CheckTemplateFile(templateFile) then echo templateFile&"模板文件不存在！"
	templatePath = "/"&sitePath&"templates/"&defaultTemplate&"/"&htmlFilePath&"/"&templateFile
	if not isNul(sortid) then	   
		set rsObj = conn.Exec("select top 1 sortID,ParentID,(select count(*) from AspCms_NewsSort where ParentID=t.SortID),topsortid,PageKey,PageDesc from Aspcms_NewsSort as t where SortID="&sortid&"","r1")	
		topsortid=rsObj("topsortid")
		ParentID=rsObj("ParentID")
		PageKey=rsObj("PageKey")
		PageDesc=rsObj("PageDesc")
		if not rsObj.eof then typeIds=rsObj(0) else echoMsgAndGo "栏目不存在！",3 
		rsObj.close:set rsObj = nothing
	end if
	with templateObj 
		.load(TemplatePath)
		.parseHtml()
		.content= replace(.content,"{aspcms:sortid}",SortID)
		.content= replace(.content,"{aspcms:parentsortid}",ParentID)	
		.content= replace(.content,"{aspcms:topsortid}",topsortid)	
		if isnul(PageKey) then 	
			.content= replace(.content,"{aspcms:sortkeyword}",siteKeyWords)	
		else	
			.content= replace(.content,"{aspcms:sortkeyword}",PageKey)		
		end if
		if isnul(PageDesc) then
			.content= replace(.content,"{aspcms:sortdesc}",decodeHtml(siteDesc))
		else
			.content= replace(.content,"{aspcms:sortdesc}",PageDesc)
		end if
		.parsePosition(SortID)			
		.parseList typeIds,page,str&"list",keys,str
		.parseCommon() 
		echo .content 
	end with	
	set templateobj =nothing : terminateAllObjects
End Sub

function strDetails(str)
	dim id,SortID,Page,SortAndID,ParentID,SortName,topsortid
	SortAndID=split(replaceStr(request.QueryString,FileExt,""),"_")	
	if isNul(replaceStr(request.QueryString,FileExt,"")) then  echoMsgAndGo "页面不存在",3 
	SortID = SortAndID(0)
	ID = SortAndID(1)	
	if not isNul(id) and isNum(id) then id=clng(id) else echoMsgAndGo "页面不存在",3 end if
	if not isNul(SortID) and isNum(SortID) then SortID=clng(SortID) else echoMsgAndGo "页面不存在",3 end if
	
	if ubound(SortAndID)=1 then page=1 else page=SortAndID(2) end if 
		
	set rsObjSmalltype = conn.Exec("select SortName,ParentID,topsortid from Aspcms_NewsSort where SortID="&SortID&"","r1")
		SortName = rsObjSmalltype(0)	
		ParentID =rsObjSmalltype(1)		
		topsortid=rsObjSmalltype(2)	
	set rsObjSmalltype=nothing
	dim templateFile
	dim templateobj,TemplatePath : set templateobj = mainClassobj.createObject("MainClass.template")
	dim rsObj,rsObjSmalltype,rsObjBigtype,channelTemplateName,tempStr,tempArr,pageStr,sql,sperStr,sperStrs,content
	
	templateFile=getTemplateFile(SortID,str,1)
	if not CheckTemplateFile(templateFile) then echo templateFile&"模板文件不存在！"
	templatePath = "/"&sitePath&"templates/"&defaultTemplate&"/"&htmlFilePath&"/"&templateFile
	with templateObj
		 .load(TemplatePath)
		 .parseHtml()
		 if SwitchComments=1 then
		 	.content= replace(.content,"{aspcms:comment}",loadFile("/"&sitePath&"plug/comment.html"))	'加载评论模板
		 else
		 	.content= replace(.content,"{aspcms:comment}","")
		 end if
		 .content= replace(.content,"{aspcms:sortid}",SortID)
		 .content= replace(.content,"{aspcms:sortname}",SortName)
		 .content= replace(.content,"{aspcms:parentsortid}",ParentID)
		 .content= replace(.content,"{aspcms:topsortid}",topsortid)
		 .content= replace(.content,"["&str&":id]",Id)
		 .content= replace(.content,"[news:id]",Id)
		 .parsePosition(SortID) 
		 .parseCommon() 		 
		 .parsePrevAndNext id,SortID
	end with	
	tempStr = templateObj.content
	
							
	'conn.Exec  "update Aspcms_News set Visits=Visits+1 where NewsID="&Id&"", "exe"	
	if str="product" then 
		sperStrs =conn.Exec("select SpecField from Aspcms_ProductSpecSet Order by SpecOrder Asc,SpecID", "arr")
		dim spec
		
		if isarray(sperStrs) then
			for each spec in sperStrs
				sperStr = sperStr&","&spec
			next
		end if
	end if
	
	sql="select Title,TitleColor,NewsSource,[Content],Author,AddTime,NewsTag,Visits,ImagePath,downurl,PageDesc,GradeID,Exclusive"&sperStr&" from Aspcms_News where NewsStatus and NewsID="&Id&""
	
	
	set rsObj = conn.Exec(sql,"r1")
	if rsObj.eof then 
		echoMsgAndGo "文章不存在！",3 
	else
		
		if not isnul(rsObj("GradeID")) then	
			if not ViewNoRight(rsObj("GradeID"),rsObj("Exclusive")) then 
				echoMsgAndGo "您当前所在用户组无查看权限！",10
				response.end()
			end if
			
		end if
		
		tempStr = replace(tempStr,"["&str&":title]",rsObj(0))
		dim newsSource 
		if isnul(rsObj(2)) then newsSource=siteTitle else newsSource=repnull(rsObj(2)) end if
		tempStr = replace(tempStr,"["&str&":source]",newsSource)
		content=decodeHtml(rsObj(3))	
		if isExistStr(content,"{aspcms:page}") then
			tempArr = split(content,"{aspcms:page}")
			if isNul(Page) then Page=1
			if isNum(Page) then
				Page=clng(Page)
				if Page<1 then Page=1 : end if
				if Page>ubound(tempArr)+1 then Page=ubound(tempArr)+1 : end if
				if Page=1 then
					pageStr=pageStr+"<div class='pages'><a href=""?"&SortID&"_"&ID&"_1"&FileExt&""">上一页</a>"
				else
					pageStr=pageStr+"<div class='pages'><a href=""?"&SortID&"_"&ID&"_"&Page-1&FileExt&""">上一页</a>"
				end if
				pageStr=pageStr+makePageNumber(Page,10,ubound(tempArr)+1,ID,SortID)
				if Page=ubound(tempArr)+1 then
					pageStr=pageStr+"<a href=""?"&SortID&"_"&ID&"_"&ubound(tempArr)+1&FileExt&""">下一页</a></div>"
				else
					pageStr=pageStr+"<a href=""?"&SortID&"_"&ID&"_"&Page+1&FileExt&""">下一页</a></div>"
				end if
				tempStr = replace(tempStr,"["&str&":info]",tempArr(Page-1)+pageStr)
			else
				echoMsgAndGo "页面不存在!",3
			end if
		else
			tempStr = replace(tempStr,"["&str&":info]",repnull(content))
		end if
		
		if str="product" then 			
			if isarray(sperStrs) then
				for each spec in sperStrs			
					tempStr = replace(tempStr,"["&str&":"&spec&"]",repnull(rsObj(spec)))
				next
			end if
		end if		
		
		dim imgPath
		if isnul(rsObj(8)) then imgPath="/"&sitePath&"images/nopic.gif" else imgPath=rsObj(8)
		tempStr = replace(tempStr,"["&str&":pic]",imgPath)	
		'if not isnul(rsObj(9))
		if isExistStr(tempStr,"["&str&":downurl]") then
			Dim downUrlStr,downUrls ,i	
			downUrls = split(repnull(rsObj(9)),",")
			for i=0 to ubound(downUrls)
				if  not isnul(trim(downUrls(i))) then 
					downUrlStr=downUrlStr&" <a href="""&downUrls(i)&"""  target=""_blank"">下载地址"&i+1&"</a> &nbsp;"
				end if
			next
			tempStr = replace(tempStr,"["&str&":downurl]",downUrlStr)
		end if	
		tempStr = replace(tempStr,"["&str&":date]",repnull(rsObj(5)))
		tempStr = replace(tempStr,"["&str&":tag]",replace(repnull(rsObj(6))," ",","))
		'tempStr = replace(tempStr,"["&str&":visits]",repnull(rsObj(7)))
		tempStr = replace(tempStr,"["&str&":visits]","<script src=""/"&sitePath&"inc/AspCms_Visits.asp?id="&id&"""></script>")	
		tempStr =tempStr&"<script src=""/"&sitePath&"inc/AspCms_VisitsAdd.asp?id="&id&"""></script>"
		if isnul(rsObj(10)) then 
			tempStr = replace(tempStr,"["&str&":desc]",left(dropHtml(content),100))
		else
			tempStr = replace(tempStr,"["&str&":desc]",rsObj(10))			
		end if
		tempStr = replace(tempStr,"["&str&":link]","http://"&siteUrl&"/"&str&"/"&runstr&SortID&"_"&ID&FileExt)
		'tempStr = replace(tempStr,"{aspcms:sort}",GetTopId(SortID))
		rsObj.close
		set rsObj = nothing
	end if	
	templateObj.content=tempStr
	templateobj.parseLoop("aboutart")
	strDetails=templateObj.content
	set templateobj =nothing : terminateAllObjects
End Function


Function getTodayVisits
	getTodayVisits=conn.Exec("select sum(Visits) from Aspcms_Visits where year(AddTime)="&Year(date)&" and month(AddTime)="&month(date)&" and day(AddTime)="&day(date),"r1")(0)
	if isnul(getTodayVisits) then getTodayVisits=0
End Function

Function getYesterdayVisits
	getYesterdayVisits=conn.Exec("select sum(Visits) from Aspcms_Visits where year(AddTime)="&Year(DateAdd("d",-1,date))&" and month(AddTime)="&month(DateAdd("d",-1,date))&" and day(AddTime)="&day(DateAdd("d",-1,date)),"r1")(0)
	if isnul(getYesterdayVisits) then getYesterdayVisits=0
End Function

Function getMonthVisits
	getMonthVisits=conn.Exec("select sum(Visits) from Aspcms_Visits where year(AddTime)="&Year(date)&" and month(AddTime)="&month(date),"r1")(0)	
	if isnul(getMonthVisits) then getMonthVisits=0
End Function

Function getAllVisits
	getAllVisits=conn.Exec("select sum(Visits) from Aspcms_Visits","r1")(0)
	if isnul(getAllVisits) then getAllVisits=0
End Function

Function GetExtend(fileName)
   GetExtend = Mid(fileName,Instr(fileName,".")+1,Len(fileName)-Instr(fileName,"."))
End Function

Function getTemplateFile(Byval sortID,Byval str,Byval sStyle)
	getTemplateFile=conn.exec("select SortTemplate from Aspcms_NewsSort where SortID="&SortID ,"r1")(0)
	if isnul(getTemplateFile) then 
		if str="" then 
			getTemplateFile="about.html"
		else
			if sStyle=1 then		
				getTemplateFile=str&".html"
			elseif sStyle=2 then
				getTemplateFile=str&"list.html"			
			end if
		end if
	end if
End Function

Function CheckTemplateFile(Byval fileName)
	CheckTemplateFile=false
	if isExistFile("/"&sitePath&"templates/"&defaultTemplate&"/"&htmlFilePath&"/"&fileName)then CheckTemplateFile=true
End Function

Function ipHide(ipstr)
 dim t,ipx,ipfb
 if not isnull(ipstr) then
        t = 0
 ipx=""
 ipfb = split(ipstr, ".",4)
  for t = 0 to 2
  ipx = ipx&ipfb(t)&"."
  next
 ipHide = ipx&"*"
 end if
end Function




Function userGradeSelrct(selName,selOption)
	dim selStr
	if isnul(selOption) then selOption=0
	selStr= "<select name="""&selName&""" id="""&selName&""">" & vbcr
	Dim rs ,sel
		sel=""
		set rs =Conn.Exec ("select [GradeID],[GradeName] from Aspcms_UserGrades order by GradeOrder", "r1")		
		Do While Not rs.Eof	
			IF CSTR(selOption)=CSTR(rs(0)) Then sel = "selected=""selected""" else sel="" end if
			selStr=selStr& "<option value="""& rs(0) &""" "&sel&">"&rs(1) &"</option>" & vbcr
			rs.MoveNext
		Loop
		rs.Close	:	Set rs=Nothing
	selStr=selStr& "</select>" & vbcr
	userGradeSelrct=selStr
end Function

Function ViewNoRight(GradeID, Exclusive)
    Dim rs, sql, ifGradeMark
    Set rs =Conn.Exec("select GradeMark from Aspcms_UserGrades where GradeID="&GradeID,"r1")
    if   not   rs.EOF   then
	 ifGradeMark = rs("GradeMark")
end  if
    rs.Close
    Set rs = Nothing
    ViewNoRight = True
     Dim GradeMark
	 If session("GradeMark") = "" Then session("GradeMark") = 0
    Select Case Exclusive
   
        Case ">="
            If Not session("GradeMark") >= GradeMark Then
                ViewNoRight = False
            End If
        Case "="
            If Not session("GradeMark") = GradeMark Then
                ViewNoRight = False
            End If
    End Select
End Function
%>