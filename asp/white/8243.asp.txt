<%@language=vbscript codepage=936 %>
<!--#include file="Conn.asp"-->
<!--#include file="inc/md5.asp"-->
<!--#include file="../inc/config.asp"-->
<% 

dim sql,rs
dim username,password,CheckCode
username=replace(trim(request("UserName")),"'","")
password=replace(trim(Request("password")),"'","")
CheckCode=replace(trim(Request("CheckCode")),"'","")
if UserName="" then
    FoundErr=true
	call showerrer("用户名不能为空!")
end if
if Password="" then
    FoundErr=true
	call showerrer("密码不能为空!")
end if
if CheckCode="" then
    FoundErr=true
	call showerrer("验证码不能为空!")
end if
if session("CheckCode")="" then
    FoundErr=true
	call showerrer("你登录时间过长，请重新返回登录页面进行登录!")
end if
if CheckCode<>CStr(session("CheckCode")) then
    FoundErr=true
	call showerrer("您输入的确认码和系统产生的不一致，请重新输入!")
end if
if FoundErr<>True then
	password=md5(password)
	set rs=server.createobject("adodb.recordset")
	sql="select * from Admin where AdminPass='"&password&"' and AdminName='"&username&"'"
	rs.open sql,conn,1,3
	if rs.bof and rs.eof then
		FoundErr=True
		call showerrer("用户名或密码错误！！")
	else
		if password<>rs("AdminPass") then
			FoundErr=True
			call showerrer("您输入的密码不正确！！！")
		else
		    RndPassword=GetRndPassword(16)
			rs("LastLoginIP")=Request.ServerVariables("REMOTE_ADDR")
			rs("LastLoginTime")=now()
			rs("LoginTimes")=rs("LoginTimes")+1
			rs("RndPassword")=RndPassword
			rs.update
			session.Timeout=SessionTimeout
			session("AdminName")=rs("AdminName")
			session("AdminPassword")=rs("AdminPass")
			session("flag")=rs("flag")		
			session("RndPassword")=RndPassword
			rs.close
			set rs=nothing
			call Closedb
			Response.Redirect "default.asp"
		end if
	end if
	rs.close
	set rs=nothing
end if
call closedb

Function GetRndPassword(PasswordLen)
	Dim Ran,i,strPassword
	strPassword=""
	For i=1 To PasswordLen
		Randomize
		Ran = CInt(Rnd * 2)
		Randomize
		If Ran = 0 Then
			Ran = CInt(Rnd * 25) + 97
			strPassword =strPassword & UCase(Chr(Ran))
		ElseIf Ran = 1 Then
			Ran = CInt(Rnd * 9)
			strPassword = strPassword & Ran
		ElseIf Ran = 2 Then
			Ran = CInt(Rnd * 25) + 97
			strPassword =strPassword & Chr(Ran)
		End If
	Next
	GetRndPassword=strPassword
End Function
%>