
<!--#include file="conn.asp"-->
<%
if session("User")="" then
	response.write"<SCRIPT language=JavaScript>alert('非法操作！');"
	response.write"top.location.href=""login.asp"";</SCRIPT>"
end if

%>

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
<title>ASP友链系统|www.aspyl.com</title>
<link href="images/right.css" rel="stylesheet" type="text/css"/>

<script language="javascript" src="js/Admin_Js.js"></script>

<script language="javascript">
function windowHeight() {
    var de = document.documentElement;
    return self.innerHeight||(de && de.clientHeight)||document.body.clientHeight;
}
window.onload=window.onresize=function(){
var wh=windowHeight();
document.getElementById("contentWrap").style.height = document.getElementById("sidebar").style.height = (wh-document.getElementById("header").offsetHeight-document.getElementById("footer").offsetHeight)+"px";
}
</script>

<script language="javascript">
function checkbanner(o){
	if(!check_in(o,"BannerName","广告说明！")){return false;};
	if(!check_in(o,"oResult","广告代码！")){return false;};
}
</script>
<SCRIPT LANGUAGE="JavaScript">
<!-- Hide

function killErrors() {
return true;
}

window.onerror = killErrors;

// -->
</SCRIPT>

<script> 
function oCopy(obj){ 
obj.select(); 
js=obj.createTextRange(); 
js.execCommand("Copy") 
alert('调用代码已复到剪贴板！请粘贴到前台需要显示广告的地方即可!')
} 
</script> 

</head>
<body>

<%

Class DmFso	

Private Fso
Public 	FsoObj

Private Sub Class_Initialize
	Set Fso=Server.CreateObject("Scripting.FileSystemObject")
	Set FsoObj=Fso
End Sub

Private Sub Class_Terminate
	Set Fso=Nothing
	Set FsoObj=Nothing
End Sub

Function IsObjInstalled(ClassStr)
	On Error Resume Next
	IsObjInstalled=False
	Err=0
	Dim TestObj
	Set TestObj=Server.CreateObject(ClassStr)
	If 0=Err Then IsObjInstalled=True
	Set TestObj=Nothing
	Err=0
End Function

Function GetPath(Byval FileDir)
	GetPath=Server.MapPath(FileDir)
End Function

Function CheckFile(Byval FileDir)
	If Fso.FileExists(GetPath(FileDir)) Then
		CheckFile=True
	Else
		CheckFile=False
	End If
End Function

Function GetFileInfo(Byval FileDir,Byval InfoType)
	If CheckFile(FileDir) Then
		Dim F
		Set F=Fso.GetFile(GetPath(FileDir))
		Select Case InfoType
			Case 1 GetFileInfo=F.Datecreated
			Case 2 GetFileInfo=F.DateLastAccessed
			Case 3 GetFileInfo=F.DateLastModified
			Case 4 GetFileInfo=F.Path
			Case 5 GetFileInfo=F.Name
			Case 6 GetFileInfo=F.Type
			Case 7 GetFileInfo=F.Size
			Case 8 GetFileInfo=F.ParentFolder
			Case 9 GetFileInfo=F.RootFolder
			Case 0 
			GetFileInfo=F.Datecreated&"@"&F.DateLastAccessed&"@"&F.DateLastModified
			GetFileInfo=GetFileInfo&"@"&F.Path&"@"&F.Name&"@"&F.Type&"@"&F.Size
			GetFileInfo=GetFileInfo&"@"&F.ParentFolder
		End Select
		Set F=Nothing
	Else
		GetFileInfo=False
	End If
End Function

Function GetFileExt(FileDir)
	If FileDir<>"" Then
		GetFileExt=Lcase(Mid(FileDir,InstrRev(FileDir,".")+1))
	Else
		GetFileExt=False
	End If
End Function

Function ReadFile(Byval FileDir)
	If CheckFile(FileDir)=False Then
		ReadFile=False
		Exit Function
	Else
		Dim F
		Set F=Fso.OpenTextFile(GetPath(FileDir))
		ReadFile=F.ReadAll
		Set F=Nothing
	End If
End Function

Function CreateFile(Byval FileDir,Byval FileContent,Byval IsMake)
	If CheckFile(FileDir) Then
		If IsMake=True Then
			Dim F
			Set F=Fso.CreateTextFile(GetPath(FileDir),True)
			F.Write FileContent
			CreateFile=True
			Set F=Nothing
		Else
			CreateFile=False
		End If
	Else
		Set F=Fso.CreateTextFile(GetPath(FileDir),True)
		F.Write FileContent
		CreateFile=True
		Set F=Nothing
	End If
End Function

Function WriteFile(Byval FileDir,Byval FileContent,ByVal WriteType)
	If CheckFile(FileDir) Then
		Dim F
		Set F=Fso.OpenTextFile(GetPath(FileDir),WriteType)
		F.Write FileContent
		Set F=Nothing
		WriteFile=True
	Else
		WriteFile=False
	ENd If
End Function

Function MoveFile(Byval FileDir,Byval NewFileDir)
	If CheckFile(FileDir) And  CheckFile(NewFileDir)=False Then
		Fso.MoveFile GetPath(FileDir),GetPath(NewFileDir)
		MoveFile=True
	Else
		MoveFile=False
	End If
End Function

Function CopyFile(Byval FileDir,Byval NewFileDir)
	If CheckFile(FileDir) Then
		Dim F
		Set F=Fso.GetFile(GetPath(FileDir))
		F.Copy (GetPath(NewFileDir))
		CopyFile=True
		Set F=Nothing
	Else
		CopyFile=False
	End If
End Function

Function DelFile(Byval FileDir)
	If CheckFile(FileDir) Then
		Fso.DeleteFile(GetPath(FileDir))
		DelFile=True
	Else
		DelFile=False
	End If
End Function

Function CheckFolder(Byval FolderDir)
	If Fso.FolderExists(GetPath(FolderDir)) Then
		CheckFolder=True
	Else
		CheckFolder=False
	End If
End Function

Function GetFolderInfo(Byval FolderDir,Byval InfoType)
	If CheckFolder(FolderDir) Then
		Dim F
		Set F=Fso.GetFolder(GetPath(FolderDir))
		Select Case InfoType
			Case 1 GetFolderInfo=F.Datecreated
			Case 2 GetFolderInfo=F.DateLastAccessed
			Case 3 GetFolderInfo=F.DateLastModified
			Case 4 GetFolderInfo=F.Path
			Case 5 GetFolderInfo=F.Name
			Case 6 GetFolderInfo=F.Type
			Case 7 GetFolderInfo=F.Size
			Case 8 GetFolderInfo=F.ParentFolder
			Case 9 GetFolderInfo=F.RootFolder
			Case 0 
			GetFolderInfo=F.Datecreated&"@"&F.DateLastAccessed&"@"&F.DateLastModified
			GetFolderInfo=GetFolderInfo&"@"&F.Path&"@"&F.Name&"@"&F.Type&"@"&F.Size
			GetFolderInfo=GetFolderInfo&"@"&F.ParentFolder
		End Select
		Set F=Nothing
	Else
		GetFolderInfo=False
	End If
End Function

Function CreateFolder(Byval FolderStr)
	Dim I,Folder,Dir
	If InStr(FolderStr,"/")>0 Or InStr(FolderStr,"\")>0 Then
		FolderStr=Replace(FolderStr,"/","\")
		Folder=Split(FolderStr,"\")
		If Left(FolderStr,2)=".." Then
			Dir="..\"
		Else
			Dir=Dir
		End If
		If Folder(0)="" Then Dir=Dir&"\"
		For I=0 To Ubound(Folder)
			If Folder(I)<>"" And  Folder(I)<>".." Then
				Dir=Dir&Folder(I)&"\"
				If CheckFolder(Dir) Then
					CreateFolder=False
				Else
					Fso.CreateFolder(GetPath(Dir))
					CreateFolder=True
				End If
			End If
		Next
	Else
		If CheckFolder(FolderStr) Then
			CreateFolder=False
		Else
			Fso.CreateFolder(GetPath(FolderStr))
			CreateFolder=True
		End If
	End If
End Function

Function MoveFolder(Byval FolderDir,Byval NewFolderDir)
	If CheckFolder(FolderDir) And CheckFolder(NewFolderDir)=False Then
		Fso.MoveFolder GetPath(FolderDir),GetPath(NewFolderDir)
		MoveFolder=True	
	Else
		MoveFolder=False
	End If
End Function

Function CopyFolder(Byval FolderDir,Byval NewFolderDir)
	If CheckFolder(FolderDir) Then
		Fso.CopyFolder GetPath(FolderDir),GetPath(NewFolderDir)
		CopyFolder=True
	Else
		CopyFolder=False
	End If
End Function

Function DelFolder(Byval FolderDir)
	If CheckFolder(FolderDir) Then
		Fso.DeleteFolder(GetPath(FolderDir))
		DelFolder=True
	Else
		DelFolder=False
	End If
End Function

Function GetFolderList(Byval FolderDir)
	If CheckFolder(FolderDir)=False Then
		GetFolderList=False
		Exit Function
	Else
		Dim FolderObj,FolderList,F
		Set FolderObj=Fso.GetFolder(GetPath(FolderDir))
		Set FolderList=FolderObj.SubFolders
		GetFolderList=FolderObj.SubFolders.Count
		For Each F In FolderList
			GetFolderList=GetFolderList&"@"&F.Name
		Next
		Set FolderList=Nothing
		Set FolderObj=Nothing
	End If
End Function

Function GetFileList(Byval FolderDir)
	If CheckFolder(FolderDir)=False Then
		GetFileList=False
		Exit Function
	Else
		Dim FileObj,FileList,F
		Set FileObj=Fso.GetFolder(GetPath(FolderDir))
		Set FileList=FileObj.Files
		GetFileList=FileObj.Files.Count
		For Each F In FileList
			GetFileList=GetFileList&"@"&F.Name
		Next
		Set FileList=Nothing
		Set FileObj=Nothing
	End If
End Function

Function FormatSize(Byval SizeNum)
	FormatSize=SizeNum&"&nbsp;Byte"
	If SizeNum>1024 Then
		SizeNum=(SizeNum/1024)
		FormatSize=Formatnumber(SizeNum,2)&"&nbsp;KB"
	End If
	If SizeNum>1024 Then
		SizeNum=(SizeNum/1024)
		FormatSize=Formatnumber(SizeNum,2)&"&nbsp;MB"
	End If
	If SizeNum>1024 Then
		SizeNum=(SizeNum/1024)
		FormatSize=Formatnumber(SizeNum,2)&"&nbsp;GB"
	End If
End Function

End Class
%>

<%
'****************************************************
'函数名：DBDel
'作  用：删除指定条件的数据
'****************************************************
Function DBDel(Table,WhereStr)
	Dim Sql
	Sql="Delete From "&Table&" Where "&WhereStr&""
	Conn.Execute(Sql)
End Function

'****************************************************
'函数名：DBUpdate
'作  用：修改指定条件和字段的内容
'****************************************************
Function DBUpdate(Table,UpVale,WhereStr)
	Dim Sql
	Sql="Update "&Table&" Set "&UpVale&" Where "&WhereStr&""
	Conn.Execute(Sql)
End Function
'****************************************************
'---------------------分页相关函数-------------------
'****************************************************
'函数名：GetTotalCount
'作  用：获取制定条件的数据总数
'****************************************************
Function GetTotalCount(Tag,Table,WhereStr)
	Dim Rs,Sql
	Set Rs=Server.CreateObject("Adodb.Recordset")
	Sql="Select Count("&Tag&") From "&Table&" "&WhereStr&""
	Rs.Open Sql,Conn,1,1
	GetTotalCount=Rs(0)
	Rs.Close
	Set Rs=Nothing
End Function
'****************************************************
'函数名：GetPageCount
'作  用：获取页数总数
'****************************************************
Function GetPageCount(TotalCount,PageSize)
	Dim PageCount
	PageCount=TotalCount/PageSize
	If (TotalCount Mod PageSize)=0 Then
		PageCount=Int(PageCount)
	Else
		PageCount=Int(PageCount)+1
	End If
	GetPageCount=PageCount
End Function
'****************************************************
'函数名：GetPageNow
'作  用：获取当前页数
'****************************************************
Function GetPageNow(PageCount)
	Dim Page
	Page=CheckNum(Request("Page"))
	If Page<1 Then
		Page=1
	End If
	If Page>PageCount Then
		Page=PageCount
	End If
	GetPageNow=Page
End Function
'****************************************************
'函数名：GetPrevID
'作  用：获取上一页最后一条数据的编号
'****************************************************
Function GetPrevID(Tag,Table,Page,PageSize,Order,WhereStr)
	Dim Rs,Sql
	Set Rs=Server.CreateObject("Adodb.Recordset")
	Sql="Select Min("&Tag&") From (Select Top "&(Page-1)*PageSize&" "&Tag&" From "&Table&" "&WhereStr&" Order By "&Order&" Desc) Ta"
	Rs.Open Sql,Conn,1,1
	GetPrevID=Rs(0)
	Rs.Close
	Set Rs=Nothing
End Function
'****************************************************
'函数名：GetPageSql
'作  用：获取查询的Sql语句
'****************************************************
Function GetPageSql(Tag,Table,TagStr,Order,PageSize,WhereStr,Page)
	Dim PrevID,Sql,AndStr
	AndStr=Replace(WhereStr,Left(WhereStr,6),"And ")
	If Page>1 Then
		PrevID=GetPrevID(Tag,Table,Page,PageSize,Order,WhereStr)
		Sql="Select Top "&PageSize&" "&TagStr&" From "&Table&" Where "&Tag&"<"&PrevID&" "&AndStr&" Order By "&Order&" Desc"
	Else
		Sql="Select Top "&PageSize&" "&TagStr&" From "&Table&" "&WhereStr&" Order By "&Order&" Desc"
	End If
	GetPageSql=Sql
End Function
'****************************************************
'函数名：PageShow
'作  用：显示分页信息
'****************************************************
Function PageShow(TotalCount,PageSize,Page,PageCount,Url,LinkNum)
	If Url<>"?" Then
		If InStr(Url,"?")=0 Then
			Url=Url&"?"
		Else
			Url=Url&"&"
		End If
	End If
	Rw (" ")&vbcrlf
	Rw ("共"&TotalCount&"条 每页"&PageSize&"条 "&Page&"/"&PageCount&" ")
	If Page>1 Then
		Rw ("<a href="&Url&"Page=1 title='第一页'><<</a>")&vbcrlf
		Rw ("<a href="&Url&"Page="&Page-1&" title='上一页'><</a>")&vbcrlf
	End If
	Dim I,PageStart,PageEnd
	If Page<=LinkNum Then
		PageStart=1
		PageEnd=LinkNum*2+1
	Else
		PageStart=Page-LinkNum
		PageEnd=Page+LinkNum
	End If
	For I=PageStart To PageEnd
		If I>0 And I<=PageCount Then
			If I<>Page Then
				Rw ("<a href="&Url&"Page="&I&" class='off' title='第"&I&"页'>"&I&"</a>")&vbcrlf
			Else
				Rw ("<a href="&Url&"Page="&I&" class='on' title='第"&I&"页'>"&I&"</a>")&vbcrlf
			End If
		End If
	Next
	If Page<PageCount Then
		Rw ("<a href="&Url&"Page="&Page+1&" title='下一页'>></a>")&vbcrlf
		Rw ("<a href="&Url&"Page="&PageCount&" title='最后一页'>>></a>")&vbcrlf
	End If
	Rw (" ")&vbcrlf
End Function
'-------------------字符处理函数---------------------
'****************************************************
'函数名：Rw
'作  用：输出字符串
'参  数：str ----字符串内容
'返回值：字符串
'****************************************************
Function Rw(ByVal Str)
	Response.Write(Str)
End Function
'****************************************************
'函数名：Rf
'作  用：缓冲输出字符串
'参  数：str ----字符串内容
'返回值：字符串
'****************************************************
Function Rf(ByVal Str)
	Response.Write(Str)
	Response.Flush()
End Function
'****************************************************
'函数名：RwEnd
'作  用：结束输出字符串
'参  数：str ----字符串内容
'返回值：字符串
'****************************************************
Function RwEnd(ByVal Str)
	Response.Write(Str)
	Response.End()
End Function
'****************************************************
'函数名：ReUrl
'作  用：跳转Url地址
'参  数：Url ----Url地址
'返回值：Url地址
'****************************************************
Function ReUrl(ByVal Url)
	Response.Redirect(Url)
End Function
'****************************************************
'函数名：GoReferer
'作  用：跳转会上一页地址
'参  数：无
'返回值：Url地址
'****************************************************
Function GoReferer()
	Response.Redirect(Request.ServerVariables("HTTP_REFERER"))
End Function
'****************************************************
'函数名：GetLen
'作  用：获取字符串长度
'参  数：Str ----字符串
'返回值：字符串长度
'****************************************************
Function GetLen(ByVal Str)
	Dim X,Y,I,M,C
	Str=Trim(Str)
	If IsNull(Str) Then
		GetLen=0
		Exit Function
	End If
	X=Len(Str)
	Y=0
	For I = 1 To X
		M=Mid(Str,I,1)
		C=Asc(M)
		If C<0 Then C=C+65536
		If C>255 Then
			Y=Y+2
		Else
			Y=Y+1
		End If
	Next
	GetLen=Y
End Function
'****************************************************
'函数名：LeftStr
'作  用：获取字符串
'参  数：Str ----字符串 Length截取长度
'返回值：要截取的字符串
'****************************************************
Function LeftStr(ByVal Str,ByVal Length)
	Dim X,Y,I,M,C
	Str=Trim(Str)
	If IsNull(Str) Then
		LeftStr=""
		Exit Function
	End If
	X=Len(Str)
	Y=0
	For I = 1 To X
		M=Mid(Str,I,1)
		C=Asc(M)
		If C<0 Then C=C+65536
		If C>255 Then
			Y=Y+2
		Else
			Y=Y+1
		End If
		If Y>=Length Then
			'LeftStr=Left(Str,I)&"..."
			LeftStr=Left(Str,I)
			Exit For
		Else
			LeftStr=Str
		End If
	Next
End Function
'****************************************************
'函数名：GetSpell
'作  用：获取字符串的拼音
'参  数：Str ----字符串 Length截取长度
'返回值：字符串的拼音
'****************************************************
Function GetSpell(ByVal Str,ByVal Length)
	On Error Resume Next
	Str=Replace(Str,"/","")
	Str=Replace(Str,"\","")
	Str=Replace(Str,"?","")
	Str=Replace(Str,";","")
	Str=Replace(Str,":","")
	Str=Replace(Str,"""","")
	Str=Replace(Str,"|","")
	Str=Replace(Str,"<","")
	Str=Replace(Str,">","")
	Str=Replace(Str,"'","")
	Str=Replace(Str,"~","")
	Str=Replace(Str,"!","")
	Str=Replace(Str,"@","")
	Str=Replace(Str,"#","")
	Str=Replace(Str,"$","")
	Str=Replace(Str,"%","")
	Str=Replace(Str,"^","")
	Str=Replace(Str,"&","")
	Str=Replace(Str,"*","")
	Str=Replace(Str,"+","")
	Str=Replace(Str,"-","")
	Str=Replace(Str,",","")
	Str=Replace(Str,".","")
	Str=Replace(Str," ","")
	Str=Replace(Str,"  ","")
	Dim SpellStr,I,Rs,Key
	SpellStr=""
	For I = 1 To Len(Str)
		Key=Mid(Str,I,1)
		Set Rs=Conn.Execute("Select Spell From Dm_Spell Where Chinese='"&Key&"'")
		If Not(Rs.Bof And Rs.Eof) Then
			If GetLen(Key)=1 Then 
				SpellStr=SpellStr&Key
			Else
				SpellStr=SpellStr&Rs(0)
			End If
		End If
		Rs.Close
		Set Rs=Nothing
	Next
	GetSpell=Left(Trim(SpellStr),Length)
End Function

'-------------------检测过滤函数---------------------
'****************************************************
'函数名：CheckNum
'作  用：检测是否为数字
'参  数：Str - 字符串
'返回值：不是数字返回0 是数字返回数字
'****************************************************
Function CheckNum(Byval Str)
	If IsNumeric(Str)=False Then
		CheckNum=0
		Exit Function
	Else
		CheckNum=Clng(Str)
	End If
End Function
'****************************************************
'函数名：CheckStr
'作  用：过滤危险字符
'参  数：Str - 字符串
'返回值：返回字符串
'****************************************************
Function CheckStr(ByVal Str)
	If Str="" Or IsNull(Str) Then
		CheckStr=""
		Exit Function
	End If
	'Str=LCase(Str)
	Str=Replace(Str,Chr(0),"")
	Str=Replace(Str,"""","&quot;")
	Str=Replace(Str,"'","''")
	Str=Replace(Str,"=","&#061")
	Str=Replace(Str,"<","&lt;")
	Str=Replace(Str,">","&gt;")
	Str=Replace(Str,"[","&#091;")
	Str=Replace(Str,"]","&#093;")
	Str=Replace(Str,"select","sel&#101;ct")
	Str=Replace(Str,"execute","&#101;xecute")
	Str=Replace(Str,"exec","&#101;xec")
	Str=Replace(Str,"join","jo&#105;n")
	Str=Replace(Str,"insert","ins&#101;rt")
	Str=Replace(Str,"delete","del&#101;te")
	Str=Replace(Str,"update","up&#100;ate")
	Str=Replace(Str,"drop","dro&#112;")
	Str=Replace(Str,"create","cr&#101;ate")
	Str=Replace(Str,"truncate","trunc&#097;te")
	Str=Replace(Str,"chr","c&#104;r")
	Str=Replace(Str,"count","co&#117;nt")
	Str=Replace(Str,"mid","m&#105;d")
	Str=Replace(Str,"char","ch&#097;r")
	Str=Replace(Str,"alter","alt&#101;r")
	Str=Replace(Str,"exists","e&#120;ists")
	Str=Replace(Str,"script","&#115;cript")
	Str=Replace(Str,"object","&#111;bject")
	Str=Replace(Str,"applet","&#097;pplet")
	CheckStr=Str
End Function
'****************************************************
'函数名：CheckName
'作  用：名字字符检验	
'参  数：str ----字符串
'返回值：true无误，false有误
'****************************************************
Function CheckName(Str)
	If Str="" Or IsNull(Str) Then
		CheckName=False
		Exit Function
	End If
	CheckName=True
	Dim Rep,Pass
	Set Rep=New RegExp
	Rep.Global=True
	Rep.IgnoreCase=True
	Rep.Pattern="^[a-zA-Z_u4e00-\u9fa5][\w\u4e00-\u9fa5]+$"
	Set Pass=Rep.Execute(Str)
	If Pass.Count=0 Then CheckName=false
	Set Rep=Nothing
End Function

'****************************************************
'函数名：HtmlCode
'作  用：过滤Html标记
'参  数：Str ----字符串
'返回值：过滤后的字符串
'****************************************************
Function HtmlCode(Str)
	If Str="" Or IsNull(Str) Then
		HtmlCode=""
		Exit Function
	End If
	Str=replace(Str,">","&gt;")
	Str=replace(Str,"<","&lt;")
	Str=Replace(Str,CHR(32)," ")
	Str=Replace(Str,CHR(9),"&nbsp;")
	Str=Replace(Str,CHR(34),"&quot;")
	Str=Replace(Str,CHR(39),"&#39;")
	Str=Replace(Str,CHR(13),"")
	Str=Replace(Str,CHR(10),"")
	Str=Replace(Str,"script","&#115cript")
	HtmlCode=Str
End Function
'****************************************************
'函数名：HtmlDel
'作  用：清理所有html标记
'参  数：Str ----字符串
'返回值：过滤后的字符串
'****************************************************
Function HtmlDel(Str)
	If Str="" Or IsNull(Str) Then
		HtmlDel=""
		Exit Function
	End If
	Dim Rep,Pass
	Set Rep=New RegExp
	Rep.Global=True
	Rep.IgnoreCase=True
	Rep.Pattern="(<.+?>)"
	HtmlDel=Rep.Replace(Str,"")
	Set Rep=Nothing
End Function
'****************************************************

'函数名：StopSql
'作  用：防止SQL注入
'参  数：无
'返回值：输出文本信息
'****************************************************
Function StopSql()
	Badwords= "'||''||;||and||exec||insert||select||update||delete||count||*||%||chr||mid||master||truncate||char||declare"
	Badword=Split(Badwords,"||")
	If Request.Form<>"" Then
		For Each TF_Post In Request.Form
			For i=0 To Ubound(Badword)
				If Instr(LCase(Request.Form(TF_Post)),Badword(i))>0 Then
					Response.Write("Error")
					Response.End()
					Exit function
				End If
			Next
		Next
	End If
	If Request.QueryString<>"" Then
		For Each TF_Get In Request.QueryString
			For i=0 To Ubound(Badword)
				If Instr(LCase(Request.QueryString(TF_Get)),Badword(i))>0 Then
					Response.Write("Error")
					Response.End()
					Exit function
				End If
			Next
		Next
	End If
End Function
'-------------------信息提示函数---------------------
'****************************************************
'函数名：ShowErr
'作  用：Js弹出信息 并返回上一页
'参  数：Str ----字符串
'返回值：弹出信息 返回上一页
'****************************************************
Function ShowErr(ByVal Str)
	Response.Write("<script>alert('"&Str&"');history.back(-1);</script>")
	Response.End
End Function
'****************************************************
'函数名：ShowSucc
'作  用：Js弹出信息 并跳转
'参  数：Str-字符串 Url-要跳转的地址
'返回值：弹出信息 并跳转
'****************************************************
Function ShowSucc(ByVal Str,Byval Url)
	Response.Write("<script>alert('"&Str&"');location.href=('"&Url&"');</script>")
	Response.End()
End Function
'****************************************************
'函数名：ShowErrMsg
'作  用：文本错误提示信息
'参  数：Str-字符串
'返回值：文本错误提示信息
'****************************************************
Function ShowErrMsg(Str)
	Dim TempStr
	TempStr=TempStr&"<meta http-equiv=refresh content=3;url=javascript:history.go(-1)>"
	TempStr=TempStr&"<table border=""0"" cellspacing=""1"" cellpadding=""0"" class=""msg"" align=""center"">"
	TempStr=TempStr&"  <tr valign=""top"">"
	TempStr=TempStr&"    <td colspan=""2"" class=""msgt"">错误提示信息</td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"  <tr valign=""top"">"
	TempStr=TempStr&"    <td class=""msgp""><img src=""Images/err.gif""/></td>"
	TempStr=TempStr&"    <td class=""msgm"">"
	TempStr=TempStr&"    <b style='color:blue;'><span id=jump>3</span> 秒钟后系统将自动返回</b><br>"
	TempStr=TempStr&"	 <li>产生错误的可能原因：</li>"&Str&""
	TempStr=TempStr&"    </td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"  <tr>"
	TempStr=TempStr&"    <td colspan=""2"" class=""msgb""><a href='javascript:history.go(-1);'>返回上一页</a></td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"</table>"
	TempStr=TempStr&"<script>function countDown(secs){jump.innerText=secs;if(--secs>0)setTimeout(""countDown(""+secs+"")"",1000);}countDown(3);</script>"
	Response.Write(TempStr)
	Response.End()
End Function
'****************************************************
'函数名：ShowSuccMsg
'作  用：文本成功提示信息
'参  数：Str-字符串
'返回值：文本成功提示信息
'****************************************************
Function ShowSuccMsg(Str,ReUrl)
	Dim TempStr
	If ReUrl<>"" Then
		TempStr=TempStr&"<meta http-equiv=refresh content=3;url="&ReUrl&">"
	Else
		TempStr=TempStr&"<meta http-equiv=refresh content=3;url="&Request.ServerVariables("HTTP_REFERER")&">"
	End If
	TempStr=TempStr&"<table width=""400"" border=""1"" align=""center"" bordercolor=""#b1c7e0"" bordercolordark=""#FFFFFF"" cellspacing=""0"" cellpadding=""0"" class=""tab01"">"
	TempStr=TempStr&"  <tr>"
	TempStr=TempStr&"    <td class=""t"" align=""left"" height=""28"" colspan=""2"" width=""100%"">&nbsp;成功提示信息</td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"  <tr>"
	TempStr=TempStr&"    <td align=""center"" height=""80"" valign=""middle""><img src=""Images/succ.gif""/></td>"
	TempStr=TempStr&"    <td align=""center"" valign=""middle"">"
	TempStr=TempStr&"    <b style='color:blue;'><span id=jump>3</span> 秒钟后系统将自动返回</b><br>"
	TempStr=TempStr&"	 "&Str&""
	TempStr=TempStr&"    </td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"  <tr>"
	TempStr=TempStr&"    <td colspan=""2"" height=""28"" align=""right""><a href="&Request.ServerVariables("HTTP_REFERER")&">返回上一页</a></td>"
	TempStr=TempStr&"  </tr>"
	TempStr=TempStr&"</table>"
	TempStr=TempStr&"<script>function countDown(secs){jump.innerText=secs;if(--secs>0)setTimeout(""countDown(""+secs+"")"",1000);}countDown(3);</script>"
	Response.Write(TempStr)
	Response.End()
End Function

%>
 

       


<div id="sidebar" class="right">
	  <div class="column">
	    <span>ASP友链系统（开源版）欢迎您！-广告管理！</span>
	  </div>
	  <div class="content">
		
               <%
Call OpenDB()
Dim Action,BannerID
Action=Request("Action")
BannerID=Request("BannerID")
Select Case Action
	Case "BannerEdit"
		Call BannerEdit()
	Case "BannerSave"
		Call BannerSave()
	Case "BannerDel"
		Call BannerDel()
	Case Else
		Call BannerList()
End Select
%>
         <%
Function BannerList()
%>
<div class="ts"><b>已有广告列表：</b></div>
<table width="100%" border="1" bordercolor="#b1c7e0" bordercolordark="#FFFFFF" cellspacing="0" cellpadding="0" class="tab01">
  <tr>
    <td class="t" align="center" height="28" width="10%">编号</td>
    <td class="t" align="center">说明</td>
    <td class="t" align="center">JS调用地址（点击文本框复制）</td>
    <td class="t" align="center" width="25%">管理操作</td>
  </tr>
  <%
	Dim Tag,Table,TagStr,Order,PageSize,PageUrl
	Tag="BannerID"										
	Table="[Dm_Banner]"								
	TagStr="*"	
	Order="BannerID"									
	PageSize=10									
	PageUrl="ad.asp"								
	LinkNum=10										
	Dim TotalCount,PageCount,Page
	Dim Rs,Sql,WhereStr
	TotalCount=GetTotalCount(Tag,Table,WhereStr)
	PageCount=GetPageCount(TotalCount,PageSize)
	Page=GetPageNow(PageCount)
	Sql=GetPageSql(Tag,Table,TagStr,Order,PageSize,WhereStr,Page)
	Set Rs=Server.CreateObject("Adodb.RecordSet")
	Rs.Open Sql,Conn,1,1
	If Rs.Bof And Rs.Eof Then
		Rw("<tr>")
    	Rw("<td colspan='4'>暂无数据！</td>")
  		Rw("</tr>")
	Else
		Do While Not Rs.Eof
%>
  <tr>
    <td align="center" height="28"><%=Rs("BannerID")%></td>
    <td align="center"><%=Rs("BannerName")%></td>
    <td align="center"><input name="<%=Rs("BannerID")%>" type="text" onclick="oCopy(this)" value="&lt;script type=&quot;text/javascript&quot; src=&quot;/js/banner_<%=Rs("BannerID")%>.js&quot; language=&quot;javascript&quot;&gt;&lt;/script&gt;" size="70" /></td>
    <td align="center"><a href="?Action=BannerEdit&BannerID=<%=Rs("BannerID")%>"><img src="Images/edit.gif" align="absmiddle" border="0" /> 修改</a>&nbsp;<a href="?Action=BannerDel&BannerID=<%=Rs("BannerID")%>"><img src="Images/del.gif" align="absmiddle" border="0" /> 删除</a></td>
  </tr>
  <%
		Rs.MoveNext
		Loop
%>

<%
	End If
	Rs.Close
	Set Rs=Nothing
%>

<tr>
    <td colspan="4" align="center" height="28"><%Call PageShow(TotalCount,PageSize,Page,PageCount,PageUrl,LinkNum)%></td>
  </tr>

</table>

<div class="ts2"><b>添加新广告：</b></div>
<form id="form1" name="form1" method="post" action="?Action=BannerSave" onsubmit="return checkbanner(this);">
<table width="100%" border="1" bordercolor="#b1c7e0" bordercolordark="#FFFFFF" cellspacing="0" cellpadding="0" class="tab01">
  <tr>
    <td class="t" style="padding-right:10px" align="right" height="28" width="120">名称</td>
    <td class="t" style="padding-left:10px" align="left">信息</td>

  </tr>
  <tr>
    <td height="28" style="padding-right:10px" align="right">广告说明:</td>
    <td style="padding-left:10px" align="left"><input name="BannerName" type="text"  id="BannerName" size="30" /></td>    
  </tr>
    <tr>
    <td height="28" style="padding-right:10px" align="right">广告代码:</td>
    <td style="padding-left:10px" align="left"><textarea name="oSource" id="oSource" onpropertychange="change()" cols="100" rows="4" class="textrea"></textarea></td>    
  </tr>
    <tr>
    <td height="28" style="padding-right:10px" align="right">转换后JS:</td>
    <td style="padding-left:10px" align="left"><textarea name="oResult" cols="100" rows="4" id="oResult"></textarea></td>    
  </tr>
    
    <tr>
    <td height="28" style="padding-right:10px" align="right">&nbsp;</td>
    <td style="padding-left:10px" align="left"> <input name="button" type="submit" id="button" value="提交" />
      <input name="button2" type="reset" id="button2" value="重置" /></td>    
  </tr>
  
</table>
</form>

   <%
End Function
%>  


<%
Function BannerSave()
	Dim BannerName,BannerCode
	BannerName=Request("BannerName")
	BannerCode=Request("oResult")
	If BannerName="" Then Call ShowErr("请输入广告说明！")
	If BannerCode="" Then Call ShowErr("请输入广告代码！")
	Dim Rs,Sql
	If BannerID=0 Then
		Set Rs=Server.Createobject("Adodb.Recordset")
		Sql="Select * From Dm_Banner"
		Rs.Open Sql,Conn,1,3
		Rs.AddNew
		Rs("BannerName")=BannerName
		Rs("BannerCode")=BannerCode
		Rs.Update
		ID=Rs("BannerID")
		Rs.Close
		Set Rs=Nothing
	Else
		Set Rs=Server.Createobject("Adodb.Recordset")
		Sql="Select * From Dm_Banner Where BannerID="&BannerID&""
		Rs.Open Sql,Conn,1,3
		Rs("BannerName")=BannerName
		Rs("BannerCode")=BannerCode
		Rs.Update
		ID=Rs("BannerID")
		Rs.Close
		Set Rs=Nothing
	End If
	Dim Fso
	Set Fso=New DmFso
		Call Fso.CreateFile("../js/banner_"&ID&".js",BannerCode,True)
	Set Fso=Nothing
	Call ShowSuccMsg("<li>网站广告保存成功！</li>","ad.asp")
End Function
%>
<%
Function BannerEdit()
	If Action="BannerEdit" And BannerID<>0 Then
		Dim Rs,Sql
		Set Rs=Server.Createobject("Adodb.Recordset")
		Sql="Select * From Dm_Banner Where BannerID="&BannerID&""
		Rs.Open Sql,Conn,1,3
		If Rs.Bof And Rs.Eof Then
			Call ShowErr("该广告不存在！")
		Else
			BannerName=Rs("BannerName")
			BannerCode=Rs("BannerCode")
		End If
		Rs.Close
		Set Rs=Nothing
	End If
%>




<div class="ts2"><b>修改广告：</b></div>
<form id="form1" name="form1" method="post" action="?Action=BannerSave" onsubmit="return checkbanner(this);">
<table width="100%" border="1" bordercolor="#b1c7e0" bordercolordark="#FFFFFF" cellspacing="0" cellpadding="0" class="tab01">
  <tr>
    <td class="t" style="padding-right:10px" align="right" height="28" width="120">名称</td>
    <td class="t" style="padding-left:10px" align="left">信息</td>

  </tr>
  <tr>
    <td height="28" style="padding-right:10px" align="right">广告说明:</td>
    <td style="padding-left:10px" align="left"><input name="BannerName" type="text" id="BannerName" value="<%=BannerName%>" size="30" />
      <input name="BannerID" type="hidden" id="BannerID" value="<%=BannerID%>" /></td>    
  </tr>
    <tr>
    <td height="28" style="padding-right:10px" align="right">广告代码:</td>
    <td style="padding-left:10px" align="left"><textarea name="oSource" id="oSource" onpropertychange="change()" cols="100" rows="4" class="textrea"></textarea></td>    
  </tr>
    <tr>
    <td height="28" style="padding-right:10px" align="right">转换后JS:</td>
    <td style="padding-left:10px" align="left"><textarea name="oResult" cols="100" rows="4" id="oResult"><%=BannerCode%></textarea></td>    
  </tr>
    
    <tr>
    <td height="28" style="padding-right:10px" align="right">&nbsp;</td>
    <td style="padding-left:10px" align="left"> <input name="button" type="submit" id="button" value="提交" />
      <input name="button2" type="reset" id="button2" value="重置" /></td>    
  </tr>
  
</table>
</form>



<%
End Function
%>
<%
Function BannerDel()
	Call DBDel("Dm_Banner","BannerID="&BannerID&"")
	Set Fso=New DmFso
		If Fso.CheckFile("../js/banner_"&BannerID&".js") Then
			Call Fso.DelFile("../js/banner_"&BannerID&".js")
		End If
	Set Fso=Nothing
	Call ShowSuccMsg("<li>网站广告删除成功！</li>","ad.asp")
End Function

%>

   <center>
<div class="ts2">(C) 2012 阿邦工作室&网络延迟工作室 联合出品 版权所有</div>
</center>
	  </div>
	</div>

</body>
</html>