<!--#include file="cominc/conndb.asa"-->
<!--#include file="cominc/function.asp"-->
<!--#include file="cominc/template.asp"-->
<%
cz=checkStr(Request.QueryString("cz"))
'cz="xftype"


If cz="xfarea" Then 
	set rs2=server.CreateObject("adodb.recordset")
		sql2="select * from "&areaTable&" order by area_id desc"
		rs2.open sql2,conn,1,3
		SQLQueryNums=SQLQueryNums+1
		do while not rs2.eof
			rs2("area_order")=rs2("area_id")
			response.write "修复区域ID为"&rs2("area_id")&"的地区成功！<BR>"
			rs2.update 
		rs2.movenext
		Loop
		rs2.close
	set rs2=Nothing
ElseIf cz="xftype" Then
	set rs2=server.CreateObject("adodb.recordset")
		sql2="select * from "&typeTable&" order by type_id desc"
		rs2.open sql2,conn,1,3
		SQLQueryNums=SQLQueryNums+1
		do while not rs2.eof
			rs2("type_order")=rs2("type_id")
			response.write "修复分类：ID"&rs2("type_id")&"成功！<BR>"
			rs2.update 
		rs2.movenext
		Loop
		rs2.close
	set rs2=Nothing
ElseIf cz="upadte" Then
	Call AddColumn(shopTable,"template","VARCHAR(50) NULL")
	Call AddColumn(shopTable,"filename","VARCHAR(50) NULL")
	Call AddColumn(shopTable,"isFoucs","Integer default 0")
	Sql = "update ["&shopTable&"] set isFoucs=0"
	Conn.Execute Sql
	Call AddColumn(userTable,"coin","Integer default 0")
	Sql = "update ["&userTable&"] set coin=0"
	Conn.Execute Sql



ElseIf cz="xfcache" Then 
	Application.Contents.RemoveAll()
	response.write "修复缓存成功！<BR>"

Else
%>
<CENTER><H3>系统修复</H3></CENTER>

<TABLE width=500 align=center border=1>
<TR>
	<TD>1</TD>
	<TD><A HREF="xiufu.asp?cz=xfarea">修复区域排序</A></TD>
</TR>
<TR>
	<TD>2</TD>
	<TD><A HREF="xiufu.asp?cz=xftype">修复分类排序</A></TD>
</TR>
<TR>
	<TD>3</TD>
	<TD><A HREF="xiufu.asp?cz=upadte">升级数据库到1.0标准版</A></TD>
</TR>
<TR>
	<TD>4</TD>
	<TD><A HREF="xiufu.asp?cz=xfcache">修复缓存</A></TD>
</TR>



</TABLE>

<%
End If 
%>

<BR>
<CENTER><A HREF="xiufu.asp">返回修复页面</A></CENTER>
<%
'*******************************************
'这里是数据库操作函数，慎用！！！
'调用方式！
'	Call AddColumn("PPst_News","SmallClassID","Integer default 0")
'	Call AddColumn("PPst_News","Shenhe","Integer default 0")
'	Call AddColumn("PPst_News","SmallPicURL","Text(255)")
'	Call Addtable("PPst_SmallClass","SmallClassID")
'	Call AddColumn("PPst_SmallClass","SmallClassName","Text(50)")
'	Call AddColumn("PPst_SmallClass","SmallClassURL","Text(255)")
'	Call AddColumn("PPst_SmallClass","ForBigClassID","Integer default 0")
'添加字段//删除字段//增加表//
'Conn.Execute("ALTER TABLE reply drop COLUMN jd_user")--这个是删除函数，没有作
'*******************************************
'添加字段通用函数
Sub AddColumn(TableName,ColumnName,ColumnType)
	On Error Resume Next
	Conn.Execute("Alter Table "&TableName&" Add "&ColumnName&" "&ColumnType&"")
	If Err Then
		Response.Write "新建 "&TableName&" 表中字段<font color=blue>错误</font>，请手动将数据库中 <B>"&ColumnName&"</B> 字段建立，<br>属性为 <B>"&ColumnType&"</B>，原因：" & Err.Description & "<BR>"
		Err.Clear
		Response.Flush
	Else
		Response.Write "新建 "&TableName&" 表中字段 "&ColumnName&" 成功 <BR>"
		Response.Flush
	End If
	Response.Write "<hr>"
End Sub
'删除字段通用函数
Sub DelColumn(TableName,ColumnName)
	On Error Resume Next
	Conn.Execute("Alter Table "&TableName&" Drop "&ColumnName&"")
	If Err Then
		Response.Write "删除 "&TableName&" 表中字段<font color=blue>错误</font>，请手动将数据库中 <B>"&ColumnName&"</B> 字段删除，原因：" & Err.Description & "<BR>"
		Err.Clear
		Response.Flush
	Else
		Response.Write "删除 "&TableName&" 表中字段 "&ColumnName&" 成功 <BR>"
		Response.Flush
	End If
	Response.Write "<hr>"
End Sub
'增加表通用函数
Sub AddTable(TableName,ColumnName)

	On Error Resume Next
	Conn.Execute("Create table "&TableName&" ("&ColumnName&" integer identity(1,1) primary key)")
	If Err Then
		Response.Write "建立数据库表 "&TableName&" <font color=blue>错误</font>，请手动将数据库中 表<B>"&ColumnName&"</B> 建立，主键字段为："&ColumnName&"原因：" & Err.Description & "<BR>"
		Err.Clear
		Response.Flush
	Else
		Response.Write "建立数据库表 "&TableName&" 表、自动编号字段 "&ColumnName&" 成功 <BR>"
		Response.Flush
	End If
	Response.Write "<hr>"
End Sub

%>