<!--#include file = "Startup.asp"-->
<!-- #include file="Function.asp" -->

<%

' ======================
' 功能：修改新闻
' 描述：提供一个修改表单，包括新闻标题和新闻内容，新闻内容使用eWebEditor进行编辑；
'       同时接收上传文件，以便删除新闻时，同时删除上传文件；
'       并由编辑区中上传的文件，提供标题新闻的图片选择；
'		功能与新增相同，不同在于只是修改表单具有初始值。
' ======================
call adminer()
Call Header()
Call ComeUrl()
Call Content()



' 本页内容区
Sub Content()
	
	' 传入参数：新闻ID
	Dim sNewsID
	sNewsID = Trim(Request("id"))

	' 新闻ID有效性验证，防止有些人恶意的破坏此演示程序
	If IsNumeric(sNewsID) = False Then
		GoError "请通过页面上的链接进行操作，不要试图破坏此演示系统。"
	End If

	' 从数据库中取初始值
	Dim sTitle,sClassID,sTime, sContent, sPicture, sOriginalFileName, sSaveFileName, sSavePathFileName,sAuthor,sSource
	sSql = "SELECT * FROM NewsData WHERE D_ID=" & sNewsID
	oRs.Open sSql, oConn, 0, 1
	If Not oRs.Eof Then
		sTitle = oRs("D_Title")
		sClassID = oRs("D_ClassID")
		sTime=oRs("D_Time")
		sAuthor=oRs("d_Author")
		sSource=oRs("d_Source")
		sContent = oRs("D_Content")
		sPicture = oRs("D_Picture")
		sOriginalFileName = oRs("D_OriginalFileName")
		sSaveFileName = oRs("D_SaveFileName")
		sSavePathFileName = oRs("D_SavePathFileName")
		nkey1=ors("nkey1")
		nkey2=ors("nkey2")
		jianjie=ors("jianjie")
	Else
		GoError "无效的新闻ID，请点页面上的链接进行操作！"
	End If
	oRs.Close

	' 把带"|"的字符串转为数组，用于初始下拉框表单
	Dim aSavePathFileName
	aSavePathFileName = Split(sSavePathFileName, "|")

	' 根据新闻内容中相关上传文件为数据，初始下拉框表单
	' 函数InitSelect，根据数组值及初始值返回下拉框输出字串，具体请见startup.asp文件中函数的说明部分
	Dim sOptionSavePath
	sOptionSavePath = InitSelect("d_picture", aSavePathFileName, aSavePathFileName, sPicture, "", "不指定首页图片")

	%>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312">
	<Script Language=JavaScript>
	// 当上传图片等文件时，往下拉框中填入图片路径，可根据实际需要更改此函数
	function doChange(objText, objDrop){
		if (!objDrop) return;
		var str = objText.value;
		var arr = str.split("|");
		var nIndex = objDrop.selectedIndex;
		objDrop.length=1;
		for (var i=0; i<arr.length; i++){
			objDrop.options[objDrop.length] = new Option(arr[i], arr[i]);
		}
		objDrop.selectedIndex = nIndex;
	}

	// 表单提交客户端检测
	function doSubmit(){
		if (document.myform.d_title.value==""){
			alert("新闻标题不能为空！");
			return false;
		}
  		if (document.myform.d_ClassID.value=="")
  		{
    		alert("文章所属栏目不能指定为含有子栏目的栏目！");
			return false;
  		}
		// getHTML()为eWebEditor自带的接口函数，功能为取编辑区的内容
		if (eWebEditor1.getHTML()==""){
			alert("新闻内容不能为空！");
			return false;
		}
		//document.myform.submit();
	}
	</Script>
	
	<form action="News_Save.asp?id=<%=sNewsID%>&Action=ModifySaveNews" method="post" name="myform">
	<% '取源文件名 %>
	<input type=hidden name=d_originalfilename value="<%=sOriginalFileName%>">
	<% '取保存的方件名（带路径），如果不要带路径的填充下拉框，可以在下面的表单项加入onchange事件 %>
	<input type=hidden name=d_savefilename value="<%=sSaveFileName%>">
	<% '取保存的文件名，使用带路径的填充下拉框 %>
	<input type=hidden name=d_savepathfilename onchange="doChange(this,document.myform.d_picture)" value="<%=sSavePathFileName%>">

	<table width="100%" align=center cellpadding="3" cellspacing=1 bgcolor="#DEDFDE">
	<tr bgcolor="#F7F7F7">
      <td colspan="2"><div align="center"><strong>新　闻　修　改</strong></div></td>
	  </tr>
	<tr bgcolor="#FFFFFF">
		<td width="100">新闻标题：</td>
		<% ' 函数inHTML：去除Html格式，用于从数据库中取出值填入输入框时，具体请见startup.asp文件中函数的说明 %>
	  <td><input type="text" name="d_title" value="<%=inHTML(sTitle)%>" size="80" class="input_text"></td>
	</tr>
	<tr bgcolor="#FFFFFF">
	  <td width="100">新闻分类：</td>
	  <td><select name="D_ClassID">	
				<%call NewsClass_Option(3,sClassID)%>
	  </select></td>
	  </tr>
	  <tr bgcolor="#FFFFFF">
	  <td>关键词：</td>
	  <td><input name="nkey1" type="text" id="nkey1"  value="<%=nkey1%>"/></td>
	</tr>
	<tr bgcolor="#FFFFFF">
	  <td>页面描述：</td>
	  <td><textarea name="nkey2" cols="50" rows="5" id="nkey2"><%=nkey2%></textarea></td>
	</tr>
	<tr bgcolor="#FFFFFF">
      <td>作　　者：</td>
      <td><input name="d_Author" type="text" class="input_text" id="d_Author" value="<%=sAuthor%>" size="20"></td>
	  </tr>
	<tr bgcolor="#FFFFFF">
      <td>出　　处：</td>
      <td><input name="d_Source" type="text" class="input_text" id="d_Source" value="<%=sSource%>" size="20"></td>
	  </tr>
	<tr bgcolor="#FFFFFF">
      <td width="100" rowspan="2">图片新闻：</td>
      <td><input name="d_picture" type="text" class="input_text" id="d_picture" value="<%=sPicture%>" size="40">
          　 
      <font color="#FF0000">添加首页图片新闻，请您选择相应的图片</font></td>
	  </tr>
	<tr bgcolor="#FFFFFF">
	  <td><iframe style="top:2px" ID="UploadFiles" src="upload_Photo.asp?PhotoUrlID=5" frameborder=0 scrolling=no width="320" height="25"></iframe></td>
	  </tr>
	  	<tr bgcolor="#FFFFFF">
	  <td> 新闻简介：</td>
	  <td><textarea name="jianjie" cols="60" rows="8" id="jianjie"><%=jianjie%></textarea></td>
	</tr>
	<tr bgcolor="#FFFFFF">
	  <td width="100">添加时间： </td>
	  <td><input name="d_Time" type="text" id="d_Time" value="<%=sTime%>" class="input_text"></td>
	  </tr>
	<tr bgcolor="#FFFFFF">
		<td width="100"><p>新闻内容：</p>
	    <p>・换行请按<FONT color=#ff6600>Shift+Enter</FONT><BR>
	      ・另起一段请按<FONT color=#ff6600>Enter</FONT><BR>
      </p></td>
		<td>
			<%
			' ewebeditor.asp文件调用的参数：
			'	id：下面表单项textarea的名称，在此表单中是d_content，注意大小写
			'	style：编辑器的样式名称，可在eWebEditor的后台设置
			'	originalfilename：用于获取源文件名的表单项名，在此表单中是d_originalfilename
			'	savefilename：用于获取保存文件名的表单项名，在此表单中是d_savefilename
			'	savepathfilename：用于获取保存带路径文件名的表单项名，在此表单中是d_savepathfilename

			' 请一定要用Server.HtmlEncode()函数对内容进行转换，否则将有可以出现某些内容出了编辑区的情况
			%>
			<textarea name="d_content" style="display:none"><%=Server.HtmlEncode(sContent)%></textarea>
			<iframe ID="eWebEditor1" src="aa/ewebeditor.asp?id=d_content&style=standardnew&originalfilename=d_originalfilename&savefilename=d_savefilename&savepathfilename=d_savepathfilename" frameborder="0" scrolling="no" width="600" HEIGHT="350"></iframe>	  </td>
	</tr>
	<tr bgcolor="#FFFFFF">
	  <td height="40" colspan="2"><div align="center">
          <input type=submit name=btnSubmit value=" 提　交 " onclick="return doSubmit()" class="input_submit">
	    　　　　　
        <input type=reset name=btnReset value=" 重　填 " class="input_submit">
	  </div></td>
	  </tr>
	</table>
	</form>

	<%
End Sub

%>

 
