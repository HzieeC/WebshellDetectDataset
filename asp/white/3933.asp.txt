<%
Dim Rs,SQL,foundstr
Dim classid,ChildStr
Dim RssBody,RssTitle,RssHomePageUrl
Dim XMLDOM,node,Cnode,Cnode1,msginfo
Dim TextContent,ChannelRootDir,ChannelDirectory

Set XMLDOM = NewAsp.CreateXmlDoc("msxml2.FreeThreadedDOMDocument"& MsxmlVersion)
XMLDOM.appendChild(XMLDOM.createElement("rss"))
XMLDOM.documentElement.attributes.setNamedItem(XMLDOM.createNode(2,"version","")).text="2.0"
Set node = XMLDOM.documentElement.appendChild(XMLDOM.createNode(1,"channel",""))
node.appendChild(XMLDOM.createTextNode(vbCrLf))
RssTitle = "获取信息列表"
classid = Newasp.CheckNumeric(Request("classid"))

If Newasp.BindDomain = "0" Then
	ChannelRootDir = NewAsp.MainDomain & Newasp.InstallDir & Newasp.ChannelDir
	RssHomePageUrl = NewAsp.MainDomain
	ChannelDirectory = Newasp.ChannelDir
Else
	ChannelRootDir = Newasp.DomainName & "/"
	RssHomePageUrl = Newasp.DomainName
	ChannelDirectory = ""
End If

Sub XMLFlashList()
	If classid > 0 Then
		SQL = "SELECT ClassName,ChildStr FROM [NC_Classify] WHERE ChannelID=" & ChannelID & " And classid=" & CLng(classid)
		Set Rs = Newasp.Execute(SQL)
		If Rs.BOF And Rs.EOF Then
			Set Cnode=node.appendChild(XMLDOM.createNode(1,"item",""))
			Cnode.appendChild(XMLDOM.createNode(1,"title","")).text="没有找到分类"
			Cnode.appendChild(XMLDOM.createNode(1,"link","")).text=RssHomePageUrl
			Cnode.appendChild(XMLDOM.createNode(1,"author","")).text=NewAsp.MainSetting(1)
			Cnode.appendChild(XMLDOM.createNode(1,"pubDate","")).text=NewAsp.FormatToDate(Now(),"yyyy-MM-dd hh:mm:ss")
			Set Cnode1=Cnode.appendChild(XMLDOM.createNode(1,"description",""))
			msginfo= "没有找到软件分类！"
			Cnode1.appendChild(XMLDOM.createCDATASection(msginfo))
			Rs.Close: Set Rs = Nothing
			Exit Sub
		Else
			RssTitle = Rs("ClassName")
			ChildStr = Rs("ChildStr")
		End If
		Rs.Close:Set Rs = Nothing
		foundstr = "And A.classid in (" & ChildStr & ") ORDER BY A.addTime DESC ,A.flashid DESC"
	Else
		RssTitle = "全部软件列表"
		foundstr = "ORDER BY A.addTime DESC ,A.flashid DESC"
	End If

	node.appendChild(XMLDOM.createNode(1,"title","")).text=RssTitle & " - " & NewAsp.MainSetting(1)
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"link","")).text=NewAsp.MainDomain
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"language","")).text="zh-cn"
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"description","")).text=NewAsp.MainSetting(1)
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"copyright","")).text=NewAsp.MainDomain
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"generator","")).text="Rss Generator By Newasp.Net"
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	node.appendChild(XMLDOM.createNode(1,"docs","")).text=RssHomePageUrl & Request.ServerVariables("PATH_INFO")
	node.appendChild(XMLDOM.createTextNode(vbCrLf))
	Set Cnode=node.appendChild(XMLDOM.createNode(1,"image",""))
	Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
	Cnode.appendChild(XMLDOM.createNode(1,"title","")).appendChild(XMLDOM.createCDATASection(Replace(Trim(NewAsp.MainSetting(1)), "&nbsp;", "")))
	Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
	Cnode.appendChild(XMLDOM.createNode(1,"url","")).text=NewAsp.MainDomain & "/images/logo.gif"
	Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
	Cnode.appendChild(XMLDOM.createNode(1,"link","")).text=NewAsp.MainDomain
	Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))

	Dim HtmlFileName,HtmlFileUrl
	SQL = " A.flashid,A.classid,A.title,A.Introduce,A.addTime,A.HtmlFileDate,A.Author,A.username,"
	SQL = "SELECT TOP 100 " & SQL & " C.ClassName,C.HtmlFileDir,B.ChannelDir,B.StopChannel,B.ModuleName,B.IsCreateHtml,B.HtmlExtName FROM ([NC_FlashList] A INNER JOIN [NC_Classify] C On A.classid=C.classid) INNER JOIN [NC_Channel] B On A.ChannelID=B.ChannelID WHERE A.isAccept>0 And A.ChannelID=" & CLng(ChannelID) & " " & foundstr & ""
	Set Rs = Newasp.Execute(SQL)
	If Rs.BOF And Rs.EOF Then
		Set Cnode=node.appendChild(XMLDOM.createNode(1,"item",""))
		Cnode.appendChild(XMLDOM.createNode(1,"title","")).text="没有找到你想要的信息"
		Cnode.appendChild(XMLDOM.createNode(1,"link","")).text=RssHomePageUrl
		Cnode.appendChild(XMLDOM.createNode(1,"author","")).text=NewAsp.MainSetting(1)
		Cnode.appendChild(XMLDOM.createNode(1,"pubDate","")).text=NewAsp.FormatToDate(Now(),"yyyy-MM-dd hh:mm:ss")
		Set Cnode1=Cnode.appendChild(XMLDOM.createNode(1,"description",""))
		msginfo= "没有找到你想下载的软件！"
		Cnode1.appendChild(XMLDOM.createCDATASection(msginfo))

		Rs.Close: Set Rs = Nothing
		Exit Sub
	Else
		Do While Not Rs.EOF
			If Rs("IsCreateHtml") <> 0 Then
				HtmlFileUrl = RssHomePageUrl & NewAsp.HtmlDestination(Newasp.InfoDestination, ChannelDirectory, Rs("HtmlFileDate"),Rs("HtmlFileDir"),Rs("classid"),Rs("flashid"),1,"")
			Else
				If IsURLRewrite Then
					HtmlFileUrl = ChannelRootDir & Rs("flashid") & Newasp.HtmlExtName
				Else
					HtmlFileUrl = ChannelRootDir & "show.asp?id=" & Rs("flashid")
				End If
			End If
			TextContent = Rs("Introduce")&""
			node.appendChild(XMLDOM.createTextNode(vbCrLf))
			Set Cnode=node.appendChild(XMLDOM.createNode(1,"item",""))
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Cnode.appendChild(XMLDOM.createNode(1,"title","")).appendChild(XMLDOM.createCDATASection(Replace(Trim(NewAsp.EscapeInvalidUnicode(Rs("title"))), "&nbsp;", "")))
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Cnode.appendChild(XMLDOM.createNode(1,"link","")).text=HtmlFileUrl
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Cnode.appendChild(XMLDOM.createNode(1,"author","")).text=NewAsp.EscapeInvalidUnicode(Rs("Author"))
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Cnode.appendChild(XMLDOM.createNode(1,"category","")).appendChild(XMLDOM.createCDATASection(NewAsp.EscapeInvalidUnicode(Rs("ClassName"))))
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Cnode.appendChild(XMLDOM.createNode(1,"pubDate","")).text=NewAsp.FormatToDate(Rs("addTime"),"yyyy-MM-dd hh:mm:ss")
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Set Cnode1=Cnode.appendChild(XMLDOM.createNode(1,"description",""))
			msginfo= NewAsp.EscapeInvalidUnicode(NewAsp.CutString(TextContent, 300))
			'msginfo=  "要浏览本条信息请点击标题名称。"
			Cnode1.appendChild(XMLDOM.createCDATASection(msginfo))
			Cnode.appendChild(XMLDOM.createTextNode(vbCrLf))
			Rs.MoveNext
		Loop
	End If
	Rs.Close: Set Rs = Nothing
End Sub

Sub ShowXML()
	Response.Clear
	Response.CharSet="gb2312"
	Response.ContentType="text/xml"
	Response.Write "<?xml version=""1.0"" encoding=""gb2312""?>"&vbNewLine
	Response.Write XMLDOM.xml
	Set XMLDOM=Nothing
End Sub

Sub main()
	Call XMLFlashList()
	Call ShowXML()
End Sub
%>