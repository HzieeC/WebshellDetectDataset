<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


'打开用户表，并检测用户是否登陆
Call OpenMemberexe("","login",0)

Call WebStyleTop("")

response.write(_
"<script language='javascript' type='text/javascript' src='js/inc/list.js'></script>"&_
"<script language='javascript' type='text/javascript' src='js/vote.js'></script>"&_
"")





Select Case mudi
	Case "add"
		Call IsSecMenuRight("alertBack",123,dataType)
		Call AddOrRev()

	Case "rev"
		Call IsSecMenuRight("alertBack",126,dataType)
		Call AddOrRev()

	Case "refer","manage"
		Call IsSecMenuRight("alertBack",124,dataType)
		Call refer()
	
	Case "show"
		Call show()

End Select


Call CloseMemberexe()
Call CloseConobj()
Call WebStyleBottom()









'新增、修改信息
Private Sub AddOrRev()
	backURL		= ToGetStr("backURL")
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")

	If mudi="rev" Then
		dataID=ToGetInt("dataID")
		Set revexe=conobj.execute("select * from OT_vote where VT_ID="& dataID)
			If revexe.EOF Then
				Call JS_AlertBackEnd("无该记录！")
			End If
			VT_ID		= revexe("VT_ID")
			VT_time		= revexe("VT_time")
			VT_mode		= revexe("VT_mode")
			VT_theme	= revexe("VT_theme")
			VT_content	= revexe("VT_content")
			VT_startTime= revexe("VT_startTime")
			VT_endTime	= revexe("VT_endTime")
			VT_rank		= revexe("VT_rank")
			VT_state	= revexe("VT_state")
		revexe.close
		Set revexe=Nothing

		mudiCN="修改"
	Else
		VT_time		= Now()
		VT_mode		= 1
		VT_content	= "投票选项一|0"& Chr(10) &"投票选项二|0"
		VT_startTime= Now()
		VT_rank		= ToInt(conobj.execute("select max(VT_rank) from OT_vote")(0))+10
		VT_state	= 1

		mudiCN="添加"
	End If



	response.write(_
	"<form name='addform' method='post' action='vote_deal.asp?mudi="& mudi &"&nohrefStr=close' onsubmit='return CheckForm()'>"&_
	"<input type='hidden' id='dataID' name='dataID' value='"& VT_ID &"' />"&_
	"<input type='hidden' id='dataTypeCN' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
	"<input type='hidden' id='dataType' name='dataType' value='"& dataType &"' />"&_
	"<input type='hidden' id='dataMode' name='dataMode' value='"& dataMode &"' />"&_
	"<input type='hidden' id='dataModeStr' name='dataModeStr' value='"& dataModeStr &"' />"&_
	"")
		If backURL<>"" Then
			response.write(_
			"<input type='hidden' id='backURL' name='backURL' value='"& backURL &"' />")
		Else
			response.write(_
			"<script language='javascript' type='text/javascript'>document.write(""<input type='hidden' id='backURL' name='backURL' value='""+ document.location.href +""' />"")</script>")
		End If

	Call ShareTableTop("share_"& mudi &".gif","",mudiCN & dataTypeCN)
		response.write(_
		"<table width='98%' align='center' cellpadding='0' cellspacing='0' summary='' class='padd3'>"&_
		"<tr>"&_
			"<td align='right'>类型：</td>"&_
			"<td align='left'>"&_
				"<label><input type='radio' name='mode' value='1' "& IsChecked(VT_mode,1) &" />单选</label>&nbsp;&nbsp;"&_
				"<label><input type='radio' name='mode' value='2' "& IsChecked(VT_mode,2) &" />多选</label>&nbsp;&nbsp;"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>标题：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='theme' name='theme' size='50' style='width:400px;' value="""& VT_theme &""" />&nbsp;"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right' valign='top'>项目内容：</td>"&_
			"<td align='left'>"&_
				"<textarea type='text' id='content' name='content' size='50' style='width:400px;height:200px;' >"& VT_content &"</textarea>"&_
				"<span class='font2_2'>&nbsp;格式：项目标题|票数，一行一个项目</span>"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>投票时间：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='startTime' name='startTime' size='20' style='width:160px;' value='"& VT_startTime &"' onfocus=""WdatePicker({dateFmt:'yyyy-MM-dd HH:mm:ss'})"" class='Wdate' />"&_
				"&nbsp;至&nbsp;"&_
				"<input type='text' id='endTime' name='endTime' size='20' style='width:160px;' value='"& VT_endTime &"' onfocus=""WdatePicker({dateFmt:'yyyy-MM-dd HH:mm:ss'})"" class='Wdate' />"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>排序：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='rank' name='rank' size='50' style='width:30px;' value="""& VT_rank &""" />"&_
				"<span class='font2_2'>&nbsp;值越高排越前</span>"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>状态：</td>"&_
			"<td align='left'>"&_
				"<label><input type='radio' name='state' value='1' "& IsChecked(VT_state,1) &" />显示</label>&nbsp;&nbsp;&nbsp;&nbsp;"&_
				"<label><input type='radio' name='state' value='0' "& IsChecked(VT_state,0) &" />隐藏</label>&nbsp;&nbsp;&nbsp;&nbsp;"&_
			"</td>"&_
		"</tr>"&_
		"</table>"&_
		"")
	Call ShareTableBottom()

	response.write(_
	"<table style='height:16px;' cellpadding='0' cellspacing='0' summary=''><tr><td></td></tr></table>"&_

	"<center><input type='image' src='images/button_"& mudi &".gif' /></center>"&_

	"</form>"&_
	"")

	If mudi="rev" Then
		response.write("<br /><center><a href='javascript:void(0);' onclick='history.back();' class='font2_1'>【返回】</a></center>")
	End If

End Sub









'查询信息
Private Sub refer()
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")

	refMode		= ToGetInt("refMode")
	refTheme	= ToGetRegExpStr("refTheme","sql")
	refContent	= ToGetRegExpStr("refContent","sql")
	refDate1	= ToGetStr("refDate1")
		If IsDate(refDate1)=False Then refDate1=""
	refDate2	= ToGetStr("refDate2")
		If IsDate(refDate2)=False Then refDate2=""

	SQLstr="select * from OT_vote where VT_type='"& dataType &"'"

	If refMode>0 Then SQLstr=SQLstr &" and VT_mode="& refMode
	If refTheme<>"" Then SQLstr=SQLstr &" and VT_theme like '%"& refTheme &"%'"
	If refContent<>"" Then SQLstr=SQLstr &" and VT_content like '%"& refContent &"%'"
'	If refDate1<>"" Then SQLstr=SQLstr &" and VT_time>="& SqlForTime(refDate1)
'	If refDate2<>"" Then SQLstr=SQLstr &" and VT_time<="& SqlForTime(DateAdd("d",1,refDate2))
	If refDate1<>"" Then SQLstr=SQLstr &" and VT_startTime>="& SqlForTime(refDate1)
	If refDate2<>"" Then SQLstr=SQLstr &" and VT_endTime<="& SqlForTime(DateAdd("d",1,refDate2))


	orderName=ToGetStr("orderName")
		If orderName<>"theme" And orderName<>"mode" And orderName<>"startTime" And orderName<>"endTime" And orderName<>"state" Then orderName="rank"
	orderSort=ToGetStr("orderSort")
		If orderSort<>"ASC" Then orderSort="DESC"


	Call ShareTableTop("share_refer.gif","",dataTypeCN &"查询")
	response.write(_
		"<form name='refForm' method='get' action=''>"&_
		"<input type='hidden' name='mudi' value='"& mudi &"' />"&_
		"<input type='hidden' name='dataType' value='"& dataType &"' />"&_
		"<input type='hidden' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
		"<input type='hidden' name='dataMode' value='"& dataMode &"' />"&_
		"<input type='hidden' name='dataModeStr' value='"& dataModeStr &"' />"&_

		"<table style='width:100%; height:100%' border='0' cellSpacing='0' cellPadding='0' summary=''>"&_
		"<tr>"&_
			"<td style='width:20%; padding:5px' class='font1_2'>"&_
				"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;类型："&_
				"<select id='refMode' name='refMode'>"&_
					"<option value=''>&nbsp;</option>"&_
					"<option value='1' "& IsSelected(refMode,1) &">单选</option>"&_
					"<option value='2' "& IsSelected(refMode,2) &">多选</option>"&_
				"</select>"&_
			"</td>"&_
			"<td style='width:35%; padding:5px' class='font1_2'>"&_
				"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;标题：<input type='text' name='refTheme' size='20' value='"& refTheme &"' />"&_
			"</td>"&_
			"<td style='width:45%; padding:5px' class='font1_2'>"&_
				"投票时间：<input type='text' name='refDate1' size='10' value='"& refDate1 &"' onfocus=""WdatePicker()"" />&nbsp;"&_
				"至&nbsp;<input type='text' name='refDate2' size='10' value='"& refDate2 &"' onfocus=""WdatePicker()"" />"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='center' style='padding:5px;padding-top:20px' colspan='3'>"&_
				"<input type='image' src='images/button_refer.gif' />"&_
				"&nbsp;&nbsp;&nbsp;&nbsp;"&_
				"<img src='images/button_reset.gif' onclick=""document.location.href='?mudi="& mudi &"&amp;dataType="& dataType &"&amp;dataTypeCN="& server.URLEncode(dataTypeCN) &"'"" style='cursor:pointer' alt='' />"&_
			"</td>"&_
		"</tr>"&_
		"</table>"&_
		"</form>")

	Call ShareTableBottom()


	response.write(_
	"<br />")

	response.write(_
	"<form name='listForm' method='post' action='vote_deal.asp?mudi=moreDel' onsubmit='return CheckListForm()'>"&_
	"<script language='javascript' type='text/javascript'>document.write (""<input type='hidden' name='URL' value='""+ document.location.href +""' />"")</script>"&_
	"<input type='hidden' name='dataType' value='"& dataType &"' />"&_
	"<input type='hidden' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
	"<input type='hidden' name='dataMode' value='"& dataMode &"' />"&_
	"<input type='hidden' name='dataModeStr' value='"& dataModeStr &"' />"&_
	"")

	Call ShareTableTop2("share_list.gif","",dataTypeCN &"列表")
	Call ShareTableItemTitle("5%,5%,6%,36%,10%,10%,6%,6%,14%","<input type='checkbox' name='selAll' onclick='CheckBoxAll()' />,序号,"& ShowArrow("类型","itemType",orderName,orderSort) &","& ShowArrow("标题","theme",orderName,orderSort) &","& ShowArrow("开始时间","startTime",orderName,orderSort) &","& ShowArrow("结束时间","endTime",orderName,orderSort) &","& ShowArrow("排序","rank",orderName,orderSort) &","& ShowArrow("状态","state",orderName,orderSort) &",预览　修改　删除")


	Set showrec=server.CreateObject("ADODB.recordset")
	showrec.open SQLstr &" order by VT_"& orderName &" "& orderSort &"",conobj,3,1
		If showrec.EOF Then
			response.write("</table><br /><center class='font1_1'>暂无内容</center>")
			response.End
		End If
			showPageSize=memberexe("MB_itemNum")		'每页条数
			showrec.pagesize=showPageSize	
			showPageCount=showrec.pagecount
			showCount=showrec.recordcount
			page=ToGetInt("page")
			if page < 1 Or page > showPageCount Then page=1
			showrec.absolutepage=page


		response.write(_
		"<tbody class='tabBody padd3'>"&_
		"")
		number=1+(page-1)*showPageSize
		For i=1 To showPageSize
			If showrec.EOF Then Exit For
			If i Mod 2 = 0 Then bgcolor="class='tabColorTr'" Else bgcolor=""

		response.write(_
		"<tr "& bgcolor &" id='closeData"& showrec("VT_ID") &"'>"&_
			"<td align='center'><input type='checkbox' name='selDataID' value='"& showrec("VT_ID") &"' /></td>"&_
			"<td align='center'>"& number &"</td>"&_
			"<td align='center'>"& ModeCN(showrec("VT_mode")) &"<br /></td>"&_
			"<td align='left'>"& showrec("VT_theme") &"<br /></td>"&_
			"<td align='center'>"& showrec("VT_startTime") &"<br /></td>"&_
			"<td align='center'>"& showrec("VT_endTime") &"<br /></td>"&_
			"<td align='center'>"& showrec("VT_rank") &"<br /></td>"&_
			"<td align='center'>"& ItemSwitch("OT_vote",showrec("VT_ID"),showrec("VT_state"),"state","") &"<br /></td>"&_
			"<td align='center'>"&_
				"<img src='images/img_det.gif' style='cursor:pointer' onclick=""var a=window.open('?mudi=show&nohrefStr=close&dataMode="& dataMode &"&dataModeStr="& dataModeStr &"&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&dataID="& showrec("VT_ID") &"')"" alt='' />&nbsp;&nbsp;&nbsp;"&_
				"<img src='images/img_rev.gif' style='cursor:pointer' onclick=""document.location.href='?mudi=rev&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&dataMode="& dataMode &"&dataModeStr="& dataModeStr &"&dataID="& showrec("VT_ID") &"&backURL='+ escape(document.location.href)"" alt='' />&nbsp;&nbsp;&nbsp;"&_
				"<img src='images/img_del.gif' style='cursor:pointer' onclick=""if(confirm('你确定要删除？')==true){DataDeal.location.href='vote_deal.asp?mudi=del&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&theme="& server.URLEncode(showrec("VT_theme")) &"&dataID="& showrec("VT_ID") &"'}"" alt='' />"&_
			"</td>"&_
		"</tr>"&_
		"")
		showrec.movenext
		number=number+1
		Next
		response.write(_
		"</tbody>"&_
		"")
		response.write(_
		"<tr class='tabColorB padd5'>"&_
			"<td align='left' colspan='20'>"&_
				"<input type='button' value='全选' onclick='AllSelBox()' class='form_button1' />"&_
				"<input type='button' value='反选' onclick='RevSelBox()' class='form_button1' />"&_
				"&nbsp;"&_
				"<input type='submit' value='批量删除' />"&_
			"</td>"&_
		"</tr>"&_
		"")
	showrec.close
	Set showrec=Nothing
	response.write(_
	"</form>")

	Call ShareTableBottom2()
End Sub






Private Sub show()
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")
	dataID		= ToGetInt("dataID")

	Set showexe=conobj.execute("select * from OT_vote where VT_type='"& dataType &"' and VT_ID="& dataID)
		If showexe.EOF Then
			Call JS_AlertCloseEnd("指定ID错误")
		End If

		response.write(""&_
		"<script language='javascript' type='text/javascript'>document.title="""& dataTypeCN &"详细信息"";</script>"& Chr(13) &_
		"<table width='700' align='center' cellpadding='0' cellspacing='0' border='0' summary=''><tr><td>"&_
		"")

		Call ShareTableTop("share_list.gif","",""& dataTypeCN &"详细信息")
		response.write(_
		"<table style='width:100%' align='center' cellpadding='0' cellspacing='0' border='0' class='padd3'>"&_
		"<tr>"&_
			"<td style='width:100px;' align='right'>添加时间：</td>"&_
			"<td class='font1_2' style='padding:3px;'>"& showexe("VT_time") &"</td>"&_
		"</tr>"&_
		"<tr><td align='right' valign='top'>类型：</td><td>"& ModeCN(showexe("VT_mode")) &"</td></tr>"&_
		"<tr><td align='right' valign='top'>标题：</td><td>"& showexe("VT_theme") &"</td></tr>"&_
		"")
			voteRowArr=Split(showexe("VT_content"),Chr(13)&Chr(10))
			For n=0 To UBound(voteRowArr)
				rowArr = Split(voteRowArr(n),"|")
				rowArr(1)=ToInt(rowArr(1))
				voteRowArr(n)=Array(rowArr(0),rowArr(1))
				scoreCount=scoreCount + rowArr(1)
			Next
			If scoreCount=0 Then scoreCount=1

			For i=0 To UBound(voteRowArr)
		response.write(_
		"<tr>"&_
			"<td align='right'>项目"& (i+1) &"：</td>"&_
			"<td style='padding-top:8px;'>"&_
				voteRowArr(i)(0) &"<br />"&_
				"<table cellpadding='0' cellspacing='0'><tr>"&_
				"<td>"&_
					"<table cellpadding='0' cellspacing='0'><tr>"&_
					"<td style='width:2px;'><img src='images/vote_left.gif'></td>"&_
					"<td style='width:"& FormatNumber(400 * voteRowArr(i)(1) / scoreCount,0,true) &"px; background:url(images/vote_bg.gif);'></td>"&_
					"<td style='width:4px;'><img src='images/vote_right.gif'></td>"&_
					"</tr></table>"&_
				"</td>"&_
				"<td>"&_
					"<span class='font1_2' style='padding-left:5px;'>"& FormatNumber(voteRowArr(i)(1) / scoreCount * 100,2,true) &"%</span>"&_
					"<span class='font2_2' style='padding-left:5px;'>(票数："& voteRowArr(i)(1) &")</span>"&_
				"</td>"&_
				"</tr></table>"&_
			"</td>"&_
		"</tr>")
			Next
		response.write(_
		"</table>")
		Call ShareTableBottom()
		response.write(""&_
		"</td></tr></table>"&_
		"")

End Sub






'类型中文化
Function ModeCN(num)
	If num=1 Then
		ModeCN="单选"
	ElseIf num=2 Then
		ModeCN="多选"
	End If
End Function
%>