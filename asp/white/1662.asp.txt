
<%
'*******************************************************************************************
' 创 建 人：Spring (Spring@ibw.com.cn)
' 更新时间：2007-6-13 17:48
' 说 明：生成静态页面类及调用函数
'*******************************************************************************************
class HTMLMaker

 public saveUrl, folderName, fileName, fileUrl
 public fso, stream, XMLHttp
 public thisError
 private i, j

 ' 构造函数
 private sub class_initialize()
  folderName = ""
  fileName = "save_html.html"
  set fso = server.createObject("scripting.filesystemobject")
  set stream = server.createObject("adodb.stream")
  dim XMLDocArr(3), XMLDocStr
  XMLDocArr(0) = "msXML2.serverXMLHttp.6.0"
  XMLDocArr(1) = "msXML2.serverXMLHttp.5.0"
  XMLDocArr(2) = "msXML2.serverXMLHttp.4.0"
  XMLDocArr(3) = "msXML2.serverXMLHttp.3.0"
  for i = 0 to ubound(XMLDocArr)
   XMLDocStr = XMLDocArr(i)
   if isObj(XMLDocStr) then exit for
  next
  erase XMLDocArr
  if objErr(XMLDocStr) then
   response.clear
   rwrite thisError
   response.end
  end if
  set XMLHttp = server.createObject (XMLDocStr)
 end sub

 '析构函数
 private sub class_terminate()
  if isObject(fso) then set fso = nothing
  if isObject(stream) then set stream = nothing
  if isObject(XMLHttp) then set XMLHttp = nothing
 end sub

 private function sco(byval objStr)
  set sco = server.createObject (objStr)
 end function

 private function isObj(byval objStr)
  dim testObj
  on error resume next
  set testObj = server.createObject (objStr)
  if err <> -2147221005 then
   isObj = true
  else
   isObj = false
  end if
  set testObj = nothing
  err.clear
 end function

 private function objErr(byval objStr)
  objErr = false
  if not isObj(objStr) then
   thisError = objStr & "组件注册错误！"
   objErr = true
  end if
 end function

 ' Html文件生成
 public sub save()
  dim filePath
  dim binFileData
  filePath = server.mappath(fileName)
  if not fso.folderexists(server.mappath(saveUrl & "/" & folderName)) and folderName <> "" then
'   fso.createfolder server.mappath(folderName)
  end if
  XMLHttp.open "GET",fileUrl,false
  XMLHttp.send()
  binFileData = XMLHttp.responseBody
  Stream.Type = 1
  Stream.Open()
  Stream.Write(binFileData)
  Stream.SaveToFile filePath, 2
  Stream.Close()
 end sub
End class
'*******************************************************************************************

'静态页面生成
'@param: saveUrl, 保存路径
'@param: folderName, 保存文件夹
'@param: fileName, 保存文件名
'@param: fileUrl, 请求生成页面
Function makeHTML(saveUrl, folderName, fileName, fileUrl)
 dim objSave : set objSave = new HTMLMaker

 objSave.saveUrl  = saveUrl
 objSave.folderName = folderName
 objSave.fileName = fileName
 objSave.fileUrl  = fileUrl
 objSave.save
 set objSave = nothing

 'makeHTML = "生成成功"
End Function

    
%>



