<%

'====错误转向
sub ShowError(Errid,ErrString)
	response.redirect "/Error.asp?errid=" & errid & "&errmsg=" & ErrString
	response.end
end sub

'====SQL 防注入
Function GetSqlInt(ParaName)
  Dim ParaValue 
  ParaValue=Request(ParaName)
  If len(ParaValue)=0 then
	ParaValue=0
  Else
	If not isNumeric(ParaValue) then 
		call ShowError(1001,"参数" & ParaName & "必须为数字型！")
	else
		ParaValue=clng(ParaValue)
	End if 
  End if
  GetSqlInt=ParaValue
End function

Function GetSqlString(ParaName)
  Dim ParaValue 
  ParaValue=Request(ParaName)
  If len(ParaValue)=0 then
	ParaValue=""
  Else
	ParaValue=replace(ParaValue,"'","''") 
  End if
  GetSqlString=ParaValue

End function

Function GetString(ParaName)

  Dim ParaValue 
  ParaValue=Request(ParaName)
  GetString=ParaValue

End function

Function GetExtName(filename)

	GetExtName=lcase(right(filename,len(filename)-inStrRev(filename,".")))
	
end Function
  '===============
  sub showmsgbox(title,msg,icon)
  dim imgicon
	select case icon 
		case 0	
			imgicon=""
		case 1	
			imgicon="<img src='/images/msg_information.gif'>"
		case 2	
			imgicon="<img src='/images/msg_exclamation.gif'>"
		case 3	
			imgicon="<img src='/images/msg_question.gif'>"
		case 4	
			imgicon="<img src='/images/msg_critical.gif'>"
		case else	
			imgicon=""
	end select

 %>
<table width="350" border="0"  cellpadding="0" cellspacing="0" align="center" >
  <tr>
    <td width="300" height="28"  background="images/window_top.gif">&nbsp;&nbsp;<font style="font-size:10.5pt" color="#FFFFFF"><strong><%= title %></strong></font></td>
  </tr>
  <tr>
    <td height="130" valign="middle" bgcolor=#f1fbcd>
      <table width="95%"  border="0" align="center">
        <tr>
          <td width="60" height="60" align="center" valign="top"><%=imgicon %></td>
          <td width="186" align="left" valign="top"><font style="font-size:9pt"><%= msg %></font></td>
        </tr>
        <tr align="center">
          <td height="30" colspan="2">
          <input type="button" name="Submit2" value=" 确定 " onClick="javascript:window.close()"></td>
        </tr>
      </table></td>
  </tr>
  <tr>
    <td width="300" height="26"  background="images/window_bottom.gif">&nbsp;</td>
  </tr>
</table>
<% end sub %>
<% 
function FixNumber(inum,ilng)
	dim stemp
	stemp=cstr(inum)
	if len(stemp)<cint(ilng) then
		stemp=replace(space(cint(ilng)-len(stemp))," ","0") & stemp
	end if
	FixNumber=stemp
end function
%>

