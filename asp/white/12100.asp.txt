<%
If OT_ISOTCMS<>True Then
	response.write("非法访问")
	response.End()
End If

'******************* 调用常用JS语句 **********************
Function JS_Href(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"document.location.href="""& str &""";"&_
	"</script>")
End Function 

Function JS_HrefEnd(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"document.location.href="""& str &""";"&_
	"</script>")
	response.End()
End Function 

Function JS_Write(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"document.write("""& str &""");"&_
	"</script>")
End Function 

Function JS_WriteEnd(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"document.write("""& str &""");"&_
	"</script>")
	response.End()
End Function 

Function JS_Alert(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");"&_
	"</script>")
End Function 

Function JS_AlertEnd(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");"&_
	"</script>")
	response.End()
End Function 

Function JS_AlertClose(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");window.close();"&_
	"</script>")
End Function 

Function JS_AlertCloseEnd(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");window.close();"&_
	"</script>")
	response.End()
End Function 

Function JS_AlertBackEnd(ByVal str)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");history.back();"&_
	"</script>")
	response.End()
End Function 

Function JS_AlertHref(ByVal str,ByVal url)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");document.location.href='"& url &"';"&_
	"</script>")
End Function

Function JS_AlertHrefEnd(ByVal str,ByVal url)
	response.write(_
	"<script language='javascript'>"&_
	"alert("""& str &""");document.location.href='"& url &"';"&_
	"</script>")
	response.End()
End Function


Function JS_ModeDeal(ByVal mode,ByVal jud,ByVal value,ByVal href)
	If mode="jud" Then
		JS_ModeDeal = jud
		Exit Function

	ElseIf mode="show" Then
		response.write(value)
		response.End()

	ElseIf mode="get" Then
		JS_ModeDeal = value
		Exit Function

	ElseIf mode="alert" Then
		Call JS_AlertEnd(value)

	ElseIf mode="alertBack" Then
		Call JS_AlertBackEnd(value)

	ElseIf mode="alertHref" Then
		Call JS_AlertHrefEnd(value,href)

	ElseIf mode="alertStr" Then
		response.write("alert("""& value &""");")
		response.End()

	ElseIf mode="alertBackStr" Then
		response.write("alert("""& value &""");history.back();")
		response.End()

	ElseIf mode="alertHrefStr" Then
		response.write("alert("""& value &""");document.location.href='"& href &"';")
		response.End()

	ElseIf mode="alertPopupStr" Then
		response.write("alert("""& value &""");OpenPopup('login');")
		response.End()

	End If
End Function


'******************* 获取参数GET/POST **********************
Function ToGet(ByVal str)
	ToGet=request.querystring(str)
End Function 

Function ToGetFloat(ByVal str)
	ToGetFloat=ToFloat(request.querystring(str))
End Function 

Function ToGetInt(ByVal str)
	ToGetInt=ToInt(request.querystring(str))
End Function 

Function ToGetInt2(ByVal str,ByVal def)
	ToGetInt2=ToInt2(request.querystring(str),def)
End Function 

Function ToGetStr(ByVal str)
	ToGetStr=Trim(request.querystring(str))
End Function 

Function ToGetRegExpStr(ByVal str,ByVal repType)
	ToGetRegExpStr=RegExpStr(request.querystring(str),repType)
End Function 

Function ToPost(ByVal str)
	ToPost=request.Form(str)
End Function 

Function ToPostFloat(ByVal str)
	ToPostFloat=ToFloat(request.Form(str))
End Function 

Function ToPostInt(ByVal str)
	ToPostInt=ToInt(request.Form(str))
End Function 

Function ToPostInt2(ByVal str,ByVal def)
	ToPostInt2=ToInt2(request.Form(str),def)
End Function 

Function ToPostStr(ByVal str)
	ToPostStr=Trim(request.Form(str))
End Function 

Function ToPostRStr(ByVal str)
	ToPostRStr=RTrim(request.Form(str))
End Function 

Function ToPostReplaceStr(ByVal str,ByVal repType)
	ToPostReplaceStr=ReplaceStr(request.Form(str),repType)
End Function 

Function ToPostRegExpStr(ByVal str,ByVal repType)
	ToPostRegExpStr=RegExpStr(request.Form(str),repType)
End Function 

Function ToRequestFloat(ByVal str)
	ToRequestFloat=request.querystring(str)
	If ToRequestFloat="" Then ToRequestFloat=request.Form(str)
	ToRequestFloat=ToFloat(ToRequestFloat)
End Function 

Function ToRequestInt(ByVal str)
	ToRequestInt=request.querystring(str)
	If ToRequestInt="" Then ToRequestInt=request.Form(str)
	ToRequestInt=ToInt(ToRequestInt)
End Function 

Function ToRequestStr(ByVal str)
	ToRequestStr=Trim(request.querystring(str))
	If ToRequestStr="" Then ToRequestStr=Trim(request.Form(str))
End Function 

'******************* AJAX获取参数GET/POST **********************
Function AjaxGet(ByVal str)
	If OT_Charset="utf-8" Then
		AjaxGet=request.querystring(str)
	Else
		AjaxGet=unescape(request.querystring(str))
	End If
End Function 

Function AjaxGetInt(ByVal str)
	AjaxGetInt=ToInt(request.querystring(str))
End Function 

Function AjaxGetInt2(ByVal str,ByVal def)
	AjaxGetInt2=ToInt2(request.querystring(str),def)
End Function 

Function AjaxGetStr(ByVal str)
	If OT_Charset="utf-8" Then
		AjaxGetStr=Trim(request.querystring(str))
	Else
		AjaxGetStr=Trim(unescape(request.querystring(str)))
	End If
End Function 

Function AjaxGetRegExpStr(ByVal str,ByVal repType)
	If OT_Charset="utf-8" Then
		AjaxGetRegExpStr=RegExpStr(request.querystring(str),repType)
	Else
		AjaxGetRegExpStr=RegExpStr(unescape(request.querystring(str)),repType)
	End If
End Function 

Function AjaxPost(ByVal str)
	If OT_Charset="utf-8" Then
		AjaxPost=request.Form(str)
	Else
		AjaxPost=unescape(request.Form(str))
	End If
End Function 

Function AjaxPostInt(ByVal str)
	AjaxPostInt=ToInt(request.Form(str))
End Function 

Function AjaxPostInt2(ByVal str,ByVal def)
	AjaxPostInt2=ToInt2(request.Form(str),def)
End Function 

Function AjaxPostStr(ByVal str)
	If OT_Charset="utf-8" Then
		AjaxPostStr=Trim(request.Form(str))
	Else
		AjaxPostStr=Trim(unescape(request.Form(str)))
	End If
End Function 

Function AjaxPostRStr(ByVal str)
	If OT_Charset="utf-8" Then
		AjaxPostRStr=RTrim(request.Form(str))
	Else
		AjaxPostRStr=RTrim(unescape(request.Form(str)))
	End If
End Function 

Function AjaxPostReplaceStr(ByVal str,ByVal repType)
	If OT_Charset="utf-8" Then
		AjaxPostReplaceStr=ReplaceStr(request.Form(str),repType)
	Else
		AjaxPostReplaceStr=ReplaceStr(unescape(request.Form(str)),repType)
	End If
End Function 

Function AjaxPostRegExpStr(ByVal str,ByVal repType)
	If OT_Charset="utf-8" Then
		AjaxPostRegExpStr=RegExpStr(request.Form(str),repType)
	Else
		AjaxPostRegExpStr=RegExpStr(unescape(request.Form(str)),repType)
	End If
End Function 


If SYS_URL="" Then
	OT_WebHost = dbPathPart
Else
	OT_WebHost = SYS_URL
End If
'************************** 动/静态路径 *************
Function Url_GetListTypeStr()
	Url_GetListTypeStr = ToGetStr(SYS_newsListVarTypeStr)
	If Url_GetListTypeStr="" Then
		Url_GetListTypeStr = ToGetStr("typeStr")
	End If
End Function

Function Url_GetListTypeID()
	Url_GetListTypeID = ToGetInt(SYS_newsListVarTypeID)
	If Url_GetListTypeID=0 Then
		Url_GetListTypeID = ToGetInt("typeID")	' 网钛
	Else
		Exit Function
	End If
	If Url_GetListTypeID=0 Then
		Url_GetListTypeID = ToGetInt("id")		' ok3w
	Else
		Exit Function
	End If
	If Url_GetListTypeID=0 Then
		Url_GetListTypeID = ToGetInt("ID")		' 老Y
	Else
		Exit Function
	End If
End Function

Function Url_GetListPageID()
	Url_GetListPageID = ToGetInt(SYS_newsListVarPageID)
	If Url_GetListPageID=0 Then
		Url_GetListPageID = ToGetInt("page")	' 网钛、老Y
	Else
		Exit Function
	End If
	If Url_GetListPageID=0 Then
		Url_GetListPageID = ToGetInt("PageNo")	' ok3w
	Else
		Exit Function
	End If
End Function

Function Url_GetShowDataID()
	Url_GetShowDataID = ToGetInt(SYS_newsShowVarDataID)
	If Url_GetShowDataID=0 Then
		Url_GetShowDataID = ToGetInt("dataID")	' 网钛
	Else
		Exit Function
	End If
	If Url_GetShowDataID=0 Then
		Url_GetShowDataID = ToGetInt("id")		' ok3w
	Else
		Exit Function
	End If
	If Url_GetShowDataID=0 Then
		Url_GetShowDataID = ToGetInt("ID")		' 老Y
	Else
		Exit Function
	End If
End Function

Function Url_GetShowPageID()
	Url_GetShowPageID = ToGetInt(SYS_newsShowVarPageID)
	If Url_GetShowPageID=0 Then
		Url_GetShowPageID = ToGetInt("page")	' 网钛
	Else
		Exit Function
	End If
	If Url_GetShowPageID=0 Then
		Url_GetShowPageID = ToGetInt("PageNo")	' ok3w
	Else
		Exit Function
	End If
	If Url_GetShowPageID=0 Then
		Url_GetShowPageID = ToGetInt("Page")	' 老Y
	Else
		Exit Function
	End If
End Function


Function Url_NewsListStr(ByVal urlTypeStr,ByVal urlPageID)
	Dim retStr,pageStr

	retStr = ""
	pageStr= ""
	Select Case SYS_newsListUrlMode
		Case "html-2.x"	' news/list_announ.html 、news/list_announ_2.html
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/list_"& urlTypeStr & pageStr &".html"

		Case "dyn-2.x"	' news/?list_announ.html 、news/?list_announ_2.html
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/?list_"& urlTypeStr & pageStr &".html"

		Case "dyn-1.x"	' newsList.asp?typeStr=announ 、newsList.asp?typeStr=announ&page=2
			If urlPageID>1 Then pageStr="&"& SYS_newsListVarPageID &"="& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"?"& SYS_newsListVarTypeStr &"="& urlTypeStr & pageStr

	End Select
	Url_NewsListStr = retStr
End Function

Function Url_NewsListID(ByVal urlHtmlName,ByVal urlDataID,ByVal urlPageID)
	Dim retStr,pageStr

	retStr = ""
	pageStr= ""
	Select Case SYS_newsListUrlMode
		'网钛：news/list_2_2.html、news/list_2_2.html
		'OK3W：article/list_1.html、 article/list_1_2.html
		Case "html-2.x"
			If urlHtmlName="" Then urlHtmlName=urlDataID
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/list_"& urlHtmlName & pageStr &".html"

		'网钛：news/?list_2_2.html、news/?list_2.html
		Case "dyn-2.x"
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/?list_"& urlDataID & pageStr &".html"

		'网钛：newsList.asp?typeID=2&page=2
		'OK3W：list.asp?id=1 / list.asp?id=2&PageNo=1
		'老Y： Class.asp?ID=7 / Class.asp?ID=7&page=2
		Case "dyn-1.x"
			If urlPageID>1 Then pageStr="&"& SYS_newsListVarPageID &"="& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"?"& SYS_newsListVarTypeID &"="& urlDataID & pageStr

	End Select
	Url_NewsListID = retStr
End Function

Function Url_NewsShowID(urlDataID,urlPageID)
	retStr = ""
	pageStr= ""
	Select Case SYS_newsShowUrlMode
		' 网钛：news/12.html
		' OK3W：article/34.html
		Case "html-2.x"
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"/"& urlDataID & pageStr &".html"

		' 网钛：news/?list_12.html
		' 老Y： Html/?360.html、 Html/?360_2.html （假静态）
		Case "dyn-2.x"
			If urlPageID>1 Then pageStr="_"& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"/?"& urlDataID & pageStr &".html"

		' 网钛：newsShow.asp?dataID=12
		' OK3W：show.asp?id=51
		' 老Y： List.asp?ID=360 / List.asp?ID=253&Page=2
		Case "dyn-1.x"
			If urlPageID>1 Then pageStr="&"& SYS_newsShowVarPageID &"="& urlPageID Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"?"& SYS_newsShowVarDataID &"="& urlDataID & pageStr

	End Select
	Url_NewsShowID = retStr
End Function

Function Url_NewsListStr_pageSign(ByVal urlTypeStr,ByVal urlPageID)
	Dim retStr,pageStr

	retStr = ""
	pageStr= ""
	Select Case SYS_newsListUrlMode
		Case "html-2.x"	' news/list_announ.html 、news/list_announ_2.html
			If urlPageID>1 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/list_"& urlTypeStr & pageStr &".html"

		Case "dyn-2.x"	' news/?list_announ.html 、news/?list_announ_2.html
			If urlPageID>1 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/?list_"& urlTypeStr & pageStr &".html"

		Case "dyn-1.x"	' newsList.asp?typeStr=announ 、newsList.asp?typeStr=announ&page=2
			If urlPageID>1 Then pageStr="&"& SYS_newsListVarPageID &"=[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"?"& SYS_newsListVarTypeStr &"="& urlTypeStr & pageStr

	End Select
	Url_NewsListStr_pageSign = retStr
End Function

Function Url_NewsListID_pageSign(ByVal urlHtmlName,ByVal urlDataID,ByVal urlPageID)
	Dim retStr,pageStr

	retStr = ""
	pageStr= ""
	Select Case SYS_newsListUrlMode
		'网钛：news/list_2_2.html、news/list_2_2.html
		'OK3W：article/list_1.html、 article/list_1_2.html
		Case "html-2.x"
			If urlHtmlName="" Then urlHtmlName=urlDataID
			If urlPageID>1 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/list_"& urlHtmlName & pageStr &".html"

		'网钛：news/?list_2_2.html、news/?list_2.html
		Case "dyn-2.x"
			If urlPageID>1 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"/?list_"& urlDataID & pageStr &".html"

		'网钛：newsList.asp?typeID=2&page=2
		'OK3W：list.asp?id=1 / list.asp?id=2&PageNo=1
		'老Y： Class.asp?ID=7 / Class.asp?ID=7&page=2
		Case "dyn-1.x"
			If urlPageID>1 Then pageStr="&"& SYS_newsListVarPageID &"=[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsListFileName &"?"& SYS_newsListVarTypeID &"="& urlDataID & pageStr

	End Select
	Url_NewsListID_pageSign = retStr
End Function

Function Url_NewsShowID_pageSign(urlDataID,urlPageID)
	retStr = ""
	pageStr= ""
	Select Case SYS_newsShowUrlMode
		' 网钛：news/12.html
		' OK3W：article/34.html
		Case "html-2.x"
			If urlPageID>0 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"/"& urlDataID & pageStr &".html"

		' 网钛：news/?list_12.html
		' 老Y： Html/?360.html、 Html/?360_2.html （假静态）
		Case "dyn-2.x"
			If urlPageID>0 Then pageStr="_[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"/?"& urlDataID & pageStr &".html"

		' 网钛：newsShow.asp?dataID=12
		' OK3W：show.asp?id=51
		' 老Y： List.asp?ID=360 / List.asp?ID=253&Page=2
		Case "dyn-1.x"
			If urlPageID>0 Then pageStr="&"& SYS_newsShowVarPageID &"=[page]" Else pageStr=""
			retStr = OT_WebHost & SYS_newsShowFileName &"?"& SYS_newsShowVarDataID &"="& urlDataID & pageStr

	End Select
	Url_NewsShowID_pageSign = retStr
End Function


'********************************** 常用函数 ***************************


'字符串转为数值
Function ToInt(ByVal num)
'	If num<>"" And IsNumeric(num)=True Then ToInt=CLng(num) Else ToInt=0
	If IsNum(num)=True Then ToInt=CLng(num) Else ToInt=0
End Function

Function ToInt2(ByVal num,ByVal def)
'	If num<>"" And IsNumeric(num)=True Then ToInt2=CLng(num) Else ToInt2=def
	If IsNum(num)=True Then ToInt2=CLng(num) Else ToInt2=def
End Function


'字符串转为浮点数
Function ToFloat(ByVal num)
'	If num<>"" And IsNumeric(num)=True Then ToFloat=CDbl(num) Else ToFloat=0
	If IsNum(num)=True Then ToFloat=CDbl(num) Else ToFloat=0
End Function

' 判断是否为数值
function IsNum(ByVal strng)
	strng = ""& strng
	If Len(strng) = 0 Then
		IsNum = False
		Exit Function
	End If
	If strng="-" Or strng="." Then
		IsNum = False
		Exit Function
	End If
	Dim regex
	Set regex = new Regexp
	regex.Global = False
	regex.IgnoreCase = False
	regex.Pattern="^[-]{0,1}[0-9]{0,}[.]{0,1}[0-9]{0,}$"
	''regex.Pattern = "^[-]*\d*[.][\d+]{0,}$"
	If regex.Test(strng) Then
		IsNum=True
	Else
		IsNum=False
	End If
	Set regex=Nothing
End Function


' 判断是否为正确IP地址
function IsIp(ByVal strng)
	strng = ""& strng
	If Len(strng) = 0 Then
		IsIp = False
		Exit Function
	End If
	Dim regex
	Set regex = new Regexp
	regex.Global = False
	regex.IgnoreCase = False
	regex.Pattern="^(\d{1,3})\.(\d{1,3})\.(\d{1,3})\.(\d{1,3})$"
	If regex.Test(strng) Then
		IsIp=True
	Else
		IsIp=False
	End If
	Set regex=Nothing
End Function


Function IsMail(ByVal email)
	email = ""& email
	IsMail=True
	Dim Rep
	Set Rep = new RegExp
	rep.pattern="^([\.a-zA-Z0-9_-]){2,10}@([a-zA-Z0-9_-]){2,10}(\.([a-zA-Z0-9]){2,}){1,4}$"
	pass=rep.Test(email)
	Set Rep=Nothing
	If Not pass Then IsMail=False
End function 


' html转换成js
Function HtmlToJs(ByVal contentStr)
	contentStr = Replace(""& contentStr,"\","\\")
	contentStr = Replace(contentStr,"/","\/")
	contentStr = Replace(contentStr,"'","\'")
	contentStr = Replace(contentStr,"""","\""")
	contentStr = Join(split(contentStr,Chr(13)&Chr(10)),""");"& Chr(13)&Chr(10) &"document.writeln(""")
	contentStr = "document.writeln("""& contentStr &""");"
	HtmlToJs = contentStr
End Function


' js转换成html
Function JsToHtml(ByVal contentStr)
	contentStr = Replace(contentStr,"document.writeln(""","")
	contentStr = Replace(contentStr,""");","")
	contentStr = Replace(contentStr,"\""","""")
	contentStr = Replace(contentStr,"\'","'")
	contentStr = Replace(contentStr,"\/","/")
	contentStr = Replace(contentStr,"\\","\")
	JsToHtml = contentStr
End Function



'字符串长度截取
Function StrLen(ByVal fontStr,ByVal fontNum)
	Dim fontStrLen,fontStrNum,fontStrNum2,fontStri

	If fontNum>0 Then
		fontStr = fontStr &""
		fontNum = CInt(fontNum)
		fontStrLen=Len(fontStr)
		fontStrNum=0
		fontStrNum2=0
		For fontStri=1 To fontStrLen
			If Asc(Mid(fontStr,fontStri,1)) > 127 Or Asc(Mid(fontStr,fontStri,1)) < 0 Then
				fontStrNum = fontStrNum + 2
			Else
				fontStrNum = fontStrNum + 1
			End If
			If fontStrNum <= fontNum Then fontStrNum2=fontStri
		Next
		If fontStrNum > (fontNum+1) Then
			StrLen=Left(fontStr,fontStrNum2) &"..."
		Else
			StrLen=fontStr
		End If
	Else
		StrLen=fontStr
	End If
End Function



'计算字符串长度
Function CalcStrLen(ByVal str)
	str = str &""
	themelen=Len(str)
	themenum=0
	For stri=1 To themelen
		If Asc(Mid(str,stri,1)) > 127 Or Asc(Mid(str,stri,1)) < 0 Then
			themenum = themenum + 2
		Else
			themenum = themenum + 1
		End If
	Next
	CalcStrLen = themenum
End Function


'限定数字的长度，不足用0填充
Function FixIntLen(ByVal intStr,ByVal fixLen)
	If Len(""& intStr)>=fixLen Then FixIntLen=intStr Else FixIntLen=String(fixLen-Len(""& intStr),"0") & intStr
End Function



'获取范围内的数字随机数
Function RndNumTo(ByVal minNum,ByVal maxNum)
	Randomize 
	RndNumTo=int((maxNum-minNum+1)*rnd+minNum)
End Function


'获取指定长度数字随机数
Function RndNum(ByVal length)
	Dim i,charLen,charStr,newStr
	charStr = "0123456789"
	newStr = ""
	charLen = Len(charStr)
	For i = 1 To length
		Randomize
		newStr = newStr & Mid(charStr,Int(charLen * Rnd + 1),1)
	Next
	RndNum = newStr
End Function


'获取指定长度字符随机数
Function RndChar(ByVal length)
	Dim i,charLen,charStr,newStr
	charStr = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
	newStr = ""
	charLen = Len(charStr)
	For i = 1 To length
		Randomize
		newStr = newStr & Mid(charStr,Int(charLen * Rnd + 1),1)
	Next
	RndChar = newStr
End Function

'获取指定长度字符随机数
Function RndABC(ByVal length)
	Dim i,charLen,charStr,newStr
	charStr = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
	newStr = ""
	charLen = Len(charStr)
	For i = 1 To length
		Randomize
		newStr = newStr & Mid(charStr,Int((charLen - 0 + 1) * Rnd + 1),1)
	Next
	RndABC = newStr
End Function



'判断是否一样，一样返回checked,否者返回空值
Function IsChecked(ByVal str,ByVal str2)
	If str=str2 Then IsChecked="checked" Else IsChecked=""
End Function

'判断是否一样，一样返回selected,否者返回空值
Function IsSelected(ByVal str,ByVal str2)
	If str=str2 Then IsSelected="selected" Else IsSelected=""
End Function

'判断是否一样，一样返回checked,否者返回空值
Function IsInstrChecked(ByVal str,ByVal str2)
	If InStr(str,str2)<>0 Then IsInstrChecked="checked='checked'" Else IsInstrChecked=""
End Function

'判断是否一样，一样返回selected,否者返回空值
Function IsInstrSelected(ByVal str,ByVal str2)
	If InStr(str,str2)<>0 Then IsInstrSelected="selected='selected'" Else IsInstrSelected=""
End Function


'内容分页
Function ContentPageNum(ByVal contentStr,ByVal pageWord)
	dim contentLen,pages

	ContentPageNum = 1 
	If Instr(contentStr,"[OT_page]")>0 Then
		contentArr=split(contentStr,"[OT_page]")
		ContentPageNum=Ubound(contentArr)+1

	ElseIf pageWord>1 Then
		contentLen=len(contentStr)

		If contentLen<=pageWord Then
			ContentPageNum = 1
		Else
			pages=contentLen\pageWord
			If pageWord*pages<contentLen Then
				pages=pages+1
			End If
			ContentPageNum = pages
		End If

	End If
End Function


' 根据标记获取内容(截至到第一个结束标签)
Function GetSignStr(ByVal contentStr,ByVal startCode,ByVal endCode,ByVal incStart,ByVal incEnd)
	If contentStr="" Or startCode="" Or endCode="" Then
		GetSignStr=""
		Exit Function
	End If
	Dim contentTemp
	Dim Start,Over
	contentTemp=Lcase(contentStr)
	startCode=Lcase(startCode)
	endCode=Lcase(endCode)
	Start = InStrB(1, contentTemp, startCode, vbBinaryCompare)
	If Start<=0 then
		GetSignStr=""
		Exit Function
	Else
		If incStart=False Then
			Start=Start+LenB(startCode)
		End If
	End If
	Over=InStrB(Start,contentTemp,endCode,vbBinaryCompare)
	If Over<=0 Or Over<=Start then
		GetSignStr=""
		Exit Function
	Else
		If incEnd=True Then
			Over=Over+LenB(endCode)
		End If
	End If
	GetSignStr=MidB(contentStr,Start,Over-Start)
End Function

' 根据标记获取内容(截至到最后一个结束标签)
Function GetSignStrEnd(ByVal contentStr,ByVal startCode,ByVal endCode,ByVal incStart,ByVal incEnd)
	If contentStr="" Or startCode="" Or endCode="" Then
		GetSignStrEnd=""
		Exit Function
	End If
	Dim contentTemp
	Dim Start,Over
	contentTemp=Lcase(contentStr)
	startCode=Lcase(startCode)
	endCode=Lcase(endCode)
	Start = InStrB(1, contentTemp, startCode, vbBinaryCompare)
	If Start<=0 then
		GetSignStrEnd=""
		Exit Function
	Else
		If incStart=False Then
			Start=Start+LenB(startCode)
		End If
	End If

	Over=InStrB(Start,contentTemp,endCode,vbBinaryCompare)
	overEndPoint = 1
	Do While overEndPoint>0
		overEndPoint=InStrB(Over+LenB(endCode),contentTemp,endCode,vbBinaryCompare)
		If overEndPoint>0 Then Over=overEndPoint
	Loop

	If Over<=0 Or Over<=Start then
		GetSignStrEnd=""
		Exit Function
	Else
		If incEnd=True Then
			Over=Over+LenB(endCode)
		End If
	End If
	GetSignStrEnd=MidB(contentStr,Start,Over-Start)
End Function


'过滤特殊符号
Function FilterSpecSign(ByVal str)
	Dim re
	Set re = New RegExp
	re.IgnoreCase = True
	re.Global = True
	re.Pattern = "(" & Chr(8) & "|" & Chr(9) & "|" & Chr(10) & "|" & Chr(13) & ")"
	str = re.Replace(str, vbNullString)
	Set re = Nothing

	FilterSpecSign = str
End Function

'过滤字符串
Function FilterStr(ByVal str,ByVal Fnum)
	Dim UserPwd_Badword,Chk_badword,Fi
	Select Case Fnum
		Case "sql"
	UserPwd_Badword=" ∥,∥.∥:∥;∥'∥""∥`∥~∥?∥!∥@∥#∥$∥%∥^∥&∥*∥<∥>∥(∥)∥+∥-∥/∥=∥\∥{∥}"		'过滤字符串，用“∥”间隔

	End Select
	Chk_badword=Split(UserPwd_Badword,"∥")
		For Fi=0 To UBound(Chk_badword)
			str=Replace(""& str,Chk_badword(Fi),"")
		Next
	FilterStr=str
End Function

   
                      
' 过滤html标签
Function FilterHtmlMark(ByVal contentStr,ByVal markName,ByVal mode)
    Dim Re
    Set Re=new RegExp
    Re.IgnoreCase =true
    Re.Global=True

	If mode=0 Then
		Select Case markName
			Case "img"
				mode=1

			Case "object","script","style"
				mode=2

			Case "iframe","div","class","table","tr","td","html","font","a","span"
				mode=3

		End Select
	End If

    Select Case mode
		Case 1
		   Re.Pattern="<" & markName & "([^>])*>"
		   contentStr=Re.Replace(contentStr,"")
		Case 2
		   Re.Pattern="<" & markName & "([^>])*>.*?</" & markName & "([^>])*>"
		   contentStr=Re.Replace(contentStr,"") 
		Case 3
		   Re.Pattern="<" & markName & "([^>])*>"
		   contentStr=Re.Replace(contentStr,"")
		   Re.Pattern="</" & markName & "([^>])*>"
		   contentStr=Re.Replace(contentStr,"")
    End Select

    FilterHtmlMark=contentStr
    Set Re=Nothing
End Function



'替换字符串
Function ReplaceStr(ByVal str,ByVal Fnum)
	Dim fiStr,reStr,fiSZ,reSZ,Ri
	Select Case Fnum		'fiStr:过滤字符串；reStr:顶替字符串，用“∥”间隔
		Case "|"
			fiStr="|∥'∥"""
			reStr="｜∥＇∥＂"

		Case "js"
			fiStr="\∥/∥""∥'∥"& Chr(10) &"∥"& Chr(13)
			reStr="\\∥\/∥\""∥\'∥\n∥"

		Case "html"
			fiStr=" ∥<∥>∥""∥"& Chr(10) &"∥"& Chr(13)
			reStr="&nbsp;∥&lt;∥&gt;∥&quot;∥<br>∥"

		Case "html2"
			fiStr=" ∥<∥>∥""∥"& Chr(10) &"∥"& Chr(13)
			reStr="　∥＜∥＞∥＂∥<br>∥"

		Case "filthtml"
			fiStr="&nbsp;∥<br>∥<p>"
			reStr=" ∥"& Chr(10) &"∥"& Chr(10) &""

		Case "defilthtml"
			fiStr=" ∥"& Chr(10) &"∥"& Chr(10) &""
			reStr="&nbsp;∥<br>∥<p>"

		Case "input"
			fiStr="'∥""∥<∥>∥"& Chr(10) &"∥"& Chr(13)
			reStr="&#39;∥&#34;∥&lt;∥&gt;∥∥"

		Case "textarea"
			fiStr="'∥""∥<∥>"
			reStr="&#39;∥&#34;∥&lt;∥&gt;"

	End Select
	fiSZ=Split(fiStr,"∥")
	reSZ=Split(reStr,"∥")
		For Ri=0 To UBound(fiSZ)
			str=Replace(""& str,fiSZ(Ri),reSZ(Ri))
		Next
	ReplaceStr=str
End Function


'过滤字符串（正则表达式）
Function RegExpStr(ByVal str,ByVal Fnum)
	Dim regEx,reStr

	str=""& str
	Set regEx = New RegExp '建立正则对象 
		Select Case Fnum
			Case "sql"
	regEx.Pattern = "[^\w\u4E00-\u9FA5]"	'过滤掉所有符号（保留数字、字母、下划线_、汉字）
	reStr=""
			Case "abcnum"
	regEx.Pattern = "[^a-zA-Z0-9]"	'过滤掉所有符号（保留数字、字母、下划线_、汉字）
	reStr=""
			Case "num"
	regEx.Pattern = "[^0-9]"	'只保留数字
	reStr=""
			Case "sql+,"
	regEx.Pattern = "[^\w\,\u4E00-\u9FA5]"	'过滤掉除“[”“]”的符号
			Case "sql+ "
	regEx.Pattern = "[^\w \u4E00-\u9FA5]"	'过滤掉除“[”“]”的符号
			Case "sql+[]"
	regEx.Pattern = "[^\w\[\]\u4E00-\u9FA5]"	'过滤掉除“[”“]”的符号
			Case "sql+mail"
	regEx.Pattern = "[^\w\.@-\u4E00-\u9FA5]"	'过滤非邮箱字符
	reStr=""
			Case "html"
	regEx.Pattern = "<[^>]*>"	'过滤掉HTML标识
	reStr=""
			Case "br"
	regEx.Pattern = "("& Chr(8) &"|"& Chr(9) &"|"& Chr(10) &"|"& Chr(13) &")"	'过滤制表符、换行等
	reStr=""
			Case "bytes"
	regEx.Pattern = "[^\u0000-\u00ff]"	'[\x00-\xff]
	reStr="**"
		End Select
	regEx.IgnoreCase = True '是否区分大小写 
	regEx.Global = True '是否全局可用 
'	regEx.Execute(str) '开始匹配 
	RegExpStr = regEx.Replace(str,reStr) '替换并返回函数值 
	Set regEx = Nothing '释放对象 
End Function

' img没有alt属性的加上alt属性
Function AddImgAlt(ByVal contentStr,ByVal altStr)
	Dim regEx
	Set regEx = New RegExp '建立正则对象
	regEx.IgnoreCase = True '是否区分大小写 
	regEx.Global = True '是否全局可用 
	regEx.Pattern="(<img[\s\S]*?)(alt=['""]{2})(.[^>]*>)"
	contentStr=regEx.Replace(contentStr,"$1$3")
	regEx.Pattern="(<img)(?![^<>]*?alt[^<>]*?>)(.*?>)"
	contentStr=regEx.Replace(contentStr,"$1 alt="""& ReplaceStr(altStr,"input") &""" $2")
	AddImgAlt=contentStr
End Function


' 文章图片的路径
Function InfoImgUrl(ByVal imgValue,ByVal imgPartUrl)
	If Left(""& imgValue,7)="http://" Then
		InfoImgUrl = imgValue
	Else
		InfoImgUrl = imgPartUrl & imgValue
	End If
End Function




' 获取地址
Function GetUrlQuery(noQueryStr,addiQueryStr)
	newStr = "?mudi="& Server.URLEncode(Request.querystring("mudi"))
	For Each objItem In Request.querystring
		If InStr("|mudi|"& noQueryStr,"|"& objItem &"|")<=0 Then newStr=newStr &"&"& objItem &"="& server.URLEncode(Request.querystring(objItem))
	Next
	GetUrlQuery = newStr & addiQueryStr
End Function


' 过滤编辑器内容
function FilterEditor(ByVal contentStr)
	Dim regEx
	Set regEx = New RegExp '建立正则对象 
	regEx.IgnoreCase = True '是否区分大小写 
	regEx.Global = True '是否全局可用 
	regEx.Pattern="(<(meta|iframe|frame|tbody|layer|form)[^>]*>|<\/(iframe|frame|meta|tbody|layer|form)>)"
	contentStr=regEx.Replace(contentStr,"")
	regEx.Pattern="<\\?\?xml[^>]*>"
	contentStr=regEx.Replace(contentStr,"")
	regEx.Pattern="<\s*xss[^>]*>"
	contentStr=regEx.Replace(contentStr,"")
	regEx.Pattern="<\s*(script[^>]*)>(.[^<]*)<\/\s*script>"
	contentStr=regEx.Replace(contentStr,"")
	regEx.Pattern="<(\w[^>|\s]*)([^>]*)(on(finish|mouse|Exit|error|click|key|load|change|focus|blur))(.[^>]*)"
	contentStr=regEx.Replace(contentStr,"<$1$2")
	regEx.Pattern="<(\w[^>|\s]*)([^>]*)(&#|window\.|javascript:|js:|about:|file:|Document\.|vbs:|cookie| name| id)(.[^>]*)"
	contentStr=regEx.Replace(contentStr,"<$1$2")
	regEx.Pattern="<a (.*?)>(.*?)<\/a>"
	contentStr=regEx.Replace(contentStr,"<a $1 target='_blank'>$2</a>")
	FilterEditor = contentStr
End Function


' 文件大小转换成带单位
Function FileSizeUnit(ByVal num)
	sizeNum=num/1048576
	If Int(sizeNum)>=1 Then
		FileSizeUnit=FormatNumber(sizeNum,2,True) &"MB"
	Else
		sizeNum=num/1024
		If Int(sizeNum)>=1 Then
			FileSizeUnit=FormatNumber(sizeNum,2,True) &"KB"
		Else
			FileSizeUnit=num &"bytes"
		End If
	End If
End Function





'是否为外部提交(true是，false否)
Function IsOutSubmit()
	Dim sHttp_Referer, sServer_Name
	sHttp_Referer = CStr(Request.ServerVariables("HTTP_REFERER"))
	sServer_Name = CStr(Request.ServerVariables("SERVER_NAME"))
	If Mid(sHttp_Referer, 8, Len(sServer_Name)) = sServer_Name Then
		IsOutSubmit = False
	Else
		IsOutSubmit = True
	End If
End Function

function CheckIsOutSubmit(mode)
	If IsOutSubmit()=True Then
		If mode="alertStr" Then
			response.write(_
			"alert('非正规路径进入，拒绝访问.');"&_
			"")
			response.End()
		Else
			response.write(_
			"<script language='javascript' type='text/javascript'>"&_
			"alert('非正规路径进入，拒绝访问.');document.location.href='index.asp';"&_
			"</script>"&_
			"")
			response.End()
		End If
	End If
End Function





' 编码/解码 26个日文片假名
' codeType:true编码，false解码
Function JapanCode(ByVal strCode,ByVal codeType)
	If isnull(strCode) Or isEmpty(strCode) Or strCode="" Then
		JapanCode=""
		Exit function
	End If
	dim i,eArr,fArr
	eArr=array("Jn0;","Jn1;","Jn2;","Jn3;","Jn4;","Jn5;","Jn6;","Jn7;","Jn8;","Jn9;","Jn10;",_
			"Jn11;","Jn12;","Jn13;","Jn14;","Jn15;","Jn16;","Jn17;","Jn18;","Jn19;","Jn20;",_
			"Jn21;","Jn22;","Jn23;","Jn24;","Jn25;")
	fArr=array(chrw(12468),chrw(12460),chrw(12462),chrw(12464),chrw(12466),chrw(12470),chrw(12472),chrw(12474),chrw(12485),chrw(12487),_
			chrw(12489),chrw(12509),chrw(12505),chrw(12503),chrw(12499),chrw(12497),chrw(12532),chrw(12508),chrw(12506),chrw(12502),_
			chrw(12500),chrw(12496),chrw(12482),chrw(12480),chrw(12478),chrw(12476))
'	fArr=array("ゴ","ガ","ギ","グ","ゲ","ザ","ジ","ズ","ヅ","デ","ド","ポ","ベ","プ","ビ","パ","ヴ","ボ","ペ","ブ","ピ","バ","ヂ","ダ","ゾ","ゼ")

	If codeType=True Then
		For i=0 To 25
			strCode=replace(strCode,fArr(i),eArr(i))
		Next
	Else
		For i=0 To 25
			strCode=replace(strCode,eArr(i),fArr(i))
		Next
	End If
	JapanCode=strCode
End Function


' 获取时间戳
Function GetTimer()
	GetTimer = DateDiff("s", "1970-1-1 8:00:00", Now())
End Function



' 检测网址URL的正确性
Function CheckUrl(ByVal strUrl)
	Dim Re
	Set Re=new RegExp
	Re.IgnoreCase =true
	Re.Global=True
	Re.Pattern="http://([\w-]+\.)+[\w-]+(/[\w-./?%&=]*)?"
'	Re.Pattern="^(http|ftp|https):\/\/[\w\-_]+(\.[\w\-_]+)+([\w\-\.,@?^=%&:/~\+#]*[\w\-\@?^=%&/~\+#])?$"
	If Re.test(strUrl)=True Then
		CheckUrl=True
	Else
	CheckUrl=False
	End If
	Set Rs=Nothing
End Function

' 获取当前域名端口
Function GetCurrUrlPost()
	Dim Const_SERVER_PORT
'	Const_SERVER_PORT = Request.ServerVariables("SERVER_PORT")
'	If Const_SERVER_PORT<>80 Then Const_SERVER_PORT=":"& Const_SERVER_PORT Else Const_SERVER_PORT=""
	GetCurrUrlPost = Const_SERVER_PORT
End Function

' 获取当前域名
Function GetCurrMainUrl()
	Dim Const_SERVER_PORT
'	Const_SERVER_PORT = Request.ServerVariables("SERVER_PORT")
'	If Const_SERVER_PORT<>80 Then Const_SERVER_PORT=":"& Const_SERVER_PORT Else Const_SERVER_PORT=""
	GetCurrMainUrl = "http://"& request.ServerVariables("Server_NAME") & Const_SERVER_PORT &"/"
End Function

' 获取当前网址
Function GetCurrUrl()
	Dim Const_SERVER_PORT
'	Const_SERVER_PORT = Request.ServerVariables("SERVER_PORT")
'	If Const_SERVER_PORT<>80 Then Const_SERVER_PORT=":"& Const_SERVER_PORT Else Const_SERVER_PORT=""
	GetCurrUrl = "http://"& request.ServerVariables("Server_NAME") & Const_SERVER_PORT & request.ServerVariables("SCRIPT_NAME")
End Function

' 获取当前网址含？后面参数
Function GetCurrUrlQuery()
	Dim Const_SERVER_PORT,queryPart
'	Const_SERVER_PORT = Request.ServerVariables("SERVER_PORT")
'	If Const_SERVER_PORT<>80 Then Const_SERVER_PORT=":"& Const_SERVER_PORT Else Const_SERVER_PORT=""
	GetCurrUrlQuery = "http://"& request.ServerVariables("Server_NAME") & Const_SERVER_PORT & request.ServerVariables("SCRIPT_NAME")
	queryPart=Request.ServerVariables("QUERY_STRING")
	If Len(queryPart)>0 Then GetCurrUrlQuery = GetCurrUrlQuery &"?"& queryPart

End Function

' 获取当前网址目录
Function GetCurrUrlDir(ByVal dirRank)
	Dim Const_currUrl
	Const_currUrl = GetCurrUrl()
	For udi=0 To dirRank
		Const_currUrl=Left(Const_currUrl,InstrRev(Const_currUrl,"/") - 1)
	Next
	GetCurrUrlDir = Const_currUrl &"/"
End Function



' UTF-8字符转换成GB2312
Function UTF2GB(ByVal UTFStr)
    For Dig=1 to len(UTFStr)
        if mid(UTFStr,Dig,1)="%" then
            if len(UTFStr) >= Dig+8 then
                GBStr=GBStr & ConvChinese(mid(UTFStr,Dig,9))
                Dig=Dig+8
            else
                GBStr=GBStr & mid(UTFStr,Dig,1)
            end if
        else
            GBStr=GBStr & mid(UTFStr,Dig,1)
        end if
    Next
    UTF2GB=GBStr
End Function

%>