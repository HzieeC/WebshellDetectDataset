<!--#include file="../../conn.asp"-->
<!--#include file="../inc/setup.asp"-->
<!--#include file="../inc/const.asp"-->
<!--#include file="../inc/check.asp"-->
<%
Admin_header
'=====================================================================
' 软件名称：新云网站管理系统
' 当前版本：NewAsp Content Management System Version 4.0
' 文件名称：admin_confirm.asp
' 更新日期：2008-09-16
' 官方网站：新云网络(www.NewAsp.net www.NewAsp.cn) QQ：94022511
'=====================================================================
' Copyright 2003-2008 NewAsp.net - All Rights Reserved.
' newasp is a trademark of NewAsp.net
'=====================================================================
%>
<table class="table1" cellspacing="1" cellpadding="3" align="center" border="0">
	<tr>
		<td class="tableline linetitle" width="100" align="left">交费确认管理</td>
		<td class="tableline" width="*" align="right"><a href="admin_index.asp">管理首页</a>
			 - <a href="admin_userorder.asp">会员充值定单</a>
			 - <a href="admin_account.asp">交易明细查询</a>
			 - <a href="admin_confirm.asp">交费确认</a>
			 - <a href="admin_paymode.asp">付款方式</a>
			 - <a href="admin_list.asp">用户列表</a>
			 - <a href="admin_group.asp">用户组管理</a>
			 - <a href="admin_master.asp">管理员列表</a>
		</td>
	</tr>
</table>
<%
Dim maxperpage,totalrec,Pcount,pagelinks,showmode,pagenow,count
Dim Action,listArry,SQLQuery

If Not ChkAdmin("Confirm") Then
	Call Transfer_error()
End If
Action = LCase(Request("action"))
Select Case Trim(Action)
Case "del"
	Call DelConfirm()
Case "delall"
	Call DelAllConfirm()
Case "view"
	Call ViewConfirm()
Case "pass"
	Call PassConfirm()
Case Else
	Call showmain()
End Select
If FoundErr=True Then
	ReturnError(ErrMsg)
End If
Admin_footer
SaveLogInfo(AdminName)
NewAsp.PageEnd

Sub showmain()
	Dim i,iCount,lCount
	iCount=2:lCount=2
	maxperpage = 30 '###每页显示数
	count=NewAsp.ChkNumeric(Request("count"))
	pagenow=NewAsp.ChkNumeric(Request("page"))
	If pagenow=0 Then pagenow=1
%>
<form name="selform" method="post" action="?action=del">
<table id="tablehovered" border="0" align="center" cellpadding="3" cellspacing="1" class="tableborder">
<tr>
	<th nowrap="nowrap">选择</th>
	<th nowrap="nowrap">付款方式</th>
	<th nowrap="nowrap">用户名称</th>
	<th nowrap="nowrap">定 单 号</th>
	<th nowrap="nowrap">支付金额</th>
	<th nowrap="nowrap">汇款人姓名</th>
	<th nowrap="nowrap">汇款人邮箱</th>
	<th nowrap="nowrap">汇款时间</th>
	<th nowrap="nowrap">查看说明</th>
</tr>
<tr>
	<td class="tablerow1" colspan="9" align="left" id="showNextPage">&nbsp;</td>
</tr>
<%
	Call showConfirmlist()
	If IsArray(listArry) Then
		For i=0 To Ubound(listArry,2)
			If Not Response.IsClientConnected Then Response.End
			If (i mod 2) = 0 Then iCount=2:lCount=1 Else iCount=1:lCount=2
%>
<tr align="center">
	<td class="tablerow<%=iCount%> hovered"><input type="checkbox" name="id" value="<%=listArry(0,i)%>"></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%=NewAsp.HTMLEncodes(listArry(1,i))%></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%=NewAsp.HTMLEncodes(listArry(2,i))%></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><font color="red"><%=NewAsp.HTMLEncodes(listArry(3,i))%></font></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><font color="blue"><%=FormatCurrency(listArry(4,i),2,-1)%></font> 元</td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%=NewAsp.HTMLEncodes(listArry(5,i))%></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%=NewAsp.HTMLEncodes(listArry(6,i))%></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%=showDateTime(listArry(7,i),"yyyy-MM-dd")%></td>
	<td class="tablerow<%=iCount%> hovered" nowrap="nowrap"><%
			If CLng(listArry(8,i))>0 Then
				Response.Write "<font color=""blue"">已处理</font>"
			Else
				Response.Write "<a href=""?action=pass&id="&listArry(0,i)&""" title=""点击处理交费确认"" onClick=""return confirm('确定处理此信息吗？')""><font color=""red"">未处理</font></a>"
			End If
	%> | <a href="?action=view&id=<%=listArry(0,i)%>" title="查看详细说明">查看说明</a></td>
</tr>
<%
		Next
		listArry=Null
	End If
%>
<tr>
	<td class="tablerow<%=lCount%>" colspan="9">
	<input class="button" type="button" name="chkall" value="全选" onClick="CheckAll(this.form)"><input class="button" type="button" name="chksel" value="反选" onClick="ContraSel(this.form)">
	<input class="button" type="submit" name="submit_button1" value="删除" onclick="{if(confirm('您确定要删除选择的确认信息吗?')){return true;}return false;}">
	<input class="button" type="button" name="submit_button2" value="清空已处理确认信息" onclick="{if(confirm('您确定要清空所有已处理确认信息吗?')){location.href='admin_confirm.asp?action=delall';return true;}return false;}"></td>
</tr>
<tr>
	<td class="tablerow<%=iCount%>" colspan="9" id="NextPageText"><var class="morePage"><%=showlistpage(pagenow,Pcount,maxperpage,totalrec,pagelinks)%></var></td>
</tr>
</table>
</form>
<script type="text/javascript">
document.getElementById("showNextPage").innerHTML = document.getElementById("NextPageText").innerHTML;
</script>
<%
End Sub

Sub showConfirmlist()
	Dim Rs,SQL
	If count=0 Then
		totalrec=NewAsp.Execute("SELECT COUNT(*) FROM [NC_Confirm]")(0)
	Else
		totalrec=count
	End If
	Pcount = CLng(totalrec / maxperpage)
	If Pcount < totalrec / maxperpage Then Pcount = Pcount + 1
	If pagenow>Pcount Then pagenow=1
	pagelinks="?showmode="&Request("showmode")&"&count="&totalrec&"&"
	Set Rs=NewAsp.CreateAXObject("ADODB.Recordset")
	SQL = "SELECT id,paymode,username,indent,PayMoney,customer,Email,PayDate,IsPass FROM [NC_Confirm] ORDER BY id DESC"
	Rs.Open SQL,Conn,1,1
	If pagenow >1 Then
		Rs.Move (pagenow-1) * maxperpage
	End If
	If Not (Rs.BOF And Rs.EOF) Then
		listArry=Rs.GetRows(maxperpage)
	Else
		listArry=Null
	End If
	Rs.close()
	Set Rs=Nothing
End Sub

Sub ViewConfirm()
	Dim Rs
	Set Rs = NewAsp.Execute("SELECT Readme FROM NC_Confirm WHERE id="& CLng(Request("id")))
	If Rs.BOF And Rs.EOF Then
		FoundErr = True
		ErrMsg = ErrMsg + "<li>没有找到说明！</li>"
		Set Rs = Nothing
		Exit Sub
	End If
	Response.Write "<table cellspacing=""1"" align=""center"" cellpadding=""3"" border=""0"" class=""tableborder"">"
	Response.Write "	<tr>"
	Response.Write "		<th>确认说明</th>"
	Response.Write "	</tr>"
	Response.Write "	<tr>"
	Response.Write "		<td class=""tablerow2"">&nbsp;&nbsp;"
	Response.Write NewAsp.HTMLEncodes(Rs("Readme"))
	Response.Write "</td>"
	Response.Write "	</tr>"
	Response.Write "	<tr>"
	Response.Write "		<td class=""tablerow1"" align=""center"">"
	Response.Write "<input class=""button"" type=""button"" onclick=""javascript:history.go(-1)"" name=""go_button"" value=""返回上一页"" />"
	Response.Write "</td>"
	Response.Write "	</tr>"
	Response.Write "</table>"
	Set Rs = Nothing
End Sub

Sub DelConfirm()
	Dim selConfirmID
	If Not IsEmpty(Request("id")) Then
		selConfirmID = NewAsp.CheckIDlist(Request("id"))
		NewAsp.Execute("DELETE FROM [NC_Confirm] WHERE id in (" & selConfirmID & ")")
		Response.Redirect (Request.ServerVariables("HTTP_REFERER"))
	Else
		FoundErr = True
		ErrMsg = ErrMsg + "<li>错误的系统参数，ID不能为空！</li>"
		Exit Sub
	End If
End Sub

Sub DelAllConfirm()
	NewAsp.Execute("DELETE FROM [NC_Confirm] WHERE IsPass>0")
	Response.Redirect ("admin_confirm.asp")
End Sub

Sub PassConfirm()
	Dim selConfirmID
	If Not IsEmpty(Request("id")) Then
		selConfirmID = NewAsp.CheckIDlist(Request("id"))
		NewAsp.Execute("UPDATE [NC_Confirm] SET isPass=1 WHERE id in (" & selConfirmID & ")")
		Response.Redirect (Request.ServerVariables("HTTP_REFERER"))
	Else
		FoundErr = True
		ErrMsg = ErrMsg + "<li>错误的系统参数，ID不能为空！</li>"
		Exit Sub
	End If
End Sub
%>