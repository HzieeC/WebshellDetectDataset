<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<!-- #include file="inc/file_memberLog.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


Call OpenMemberexe("","login",2)
Call CloseMemberexe()




Select Case mudi
	Case "add","rev"
		Call AddOrRev()

	Case "send"
		menuFileID = 197
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call Send()

	Case "del"
		menuFileID = 198
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call del()

End Select


Call CloseConobj()









'添加与修改
Private Sub AddOrRev()
	URL			= ToPostStr("URL")
	dataType	= ToPostStr("dataType")
	dataTypeCN	= ToPostStr("dataTypeCN")
	dataID		= ToPostInt("dataID")

	num			= ToPostInt("num")
	theme		= ToPostStr("theme")
	themeStyle	= ToPostStr("themeStyle")
	img			= ToPostStr("img")
	score1		= ToPostInt("score1")
	score2		= ToPostInt("score2")
	score3		= ToPostInt("score3")
	score4		= ToPostInt("score4")
	score5		= ToPostInt("score5")
	infoNum		= ToPostInt("infoNum")
	maxAddInfo	= ToPostInt("maxAddInfo")

	
	If num<1 And theme="" And img="" Then
		JS_AlertBackEnd("表单内容接收不全")
	End If

	If dataID=1 Then
		score1		= -1
		score2		= -1
		score3		= -1
		score4		= -1
		score5		= -1
	End If
	
	application.lock
		Set dealrec=server.CreateObject("ADODB.recordset")
		dealrec.open "select top 1 * from OT_userLevel where UL_ID="& dataID,conobj,0,2
			If dealrec.EOF Then
				menuFileID = 196
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				dealrec.addnew
				alertMode="添加"
			Else
				menuFileID = 197
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				alertMode="修改"
			End If
		dealrec("UL_num")		= num
		dealrec("UL_theme")		= theme
		dealrec("UL_themeStyle")= themeStyle
		dealrec("UL_img")		= img
		dealrec("UL_score1")	= score1
		dealrec("UL_score2")	= score2
		dealrec("UL_score3")	= score3
		dealrec("UL_score4")	= score4
		dealrec("UL_score5")	= score5
		dealrec("UL_infoNum")	= infoNum
		dealrec("UL_maxAddInfo")= maxAddInfo
		dealrec.update
		dealrec.close
		Set dealrec=Nothing
	application.unlock


	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】"& alertMode & alertResult &"！")


	Call JS_AlertHref(""& alertMode &"成功！",URL)
End Sub








'数据发送
Private Sub Send()
	numID		= ToGetInt("typeNum")
	dataID		= ToGetInt("dataID")
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")

	Set sendexe=conobj.execute("select top 1 * from OT_userLevel where UL_ID="& dataID)
		If sendexe.EOF Then
			JS_AlertEnd("搜索不到指定记录")
		End If
		If dataID=1 Then
			scoreRead="true"
		Else
			scoreRead="false"
		End If
		UL_themeStyle = sendexe("UL_themeStyle")
'"parent.$id('numID').innerHTML='"& numID &"';"&_
	response.write(_
	"<script language='javascript' type='text/javascript'>"&_
	"parent.$id('num').value='"& sendexe("UL_num") &"';"&_
	"parent.$id('dataID').value='"& sendexe("UL_ID") &"';"&_
	"parent.$id('theme').value="""& sendexe("UL_theme") &""";"&_
	"")
	If InStr(UL_themeStyle,"font-weight:bold;")>0 Then
		response.write(_
		"parent.$id('themeStyleB').checked=true;"&_
		"")
		UL_themeStyle = Replace(UL_themeStyle,"font-weight:bold;","")
	Else
		response.write(_
		"parent.$id('themeStyleB').checked=false;"&_
		"")
	End If

	UL_themeStyle = Replace(Replace(UL_themeStyle,"color:",""),";","")
	If Len(UL_themeStyle)=7 Then
		response.write(_
		"parent.$id('themeStyleColor').value='"& UL_themeStyle &"';"&_
		"")
	Else
		response.write(_
		"parent.$id('themeStyleColor').value='';"&_
		"")
	End If

	response.write(_
	"parent.CheckColorBox();"&_
	"parent.$id('img').value="""& sendexe("UL_img") &""";"&_
	"parent.$id('score1').value='"& sendexe("UL_score1") &"';"&_
	"try { parent.$id('score2').value='"& sendexe("UL_score2") &"'; }catch (e){}"&_
	"try { parent.$id('score3').value='"& sendexe("UL_score3") &"'; }catch (e){}"&_
	"try { parent.$id('score4').value='"& sendexe("UL_score4") &"'; }catch (e){}"&_
	"try { parent.$id('score5').value='"& sendexe("UL_score5") &"'; }catch (e){}"&_
	"try { parent.$id('num').readOnly="& scoreRead &"; }catch (e){}"&_
	"try { parent.$id('score1').readOnly="& scoreRead &"; }catch (e){}"&_
	"try { parent.$id('score2').readOnly="& scoreRead &"; }catch (e){}"&_
	"try { parent.$id('score3').readOnly="& scoreRead &"; }catch (e){}"&_
	"try { parent.$id('score4').readOnly="& scoreRead &"; }catch (e){}"&_
	"try { parent.$id('score5').readOnly="& scoreRead &"; }catch (e){}"&_
	"parent.$id('subButton').src='images/button_rev.gif';"&_
	"</script>"&_
	"")
	sendexe.close
	Set sendexe=Nothing
End Sub









'删除
Private Sub del()
	dataID	= ToGetInt("dataID")
	theme	= ToGetStr("theme")

	If dataID<=0 Then
		response.write "<script language='javascript' type='text/javascript'>alert('指定ID错误！');</script>"
		response.End
	End If

	conobj.execute("delete from OT_userLevel where UL_ID="& dataID)


	alertResult = "成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】删除"& alertResult &"！")


	response.write("<script language='javascript' type='text/javascript'>parent.$id('data"& dataID &"').style.display='none'</script>")
End Sub

%>