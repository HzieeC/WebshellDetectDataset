<%
If OT_ISOTCMS<>True Then
	response.write("非法访问")
	response.End()
End If





' 添加/修改文章
Sub NewsDeal()

	UE_ID		= userexe("UE_ID")
	UE_username	= userexe("UE_username")

	If mudi="revNews" Then
		If US_isNewsRev=0 Then
			response.write("<br /><br /><center>不允许修改文章，有问题请联系管理员</center>")
			response.End()
		End If
		
		dataID = ToGetInt("dataID")
		Set revexe = conobj.execute("select IF_ID,IF_theme,IF_source,IF_writer,IF_typeStr,IF_content,IF_pageNum,IF_themeKey,IF_contentKey,IF_voteMode,IF_isReply from OT_info where IF_ID="& dataID)
			If revexe.EOF Then
				response.write(_
				"<br /><br />"&_
				"<center style='font-size:14px;'>获取不到指定的记录.</center>"&_
				"<br /><br />"&_
				"")
				response.End()
			End If
			IF_ID			= revexe("IF_ID")
			IF_theme		= revexe("IF_theme")
			IF_source		= revexe("IF_source")
			IF_writer		= revexe("IF_writer")
			IF_typeStr		= revexe("IF_typeStr")
			IF_content		= revexe("IF_content")
				beforeURL=GetCurrUrlDir(0)
				imgUrl=beforeURL & InfoImgDir
				IF_content		= Replace(IF_content,InfoImgAdminDir,imgUrl)
			IF_pageNum		= revexe("IF_pageNum")
			IF_themeKey		= revexe("IF_themeKey")
			IF_contentKey	= revexe("IF_contentKey")
			IF_voteMode		= revexe("IF_voteMode")
			IF_isReply		= revexe("IF_isReply")
		revexe.close()
		Set revexe=Nothing

		mudiCN = "修改"
	Else
		If US_isNewsAdd=0 Then
			response.write("<br /><br /><center>文章发表已关闭，有问题请联系管理员</center>")
			response.End()
		End If
		
		IF_ID			= 0
		IF_theme		= ""
		IF_source		= US_newsSource
		IF_writer		= US_newsWriter
		IF_typeStr		= ""
		IF_content		= ""
		IF_pageNum		= ""
		IF_themeKey		= ""
		IF_contentKey	= ""
		IF_voteMode		= 1
		IF_isReply		= 1

		mudiCN = "发表"
	End If


	response.write(_
	"<form id='dealForm' name='dealForm' method='post' action='usersNews_deal.asp?mudi=deal' onsubmit='return CheckNewsForm();'>"&_
	"<script language='javascript' type='text/javascript'>document.write(""<input type='hidden' name='URL' value='""+ document.location.href +""' />"")</script>"&_
	"<input type='hidden' id='dataID' name='dataID' value='"& IF_ID &"' />"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt><span class='font2_2'>*</span>标题：</dt>"&_
		"<dd>"&_
			"<input type='text' id='theme' name='theme' class='text' value='"& ReplaceStr(IF_theme,"input") &"' style='width:500px;' />"&_
			"&nbsp;&nbsp;<a href='javascript:void(0);' onclick='CheckRepeatTheme();return false;' style='text-decoration:underline;'>检测重复标题</a>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt>来源：</dt>"&_
		"<dd>"&_
			"<input type='text' id='source' name='source' class='text' value='"& ReplaceStr(IF_source,"input") &"' style='width:500px;' />"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt>作者：</dt>"&_
		"<dd>"&_
			"<input type='text' id='writer' name='writer' class='text' value='"& ReplaceStr(Replace(IF_writer,"{username}",UE_username),"input") &"' style='width:500px;' />"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt><span class='font2_2'>*</span>分类：</dt>"&_
		"<dd>"&_
			"<select id='typeStr' name='typeStr'>"&_
			"<option value=''></option>"&_
			"")
			typeNum = 0
			Set typeexe=conobj.execute("select * from OT_infoType where IT_level=1 and IT_isUser=1 order by IT_rank ASC")
				Do Until typeexe.EOF
					typeNum = typeNum + 1
					Set type2exe=conobj.execute("select * from OT_infoType where IT_level=2 and IT_fatID="& typeexe("IT_ID") &" and IT_isUser=1 order by IT_rank ASC")
						If type2exe.EOF Then
					response.write(_
					"<option value=',"& typeexe("IT_ID") &",' "& IsInstrSelected(IF_typeStr,","& typeexe("IT_ID") &",") &">"& typeNum &"、"& typeexe("IT_theme") &"</option>"&_
					"")
						Else
					response.write(_
					"<optgroup label='"& typeNum &"、"& typeexe("IT_theme") &"' style='font-weight:normal;'></optgroup>"&_
					"")
						End If
						Do Until type2exe.EOF
							response.write(_
							"<option value=',"& typeexe("IT_ID") &","& type2exe("IT_ID") &",' "& IsInstrSelected(IF_typeStr,","& type2exe("IT_ID") &",") &">&nbsp;&nbsp;&nbsp;┣&nbsp;"& type2exe("IT_theme") &"</option>"&_
							"")
						type2exe.movenext()
						Loop
					type2exe.close

				typeexe.movenext()
				Loop
			typeexe.close
			Set typeexe=Nothing
			response.write(_
			"</select>&nbsp;"&_
			"<span class='font2_2'>如果有二级分类,一级分类不能选择</span>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt><span class='font2_2'>*</span>内容：</dt>"&_
		"<dd>"&_
			"<textarea id='content' name='content' cols='40' rows='4' style='width:506px;height:200px;' class='text' onclick=""LoadEditor('content',300);clearTimeout(editorObjTime);"" title='点击开启编辑器模式'>"& IF_content &"</textarea>"&_
			"<div>"&_
				"<input type='button' onclick=""KE.insertHtml('content', '[OT_page]');"" value='插入分页符' />"&_
			"</div>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt>自动分页字数：</dt>"&_
		"<dd>"&_
			"<input type='text' id='pageNum' name='pageNum' size='50' style='width:50px;' value="""& IF_pageNum &""" />"&_
			"&nbsp;<span class='font2_2'>页数=内容总字数÷自动分页字数；如果在内容中加入了手动分页符或不想分页,请填写0或留空</span>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt>关键词(标签)：</dt>"&_
		"<dd>"&_
			"<input type='text' id='themeKey' name='themeKey' class='text' value='"& ReplaceStr(IF_themeKey,"input") &"' style='width:500px;' />"&_
			"&nbsp;<a href='javascript:void(0);' onclick='GetDzKeyWord();return false;' style='text-decoration:underline;'>网络自动获取</a>"&_
			"&nbsp;<a href='javascript:void(0);' onclick='GetKeyWord();return false;' style='text-decoration:underline;'>本地自动获取</a>"&_
			"<br /><span class='font2_2'>多个关键词用空格、竖杆“|”或逗号“,”隔开</span>"&_
			"&nbsp;<span id='onloadThemeKey' class='font3_2'></span>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"<dl>"&_
		"<dt>内容摘要：</dt>"&_
		"<dd>"&_
			"<textarea id='contentKey' name='contentKey' cols='40' rows='4' style='width:500px;height:60px;' class='text'>"& ReplaceStr(IF_contentKey,"input") &"</textarea>"&_
			"&nbsp;<a href='javascript:void(0);' onclick='ToContentKey();return false;' style='text-decoration:underline;'>自动获取</a>"&_
			"<br /><span class='font2_2'>内容摘要为空则自动获取</span>"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"")
		If SYS_isNewsVote>0 Then
	response.write(_
	"<dl>"&_
		"<dt>投票方式：</dt>"&_
		"<dd>"&_
			"<label><input type='radio' name='voteMode' value='0' "& IsChecked(IF_voteMode,0) &" />关闭</label>&nbsp;&nbsp;"&_
			"<label><input type='radio' name='voteMode' value='1' "& IsChecked(IF_voteMode,1) &" />心情</label>&nbsp;&nbsp;"&_
			"<label><input type='radio' name='voteMode' value='2' "& IsChecked(IF_voteMode,2) &" />顶踩</label>&nbsp;&nbsp;"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"")
		End If
		If SYS_isNewsReply>0 Then
	response.write(_
	"<dl>"&_
		"<dt>评论区：</dt>"&_
		"<dd>"&_
			"<label><input type='radio' name='isReply' value='1' "& IsChecked(IF_isReply,1) &" />显示</label>&nbsp;&nbsp;"&_
			"<label><input type='radio' name='isReply' value='0' "& IsChecked(IF_isReply,0) &" />隐藏</label>&nbsp;&nbsp;"&_
		"</dd>"&_
	"</dl>"&_
	"<div class='clear'></div>"&_
	"")
		End If
	response.write(_
	"<center style='padding-right:100px;'>"&_
		"<input type='submit' value='"& mudiCN &"' class='button1' />"&_
		"<!-- &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"&_
		"<input type='button' value='清空' class='button2' onclick='NewsClearBtn();' /> -->"&_
		"<span id='loadingStr'></span>"&_
	"</center>"&_
	"</form>"&_
	"")

End Sub



' 文章管理
Sub NewsManage()

	UE_ID		= userexe("UE_ID")

	refTypeStr	= ToGetRegExpStr("refTypeStr","sql+,")
	refState	= ToPostInt2("refState",-1)
	refTheme	= ToGetRegExpStr("refTheme","sql")
	refSource	= ToGetRegExpStr("refSource","sql")
	refWriter	= ToGetRegExpStr("refWriter","sql")

	SQLstr="select IF_ID,IF_theme,IF_time,IF_isAudit,IF_isGetScore from OT_info where IF_userID="& UE_ID &""

	If refTypeStr<>"" Then SQLstr=SQLstr &" and IF_typeStr like '%"& SqlForStr(refTypeStr) &"%'"
	If refState>-1 Then SQLstr=SQLstr &" and IF_state="& refState
	If refTheme<>"" Then SQLstr=SQLstr &" and IF_theme like '%"& SqlForStr(refTheme) &"%'"
	If refSource<>"" Then SQLstr=SQLstr &" and IF_source like '%"& SqlForStr(refSource) &"%'"
	If refWriter<>"" Then SQLstr=SQLstr &" and IF_writer like '%"& SqlForStr(refWriter) &"%'"

	response.write(_
	"<input type='hidden' id='score1Name' name='score1Name' value='"& US_score1Name &"' />"&_
	"<input type='hidden' id='score2Name' name='score2Name' value='"& US_score2Name &"' />"&_
	"<input type='hidden' id='score3Name' name='score3Name' value='"& US_score3Name &"' />"&_
	"<input type='hidden' id='score4Name' name='score4Name' value='"& US_score4Name &"' />"&_
	"<input type='hidden' id='score5Name' name='score5Name' value='"& US_score5Name &"' />"&_
	"<input type='hidden' id='delScore1' name='delScore1' value='"& US_newsDelScore1 &"' />"&_
	"<input type='hidden' id='delScore2' name='delScore2' value='"& US_newsDelScore2 &"' />"&_
	"<input type='hidden' id='delScore3' name='delScore3' value='"& US_newsDelScore3 &"' />"&_
	"<input type='hidden' id='delScore4' name='delScore4' value='"& US_newsDelScore4 &"' />"&_
	"<input type='hidden' id='delScore5' name='delScore5' value='"& US_newsDelScore5 &"' />"&_

	"<form style='padding:5px;'>"&_
	"分类：<select id='refTypeStr' name='refTypeStr'>"&_
		"<option value=''></option>"&_
		"")
		typeNum = 0
		Set typeexe=conobj.execute("select * from OT_infoType where IT_level=1 and IT_isUser=1 order by IT_rank ASC")
			Do Until typeexe.EOF
				typeNum = typeNum + 1
				Set type2exe=conobj.execute("select * from OT_infoType where IT_level=2 and IT_fatID="& typeexe("IT_ID") &" and IT_isUser=1 order by IT_rank ASC")
				response.write(_
				"<option value=',"& typeexe("IT_ID") &",' "& IsInstrSelected(refTypeStr,","& typeexe("IT_ID") &",") &">"& typeNum &"、"& typeexe("IT_theme") &"</option>"&_
				"")
					Do Until type2exe.EOF
						response.write(_
						"<option value=',"& typeexe("IT_ID") &","& type2exe("IT_ID") &",' "& IsInstrSelected(refTypeStr,","& type2exe("IT_ID") &",") &">&nbsp;&nbsp;&nbsp;┣&nbsp;"& type2exe("IT_theme") &"</option>"&_
						"")
					type2exe.movenext()
					Loop
				type2exe.close

			typeexe.movenext()
			Loop
		typeexe.close
		Set typeexe=Nothing
		response.write(_
		"</select>&nbsp;&nbsp;&nbsp;"&_
	"标题：<input type='text' name='refTheme' style='width:80px;' value='"& refTheme &"' />&nbsp;&nbsp;&nbsp;"&_
	"来源：<input type='text' name='refSource' style='width:80px;' value='"& refSource &"' />&nbsp;&nbsp;&nbsp;"&_
	"作者：<input type='text' name='refWriter' style='width:80px;' value='"& refWriter &"' />&nbsp;&nbsp;&nbsp;"&_
	"<input type='submit' value='查 询' onclick='CheckRefNewsForm();return false;' />&nbsp;&nbsp;"&_
	"<input type='button' value='重 置' onclick=""OpenPopup('newsManage');"" />"&_
	"</form>"&_
	"<table id='typeTable' width='98%' cellpadding='0' cellspacing='0' border='0' class='style1'>"&_
	"<thead>"&_
	"<tr>"&_
		"<td width='7%'>编号</td>"&_
		"<td width='50%'>标题</td>"&_
		"<td width='11%'>状态</td>"&_
		"<td width='20%'>发布时间</td>"&_
		"<td width='12%'>操作</td>"&_
	"</tr>"&_
	"</thead>"&_
	"<tbody>"&_
	"")
		Set showrec=server.CreateObject("ADODB.recordset")
		showrec.open SQLstr &" order by IF_time DESC",conobj,3,1
			If showrec.EOF Then
				response.write("</table><br /><center class='font1_1'>暂无内容</center>")
				response.End
			End If
				showPageSize=20		'每页条数
				showrec.pagesize=showPageSize	
				showPageCount=showrec.pagecount
				showCount=showrec.recordcount
				page=ToGetInt("page")
				if page < 1 Or page > showPageCount Then page=1
				showrec.absolutepage=page

			number=1+(page-1)*showPageSize
			For i=1 To showPageSize
				If showrec.EOF Then Exit For
				If showrec("IF_isAudit")=1 Then auditCN="<span style='color:green;'>已审核</span>" Else auditCN="<span style='color:red;'>待审核</span>"
				response.write(_
				"<tr id='data"& showrec("IF_ID") &"'>"&_
					"<td>"& number &"</td>"&_
					"<td align='left'>"& showrec("IF_theme") &"</td>"&_
					"<td>"& auditCN &"</td>"&_
					"<td>"& showrec("IF_time") &"</td>"&_
					"<td>"&_
						"<a href='javascript:void(0);' onclick=""OpenPopup('revNews','&dataID="& showrec("IF_ID") &"');return false;"" class='font3_2'>修改</a>"&_
						"&nbsp;"&_
						"<a href='javascript:void(0);' onclick='DelNews("& showrec("IF_ID") &","& showrec("IF_isGetScore") &");return false;' class='font2_2'>删除</a>"&_
					"</td>"&_
				"</tr>"&_
				"")
			showrec.movenext
			number=number+1
			Next
	showrec.close
	Set showrec=Nothing

	response.write(_
	"</tbody>"&_
	"<tbody id='newAddStr'>"&_
	"</tbody>"&_
	"</table>"&_

	"<table align='center' style='margin-top:2px;'>"&_
	"<tr>"&_
		"<td>"&_
		"")
			Call AjaxShowNavigation("dialogBody",showPageCount,showCount,"","")
		response.write(_
		"</td>"&_
	"</tr>"&_
	"</table>"&_
	"")
End Sub
%>