<%
'//[功能] 检验提交的数据是否从本站点提交

Function CheckDataFrom()
	Dim server_v1,server_v2
	server_v1=Cstr(Request.ServerVariables("HTTP_REFERER"))
	server_v2=Cstr(Request.ServerVariables("SERVER_NAME"))
	
	If Mid(server_v1,8,Len(server_v2))<>server_v2 Then '外部提交的数据
		CheckDataFrom = False
	Else
		CheckDataFrom = True 
	End If
End Function	
'/格式化时间/
Function FormatTime(TestTime,style)
	Dim n,y,r,s,f,m
	n = Year(TestTime)
	y = Month(TestTime)
	r = Day(TestTime)
	s = Hour(TestTime)
	f = Minute(TestTime)
	m = Second(TestTime)
	if len(n) = 2 then n = "20" & n
	if len(y) = 1 then y = "0" & y
	if len(r) = 1 then r = "0" & r						
	if len(s) = 1 then s = "0" & s
	if len(f) = 1 then f = "0" & f
	if len(m) = 1 then m = "0" & m
	If style = 1 Then
		FormatTime = "<font color=""#FF0000"">"& n &"</font>年<font color=""#FF0000"">"& y &"</font>月<font color=""#FF0000"">"& r &"</font>日<font color=""#FF0000"">"& s &"</font>时"
	Elseif style = 2 Then
		FormatTime = r &"日 "& s & ":" & f & ":" & m
	Elseif style = 3 Then
		FormatTime = n &"年"& y &"月"& r &"日"
	Elseif style = 4 Then
		FormatTime = n & "/" & y & "/" & r
	Elseif style = 5 then
		FormatTime = n &"-"& y &"-"& r &"&nbsp;" & s & ":" & f
	Elseif style = 6 then
		FormatTime = n &"年"& y &"月"& r &"日" & s &":"& f
	Elseif style = 7 then
		FormatTime =  n & y & r & s & f & m
	Elseif style = 8 then
		FormatTime = n &"-"& y &"-"& r
	End if
End Function

'//[功能] 检查' " &
'//[参数] strCheck 要检查的字符串

Function CheckString(strCheck) 
	If Instr(strCheck,"'")<1 and Instr(strCheck,"""")<1 and Instr(strCheck,"&")<1 then
		CheckString = True
	Else
		CheckString = False
	End if
End Function


'//[功能] 检查管理员是否登陆

Function CheckAdminPermission()
	If Trim(Session("CnltrAdminName"))<>"" then 
		CheckAdminPermission=TRUE
	Else
		CheckAdminPermission=FALSE
	End if
	
	adminUser = Trim(Session("CnltrAdminName"))
End Function

function GetIp()
	'如果客户端用了代理服务器，则应该用ServerVariables("HTTP_X_FORWARDED_FOR")方法
	getclientip = Request.ServerVariables("HTTP_X_FORWARDED_FOR")
	If getclientip = "" Then
		'如果客户端没用代理，应该用Request.ServerVariables("REMOTE_ADDR")方法
		getclientip = Request.ServerVariables("REMOTE_ADDR")
	end if
	GetIp = getclientip
end function

'//[功能] 用Jmail发送E-mail
'//[参数] SenderName 发送者姓名,serveraddress 服务器地址,Charset 编码,Subject 邮件标题,body 邮件内容,sender 发送地址
'//       AddRecipient 接受者地址,ContentType 邮件格式

Function SendJMail(SenderName,serveraddress,Charset,Subject,body,sender,AddRecipient,ContentType)
	If IsObjInstalled("JMail.SMTPMail") Then
		set jmail=server.CreateObject("JMail.SMTPMail")		
		jmail.Priority=3						
		jmail.SenderName=SenderName
		'jmail.serveraddress=serveraddress
		jmail.Charset=Charset					
		jmail.subject=Subject			
		jmail.body=body
		jmail.sender=sender
		jmail.AddRecipient(AddRecipient)	
		jmail.ContentType=ContentType
		jmail.Execute							
		set jmail=nothing				
	End If
End function 

'//[功能] 用CDONTS发送E-mail
'//[参数] SenderName 发送者姓名,serveraddress 服务器地址,Charset 编码,Subject 邮件标题,body 邮件内容,sender 发送地址
'//       AddRecipient 接受者地址,ContentType 邮件格式

Function SendCMail(SenderName,serveraddress,Charset,Subject,body,sender,AddRecipient,ContentType)
	Set MailObject = Server.CreateObject("CDONTS.NewMail")
	MailObject.From = SenderName
	MailObject.To = AddRecipient
	MailObject.Subject = Subject
	MailObject.Body = body
	MailObject.BodyFormat = 0
	MailObject.MailFormat = 0 
	MailObject.Importance = 2
	MailObject.Send
	Set MailObject = Nothing			
End function 


'//[功能] 替换' " & < >
'//[参数] strCheck 要替换的字符串

Function RtnReplaceString(strCheck) 
	RtnReplaceString = Replace(Replace(Replace(Replace(Replace(strCheck,"'","‘"),chr(34),"&quot;"),"&",""),"<","&lt;"),">","&gt;")
End Function

'//[功能] 判断是否是数字，否则用默认值替换
'//[参数] iCheck 要替换的变量,iDefault 默认值

Function RtnReplaceInt(iCheck,iDefault) 
	If Trim(iCheck)="" Then
		RtnReplaceInt = iDefault
		Exit Function
	End If

	If IsNumeric(iCheck)=false Then
		RtnReplaceInt = iDefault
		Exit Function
	End If

	RtnReplaceInt = iCheck
End Function


'//[功能] 转换html语言和ASCI之间的显示。

function htmlencode(str)
    dim result
    dim l
    if isNULL(str) then 
       htmlencode=""
       exit function
    end if
    l=len(str)
    result=""
	dim i
	for i = 1 to l
	    select case mid(str,i,1)
	           case "<"
	                result=result+"&lt;"
	           case ">"
	                result=result+"&gt;"
              case chr(13)
	                result=result+"<br>"
	           case chr(34)
	                result=result+"&quot;"
	           case "&"
	                result=result+"&amp;"
              case chr(32)	           
	                'result=result+"&nbsp;"
	                if i+1<=l and i-1>0 then
	                   if mid(str,i+1,1)=chr(32) or mid(str,i+1,1)=chr(9) or mid(str,i-1,1)=chr(32) or mid(str,i-1,1)=chr(9)  then	                      
	                      result=result+"&nbsp;"
	                   else
	                      result=result+" "
	                   end if
	                else
	                   result=result+"&nbsp;"	                    
	                end if
	           case chr(9)
	                result=result+"    "
	           case else
	                result=result+mid(str,i,1)
         end select
       next 
       htmlencode=result
end function


'//[功能] email有效性检查

function IsValidEmail(email)
dim names, name, i, c

IsValidEmail = true
names = Split(email, "@")
if UBound(names) <> 1 then
   IsValidEmail = false
   exit function
end if
for each name in names
   if Len(name) <= 0 then
     IsValidEmail = false
     exit function
   end if
   for i = 1 to Len(name)
     c = Lcase(Mid(name, i, 1))
     if InStr("abcdefghijklmnopqrstuvwxyz_-.", c) <= 0 and not IsNumeric(c) then
       IsValidEmail = false
       exit function
     end if
   next
   if Left(name, 1) = "." or Right(name, 1) = "." then
      IsValidEmail = false
      exit function
   end if
next
if InStr(names(1), ".") <= 0 then
   IsValidEmail = false
   exit function
end if
i = Len(names(1)) - InStrRev(names(1), ".")
if i <> 2 and i <> 3 then
   IsValidEmail = false
   exit function
end if
if InStr(email, "..") > 0 then
   IsValidEmail = false
end if

end function

'//[功能] 将一文字中的HTML的标识去掉
Function FilterHTML(strToFilter)
	Dim strTemp
	strTemp = strToFilter
	While Instr(1,strTemp,"<") AND Instr(1, strTemp, ">")
		strTemp = Left(strTemp, Instr(1, strTemp, "<")-1) & Right(strTemp, Len(strTemp)-Instr(1,strTemp, ">"))
	WEnd
	FilterHTML = strTemp
End Function
'=================================================
'fy_DelFiles(strFiles)
'strdir           ----(要删除的文件)
'删除指定文件
'=================================================
Sub fy_DelFiles(strFiles)
		if strFiles="" then Exit Sub
		dim fso,arrFiles,i
		On Error Resume Next
		Err=0
		Set fso = CreateObject("Scripti"&"ng.File"&"SystemO"&"bject")
			if fso.FileExists(server.MapPath(strFiles)) then
				fso.DeleteFile(server.MapPath(strFiles))
				if 0=Err then
				response.write"<font color=blue>清除文件（"&strFiles&"）成功！</font><br>"
				else
				response.write"<font color=red>清除文件（"&strFiles&"）失败！</font><br>"
				end if
			end if
		Set fso = Nothing
		Err=0
	End Sub
%>