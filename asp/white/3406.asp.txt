<%@LANGUAGE="VBSCRIPT" CODEPAGE="936"%>
<%
Const HTML_Article_Path = "article/"
Const HTML_Soft_Path = "soft/"

If Session("Ok3w_toHtml")="" Then
	Response.Write("你还没登陆或是登陆超时")
	Response.End()
End If

Cmd = Application("Html_Cmd")
sTime = Application("Html_sTime")
sPath = Application("Html_sPath")

PageNo = Request.QueryString("PageNo")
If PageNo = "" Then PageNo = 1
pLen = 1

RsCount = Cdbl(Application("Html_RsCount"))
xCount = Cdbl(Application("Html_xCount"))

If Application("Tmp_RsCount") = "" Then
	Application("Tmp_RsCount") = RsCount + 1
	Application("Tmp_xCount") = xCount + 1
End If
Tmp_RsCount = Application("Tmp_RsCount")
Tmp_xCount = Application("Tmp_xCount")

Select Case Cmd
	Case "Index_Html"
		Call Index_Html()
		
	Case "Article_List_Html"
		ID = Application("Html_ArrData")(0,xCount)
		pLen = Application("Html_ArrData")(1,xCount)
		
		Call Article_List_Html(PageNo,ID)
		
	Case "Article_Page_Html"
		ID = Application("Html_ArrData")(0,xCount)
		pLen = Application("Html_ArrData")(1,xCount)
		
		Call Article_Page_Html(1,ID,PageNo)
		
	Case "About_Page_Html"
		ID = Application("Html_ArrData")(0,xCount)
		pLen = Application("Html_ArrData")(1,xCount)
		
		Call Article_Page_Html(2,ID,PageNo)
		
End Select

Private Sub Index_Html()
	FromURL = sPath & "index.asp?rnd=" & Now()
	FilePath = "../index.html"
	Call ToHtml(FromURL,FilePath)
	
	FromURL = sPath & "inc/rss.asp?rnd=" & Now()
	FilePath = "../" & HTML_Article_Path & "rss.xml"
	Call ToHtml(FromURL,FilePath)
End Sub

Private Sub Article_Page_Html(ChannelID,ID,iPage)
	If iPage = 1 Then
		FilePath = "../" & HTML_Article_Path & ID & ".html"
	Else
		FilePath = "../" & HTML_Article_Path & ID & "_" & iPage & ".html"
	End If
	If ChannelID=1 Then
		FromURL = sPath & "show.asp?id=" & ID & "&thisPage=" & iPage & "&rnd=" & Now()
	Else
		FromURL = sPath & "about.asp?id=" & ID & "&thisPage=" & iPage & "&rnd=" & Now()
	End If
	Call ToHtml(FromURL,FilePath)	
End Sub

Private Sub Article_List_Html(PageNo,ClassID)
	FromURL = sPath & "list.asp?id=" & ClassID & "&PageNo=" & PageNo & "&rnd=" & Now()
	FilePath = "../" & HTML_Article_Path & "list_" & ClassID
	If PageNo<>1 Then FilePath = FilePath & "_" & PageNo
	FilePath = FilePath & ".html"
	Call ToHtml(FromURL,FilePath)
End Sub

Private Sub ToHtml(FromURL,FilePath)
	Response.Write("<a href=""" & FilePath & """ target=""_blank"">" & FilePath & " --> Ok</a><br>")
	FilePath = Server.MapPath(FilePath)
	Set objXmlHttp=Server.createObject("Microsoft." & "XMLHTTP")
	objXmlHttp.open "GET",FromURL,false
	objXmlHttp.send()
	binFileData=objXmlHttp.responseBody
	Set objXmlHttp = Nothing
	Set objAdoStream=Server.createObject("ADODB." & "Stream")
	objAdoStream.Type=1
	objAdoStream.Open()
	objAdoStream.Write(binFileData)
	objAdoStream.SaveToFile FilePath,2
	objAdoStream.Close()
	Set objAdoStream = Nothing
End Sub

If Cdbl(PageNo)<Cdbl(pLen) Then
	If PageNo = 1 Then
		Application("Tmp_RsCount") = Cdbl(Application("Tmp_RsCount")) + Cdbl(pLen) - 1
	End If
	Application("Tmp_xCount") = Cdbl(Application("Tmp_xCount")) + 1
	Response.Write("<meta http-equiv=""refresh"" content=""" & sTime & ";URL=?PageNo=" & Cdbl(PageNo)+1 & "&rnd=" & Now() & """ />")
Else
	If xCount<RsCount Then
		Application("Tmp_xCount") = Cdbl(Application("Tmp_xCount")) + 1
		Application("Html_xCount") = Cdbl(Application("Html_xCount")) + 1
		Response.Write("<meta http-equiv=""refresh"" content=""" & sTime & ";URL=?PageNo=1&rnd=" & Now() & """ />")
	End If
End If
%>

<style type="text/css">
body{margin:0px; padding:0px; font-size:12px; color:#FF0000;}
</style>

<div style="border:1px solid #CCCCCC; width:454px; margin:20px; height:30px; position:relative; padding:1px;">
<div style="background-color:#0000FF; height:26px; width:<%=Int(Tmp_xCount/Tmp_RsCount*450)%>px; position:absolute; z-index:99;">
</div>
<div style="position:absolute; z-index:999; color:#FF0000; line-height:26px; width:450px; text-align:center;"><%=Tmp_xCount%>/<%=Tmp_RsCount%>（<%=Int(Tmp_xCount/Tmp_RsCount*100)%>%）
</div>
</div>