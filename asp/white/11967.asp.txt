<%
	'-----------------Author: Yonbin Meng------------------
	'This class is for connect to a database and open a table
	'Read it or updae ti
	CLASS mybDatabase
		PRIVATE conn		'Connection to database
		PUBLIC RS			'RecordSet object 
		'Set the database name
		PUBLIC PROPERTY LET DBName(path)
			DIM db, connStr
			CALL Disconnect()
			db = Server.Mappath(path)
			SET conn = Server.CreateObject("ADODB.Connection")
			connStr="Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" & db
			CALL conn.Open(connStr)
		END PROPERTY
		
		'Open a database's table for reading
		PUBLIC SUB Open_Read(sqlStr)
			CALL Close_RS()
			SET rs = Server.CreateObject("ADODB.RecordSet")
			'Read Only
			CALL rs.Open(sqlStr, conn, 1, 1)
		END SUB

		'Open a database's table for writing
		PUBLIC SUB Open_Write(sqlStr)
			CALL Close_RS()
			SET rs = Server.CreateObject("ADODB.RecordSet")
			'Write Update Delete
			CALL rs.Open(sqlStr, conn, 1, 3)
		END SUB

		'Close the RecordSet 
		PRIVATE SUB Close_RS()
			IF NOT IsEmpty(rs) THEN
				IF rs.State = 1 THEN
					CALL rs.Close()
				END IF
				SET rs = NOTHING
			END IF
			
		END SUB

		'Disconnect the connection 
		PRIVATE SUB Disconnect()
			IF NOT IsEmpty(conn) THEN
				IF conn.State = 1 THEN
					CALL conn.Close()
				END IF
				SET conn = NOTHING
			END IF
		END SUB

		'Close the recordSet and disconnect the connection
		PUBLIC SUB Close()
			CALL Close_RS()
			CALL Disconnect()
		END SUB 

		'Execute the SQL string 
		PUBLIC SUB ExecuteNonQuery(sql)
			IF IsEmpty(conn) THEN EXIT SUB
			IF conn.State <> 1 THEN EXIT SUB
			
			CALL conn.Execute(sql)
		END SUB 

	END CLASS 
%>