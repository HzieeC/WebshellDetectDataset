<!--#include FILE="upload.inc"-->
<%	
dim upload,file,formName,title,state,picSize,picType,uploadPath,fileExt,fileName,prefix

uploadPath = "/uploadfile/news/"&getFolderName()&"/"     '上传保存路径
picSize = 500                        '允许的文件大小，单位KB
picType = ".jpg,.gif,.png,.bmp"      '允许的图片格式


Set fso = Server.CreateObject("Scripting.FileSystemObject")
if fso.folderexists(server.mappath(uploadPath))=false then
	Fso.Createfolder(server.mappath(uploadPath))
end if
set fso = Nothing

'文件上传状态,当成功时返回SUCCESS，其余值将直接返回对应字符窜并显示在图片预览框，同时可以在前端页面通过回调函数获取对应字符窜
state="SUCCESS"

set upload=new upload_5xSoft         '建立上传对象
title = htmlspecialchars(upload.form("pictitle"))

for each formName in upload.file
	set file=upload.file(formName)

	'大小验证
	if file.filesize > picSize*1024 then
		state="图片大小超出限制"
	end if

	'格式验证
	fileExt = lcase(mid(file.FileName, instrrev(file.FileName,".")))
	if instr(picType, fileExt)=0 then
		state = "图片类型错误"
	end If

	'保存图片
	if state="SUCCESS" then
		fileName = uploadPath & getFileName() & fileExt
		file.SaveAs Server.mappath( fileName)
	end if
	
	'返回数据
	response.Write "{'url':'" & fileName & "','title':'"& title &"','state':'"& state &"'}"
	set file=nothing

next
set upload=nothing

function htmlspecialchars(someString)
	htmlspecialchars = replace(replace(replace(replace(someString, "&", "&amp;"), ">", "&gt;"), "<", "&lt;"), """", "&quot;")
end function
	

Function getFolderName()
	CurDate = Now()
	Nyear = year(CurDate)
	Nmonth = month(CurDate)
	if Len(Nmonth)	= 1 then	Nmonth	= "0"&Nmonth
	getFolderName	= Nyear&Nmonth
End Function
Function getFileName()
	randomize
	ranNum = int(90000*rnd)+10000
	CurDate = Now()
	Nday = day(CurDate)
	Nhour = hour(CurDate)
	Nminute = minute(CurDate)
	Nsecond = second(CurDate)
	if Len(Nday)	= 1 then	Nday	= "0"&Nday
	if Len(Nhour)	= 1 then	Nhour	= "0"&Nhour
	if Len(Nminute)	= 1 then	Nminute	= "0"&Nminute
	if Len(Nsecond)	= 1 then	Nsecond	= "0"&Nsecond
	getFileName	= Nday&Nhour&Nminute&Nsecond&ranNum
End Function
%>