<!--#include file="../../Conn.asp" -->
<!--#include file="../../Comm/Site_Class.asp" -->
<%
PagePath = "../../"

Set Qsite = New Site_Class

Qsite.SiteConfig_Master()
%>
<!--#include file="../Check_Login.asp" -->
<%
Qsite.Check_AdminFlag(arrAdminFlag(6 , 3))

If Request.Form <> "" Then
	If Request.Form("FriendLinkTypeId") = 0 Then
		SaveNewFriendLinkType()
	Else
		SaveEditFriendLinkType()
	End If
End If

If Request.QueryString("action") = "del" Then
	DelFriendLinkType(Qsite.chgStr(Trim(Request.QueryString("FriendLinkTypeId"))))
ElseIf Request.QueryString("action") = "up" Then
	UpPx(Qsite.chgStr(Trim(Request.QueryString("FriendLinkTypeId"))))
ElseIf Request.QueryString("action") = "down" Then
	DownPx(Qsite.chgStr(Trim(Request.QueryString("FriendLinkTypeId"))))
End If

Sub SaveNewFriendLinkType()
	
	Dim FriendLinkTypeName,Px
	FriendLinkTypeName = Qsite.chgStr(Trim(Request.Form("FriendLinkTypeName")))
	Px = Qsite.chgStr(Trim(Request.Form("Px")))
	
	If FriendLinkTypeName = "" Or IsNull(FriendLinkTypeName) Then
		Qsite.ShowMsg "请输入友情链接分类名称" , ""
	End If

	If Px = "" Or IsNull(Px) Or Not IsNumeric(Px) Then Px = 1
	
	Set Rs = Conn.Execute("select FriendLinkTypeName from FriendLinkTypeTbl where FriendLinkTypeName = '"& FriendLinkTypeName &"'")
	If Not Rs.Eof Then
		Qsite.ShowMsg "友情链接分类已经存在，请返回重新输入" , ""
	End If
	Set Rs = Nothing
	
	Conn.Execute("update FriendLinkTypeTbl set Px = Px + 1 where Px >= "& Px)
	
	Conn.Execute("insert into FriendLinkTypeTbl (FriendLinkTypeName , Px) values ('"& FriendLinkTypeName &"' , "& Px &")")
	
	Qsite.ShowMsg "友情链接分类添加成功" , "FriendLinkType.asp"
End Sub

Sub SaveEditFriendLinkType()
	Dim FriendLinkTypeId,FriendLinkTypeName,Px
	Dim OldPx
	FriendLinkTypeId = Qsite.chgStr(Trim(Request.Form("FriendLinkTypeId")))
	FriendLinkTypeName = Qsite.chgStr(Trim(Request.Form("FriendLinkTypeName")))
	Px = Qsite.chgStr(Trim(Request.Form("Px")))
	
	If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then FriendLinkTypeId = 0
	
	If FriendLinkTypeName = "" Or IsNull(FriendLinkTypeName) Then
		Qsite.ShowMsg "请输入友情链接分类名称" , ""
	End If
	
	If Px = "" Or IsNull(Px) Or Not IsNumeric(Px) Then Px = 1
	
	Set Rs = Conn.Execute("select FriendLinkTypeName from FriendLinkTypeTbl where FriendLinkTypeName = '"& FriendLinkTypeName &"' and FriendLinkTypeId <> "& FriendLinkTypeId)
	If Not Rs.Eof Then
		Qsite.ShowMsg "友情链接分类已经存在，请返回重新输入" , ""
	End If
	Set Rs = Nothing
	
	Set Rs = Conn.Execute("select Px from FriendLinkTypeTbl where FriendLinkTypeId = "& FriendLinkTypeId)
	If Rs.Eof Then
		OldPx = Px
	Else
		OldPx = Rs("Px")
	End If
	
	If Cint(Px) > Cint(OldPx) Then
		Conn.Execute("update FriendLinkTypeTbl set Px = Px - 1 where Px > "& OldPx &" and Px <= "& Px)
	ElseIf Cint(Px) < Cint(OldPx) Then
		Conn.Execute("update FriendLinkTypeTbl set Px = Px + 1 where Px >= "& Px &" and Px < "& OldPx)
	End If
	
	Conn.Execute("update FriendLinkTypeTbl set FriendLinkTypeName = '"& FriendLinkTypeName &"' , Px = "& Px &" where FriendLinkTypeId = "& FriendLinkTypeId)
	
	Qsite.ShowMsg "友情链接分类修改成功" , "FriendLinkType.asp"
End Sub

Sub DelFriendLinkType(FriendLinkTypeId)
	Dim OldPx,Rs,dKey
	Dim IncludeFriendLinkTypeId
	
	If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then FriendLinkTypeId = 0
	
	Set Rs = Conn.Execute("select Px from FriendLinkTypeTbl where FriendLinkTypeId = "& FriendLinkTypeId)
	If Rs.Eof Then
		OldPx = 9999
	Else
		OldPx = Rs("Px")
	End If
	Set Rs = Nothing
	
	Conn.Execute("delete from FriendLinkTbl where FriendLinkTypeId =" & FriendLinkTypeId)
	
	Conn.Execute("delete from FriendLinkTypeTbl where FriendLinkTypeId =" & FriendLinkTypeId)
	
	Conn.Execute("update FriendLinkTypeTbl set Px = Px - 1 where Px > "& OldPx)
	
	Qsite.ShowMsg "友情链接分类删除成功" , "FriendLinkType.asp"
End Sub

Sub UpPx(FriendLinkTypeId)
	Dim OldPx
	
	If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then FriendLinkTypeId = 0
	
	Set Rs = Conn.Execute("select Px from FriendLinkTypeTbl where FriendLinkTypeId = "& FriendLinkTypeId)
	If Rs.Eof Then
		OldPx = 1
	Else
		OldPx = Rs("Px")
	End If
	Set Rs = Nothing
	
	If Cint(OldPx) > 1 Then
		Conn.Execute("update FriendLinkTypeTbl set Px = Px + 1 where Px = "& OldPx - 1)
		Conn.Execute("update FriendLinkTypeTbl set Px = Px - 1 where FriendLinkTypeId = "& FriendLinkTypeId)
	End If
	
	Response.Redirect("FriendLinkType.asp")
	Response.End()
End Sub

Sub DownPx(FriendLinkTypeId)
	Dim maxPx,OldPx
	
	If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then FriendLinkTypeId = 0
	
	Set Rs = Conn.Execute("select Px from FriendLinkTypeTbl where FriendLinkTypeId = "& FriendLinkTypeId)
	If Rs.Eof Then
		OldPx = 9999
	Else
		OldPx = Rs("Px")
	End If
	Set Rs = Nothing
	
	Set Rs = Conn.Execute("select top 1 Px from FriendLinkTypeTbl order by Px desc")
	If Rs.Eof Then
		maxPx = 9999
	Else
		maxPx = Rs("Px")
	End If
	Set Rs = Nothing
	
	If Cint(OldPx) < Cint(maxPx) Then
		Conn.Execute("update FriendLinkTypeTbl set Px = Px - 1 where Px = "& OldPx + 1)
		Conn.Execute("update FriendLinkTypeTbl set Px = Px + 1 where FriendLinkTypeId = "& FriendLinkTypeId)
	End If
	
	Response.Redirect("FriendLinkType.asp")
	Response.End()
End Sub

Dim FriendLinkTypeId,FriendLinkTypeName,FriendLinkTypeKey,Px,contPx
Dim strTitle,strSubmit

Set Rs = server.createobject("ADODB.Recordset")

Sql = "select * from FriendLinkTypeTbl order by Px asc"

Rs.Open "select top 1 * from FriendLinkTypeTbl order by Px desc" , Conn ,1 , 1
If Rs.Eof Then
	contPx = 1
Else
	contPx = Rs("Px") + 1
End If
Rs.Close
%>
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
<title>友情链接分类管理</title>
<link href="../skins/css/main.css" rel="stylesheet" type="text/css">
<script language="javascript">
function FriendLinkTypeDel(FriendLinkTypeId)
{
	var question = confirm("你确定要删除此条记录吗？");
	if (question !="0")
	{
		location.href = "FriendLinkType.asp?action=del&FriendLinkTypeId=" + FriendLinkTypeId;
	}
}
</script>
</head>

<body>
<%
FriendLinkTypeId = Qsite.chgStr(Trim(Request.QueryString("FriendLinkTypeId")))

If FriendLinkTypeId = "" Or IsNull(FriendLinkTypeId) Or Not IsNumeric(FriendLinkTypeId) Then FriendLinkTypeId = 0

If FriendLinkTypeId = 0 Then
	FriendLinkTypeName = ""
	Px = contPx
	strTitle = "添加友情链接分类"
	strSubmit = "添加"
Else
	Rs.Open "select * from FriendLinkTypeTbl where FriendLinkTypeId = "& FriendLinkTypeId , Conn , 1 , 1
	If Rs.Eof Then
		FriendLinkTypeId = 0
		FriendLinkTypeName = ""
		Px = contPx
		strTitle = "添加友情链接分类"
		strSubmit = "添加"
	Else
		FriendLinkTypeName = Rs("FriendLinkTypeName")
		Px = Rs("Px")
		strTitle = "修改友情链接分类"
		strSubmit = "修改"
	End If
End If
%>
<form name="PosForm" method="post" action="FriendLinkType.asp" style="margin:0">
	<table width="100%" border="0" cellspacing="1" cellpadding="5">
		<tr>
			<td height="25" class="td_title"><%=strTitle%></td>
		</tr>
		<tr>
			<td height="23" class="td_content">友情链接分类名称：
				<input name="FriendLinkTypeName" type="text" class="input_1" id="FriendLinkTypeName" size="50" value="<%=FriendLinkTypeName%>">
排序
<input name="Px" type="text" class="input_1" id="Px" size="20" value="<%=Px%>">			
	<input name="Submit" type="submit" class="input_1" value="<%=strSubmit%>">
	<%
	If FriendLinkTypeId <> 0 Then
	%>
	<input name="Submit" type="button" class="input_1" value="返回" onClick="javascript:history.back();">
	<%
	End If
	%>
	<input name="FriendLinkTypeId" type="hidden" id="FriendLinkTypeId" value="<%=FriendLinkTypeId%>"></td>
		</tr>
	</table>
	<br>
</form>
<%
If FriendLinkTypeId = 0 Then
%>
	<table width="100%" border="0" cellspacing="1" cellpadding="5">
		<tr>
			<td height="25" colspan="2" class="td_title">友情链接分类列表</td>
		</tr>
<%
Rs.Open Sql , Conn , 1 , 1
If Rs.Eof Then
%>
		<tr>
			<td height="23" colspan="2" class="td_content">暂无友情链接分类信息</td>
		</tr>
<%
Else
%>
		<tr>
<%
	i = 0
	Do While Not Rs.Eof
%>
			<td width="50%" height="23" class="td_content"><%=Rs("FriendLinkTypeName")%> &nbsp;&nbsp;&nbsp;[<a href="FriendLinkType.asp?action=up&FriendLinkTypeId=<%=Rs("FriendLinkTypeId")%>">前移</a> | <a href="FriendLinkType.asp?action=down&FriendLinkTypeId=<%=Rs("FriendLinkTypeId")%>">后移</a> | <a href="FriendLinkType.asp?FriendLinkTypeId=<%=Rs("FriendLinkTypeId")%>">修改</a> | <a href="#" onClick="FriendLinkTypeDel('<%=Rs("FriendLinkTypeId")%>');return false;">删除</a>]</td>
<%
	Rs.MoveNext
	i = i + 1
	If i Mod 2 = 0 And Not Rs.Eof Then
		Response.Write("</tr></tr>")
	End If
	If i Mod 2 > 0 And Rs.Eof Then
		Response.Write("<td width=""50%"" class=""td_content"">&nbsp;</td>")
	End If
	Loop
%>
			
		</tr>
<%
End If
Rs.Close

Set Rs = Nothing
%>
</table>
<%
End If
%>
</body>
</html>
