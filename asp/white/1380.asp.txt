<!--#include file="../inc/mdb.asp"-->
<!--#include file="../inc/config.asp"-->
<!--#include file="../inc/cmsfunction.asp"-->
<%
'****************************************************
' 花香盈 路网站管理系统07ing V8.0 Power by 07ing.Net
' Web: http://www.07ing. net, http://www.07ing.com
' Copyright (C) 2006 07ing .Net All Rights Reserved.
'****************************************************
Function GetBookType(t)
  if t=1 then
    GetBookType="<font color=""#0000FF"">[留言]</font> "
  elseif t=2 then 
    GetBookType="<font color=""#FF00FF"">[建议]</font> "
  elseif t=3 then 
    GetBookType="<font color=""#FF7F50"">[报错]</font> "
  elseif t=4 then 
    GetBookType="<font color=""#228B22"">[连接]</font> "
  elseif t=5 then 
    GetBookType="<font color=""#1E90FF"">[其它]</font> "
  end if
End Function
Dim rs,sql,Page,act,page07ing
act=Request.QueryString("act")
Page=Request.QueryString("page")
page07ing=5
Set rs = server.CreateObject("adodb.recordset")
sql="select * from words order by Id desc"
rs.PageSize =page07ing  
rs.Open sql,conn,3,3
If Act="getPageInfo" Then
   Response.Write rs.recordCount&"|"&page07ing&"|"&rs.PageCount&"|"&Session("CurPage")&"|"&Session("CurPage")-1&"|"&Session("CurPage")+1
   Response.End		
End If
If Page="undefined" and Session("CurPage")<>"" Then Page=Session("CurPage")	
If Not isNumeric(Page) Then
   Page=1
ElseIf Cint(Page)<1 Then
   Page=1
ElseIf Cint(Page)>rs.PageCount Then
   Page=rs.PageCount
Else
   Page=cint(Page)
End If
Session("CurPage")=Page
If Not rs.eof Then 
   rs.AbsolutePage = page 
End If 
Dim i,NO,TempStr,ReturnStr,PageSize,TotalRecord
Dim title,touxian,content,replys,tools,contents,titles
PageSize=rs.PageSize
TotalRecord=Rs.RecordCount
For i=1 to rs.PageSize
    If rs.eof or rs.bof Then
       Exit For
    End If
    NO=TotalRecord-i-(PageSize*Page)+PageSize+1
    If Rs("uc")<>"" Then
       touxian="<img align='absBottom' src='"&geturl(Rs("uc"))&"' alt='' />"
    elseif Rs("sex")=1 then
       touxian="<img src='../img/face/09.gif' alt='' />"
    elseif Rs("sex")=2 then
       touxian="<img src='../img/face/01.gif' alt='' />"
    End If
    title=GetBookType(Rs("title"))&rs("zt")
    content=UBB(rs("words"))
    replys=""
    If rs("reply")<>"" Then
       replys="<div class=""side_border_x""><span class=""red"">版主回复</span>于"&formatdatetime(rs("redate"),2)&"：<br />"&UBB(Rs("reply"))&"</div>"
    End If
    tools=""
    If Rs("email")<>"" Then
       tools=tools&"<a href=""mailto:"&Rs("email")&"""><img src=""../img/mail.gif"" title=""给"&Rs("name")&"写信"" alt="""" /></a> "
    End If
    If Rs("qq")<>"" Then
       tools=tools&"<img title="""&Rs("name")&"的QQ号码是"&Rs("qq")&""" src=""../img/qq.gif"" alt="""" /> "
    End If
    If Rs("web")<>"" Then
       tools=tools&"<a href="""&Rs("web")&""" target=""_blank""><img src=""../img/web.gif"" title=""去"&Rs("name")&"的主页看看"" alt="""" /></a> "
    End If
    tools=tools&"<img title="""&Rs("name")&"的IP地址为"&Rs("ip")&""" src=""../img/ip.gif"" alt="""" /> "
    tools=tools&"发表时间："&rs("date")
    If session("adminlogin")=sessionvar Then 
       tools=tools&"&nbsp;&nbsp;<a href=""javascript:void(0)"" onclick=""javascript:getreplyer('"&rs("id")&"');"">回复</a>&nbsp;&nbsp;<a href=""javascript:void(0)"" onclick=""javascript:getdels('"&rs("id")&"');"">删除</a><div id=""replyer"&rs("id")&""" style=""DISPLAY: none""></div>"
    End if
    If Rs("admin")=2 Then
       titles="<span>NO："&NO&"</span>(待审留言，请等待管理员审核)"
       contents="─────≡ 待审核留言 ≡─────"
       if session("adminlogin")=sessionvar Then
          titles="<span>NO："&NO&"</span>"&title
          contents="─────≡ 以下为待审核留言 ≡─────<br />"&content&replys
       End If
    elseIf Rs("admin")=1 Then
       titles="<span>NO："&NO&"</span>(秘密留言，管理员可见)"
       contents="─────≡ 秘密留言！≡─────"
       if session("adminlogin")=sessionvar Then
         titles="<span>NO："&NO&"</span>"&title
         contents="─────≡ 以下为秘密留言！≡─────<br />"&content&replys
       End If
    else
       titles="<span>NO："&NO&"</span>"&title
       contents=content&replys
    End If
    TempStr="{title:"""&encode(titles)&""",username:"""&encode(rs("name"))&""",touxian:"""&touxian&""",content:"""&encode(contents)&""",tools:"""&encode(tools)&"""},"
    ReturnStr=ReturnStr+TempStr
    rs.moveNext
Next
If ReturnStr<>"" Then
   ReturnStr=Left(ReturnStr,Len(ReturnStr)-1)
Else 			
   ReturnStr="0"
End If
Response.Write(ReturnStr)
rs.close
set rs=nothing
conn.close
set conn=nothing
'****************************************************
' 零柒。蜀工作室网站管理系统07ing V8.0 Power by 07ing.Net
' Web: http://www.07ing. net, http://www.07ing.com
' Copyright (C) 2006 Hx CMS. Net All Rights Reserved.
'****************************************************
%>