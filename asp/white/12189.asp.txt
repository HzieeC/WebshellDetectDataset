<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<!-- #include file="ckeditor/ckeditor.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


'打开用户表，并检测用户是否登陆
Call OpenMemberexe("","login",0)

Call WebStyleTop("")

response.write(_
"<script language='javascript' type='text/javascript' src='js/inc/list.js'></script>"&_
"<script language='javascript' type='text/javascript' src='js/tbkRank.js'></script>"&_
"")






Select Case mudi
	Case "add"
		Call IsSecMenuRight("alertBack",152,dataType)
		Call AddOrRev()

	Case "rev"
		Call IsSecMenuRight("alertBack",153,dataType)
		Call AddOrRev()

	Case "manage"
		Call IsSecMenuRight("alertBack",153,dataType)
		Call manage()
End Select


Call CloseMemberexe()
Call CloseConobj()
Call WebStyleBottom()









'新增、修改信息
Private Sub AddOrRev()
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")
	backURL		= ToGetStr("backURL")


	If mudi="rev" Then
		dataID=ToGetInt("dataID")
		Set revexe=conobj.execute("select * from OT_tbkRank where TR_ID="& dataID)
			If revexe.EOF Then
				Call JS_AlertBackEnd("无该记录！")
			End If
		TR_ID			= revexe("TR_ID")
		TR_img			= revexe("TR_img")
		TR_title		= revexe("TR_title")
		TR_content1		= revexe("TR_content1")
		TR_content2		= revexe("TR_content2")
		TR_rank			= revexe("TR_rank")
		TR_state		= revexe("TR_state")
		mudiCN="修改"
	Else
		TR_rank			= ToInt(conobj.execute("select max(TR_rank) from OT_tbkRank")(0))+10
		TR_state		= 1
		mudiCN="添加"
	End If

	

	response.write(_
	"<form name='addform' method='post' action='tbkRank_deal.asp?mudi="& mudi &"&nohrefStr=close' onsubmit='return CheckForm()'>"&_
	"<input type='hidden' id='dataID' name='dataID' value='"& TR_ID &"' />"&_
	"<input type='hidden' id='dataType' name='dataType' value='"& dataType &"' />"&_
	"<input type='hidden' id='dataTypeCN' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
	"<input type='hidden' id='dataMode' name='dataMode' value='"& dataMode &"' />"&_
	"<input type='hidden' id='dataModeStr' name='dataModeStr' value='"& dataModeStr &"' />"&_
	"")
		If backURL<>"" Then
			response.write(_
			"<input type='hidden' id='backURL' name='backURL' value='"& backURL &"' />")
		Else
			response.write(_
			"<script language='javascript' type='text/javascript'>document.write(""<input type='hidden' id='backURL' name='backURL' value='""+ document.location.href +""' />"")</script>")
		End If

	Call ShareTableTop("share_"& mudi &".gif","",mudiCN & dataTypeCN)
		response.write(_
		"<table width='98%' align='center' cellpadding='0' cellspacing='0' summary='' class='padd3'>"&_
		"<tr>"&_
			"<td width='12%'></td>"&_
			"<td width='88%'></td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right' valign='top' style='padding-top:6px;'>图片：</td>"&_
			"<td align='left'>"&_
				"<input type='text' id='img' name='img' size='50' style='width:300px;' value="""& TR_img &""" />"&_
				"<input type='button' onclick=""OT_OpenUpImg('input','img','info','')"" value='上传图片' />"&_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right' valign='top' style='padding-top:6px;'>内容框1：</td>"&_
			"<td align='left'>"&_
			"")
				' 插入编辑器
				Set editor = new CKEditor
				editor.basePath = "ckeditor/"
				editor.config("height") = 200
				editor.editor "title", TR_title

			response.write(_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right' valign='top' style='padding-top:6px;'>内容框2：</td>"&_
			"<td align='left'>"&_
			"")
				' 插入编辑器
				Set editor = new CKEditor
				editor.basePath = "ckeditor/"
				editor.config("height") = 200
				editor.editor "content1", TR_content1

			response.write(_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right' valign='top' style='padding-top:6px;'>内容框3：</td>"&_
			"<td align='left'>"&_
			"")
				' 插入编辑器
				Set editor = new CKEditor
				editor.basePath = "ckeditor/"
				editor.config("height") = 200
				editor.editor "content2", TR_content2

			response.write(_
			"<br />"&_
			"")

			beforeURL=GetCurrUrlDir(1)
			For i=1 To 20
				response.write(_
				"<div style='width:18%; float:left; padding:3px;'>"&_
					"<img src='../inc_img/taobao/sellLevel/"& i &".gif' class='pointer' onclick=""InsertStrToEditor('content2','<img src=\'"& beforeURL &"/inc_img/taobao/sellLevel/"& i &".gif\' />');"" />"&_
				"</div>"&_
				"")
			Next
				response.write(_
				"<div style='width:18%; float:left; padding:3px;'>"&_
					"<img src='../inc_img/taobao/shop2.gif' class='pointer' onclick=""InsertStrToEditor('content2','<img src=\'"& beforeURL &"/inc_img/taobao/shop2.gif\' />');"" />"&_
				"</div>"&_
				"")

			response.write(_
			"</td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>排序：</td>"&_
			"<td align='left'><input type='text' id='rank' name='rank' size='50' style='width:30px;' value="""& TR_rank &""" /></td>"&_
		"</tr>"&_
		"<tr>"&_
			"<td align='right'>状态：</td>"&_
			"<td align='left'>"&_
				"<label><input type='radio' name='state' value='1' "& IsChecked(TR_state,1) &" />显示</label>&nbsp;&nbsp;"&_
				"<label><input type='radio' name='state' value='0' "& IsChecked(TR_state,0) &" />隐藏</label>&nbsp;&nbsp;"&_
			"</td>"&_
		"</tr>"&_
		"</table>"&_
		"")
	Call ShareTableBottom()


	response.write(_
	"<table style='height:16px;' cellpadding='0' cellspacing='0' summary=''><tr><td></td></tr></table>"&_

	"<center><input type='image' src='images/button_"& mudi &".gif' /></center>"&_

	"</form>"&_
	"")


	If mudi="rev" Then
		response.write("<br /><center><a href='javascript:void(0);' onclick='history.back();' class='font2_1'>【返回】</a></center>")
	End If
End Sub









'查询信息
Private Sub manage()
	dataMode	= ToGetStr("dataMode")
	dataModeStr	= ToGetStr("dataModeStr")


	response.write(_
	"<form name='listForm' method='post' action='tbkRank_deal.asp?mudi=moreDel' onsubmit='return CheckListForm()'>"&_
	"<script language='javascript' type='text/javascript'>document.write (""<input type='hidden' name='URL' value='""+ document.location.href +""' />"")</script>"&_
	"<input type='hidden' name='dataType' value='"& dataType &"' />"&_
	"<input type='hidden' name='dataTypeCN' value='"& dataTypeCN &"' />"&_
	"")

	Call ShareTableTop2("share_list.gif","",dataTypeCN &"列表")
	Call ShareTableItemTitle("5%,5%,40%,8%,8%,13%","<input type='checkbox' name='selAll' onclick='CheckBoxAll()' />,序号,内容1,排序,状态,修改&nbsp;删除")


	Set showrec=server.CreateObject("ADODB.recordset")
	showrec.open "select * from OT_tbkRank order by TR_rank ASC",conobj,3,1
		If showrec.EOF Then
			response.write("</table><br /><center class='font1_1'>暂无内容</center>")
			response.End
		End If
			showPageSize=memberexe("MB_itemNum")		'每页条数
			showrec.pagesize=showPageSize	
			showPageCount=showrec.pagecount
			showCount=showrec.recordcount
			page=ToGetInt("page")
			if page < 1 Or page > showPageCount Then page=1
			showrec.absolutepage=page


		response.write(_
		"<tbody class='tabBody padd3'>"&_
		"")
		number=1+(page-1)*showPageSize
		For i=1 To showPageSize
			If showrec.EOF Then Exit For
			If i Mod 2 = 0 Then bgcolor="class='tabColorTr'" Else bgcolor=""

		response.write(_
		"<tr "& bgcolor &" id='data"& showrec("TR_ID") &"'>"&_
			"<td align='center'><input type='checkbox' name='selDataID' value='"& showrec("TR_ID") &"' /></td>"&_
			"<td align='center'>"& number &"</td>"&_
			"<td align='left'>"& showrec("TR_title") &"</td>"&_
			"<td align='center'>"& showrec("TR_rank") &"<br /></td>"&_
			"<td align='center'>"& ItemSwitch("OT_tbkRank",showrec("TR_ID"),showrec("TR_state"),"state","") &"<br /></td>"&_
			"<td align='center'>"&_
				"<img src='images/img_rev.gif' style='cursor:pointer' onclick=""document.location.href='?mudi=rev&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&dataMode="& dataMode &"&dataModeStr="& dataModeStr &"&dataID="& showrec("TR_ID") &"&backURL='+ escape(document.location.href)"" alt='' />&nbsp;&nbsp;"&_
				"<img src='images/img_del.gif' style='cursor:pointer' onclick=""if(confirm('你确定要删除？')==true){DataDeal.location.href='tbkRank_deal.asp?mudi=del&dataType="& dataType &"&dataTypeCN="& server.URLEncode(dataTypeCN) &"&theme=&dataID="& showrec("TR_ID") &"'}"" alt='' />"&_
			"</td>"&_
		"</tr>"&_
		"")
		showrec.movenext
		number=number+1
		Next
		response.write(_
		"</tbody>"&_
		"")
		response.write(_
		"<tr class='tabColorB padd5'>"&_
			"<td align='left' colspan='20'>"&_
				"<input type='button' value='全选' onclick='AllSelBox()' class='form_button1' />"&_
				"<input type='button' value='反选' onclick='RevSelBox()' class='form_button1' />"&_
				"&nbsp;"&_
				"<input type='submit' value='批量删除' />"&_
			"</td>"&_
		"</tr>"&_
		"")
	showrec.close
	Set showrec=Nothing
	response.write(_
	"</form>")

	Call ShareTableBottom2()
End Sub

%>