<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<!-- #include file="inc_temp/temp_newsList.asp" -->
<!-- #include file="inc_temp/temp_index.asp" -->
<%
webTypeName	= "list"
webTitle	= ""
webTitleAddi= ""
webKey		= ""
webDesc		= ""
If InStr(""& SYS_newsListTitleStr,"|webName|")=0 Then isShowWebTitle=False

typeStr	= Url_GetListTypeStr()
refContent = ""
areaName = ""
typeID	= 0
type1ID	= 0
type1CN	= 0
page	= Url_GetListPageID()
newTitle	= "最近更新"
recomTitle	= "精彩推荐"
rankTitle	= "点击排行"
IT_showMode = 1
IT_showNum	= 10
IT_level	= 0
Select Case typeStr
	Case "announ"
		webTitle = SYS_homeAnnounName
		areaName = webTitle
		webKey		= SYS_homeAnnounName &","& SYS_webKey
		webDesc		= ""& SYS_homeAnnounName &"列表；"& SYS_webDesc

	Case "new"
		webTitle = "最新更新"
		areaName = webTitle
		webKey		= "最新文章,"& SYS_webKey
		webDesc		= "显示最新文章；"& SYS_webDesc

	Case "refer"
		refType		= ToGetStr("refType")
		refContent	= ToGetRegExpStr("refContent","sql")
		Select Case refType
			Case "content"
				refTypeCN = "正文"
			Case "source"
				refTypeCN = "来源"
			Case "writer"
				refTypeCN = "作者"
			Case Else
				refTypeCN = "标题"
		End Select
		webTitle = "搜索("& refTypeCN &") "& refContent &""
		pointStr = "&nbsp;&gt;&nbsp;搜索结果("& refTypeCN &")："& refContent &""
		areaName = "搜索结果"
		webKey		= refContent &","& refTypeCN &",搜索,"& SYS_webKey
		webDesc		= "搜索"& refTypeCN &"含有“"& refContent &"”关键词的文章；"& SYS_webDesc

	Case "mark"
		markName	= ToGetRegExpStr("markName","sql")
		webTitle = "标签："& markName &""
		pointStr = "&nbsp;&gt;&nbsp;标签："& markName &""
		areaName = "标签匹配"
		webKey		= markName &","& SYS_webKey
		webDesc		= "匹配含有标签“"& markName &"”的文章；"& SYS_webDesc

	Case Else
		webTitle = "文章栏目"
		areaName = webTitle
		typeID	= Url_GetListTypeID()
		typeStr = typeID
'		newTitle	= "本类更新"
		recomTitle	= "本类推荐"
		rankTitle	= "本类排行"
		pointStr = ""
		Set type1exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_fatID,IT_openMode,IT_showMode,IT_showNum,IT_titleAddi,IT_webKey,IT_webDesc from OT_infoType where IT_ID="& typeID)
			If type1exe.EOF Then
				Call JS_AlertHrefEnd("栏目不存在","./")
			Else
				type1ID		= type1exe("IT_ID")
				type1CN		= type1exe("IT_theme")
				IT_level	= type1exe("IT_level")
				webTitle	= type1exe("IT_theme")
				webTitleAddi= type1exe("IT_titleAddi")
				webKey		= type1exe("IT_webKey")
				webDesc		= type1exe("IT_webDesc")
				IT_showMode = type1exe("IT_showMode")
				IT_showNum	= type1exe("IT_showNum")
				If type1exe("IT_mode")="url" Then
					type1URL=type1exe("IT_URL")
				ElseIf type1exe("IT_mode")="web" Then
					type1URL=dbPathPart &"dynWeb.asp?dataID="& type1exe("IT_webID")
				Else
					type1URL=Url_NewsListID("",type1exe("IT_ID"),0)
				End If
				pointStr = pointStr &"&nbsp;&gt;&nbsp;<a href='"& type1URL &"' class='font1_2' target='"& type1exe("IT_openMode") &"'>"& type1exe("IT_theme") &"</a>"

				If type1exe("IT_fatID")>0 Then
					Set type2exe = conobj.execute("select top 1 IT_ID,IT_level,IT_theme,IT_mode,IT_webID,IT_URL,IT_openMode,IT_titleAddi from OT_infoType where IT_ID="& type1exe("IT_fatID"))
						If Not type2exe.EOF Then
							type1ID		= type2exe("IT_ID")
							type1CN		= type2exe("IT_theme")
							If IT_level=0 Then IT_level	= type2exe("IT_level")
							If InStr(""& SYS_newsListTitleStr,"|itemName|")>0 Then webTitle = webTitle & SYS_titleSign & type2exe("IT_theme")
							areaName = type2exe("IT_theme")
							If webTitleAddi="" Then webTitleAddi = type2exe("IT_titleAddi")
							If type2exe("IT_mode")="url" Then
								type2URL=type2exe("IT_URL")
							ElseIf type2exe("IT_mode")="web" Then
								type2URL=dbPathPart &"dynWeb.asp?dataID="& type2exe("IT_webID")
							Else
								type2URL=Url_NewsListID("",type2exe("IT_ID"),0)
							End If
							pointStr = "&nbsp;&gt;&nbsp;<a href='"& type2URL &"' class='font1_2' target='"& type2exe("IT_openMode") &"'>"& type2exe("IT_theme") &"</a>"& pointStr
						End If
					type2exe.close
					Set type2exe=Nothing
				End If
				areaName = type1exe("IT_theme")
			End If
		type1exe.close
		Set type1exe=Nothing
	
End Select
%>



<!-- #include file="top.asp" -->
<%
response.write(_
"<script language='javascript' type='text/javascript' src='js/newsList.js'></script>"&_

"<div class='height10'></div>"&_
"<div class='clear'></div>"&_

"<div id='mainBody'>"&_
	"<div class='areaL' id='newsList'>"&_
	"")

		Call OTCMS_NewsList(areaName,IT_showMode,IT_showNum,typeStr,IT_level)

	response.write(_
	"</div>"&_
	"<div class='areaR'>"&_
	"")
	%>
		<!-- #include file="news_right.asp" -->
	<%
	response.write(_
	"</div>"&_
	"<div class='clear'></div>"&_
"</div>"&_
"<div class='clear'></div>"&_
"")
%>
<!-- #include file="bottom.asp" -->