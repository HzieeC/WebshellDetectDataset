<%@LANGUAGE="VBSCRIPT" CODEPAGE="936"%>
<%
response.ContentType = "text/html;charset=gb2312"
%>
<!--#include file="inc/conndb.asa"-->
<!--#include file="inc/function.asp"-->
<%
Dim action,url,cdo,stype,uploadfiles
action=Request.QueryString("action")
lx=Request.QueryString("lx")
uploadfiles="upload"
stat_FileUpLoad = False

If Dreamsun_Name<>"" Then
	getuser=conn.execute("Select id,umoney from "&userTable&" where username='"&Dreamsun_name&"'")
	userid=getuser(0)
	umoney=getuser(1)
	If Int(userid)>0 And Int(umoney)>Sys_uploadMoney  Then 
		stat_FileUpLoad = True
	Else
		stat_FileUpLoad = False
		echoInfo="您尚未登录或者积分不够"&Sys_uploadMoney&",不能上传附件！"
		response.write "{""err"":"""&echoInfo&""",""msg"":""""}"
		response.End 
	End If
Else
	stat_FileUpLoad = False
	echoInfo="您尚未登录,不能上传附件！"
	response.write "{""err"":"""&echoInfo&""",""msg"":""""}"
	response.End 
End If


				

IF stat_FileUpLoad = True And (IsEmpty(action) or action="") Then
   Call UploadFile()
ElseIF action="1" Then
   Call UploadForm()
End IF

Sub UploadFile()
	dim upload,file,formName,UploadPath,id,FileExt,arrUpFileType,i,EnableUpload
	If lx="mod" Then 
		FoName="mod"
		FiName=userid
		UP_FileTypes="jpg,gif,png,bmp,mp3"
		UP_FileSize="102400"
	Else
		FoName=DateToStr(Now(), "yem")
		FiName=DateToStr(Now(), "YmdHIS")&"_"&userid
		UP_FileTypes="rar,jpg,gif,png,swf,avi,wmv,wma,mp3"
		UP_FileSize="1024000000"
	End If

	UploadPath=uploadfiles&"/"&FoName
	
	'response.write UploadPath&"/"&FiName
	'response.End 

	Dim FSO,UploadFolder
	Set FSO = Server.CreateObject("Scripting.FileSystemObject")
	If Not FSO.FoldErexists(Server.MapPath(UploadPath)) Then
		FSO.CreateFolder(Server.MapPath(UploadPath))
	End If
	
	EnableUpload=False
	set upload=new upload_5xsoft ''建立上传对象
	For each formName in upload.objFile ''列出所有上传了的文件
		set file=upload.file(formName)  ''生成一个文件对象
		if left(right(file.filename,5),1) = "." then
			FileExt = lcase(right(file.filename,4))
		Else
			FileExt = lcase(right(file.filename,3))
		End if

		arrUpFileType=split(UP_FileTypes,",")
		For i=0 To ubound(arrUpFileType)
			If FileExt<>trim(arrUpFileType(i)) Then       
				EnableUpload=False	   
			ElseIf FileExt=trim(arrUpFileType(i)) Then
				EnableUpload=True
			Exit For
			End If
		Next
        If EnableUpload=False Then
            echoInfo="类型不对"&LCase(FileExt)&",返回"
		ElseIf file.FileSize <1 Then
			echoInfo="大小太小，检查数据，返回"
		ElseIf file.FileSize > Int(UP_FileSize) Then
			echoInfo="大小超过"&UP_FileSize/1024&"，返回"
		Else
			F_Name=FiName&"."&LCase(FileExt)
            file.SaveAs Server.MapPath(UploadPath&"/"&F_Name)
			echoInfo="ok"
			isok=UploadPath&"/"&F_Name
        End If 

		If lx="mod" Then
			If isok<>"" Then 
				Response.write "<SCRIPT language=javascript>parent.save.user_mod_other_txt.value='"&isok&"';</script>"
			End If 
			Response.Write("<div style=""padding:2px""><a href='#' onclick=""window.history.back()"">"&echoInfo&"</a>："&isok&"</div>")
		Else
			If isok<>"" Then 
				response.write "{""err"":"""",""msg"":"""&isok&"""}"
			Else
				response.write "{""err"":"""&echoInfo&""",""msg"":""""}"
			End If 
		End If 

next
set FSO=nothing  ''删除FSO对象
set upload=nothing  ''删除此对象
End Sub

Sub UploadForm()
%><!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml"><head>
<style>
<!--
body { font-size:9pt   }
td{ font-size:9pt   }
INPUT{BORDER-TOP-WIDTH: 1px; PADDING-RIGHT: 1px; PADDING-LEFT: 1px; BORDER-LEFT-WIDTH: 1px; FONT-SIZE: 9pt; BORDER-LEFT-COLOR: #cccccc; BORDER-BOTTOM-WIDTH: 1px; BORDER-BOTTOM-COLOR: #cccccc; PADDING-BOTTOM: 1px; BORDER-TOP-COLOR: #cccccc; PADDING-TOP: 1px; HEIGHT: 18px; BORDER-RIGHT-WIDTH: 1px; BORDER-RIGHT-COLOR: #cccccc}
textarea {border-width: 1; border-color: #000000; font-family: 宋体; font-size: 9pt; font-style: bold;}
#sponsorAdDiv {position:absolute; height:1; width:1; top:0; left:0;}
-->
</style>
<body bgcolor="#f2f2f2"  leftmargin="0" topmargin="0" marginwidth="0" marginheight="0">
<%
response.write "<FORM action='upload.asp?lx="&lx&"' method=post encType=multipart/form-data>"
response.write   "<INPUT id='tx1' type='file' name='filedata' size=30>&nbsp;&nbsp;<INPUT type=submit value='上传'>"
response.write  "</FORM>"
%>
</body>
</html>
<%End Sub
%>
















<%
dim Data_5xsoft

Class upload_5xsoft
  
dim objForm,objFile,Version

Public function Form(strForm)
   strForm=lcase(strForm)
   if not objForm.exists(strForm) then
     Form=""
   else
     Form=objForm(strForm)
   end if
 end function

Public function File(strFile)
   strFile=lcase(strFile)
   if not objFile.exists(strFile) then
     set File=new FileInfo
   else
     set File=objFile(strFile)
   end if
 end function


Private Sub Class_Initialize 
  dim RequestData,sStart,vbCrlf,sInfo,iInfoStart,iInfoEnd,tStream,iStart,theFile
  dim iFileSize,sFilePath,sFileType,sFormValue,sFileName
  dim iFindStart,iFindEnd
  dim iFormStart,iFormEnd,sFormName
  Version="化境HTTP上传程序 Version 2.0"
  set objForm=Server.CreateObject("Scripting.Dictionary")
  set objFile=Server.CreateObject("Scripting.Dictionary")
  if Request.TotalBytes<1 then Exit Sub
  set tStream = Server.CreateObject("adodb.stream")
  set Data_5xsoft = Server.CreateObject("adodb.stream")
  Data_5xsoft.Type = 1
  Data_5xsoft.Mode =3
  Data_5xsoft.Open
  Data_5xsoft.Write  Request.BinaryRead(Request.TotalBytes)
  Data_5xsoft.Position=0
  RequestData =Data_5xsoft.Read 

  iFormStart = 1
  iFormEnd = LenB(RequestData)
  vbCrlf = chrB(13) & chrB(10)
  sStart = MidB(RequestData,1, InStrB(iFormStart,RequestData,vbCrlf)-1)
  iStart = LenB (sStart)
  iFormStart=iFormStart+iStart+1
  while (iFormStart + 10) < iFormEnd 
	iInfoEnd = InStrB(iFormStart,RequestData,vbCrlf & vbCrlf)+3
	tStream.Type = 1
	tStream.Mode =3
	tStream.Open
	Data_5xsoft.Position = iFormStart
	Data_5xsoft.CopyTo tStream,iInfoEnd-iFormStart
	tStream.Position = 0
	tStream.Type = 2
	tStream.Charset ="gb2312"
	sInfo = tStream.ReadText
	tStream.Close
	'取得表单项目名称
	iFormStart = InStrB(iInfoEnd,RequestData,sStart)
	iFindStart = InStr(22,sInfo,"name=""",1)+6
	iFindEnd = InStr(iFindStart,sInfo,"""",1)
	sFormName = lcase(Mid (sinfo,iFindStart,iFindEnd-iFindStart))
	'如果是文件
	if InStr (45,sInfo,"filename=""",1) > 0 then
		set theFile=new FileInfo
		'取得文件名
		iFindStart = InStr(iFindEnd,sInfo,"filename=""",1)+10
		iFindEnd = InStr(iFindStart,sInfo,"""",1)
		sFileName = Mid (sinfo,iFindStart,iFindEnd-iFindStart)
		theFile.FileName=getFileName(sFileName)
		theFile.FilePath=getFilePath(sFileName)
		'取得文件类型
		iFindStart = InStr(iFindEnd,sInfo,"Content-Type: ",1)+14
		iFindEnd = InStr(iFindStart,sInfo,vbCr)
		theFile.FileType =Mid (sinfo,iFindStart,iFindEnd-iFindStart)
		theFile.FileStart =iInfoEnd
		theFile.FileSize = iFormStart -iInfoEnd -3
		theFile.FormName=sFormName
		if not objFile.Exists(sFormName) then
		  objFile.add sFormName,theFile
		end if
	else
	'如果是表单项目
		tStream.Type =1
		tStream.Mode =3
		tStream.Open
		Data_5xsoft.Position = iInfoEnd 
		Data_5xsoft.CopyTo tStream,iFormStart-iInfoEnd-3
		tStream.Position = 0
		tStream.Type = 2
		tStream.Charset ="gb2312"
	        sFormValue = tStream.ReadText 
	        tStream.Close
		if objForm.Exists(sFormName) then
		  objForm(sFormName)=objForm(sFormName)&", "&sFormValue		  
		else
		  objForm.Add sFormName,sFormValue
		end if
	end if
	iFormStart=iFormStart+iStart+1
	wend
  RequestData=""
  set tStream =nothing
End Sub

Private Sub Class_Terminate  
 if Request.TotalBytes>0 then
	objForm.RemoveAll
	objFile.RemoveAll
	set objForm=nothing
	set objFile=nothing
	Data_5xsoft.Close
	set Data_5xsoft =nothing
 end if
End Sub
   
 
 Private function GetFilePath(FullPath)
  If FullPath <> "" Then
   GetFilePath = left(FullPath,InStrRev(FullPath, "\"))
  Else
   GetFilePath = ""
  End If
 End  function
 
 Private function GetFileName(FullPath)
  If FullPath <> "" Then
   GetFileName = mid(FullPath,InStrRev(FullPath, "\")+1)
  Else
   GetFileName = ""
  End If
 End  function
End Class

Class FileInfo
  dim FormName,FileName,FilePath,FileSize,FileType,FileStart
  Private Sub Class_Initialize 
    FileName = ""
    FilePath = ""
    FileSize = 0
    FileStart= 0
    FormName = ""
    FileType = ""
  End Sub
  
 Public function SaveAs(FullPath)
    dim dr,ErrorChar,i
    SaveAs=true
    if trim(fullpath)="" or FileStart=0 or FileName="" or right(fullpath,1)="/" then exit function
    set dr=CreateObject("Adodb.Stream")
    dr.Mode=3
    dr.Type=1
    dr.Open
    Data_5xsoft.position=FileStart
    Data_5xsoft.copyto dr,FileSize
    dr.SaveToFile FullPath,2
    dr.Close
    set dr=nothing 
    SaveAs=false
  end function
  End Class
%>