<%
Dim yucmedia_err
Function yucmedia_verify(ByVal sitekey,ByVal serialnum,ByVal userip,ByVal userresponse,ByVal idenkey,ByVal zbkey)
	url="http://api2.yucmedia.com/script/verify"
	url=url &"?sitekey="& server.URLEncode(sitekey)
	url=url &"&serialnum="& server.URLEncode(serialnum)
	url=url &"&userip="& server.URLEncode(userip)
	url=url &"&userresponse="& toUTF8(userresponse)
	url=url &"&idenkey="& server.URLEncode(idenkey)
	url=url &"&zbkey="& zbkey
	Dim Http
	On Error Resume Next
	Set Http=server.createobject("MSXML2.ServerXMLHTTP")
	' 解析DNS的超时时间,建立连接的超时时间,发送数据的超时时间,接收数据的超时时间
	Http.setTimeouts 10000,10000,10000,30000 
	Http.open "POST",url,false
	Http.send()
		If Http.Readystate<>4 then
			Set Http=Nothing 
			Html_GetCode="False"
			yucmedia_err="无法访问验证码"
			Exit function
		End if
	yucmedia_verify=Http.responseText
End Function



Function toUTF8(ByVal szInput)
	Dim wch,uch,szRet
	Dim x
	Dim nAsc,nAsc2,nAsc3

	If szInput = "" Then
		toUTF8 = szInput
		Exit Function
	End If

	For x = 1 To Len(szInput)
		wch = Mid(szInput,x,1)
		nAsc = AscW(wch)
		If nAsc < 0 Then nAsc = nAsc + 65536
		If (nAsc And &HFF80) = 0 Then
			szRet = szRet & wch
		Else
			If (nAsc And &HF000) = 0 Then
				uch = "%" & Hex(((nAsc \ 2 ^ 6)) or &HC0) & Hex(nAsc And &H3F or &H80)
				szRet = szRet & uch
			Else
				uch = "%" & Hex((nAsc \ 2 ^ 12) or &HE0) & "%" &_
							Hex((nAsc \ 2 ^ 6) And &H3F or &H80) & "%" &_
							Hex(nAsc And &H3F or &H80)
				szRet = szRet & uch
			End If
		End If
	Next

	toUTF8 = szRet
End Function

Function yucmedia_deal(ByVal ydMode,ByVal codeVal)
	Dim sitekey,idenkey,serialnum,userip,userresponse,zbkey,result
		sitekey="ignnol8in30lp5siy6e9vdmd8"  	'网站识别码
		idenkey="m0lute1n7t3pdavlg5kgia695"		'网站校验码
	serialnum=request.Form("BMserialnum") 	'会话流水号(如该项取不到值请查看是否将验证码脚本加在验证码框所在的form表单内)
	userip=Request.ServerVariables("REMOTE_ADDR")	'用户ip地址
	userresponse=codeVal	'获取用户输入值。这里需要您来填写
	zbkey=ydMode	'注册填zasp,登录填dasp,发帖填fasp,回帖填hasp

	If sitekey<>"" and userip<>"" and serialnum<>"" and userresponse<>"" and idenkey<>"" and zbkey<>"" Then
		result=yucmedia_verify(sitekey,serialnum,userip,userresponse,idenkey,zbkey)
		If result="true" then
			yucmedia_deal = "true"
		Else
			yucmedia_deal = "false"
			yucmedia_err = result
		End If
	Else
		yucmedia_deal = "false"
		yucmedia_err = "缺少参数"
	End If

End Function
%>
