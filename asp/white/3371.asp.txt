<%
Html_SysSiteUrl = GetPageUrlPath()

'生成静态页面
Private Sub ToHtml(FromURL,FilePath)
	Response.Write("<a href=""" & FilePath & """ target=""_blank"">" & FilePath & " --> Ok</a><br>")
	FilePath = Server.MapPath(FilePath)
	Set objXmlHttp=Server.createObject("Microsoft." & "XMLHTTP")
	objXmlHttp.open "GET",FromURL,false
	objXmlHttp.send()
	binFileData=objXmlHttp.responseBody
	Set objXmlHttp = Nothing
	Set objAdoStream=Server.createObject("ADODB." & "Stream")
	objAdoStream.Type=1
	objAdoStream.Open()
	objAdoStream.Write(binFileData)
	objAdoStream.SaveToFile FilePath,2
	objAdoStream.Close()
	Set objAdoStream = Nothing
End Sub

Private Sub Index_Html()
	FromURL = Html_SysSiteUrl & "../index.asp?rnd=" & Now()
	FilePath = "../index.html"
	Call ToHtml(FromURL,FilePath)
	
	FromURL = Html_SysSiteUrl & "../c/rss.asp?rnd=" & Now()
	FilePath = "../rss.xml"
	Call ToHtml(FromURL,FilePath)
End Sub

Private Sub Ads_Html()
	FromURL = Html_SysSiteUrl & "../inc/ads.asp?rnd=" & Now()
	FilePath = "../a2j/astr.js"
	Call ToHtml(FromURL,FilePath)
End Sub

'==========================生成新闻文章静态页===================================
Private Sub Article_Page_Html(ID)
	Sql="select top 1 ID,ChannelID,Content from Ok3w_Article where IsPass=1 and IsDelete=0"
	If ID<>"" Then Sql=Sql& " and ID=" & ID
	Sql=Sql & " order by ID desc"
	Set oRs = Conn.Execute(Sql)
	h_ID = oRs(0)
	ChannelID = oRs(1)
	Content = oRs(2)
	oRs.Close
	Set oRs = Nothing
	
	If InStr(Content,"[Ok3w_NextPage]")>0 Then
		Content_Tmp = Split(Content,"[Ok3w_NextPage]")
		Page_Count = Ubound(Content_Tmp)+1
	Else
		Page_Count = 1
'		cLen = Len(Content)
'		If cLen Mod 2000 = 0 Then
'			Page_Count = cLen \ 2000
'		Else
'			Page_Count = cLen \ 2000 + 1
'		End If
	End If
	
	For iPage=1 To Page_Count
		If iPage = 1 Then
			FilePath = "../" & HTML_Article_Path & h_ID & ".html"
			Else
				FilePath = "../" & HTML_Article_Path & h_ID & "_" & iPage & ".html"
		End If
		If ChannelID=1 Then
			FromURL = Html_SysSiteUrl & "../show.asp?id=" & h_ID & "&thisPage=" & iPage & "&rnd=" & Now()
			Else
				FromURL = Html_SysSiteUrl & "../about.asp?id=" & h_ID & "&thisPage=" & iPage & "&rnd=" & Now()
		End If
		Call ToHtml(FromURL,FilePath)
	Next	
End Sub

'=======================生成文章列表
Private Sub Article_List_Html(PageNo,ClassID)
	FromURL = Html_SysSiteUrl & "../list.asp?id=" & ClassID & "&PageNo=" & PageNo & "&rnd=" & Now()
	FilePath = "../" & HTML_Article_Path & "list_" & ClassID
	If PageNo<>1 Then FilePath = FilePath & "_" & PageNo
	FilePath = FilePath & ".html"
	Call ToHtml(FromURL,FilePath)
End Sub

Private Sub Soft_Index_Html()
	FromURL = Html_SysSiteUrl & "../soft_index.asp?rnd=" & Now()
	FilePath = "../soft_index.html"
	Call ToHtml(FromURL,FilePath)
	
	FromURL = Html_SysSiteUrl & "../c/soft_rss.asp?rnd=" & Now()
	FilePath = "../soft_rss.xml"
	Call ToHtml(FromURL,FilePath)
End Sub

Private Sub Soft_Page_Html(ID)
	Sql="select max(ID) from Ok3w_Soft where IsPass=1 and IsDelete=0"
	If ID<>"" Then Sql=Sql& " and ID=" & ID
	h_ID = Conn.Execute(Sql)(0)
	
	FilePath = "../" & HTML_Soft_Path & h_ID & ".html"
	FromURL = Html_SysSiteUrl & "../soft_show.asp?id=" & h_ID & "&rnd=" & Now()
	Call ToHtml(FromURL,FilePath)	
End Sub

Private Sub Soft_List_Html(PageNo,ClassID)
	FromURL = Html_SysSiteUrl & "../soft_list.asp?id=" & ClassID & "&PageNo=" & PageNo & "&rnd=" & Now()
	FilePath = "../" & HTML_Soft_Path & "list_" & ClassID
	If PageNo<>1 Then FilePath = FilePath & "_" & PageNo
	FilePath = FilePath & ".html"
	Call ToHtml(FromURL,FilePath)
End Sub
%>