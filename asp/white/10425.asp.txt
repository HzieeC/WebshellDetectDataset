<%@Language=VBScript  CODEPAGE="936"%>
<!--#include file="cominc/conndb.asa"-->
<!--#include file="cominc/function.asp"-->
<%
On Error Resume Next
%>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312">
<style type="text/css">
body{
margin:0px;
font-family: Verdana, Geneva, Arial, Helvetica, sans-serif;
font-size:12px;
}
</style>
</head>
<body class="attachmentBody">
<%
Server.ScriptTimeOut = 999
stat_FileUpLoad = True
memName="zd"
UP_FileSize="1024000000"
UP_FileTypes="jpg,gif,rar"
F_path="UPLOAD/"
SystemDir="/kb"
act=Request.QueryString("act")
If act="" Then act="cb_photo"
If act="flash" Then UP_FileTypes="swf"
If act="photo" Then UP_FileTypes="jpg"
UP_FileType=Split(UP_FileTypes,",")

If stat_FileUpLoad = True And memName<>Empty Then
    If Request.QueryString("action") = "upload" Then

        Dim upl, FSOIsOK
        FSOIsOK = 1
        Set upl = Server.CreateObject("Scripting.FileSystemObject")
        If Err<>0 Then
            Err.Clear
            FSOIsOK = 0
        End If
        Dim D_Name, F_Name
        If FSOIsOK = 1 Then
            D_Name = DateToStr(Now(), "yem")
            If upl.FolderExists(Server.MapPath("../"&F_path & D_Name)) = False Then
                upl.CreateFolder Server.MapPath("../"&F_path & D_Name)
            End If
        Else
            D_Name = "All_Files"
        End If
        Set upl = Nothing
		F_path=F_path & D_Name &"/"

		Dim FileUP
        Set FileUP = New Upload_File
        FileUP.GetDate( -1)
        Dim F_File, F_Type
        Set F_File = FileUP.File("File")

        F_Type =FixName(F_File.FileExt)
		randomize timer
        F_Name = DateToStr(Now(), "YmdHIS")&Int(8999*Rnd +1000)&"."&LCase(F_Type)

        If F_File.FileSize > Int(UP_FileSize) Then
            Response.Write("<div style=""padding:6px""><a href='javaScript:history.back(-1)'>文件大小超出，重新上传</a></div>")
        ElseIf IsvalidFile(LCase(F_Type)) = False Then
            Response.Write("<div style=""padding:6px""><a href='javaScript:history.back(-1)'>文件格式"&LCase(F_Type)&"非法，请返回重新上传</a></div>")
        Else
		
            F_File.SaveAs Server.MapPath(F_path & F_Name)
			If act="photo" Then 
				Response.Write("<script>window.parent.document.getElementById('photo').value='"&SystemDir &"/"& F_path & F_Name &"';</script>")
				Response.Write("<div><a herf=""#"" onclick=""window.history.back()"">文件上传成功，请返回察看</a></div>")
			Else
				Response.Write("<script>window.parent.document.getElementById('txtUrl').value='"& SystemDir &"/"& F_path & F_Name &"';</script>")
				Response.Write("<div>文件上传成功，请点击确定插入！</div>")
			End If 
        End If
        Set F_File = Nothing
        Set FileUP = Nothing
    Else
%>
<%
Response.Write("<form name=""frm"" enctype=""multipart/form-data"" method=""post"" action=""?action=upload&act="&act&"""><input name=""File"" type=""File"" size=""14"" style=""font-size:12px"">&nbsp;<input type=""Submit"" name=""Submit"" value=""上传"" style=""font-size:12px"">")
%>
</form>
<%
End If
Else
    Response.Write("<div style=""padding:2px;color:#f00"">对不起，你没有权限上传附件！</div>")
End If


'====== Upload_5xSoft Class ====================================
'  化境ASP无组件上传类 2.0
'     版权归原作者所有。
'     舜子于 2005-10-20 修改
'============================================================
dim Data_5xsoft

Class Upload_File
  
dim objForm,objFile,Version

Public function Form(strForm)
   strForm=lcase(strForm)
   if not objForm.exists(strForm) then
     Form=""
   else
     Form=objForm(strForm)
   end if
 end function

Public function File(strFile)
   strFile=lcase(strFile)
   if not objFile.exists(strFile) then
     set File=new FileInfo
   else
     set File=objFile(strFile)
   end if
 end function


Private Sub Class_Initialize 
  dim RequestData,sStart,vbCrlf,sInfo,iInfoStart,iInfoEnd,tStream,iStart,theFile
  dim iFileSize,sFilePath,sFileType,sFormValue,sFileName
  dim iFindStart,iFindEnd
  dim iFormStart,iFormEnd,sFormName
  Version="化境HTTP上传程序 Version 2.0"
  set objForm=Server.CreateObject("Scripting.Dictionary")
  set objFile=Server.CreateObject("Scripting.Dictionary")
  if Request.TotalBytes<1 then Exit Sub
  set tStream = Server.CreateObject("adodb.stream")
  set Data_5xsoft = Server.CreateObject("adodb.stream")
  Data_5xsoft.Type = 1
  Data_5xsoft.Mode =3
  Data_5xsoft.Open
  Data_5xsoft.Write  Request.BinaryRead(Request.TotalBytes)
  Data_5xsoft.Position=0
  RequestData =Data_5xsoft.Read 

  iFormStart = 1
  iFormEnd = LenB(RequestData)
  vbCrlf = chrB(13) & chrB(10)
  sStart = MidB(RequestData,1, InStrB(iFormStart,RequestData,vbCrlf)-1)
  iStart = LenB (sStart)
  iFormStart=iFormStart+iStart+1
  while (iFormStart + 10) < iFormEnd 
	iInfoEnd = InStrB(iFormStart,RequestData,vbCrlf & vbCrlf)+3
	tStream.Type = 1
	tStream.Mode =3
	tStream.Open
	Data_5xsoft.Position = iFormStart
	Data_5xsoft.CopyTo tStream,iInfoEnd-iFormStart
	tStream.Position = 0
	tStream.Type = 2
	tStream.Charset ="UTF-8"
	sInfo = tStream.ReadText
	tStream.Close
	'取得表单项目名称
	iFormStart = InStrB(iInfoEnd,RequestData,sStart)
	iFindStart = InStr(22,sInfo,"name=""",1)+6
	iFindEnd = InStr(iFindStart,sInfo,"""",1)
	sFormName = lcase(Mid (sinfo,iFindStart,iFindEnd-iFindStart))
	'如果是文件
	if InStr (45,sInfo,"filename=""",1) > 0 then
		set theFile=new FileInfo
		'取得文件名
		iFindStart = InStr(iFindEnd,sInfo,"filename=""",1)+10
		iFindEnd = InStr(iFindStart,sInfo,"""",1)
		sFileName = Mid (sinfo,iFindStart,iFindEnd-iFindStart)
		theFile.FileName=getFileName(sFileName)
		theFile.FilePath=getFilePath(sFileName)
		theFile.FileExt=getFileExt(sFileName)
		'取得文件类型
		iFindStart = InStr(iFindEnd,sInfo,"Content-Type: ",1)+14
		iFindEnd = InStr(iFindStart,sInfo,vbCr)
		theFile.FileType =Mid (sinfo,iFindStart,iFindEnd-iFindStart)
		theFile.FileStart =iInfoEnd
		theFile.FileSize = iFormStart -iInfoEnd -3
		theFile.FormName=sFormName
		if not objFile.Exists(sFormName) then
		  objFile.add sFormName,theFile
		end if
	else
	'如果是表单项目
		tStream.Type =1
		tStream.Mode =3
		tStream.Open
		Data_5xsoft.Position = iInfoEnd 
		Data_5xsoft.CopyTo tStream,iFormStart-iInfoEnd-3
		tStream.Position = 0
		tStream.Type = 2
		tStream.Charset ="gb2312"
	        sFormValue = tStream.ReadText 
	        tStream.Close
		if objForm.Exists(sFormName) then
		  objForm(sFormName)=objForm(sFormName)&", "&sFormValue		  
		else
		  objForm.Add sFormName,sFormValue
		end if
	end if
	iFormStart=iFormStart+iStart+1
	wend
  RequestData=""
  set tStream =nothing
End Sub

Private Sub Class_Terminate  
 if Request.TotalBytes>0 then
	objForm.RemoveAll
	objFile.RemoveAll
	set objForm=nothing
	set objFile=nothing
	Data_5xsoft.Close
	set Data_5xsoft =nothing
 end if
End Sub
   
 
 Private function getFilePath(FullPath)
  If FullPath <> "" Then
   GetFilePath = left(FullPath,InStrRev(FullPath, "\"))
  Else
   GetFilePath = ""
  End If
 End  function
 
 Private function getFileName(FullPath)
  If FullPath <> "" Then
   GetFileName = mid(FullPath,InStrRev(FullPath, "\")+1)
  Else
   GetFileName = ""
  End If
 End  function
 
 Private function getFileExt(FullPath)
  If FullPath <> "" Then
		GetFileExt = mid(FullPath,InStrRev(FullPath, ".")+1)
	Else
		GetFileExt = ""
  End If
 End function
 
End Class

Class FileInfo
  dim FormName,FileName,FilePath,FileSize,FileType,FileStart,FileExt
  Private Sub Class_Initialize 
    FileName = ""
    FilePath = ""
    FileSize = 0
    FileStart= 0
    FormName = ""
    FileType = ""
    FileExt=""
  End Sub
  
 Public function SaveAs(FullPath)
    dim dr,ErrorChar,i
    SaveAs=true
    if trim(fullpath)="" or FileStart=0 or FileName="" or right(fullpath,1)="/" then exit function
    set dr=CreateObject("Adodb.Stream")
    dr.Mode=3
    dr.Type=1
    dr.Open
    Data_5xsoft.position=FileStart
    Data_5xsoft.copyto dr,FileSize
    dr.SaveToFile FullPath,2
    dr.Close
    set dr=nothing 
    SaveAs=false
  end function
  End Class
%>
