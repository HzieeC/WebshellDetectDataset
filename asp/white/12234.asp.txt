<!-- #include file="codePage.asp" -->
<!-- #include file="check.asp" -->
<!-- #include file="inc/file_memberLog.asp" -->
<!-- #include file="collData/conobj.asp" -->
<%
Call CheckIframe()

'**** 版权所有 ****

' 网钛文章管理系统(OTCMS)
' 程序开发：网钛科技
' 官方网站：http://otcms.com
' 联系ＱＱ：877873666
' 联系邮箱：877873666@qq.com

'**** 版权所有 ****


Call OpenMemberexe("","login",2)
Call CloseMemberexe()


Select Case mudi
	Case "add","rev"
		Call AddOrRev()

	Case "del"
		menuFileID = 160
		Call IsSecMenuRight("alert",menuFileID,dataType)
		Call del()

End Select


Call CloseCollObj()
Call CloseConobj()











'添加与修改
Private Sub AddOrRev()
	backURL		= ToPostStr("backURL")
	dataType	= ToPostStr("dataType")
	dataTypeCN	= ToPostStr("dataTypeCN")

	dataID		= ToPostInt("dataID")
	theme		= ToPostStr("theme")
	imgPath		= ToPostStr("imgPath")
	saveFileType= ToPostStr("saveFileType")
	imgMaxSize	= ToPostInt("imgMaxSize")
	overtime	= ToPostInt("overtime")
	rank		= ToPostInt("rank")
	state		= ToPostInt("state")

	
	If theme="" Then
		Call JS_AlertBackEnd("表单内容接收不全")
	End If
	
	application.lock
		Set dealrec=server.CreateObject("ADODB.recordset")
		dealrec.open "select top 1 * from OT_collType where CT_ID="& dataID,collObj,1,3
			If dealrec.EOF Then
				menuFileID = 157
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				dealrec.addnew
				alertMode="添加"
			Else
				menuFileID = 159
				Call IsSecMenuRight("alertBack",menuFileID,dataType)
				alertMode="修改"
			End If
		dealrec("CT_theme")		= theme
		dealrec("CT_imgPath")	= imgPath
		dealrec("CT_saveFileType")	= saveFileType
		dealrec("CT_imgMaxSize")	= imgMaxSize
		dealrec("CT_overtime")	= overtime
		dealrec("CT_rank")		= rank
		dealrec("CT_state")		= state
		dealrec.update
		dealrec.close
		Set dealrec=Nothing
	application.unlock

	alertResult="成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】"& alertMode & alertResult &"！")

	Call JS_AlertHref(""& alertMode &"成功！",backURL)
End Sub







'删除类别
Private Sub del()
	dataType	= ToGetStr("dataType")
	dataTypeCN	= ToGetStr("dataTypeCN")
	dataID		= ToGetInt("dataID")
	theme		= ToGetStr("theme")

	If dataID<=0 Then
		response.write "<script language='javascript' type='text/javascript'>alert('指定ID错误！');</script>"
		response.End
	End If

	collObj.execute("delete from OT_collType where CT_ID="& dataID)

	alertResult="成功"
	Call MemberLog_Deal(0,"","",theme,"【"& dataTypeCN &"】删除"& alertResult &"！")

	response.write("<script language='javascript' type='text/javascript'>parent.$id('data"& dataID &"').style.display='none'</script>")
End Sub
%>