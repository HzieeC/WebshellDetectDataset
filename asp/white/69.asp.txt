<!--#include file="Adn_Conn.asp"-->
<!--#include file="../Cache/AdnCms.Admin.asp"-->
<!--#include file="../Cache/AdnCms.Class.asp"-->
<%Dim action,order,class_id,server_id,shuxin_id,search_type,search_keyword,special_id
Dim startime,endtime,class_bigid,search_class,pagelink
startime=Timer():sql="":S1="":S2="":S3=""
action=Trim(Request.QueryString("action"))
order=Trim(Request.QueryString("order"))
class_id=Adn_CLng(Request.QueryString("class_id"))  '分类筛选参数－栏目
server_id=Adn_CLng(Request.QueryString("server_id"))'分类筛选参数－服务器组
shuxin_id=Adn_CLng(Request.QueryString("shuxin_id"))'分类筛选参数－电影属性
special_id=Adn_CLng(Request.QueryString("special_id"))'分类筛选参数－专题
search_type=Trim(Request.QueryString("search_type"))'搜索筛选参数－影片名
search_keyword=Trim(Request.QueryString("search_keyword"))'搜索关键字
page=Adn_CLng(Request.QueryString("page")):If page<=0 or page="" Then page=1'初始化分页参数
Call OpenConn()
%><html>
<head>
<meta http-equiv="content-type" content="text/html; charset=gb2312" />
<TITLE>管理页面</TITLE>
<link href="skins/css/adn_main.css" rel="stylesheet" type="text/css" />
<script language = "javaScript" src = "Adn_All.js" type="text/javascript"></script>
<script type='text/javascript' src='../Js/AdnCms.Ajax.js'></script>
<style type="text/css">
.td2 td{ background:#FFF;text-align:center;height:28px;} 
.td2 .left{text-align:left;padding-left:5px;}
.td2 sup{color:#FF0000;font-weight:bold;cursor:pointer;}
</style>
</head>
<body>
<table width="100%" border="0" cellspacing="0" cellpadding="0">
<tr><th>
  <table width="100%" border="0">
    <tr>
      <td width="320">
  <table border="0" align="right" cellpadding="0" cellspacing="0">
  <form method="get" action="adn_data.asp"><input type="hidden" name="action" value="search">
  <tr>
  <td width="80">搜索>></td>
  <td width="80"><select name="search_type"><option value="name" selected>电影名称</option><option value="actor">影片主演</option><option value="director">影片导演</option><option value="year">影片年代</option><option value="erea">影片地区</option><option value="language">影片语言</option><option value="inputer">录入作者</option></select></td>
  <td><input name="search_keyword" type="text" style="width:120px; text-align:left" onFocus="if(this.value=='关键字')this.value='';" value="<%If search_keyword<>"" Then Response.Write search_keyword Else Response.Write "关键字" End If%>"></td>
  <td width="80"><input type="submit" name="Submit" value="提交" /></td>
  </tr>
  </form>
  </table>
	 </td>
	  <!-- -->
     <td>
 <table width="440" border="0" cellspacing="0" cellpadding="0" style="text-align:center"><tr>
 <td><select name="select" onChange="MM_jumpMenu('parent',this,0)"><option value='<%=LinkPath(0,server_id,0,shuxin_id)%>'>按类型选择</option><%=Fen_ClassName(0,"",class_id)%></select></td>
 <td><select name="select" onChange="MM_jumpMenu('parent',this,0)"><option value='<%=LinkPath(class_id,0,0,shuxin_id)%>'>按播放地址</option><%Call Fen_ServerName%></select></td>
 <td><select name="select" onChange="MM_jumpMenu('parent',this,0)"><option value='<%=LinkPath(0,0,special_id,shuxin_id)%>'>按专题</option><%Call Fen_SpecialName%></select></td>
 <td><select name="select" onChange="MM_jumpMenu('parent',this,0)"><option value='<%=LinkPath(class_id,server_id,0,0)%>'>按影片属性</option>
<option value='<%=LinkPath(class_id,server_id,special_id,1)%>' <%If shuxin_id=1 then%>selected='selected'<%end if%>>待审影片</option>
<option value='<%=LinkPath(class_id,server_id,special_id,2)%>' <%If shuxin_id=2 then%>selected='selected'<%end if%>>已审影片</option>
<option value='<%=LinkPath(class_id,server_id,special_id,3)%>' <%If shuxin_id=3 then%>selected='selected'<%end if%>>推荐影片</option>
<option value='<%=LinkPath(class_id,server_id,special_id,4)%>' <%If shuxin_id=4 then%>selected='selected'<%end if%>>热门影片</option>
<option value='<%=LinkPath(class_id,server_id,special_id,5)%>' <%If shuxin_id=5 then%>selected='selected'<%end if%>>连载影片</option></select></td>
  <td><select name="select" onChange="MM_jumpMenu('parent',this,0)"><option value='adn_data.asp'>排序方式</option>
<option value='<%=LinkPath(class_id,server_id,special_id,shuxin_id)%>order=id' <%If order="id" then%>selected='selected'<%end if%>>影片ID</option>
<option value='<%=LinkPath(class_id,server_id,special_id,shuxin_id)%>order=times' <%If order="times" then%>selected='selected'<%end if%>>按时间</option>
<option value='<%=LinkPath(class_id,server_id,special_id,shuxin_id)%>order=hits' <%If order="hits" then%>selected='selected'<%end if%>>按人气</option></select></td></tr>
</table>
	</td>
 </tr>
 </table>
</th></tr>
<!-- -->
<tr><td class="td2">
	<table width="100%" border=0 align=center cellpadding="5" cellspacing=1 style="background: #cad9ea;">
	<form name="myform" method="Post" action="">
	<tr bgcolor="#e4edf9" style="text-align:center;">
	  <td width="40">选中</td>
	  <td width="35">ID</td>
	  <td >影片名称 <%If Session("httpimg")>0 Then%>[共<font color='red'><b><%=Session("httpimg")%></b></font>张远程图片] [<a href="Adn_Img.asp"><font color='red'><b>保存到本地</b></font></a>]<%End If%></td>
	  <td width="40">人气</td>
	  <td width="40">专题</td>
	  <td width="40">推荐</td>
	  <td width="40">审核</td>
	  <td width="80">更新时间</td>
	  <td width="80">操作</td>
	  <td width="80">影片来源</td>
	</tr>
<%Set rs = Server.CreateObject("adodb.recordset")
Call Data_sql
rs.Open sql, Conn, 1, 1
If not(rs.bof And rs.eof) Then
	rs.pagesize=AdminId(9)'每页数据
	rs.AbsolutePage=page
	m=rs.recordcount     '总记录
	n=rs.PageCount       '总页数
	z=rs.pagesize        '每页数量
	i=0
	Do While Not rs.eof and i<rs.pagesize
	i=i+1
%>	<tr>
      <td><input name='data_id' type='checkbox' value='<%=rs("data_id")%>' style="border:none"></td>
      <td><%=rs("data_id")%></td>
	  <td class="left">[<a href="adn_data.asp?action=fenlei&data_type=class&class_id=<%=rs("class_id")%>"><%=GetClassName(rs("class_id"))%></a>] <a href="<%=GetDataLink(rs("class_id"),rs("data_id"))%>" target="_blank"><%=GetColor(rs("data_name"),rs("data_color"))%></a> <span id="ct<%=rs("data_id")%>"><%If Adn_CLng(rs("data_continu"))>0 Then%><sup onClick="setday('ct',<%=rs("data_id")%>,0)" title='修改连载集数'>(<%=Adn_CLng(rs("data_continu"))%>)</sup><%Else%><img src="Images/ct_01.gif" title="设置连载集数" style="cursor:pointer" onClick="setday('ct',<%=rs("data_id")%>,0)"/><%End If%></span> <%If Config(17)="html" Then%> <a href="#" onClick="ops('Adn_CreateHtml.asp?action=dataid&dataid=<%=rs("data_id")%>&ClassId=<%=rs("class_id")%>','100','400');"><font color="green">生成</font></a><%End If%></td>
	  <td><%=Adn_CLng(rs("data_hits"))%></td>
	  <td id="sp<%=rs("data_id")%>"><%If rs("special_id")>0 Then
	      Response.Write"<img src=""images/icon_01.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setday('sp',"&rs("data_id")&","&Adn_CLng(rs("special_id"))&")""/>" 
		  Else 
		  Response.Write"<img src=""images/icon_02.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setday('sp',"&rs("data_id")&","&Adn_CLng(rs("special_id"))&")""/>"
		  End If%></td>
	  <td id="el<%=rs("data_id")%>"><%If Adn_CLng(rs("data_elite"))=1 Then
	      Response.Write"<img src=""images/icon_01.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setajax('el',"&rs("data_id")&",0)""/>" 
		  Else 
		  Response.Write"<img src=""images/icon_02.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setajax('el',"&rs("data_id")&",1)""/>"
		  End If%></td>
	  <td id="st<%=rs("data_id")%>"><%If Adn_CLng(rs("data_status"))=1 Then
	      Response.Write"<img src=""images/icon_01.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setajax('st',"&rs("data_id")&",0)""/>" 
		  Else 
		  Response.Write"<img src=""images/icon_02.gif"" border=""0"" style=""cursor: pointer;"" onClick=""setajax('st',"&rs("data_id")&",1)""/>"
		  End If%></td>
	  <td><span title="<%=rs("data_lastdate")%>"><%=Adn_Time(rs("data_lastdate"),2)%></span></td>
	  <td><a href="adn_data_edit.asp?data_id=<%=rs("data_id")%>">修改</a> | <a href="adn_data_save.asp?action=del&data_id=<%=rs("data_id")%>" onClick="return confirm('删除后将不能恢复,确定要删除吗?');">删除</a></td>
	  <td><%Call ServerName%></td>
    </tr>
<%rs.movenext : loop%>	
</table>
</td></tr>
<!-- -->
<tr >
  <td class="td2" style="text-align:left; height:30px;"><table width="100%" border="0" cellspacing="0" cellpadding="0">
  <tr>
    <td width="100"><input name='chkAll' type='checkbox' id='chkAll' onclick='CheckAll(this.form)' value='checkbox' style="border:none">全选/反选</td>
    <td style="text-align:left"><input name='submit1' type='submit' value='批量审核' onClick="document.myform.action='adn_data_all.asp?action=shenhe';">&nbsp;<input name='submit1' type='submit' value='取消审核' onClick="document.myform.action='adn_data_all.asp?action=daishen';">&nbsp;<input name='submit1' type='submit' value='批量推荐' onClick="document.myform.action='adn_data_all.asp?action=tuijian';">&nbsp;<input name='submit1' type='submit' value='取消推荐' onClick="document.myform.action='adn_data_all.asp?action=daijian';">&nbsp;<input name='submit1' type='submit' value='设为热门' onClick="document.myform.action='adn_data_all.asp?action=remen';">&nbsp;<input name='submit1' type='submit' value='取消热门' onClick="document.myform.action='adn_data_all.asp?action=daire';">&nbsp;<input name='submit1' type='submit' value='批量移动' onClick="document.myform.action='adn_data_all.asp?action=yidong';">&nbsp;<input name='submit1' type='submit' value='批量删除' onClick="document.myform.action='adn_data_all.asp?action=delall';">&nbsp;<input name='submit1' type='submit' value='批量设置' onClick="document.myform.action='adn_data_all.asp?action=shezhi';"></td>
    <td >&nbsp;</td>
  </tr>
</table></td>
</tr>
</form> 
<tr><th style="text-align:center;padding:5px;"><%=ShowPage(pagelink&"order="&order, m, z, page, True, True, "条", False)%></th></tr>
</table>
<%
Else
    Call Adn_Err("暂无数据！")
End If
rs.Close
Set rs=nothing
endtime=timer()
Call CloseConn()
'作用：处理SQL查询语句
Sub Data_sql  
	If action="fenlei" Then
	     If class_id>0 Then S1="and (class_id="&class_id&" or class_id in (select class_id from adnim_class where class_bigid="&class_id&"))"'一同查询大类下的小类id 
	     If server_id>0 Then S2="and server_id like '%"&server_id&"|%'"
		 If special_id>0 Then S4=" and special_id="&special_id&" "
		 If shuxin_id=1 Then'待审电影
			 S3="and data_status=0"
		 ElseIf	shuxin_id=2 Then'已审电影
			 S3="and data_status=1"
		 ElseIf	shuxin_id=3 Then'推荐电影
			 S3="and data_elite=1"	
		 ElseIf	shuxin_id=4 Then'热门电影
			 S3="and data_hits>="&Clng(Config(10))&""
		 ElseIf	shuxin_id=5 Then'连载电影
			 S3="and data_continu>0"
		 End If
		 pagelink=LinkPath(class_id,server_id,special_id,shuxin_id)'分页路径
	ElseIf action="search" Then
	     dim keyword:keyword=search_keyword
		 sql="and "&GetSearchStr(search_type,keyword)&""
		 pagelink="Adn_Data.asp?Action=search&search_type="&search_type&"&search_keyword="&search_keyword&"&" '分页路径
	Else
	    sql=""
	    pagelink=LinkPath(0,0,0,0)'分页路径
	End If
	If order="hits" then
	    orderby="data_hits"    
	Elseif order="times" then
	    orderby="data_lastdate"
	Else
	    orderby="data_id"
	End If
	sql = "Select data_id,data_name,class_id,server_id,special_id,data_hits,data_elite,data_status,data_inputer,data_lastdate,data_color,data_continu from adnim_data where data_id>0 "&sql&" "&S1&" "&S2&" "&S3&" "&S4&" order by "&orderby&" desc"
End Sub
'作用:处理列表链接
Function LinkPath(classid,serverid,specialid,shuxinid)
    LinkPath="Adn_Data.asp?Action=fenlei&Class_id="&classid&"&Server_id="&serverid&"&special_id="&specialid&"&shuxin_id="&shuxinid&"&"
End Function		
'作用:处理循环影片的服务器组
Sub ServerName
	Dim server_name,server_endid,i,server_idd
	If rs("server_id")="|" Then
	    Response.Write("<font color=#CCCCCC>无地址</font>")
	Else
		server_name=split(rs("server_id"),"|")
		server_endid=ubound(server_name)
		If Adn_CLng(AdminId(16))=1 Then'判断是否详细显示组名
			Response.Write("<select name='select'>")
			Response.Write("<option>共有"&server_endid&"组地址</option>")		
			For i=0 to (server_endid-1)
			server_id=Adn_CLng(server_name(i))
			Set rs_server_name = Server.CreateObject("adodb.recordset")
			sql = " select top 1 server_id,server_name from adnim_server where server_id = "&server_id&" "
			rs_server_name.Open sql, Conn, 1, 1
			If Not(rs_server_name.bof And rs_server_name.eof) Then
				Response.Write("<option>"&rs_server_name("server_name")&"</option>")
			Else
				Response.Write("<option style:""#CCC"">找不到该服务器组！</option>")
			End if
			rs_server_name.Close
			Set rs_server_name=nothing
			Next
			Response.Write("</select>")
		Else
			For i=0 to (server_endid-1)
			Response.Write server_name(i)&"服 "
			Next
		End If
	End If
End Sub
'作用:分类递归查询分类列表
Function Fen_ClassName(TypeID,CompatStr,ID)
		Dim RSclass,TempStr
		Set RSclass = Conn.Execute("select class_id,class_name,class_dir,class_orderid,class_bigid from adnim_class where class_bigid="&TypeID&" order by class_orderid asc" )
		TempStr =CompatStr & "┄"
		Do While Not RSclass.Eof
			if RSclass("class_id")=ID then
				Fen_ClassName = Fen_ClassName & "<option value='"&LinkPath(RSclass("class_id"),server_id,special_id,shuxin_id)&"' selected>"
			else
				Fen_ClassName = Fen_ClassName & "<option value='"&LinkPath(RSclass("class_id"),server_id,special_id,shuxin_id)&"'>"
			end if
			Fen_ClassName = Fen_ClassName & "├" & TempStr &  RSclass("class_name")
			Fen_ClassName = Fen_ClassName & "</option>" & Chr(13) & Chr(10)
			Fen_ClassName = Fen_ClassName &Fen_ClassName(RSclass("class_id"),TempStr,ID)
			RSclass.MoveNext
		Loop
		RSclass.Close
		Set RSclass = Nothing
End Function
'作用:分类筛选按地址
Sub Fen_ServerName
    Set rs_Fen_ServerName = Server.CreateObject("adodb.recordset")
	sql_fs = "Select server_id,server_playerid,server_orderid,server_name from adnim_server order by server_orderid "
	rs_Fen_ServerName.Open sql_fs, Conn, 1, 1
	If Not(rs_Fen_ServerName.bof And rs_Fen_ServerName.eof) Then
	    Do while not rs_Fen_ServerName.eof
	    If rs_Fen_ServerName("server_id")=server_id Then
	    Response.Write("<option value='"&LinkPath(class_id,rs_Fen_ServerName("server_id"),special_id,shuxin_id)&"' selected='selected'>"&rs_Fen_ServerName("server_name")&"</option>")
	    Else
	    Response.Write("<option value='"&LinkPath(class_id,rs_Fen_ServerName("server_id"),special_id,shuxin_id)&"'>"&rs_Fen_ServerName("server_name")&"</option>")
	    End If
	    rs_Fen_ServerName.movenext
	    Loop
	End if
	rs_Fen_ServerName.Close
	Set rs_Fen_ServerName=nothing
End Sub
'作用:分类筛选按专题
Sub Fen_SpecialName
    Set rs_Fen_SpecialName = Server.CreateObject("adodb.recordset")
	sql_fs = "Select special_id,special_orderid,special_name,special_skin,special_intro from adnim_special order by special_orderid "
	rs_Fen_SpecialName.Open sql_fs, Conn, 1, 1
	If Not rs_Fen_SpecialName.eof Then
	    Do while not rs_Fen_SpecialName.eof
	    If rs_Fen_SpecialName("special_id")=special_id Then
	    Response.Write("<option value='"&LinkPath(class_id,server_id,rs_Fen_SpecialName("special_id"),shuxin_id)&"' selected='selected'>"&rs_Fen_SpecialName("special_name")&"</option>")
	    Else
	    Response.Write("<option value='"&LinkPath(class_id,server_id,rs_Fen_SpecialName("special_id"),shuxin_id)&"'>"&rs_Fen_SpecialName("special_name")&"</option>")
	    End If
	    rs_Fen_SpecialName.movenext
	    Loop
	End If
	rs_Fen_SpecialName.Close
	Set rs_Fen_SpecialName=nothing
End Sub
%>
<div style="text-align:center; margin-top:5px; color: #0066FF">页面执行时间：<%=FormatNumber((endtime-startime)*1000,3)%> 毫秒</div>
</body>
</html>